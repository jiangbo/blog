# 0446-Bomb-重构引擎模块

## 环境

- Time 2024-03-15
- Zig 0.12.0-dev.3152+90c1a2c41
- WSL-Ubuntu 22.04.3 LTS
- raylib 5.0

## 前言

### 说明

参考资料：

1. 《游戏开发：世嘉新人培训教材》
2. 图片素材：<https://www.ituring.com.cn/book/1742>

### 目标

engine 模块的内容越来越多，将其分离到 engine 模块文件夹中。将 raylib 模块重命名为 backend 模块。

## engine.zig

引擎的总入口，将大多数的逻辑分散到了 engine 文件夹中。

```zig
const std = @import("std");

pub usingnamespace @import("engine/proxy.zig");
pub const Rectangle = @import("engine/basic.zig").Rectangle;
pub const Key = @import("engine/key.zig").Key;
```

## engine/proxy.zig

引擎的代理模块，主要代理了之前引擎相关的操作，并且委托给了 backend 模块。

```zig
const std = @import("std");
const backend = @import("backend.zig");

pub fn init(width: usize, height: usize, title: [:0]const u8) void {
    backend.init(width, height, title);
}

pub fn deinit() void {
    backend.deinit();
}

pub fn beginDraw() void {
    backend.beginDraw();
}

pub fn endDraw() void {
    backend.endDraw();
}

pub fn getPressed() usize {
    return backend.getPressed();
}

pub fn isPressed(key: usize) bool {
    return backend.isPressed(key);
}

pub fn time() usize {
    return backend.time();
}

pub fn shoudContinue() bool {
    return backend.shoudContinue();
}

const maxPathLength = 30;

pub fn readStageText(allocator: std.mem.Allocator, level: usize) ![]const u8 {
    var buf: [maxPathLength]u8 = undefined;
    const path = try std.fmt.bufPrint(&buf, "data/stage/{}.txt", .{level});

    std.log.info("load stage: {s}", .{path});
    return try readAll(allocator, path);
}

fn readAll(allocator: std.mem.Allocator, name: []const u8) ![]const u8 {
    const file = try std.fs.cwd().openFile(name, .{});
    defer file.close();

    return try file.readToEndAlloc(allocator, std.math.maxInt(usize));
}

pub const Texture = backend.Texture;
```

## engine/basic.zig

基础模块，一些常用的结构体。

```zig
pub const Vector = struct {
    x: usize = 0,
    y: usize = 0,
};

pub const Rectangle = struct {
    x: usize = 0,
    y: usize = 0,
    width: usize = 0,
    height: usize = 0,
};
```

## engine/backend.zig

引擎的后端模块，去除掉了 raylib.zig 的命名，将所有的操作封装到了后端。

```zig
const ray = @cImport({
    @cInclude("raylib.h");
});
const std = @import("std");
const basic = @import("basic.zig");

var screenWidth: usize = 0;

pub fn init(width: usize, height: usize, title: [:0]const u8) void {
    ray.InitWindow(@intCast(width), @intCast(height), title);
    ray.SetTargetFPS(60);
    ray.SetExitKey(ray.KEY_NULL);
    screenWidth = width;
    return;
}

pub fn deinit() void {
    ray.CloseWindow();
}

pub fn shoudContinue() bool {
    return !ray.WindowShouldClose();
}

pub fn beginDraw() void {
    ray.BeginDrawing();
    ray.ClearBackground(ray.WHITE);
}

pub fn endDraw() void {
    ray.DrawFPS(@intCast(screenWidth - 100), 10);
    ray.EndDrawing();
}

pub fn time() usize {
    return @intFromFloat(ray.GetTime() * 1000);
}

pub fn getPressed() usize {
    return @intCast(ray.GetKeyPressed());
}

pub fn isPressed(key: usize) bool {
    return ray.IsKeyPressed(@intCast(key));
}

fn toRayVector2(self: basic.Vector) ray.Vector2 {
    return ray.Vector2{
        .x = @floatFromInt(self.x),
        .y = @floatFromInt(self.y),
    };
}

fn toRayRectangle(self: basic.Rectangle) ray.Rectangle {
    return ray.Rectangle{
        .x = @floatFromInt(self.x),
        .y = @floatFromInt(self.y),
        .width = @floatFromInt(self.width),
        .height = @floatFromInt(self.height),
    };
}

const maxPathLength = 100;

pub const Texture = struct {
    texture: ray.Texture2D,

    pub fn init(name: []const u8) Texture {
        var buf: [maxPathLength]u8 = undefined;
        const format = "data/image/{s}";
        const path = std.fmt.bufPrintZ(&buf, format, .{name}) catch |e| {
            std.log.err("load texture error: {}", .{e});
            return Texture{ .texture = ray.Texture2D{} };
        };

        return Texture{ .texture = ray.LoadTexture(path) };
    }

    pub fn draw(self: Texture) void {
        ray.DrawTexture(self.texture, 0, 0, ray.WHITE);
    }

    pub fn drawPositin(self: Texture, x: usize, y: usize) void {
        const vec = basic.Vector{ .x = x, .y = y };
        ray.DrawTextureV(self.texture, toRayVector2(vec), ray.WHITE);
    }

    pub fn drawRectangle(self: Texture, rec: basic.Rectangle, pos: basic.Vector) void {
        const rectangle = toRayRectangle(rec);
        ray.DrawTextureRec(self.texture, rectangle, toRayVector2(pos), ray.WHITE);
    }

    pub fn deinit(self: Texture) void {
        ray.UnloadTexture(self.texture);
    }
};
```

## 效果

效果和之前一致，未改变程序的代码逻辑。

## 总结

重构了引擎模块，将之前到大文件中的内容分散到了多个小文件。

## 附录
