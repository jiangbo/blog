# 0444-Bomb-游戏结束功能

## 环境

- Time 2024-03-14
- Zig 0.12.0-dev.3152+90c1a2c41
- WSL-Ubuntu 22.04.3 LTS
- raylib 5.0

## 前言

### 说明

参考资料：

1. 《游戏开发：世嘉新人培训教材》
2. 图片素材：<https://www.ituring.com.cn/book/1742>

### 目标

如果游戏结束，弹出一个游戏结束的提示信息，等待三秒后跳转到主题界面。

## play.zig

删除了推箱子游戏的玩法，加入了按键调试代码。

```zig
const std = @import("std");
const engine = @import("engine.zig");
const map = @import("map.zig");

pub fn init(allocator: std.mem.Allocator, level: usize, box: engine.Texture) ?Play {
    const m = map.Map.init(allocator, level) catch |err| {
        std.log.err("init stage error: {}", .{err});
        return null;
    } orelse return null;
    return .{ .map = m, .box = box };
}

pub const Play = struct {
    map: map.Map,
    box: engine.Texture,

    pub fn update(_: *Play) ?@import("popup.zig").PopupType {
        if (engine.isPressed(engine.Key.x)) return .over;

        return null;
    }

    pub fn draw(self: Play) void {
        for (0..self.map.height) |y| {
            for (0..self.map.width) |x| {
                const item = self.map.data[y * self.map.width + x];
                if (item != map.MapItem.WALL) {
                    self.drawCell(x, y, if (item.hasGoal()) .GOAL else .SPACE);
                }
                if (item != .SPACE) self.drawCell(x, y, item);
            }
        }
    }

    fn drawCell(play: Play, x: usize, y: usize, item: map.MapItem) void {
        var source = engine.Rectangle{ .width = 32, .height = 32 };
        source.x = item.toImageIndex() * source.width;
        const position = .{ .x = x * source.width, .y = y * source.height };
        play.box.drawRectangle(source, position);
    }

    pub fn deinit(self: Play) void {
        self.map.deinit();
    }
};
```

## popup.zig

新增了一种菜单状态 over，表示游戏结束。

```zig
const std = @import("std");
const engine = @import("engine.zig");
const map = @import("map.zig");

pub const MenuType = enum { quit, title, select, reset, next };
pub const PopupType = enum { loading, menu, clear, over };

pub const Popup = union(PopupType) {
    loading: Loading,
    menu: Menu,
    clear: Clear,
    over: Over,

    pub fn update(self: *Popup) ?MenuType {
        return switch (self.*) {
            inline else => |*case| case.update(),
        };
    }

    pub fn draw(self: Popup) void {
        switch (self) {
            inline else => |sequence| sequence.draw(),
        }
    }

    pub fn deinit(self: Popup) void {
        switch (self) {
            inline else => |sequence| sequence.deinit(),
        }
    }
};

pub const Loading = struct {
    texture: engine.Texture,
    time: usize,

    pub fn init() Loading {
        return Loading{
            .texture = engine.Texture.init("loading.dds"),
            .time = engine.time(),
        };
    }

    fn update(self: Loading) ?MenuType {
        return if (engine.time() - self.time > 1000) return .quit else null;
    }

    fn draw(self: Loading) void {
        self.texture.draw();
    }

    fn deinit(self: Loading) void {
        self.texture.deinit();
    }
};

pub const Menu = struct {
    texture: engine.Texture,

    pub fn init() Menu {
        return Menu{ .texture = engine.Texture.init("menu.dds") };
    }

    fn update(_: Menu) ?MenuType {
        const char = engine.getPressed();
        return switch (char) {
            '1' => .reset,
            '2' => .select,
            '3' => .title,
            '4' => .quit,
            else => null,
        };
    }

    fn draw(self: Menu) void {
        self.texture.draw();
    }

    fn deinit(self: Menu) void {
        self.texture.deinit();
    }
};

pub const Over = struct {
    texture: engine.Texture,
    time: usize,

    pub fn init() Over {
        return Over{ .texture = engine.Texture.init("over.png"), .time = engine.time() };
    }

    fn update(self: Over) ?MenuType {
        return if (engine.time() - self.time > 3000) .title else null;
    }

    fn draw(self: Over) void {
        self.texture.draw();
    }

    fn deinit(self: Over) void {
        self.texture.deinit();
    }
};

pub const Clear = struct {
    texture: engine.Texture,
    time: usize,

    pub fn init() Clear {
        return Clear{
            .texture = engine.Texture.init("clear.dds"),
            .time = engine.time(),
        };
    }

    fn update(self: Clear) ?MenuType {
        return if ((engine.time() - self.time) > 1000) return .next else null;
    }

    fn draw(self: Clear) void {
        self.texture.draw();
    }

    fn deinit(self: Clear) void {
        self.texture.deinit();
    }
};
```

## 效果

![bomb][1]

## 总结

实现炸弹人的游戏结束功能。

[1]: images/bomb02.png

## 附录
