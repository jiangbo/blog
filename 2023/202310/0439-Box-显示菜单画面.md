# 0439-Box-显示菜单画面

## 环境

- Time 2024-03-13
- Zig 0.12.0-dev.3152+90c1a2c41
- WSL-Ubuntu 22.04.3 LTS
- raylib 5.0

## 前言

### 说明

参考资料：

1. 《游戏开发：世嘉新人培训教材》
2. 图片素材：<https://www.ituring.com.cn/book/1742>

### 目标

在上一节的基础上，增加菜单画面的显示。重构代码分离了弹出框 popup 的代码。

## stage.zig

```zig
const std = @import("std");
const file = @import("file.zig");
const ray = @import("raylib.zig");
const pop = @import("popup.zig");
const play = @import("play.zig");

pub const SequenceType = enum { title, select, stage };
pub const SequenceData = union(SequenceType) {
    title: void,
    select: void,
    stage: usize,
};
const PopupType = pop.PopupType;

pub fn init(allocator: std.mem.Allocator, level: usize, box: file.Texture) ?Stage {
    return Stage{
        .current = play.init(allocator, level, box) orelse return null,
        .popup = .{ .loading = pop.Loading.init() },
    };
}

pub const Stage = struct {
    current: play.Play,
    popup: ?pop.Popup = null,

    pub fn update(self: *Stage) ?SequenceData {
        if (self.popup) |*option| {
            const popup = option.update() orelse return null;
            switch (popup) {
                .title => return .title,
                .quit => self.popup = null,
                .clear, .menu, .loading => unreachable,
            }
            return null;
        }

        const sequence = self.current.update() orelse return null;
        switch (sequence) {
            .clear => self.popup = .{ .clear = pop.Clear.init() },
            .menu => self.popup = .{ .menu = pop.Menu.init() },
            .title, .quit, .loading => unreachable,
        }

        return null;
    }

    pub fn draw(self: Stage) void {
        self.current.draw();
        if (self.popup) |popup| popup.draw();
    }

    pub fn deinit(self: Stage) void {
        if (self.popup) |popup| popup.deinit();
        self.current.deinit();
    }
};
```

## play.zig

```zig
const std = @import("std");
const map = @import("map.zig");
const file = @import("file.zig");
const ray = @import("raylib.zig");

pub fn init(allocator: std.mem.Allocator, level: usize, box: file.Texture) ?Play {
    const m = map.Map.init(allocator, level) catch |err| {
        std.log.err("init stage error: {}", .{err});
        return null;
    } orelse return null;
    return .{ .map = m, .box = box };
}

pub const Play = struct {
    map: map.Map,
    box: file.Texture,

    pub fn update(self: *Play) ?@import("popup.zig").PopupType {
        if (ray.IsKeyPressed(ray.KEY_SPACE)) return .menu;

        // 操作角色移动的距离
        const delta: isize = switch (ray.GetKeyPressed()) {
            ray.KEY_W, ray.KEY_UP => -@as(isize, @intCast(self.map.width)),
            ray.KEY_S, ray.KEY_DOWN => @as(isize, @intCast(self.map.width)),
            ray.KEY_D, ray.KEY_RIGHT => 1,
            ray.KEY_A, ray.KEY_LEFT => -1,
            else => return null,
        };

        const currentIndex = self.map.playerIndex();
        const index = @as(isize, @intCast(currentIndex)) + delta;
        if (index < 0 or index > self.map.size()) return null;

        // 角色欲前往的目的地
        const destIndex = @as(usize, @intCast(index));
        self.updatePlayer(currentIndex, destIndex, delta);

        return if (self.map.hasCleared()) .clear else null;
    }

    fn updatePlayer(play: *Play, current: usize, dest: usize, delta: isize) void {
        var state = play.map.data;
        if (state[dest] == .SPACE or state[dest] == .GOAL) {
            // 如果是空地或者目标地，则可以移动
            state[dest] = if (state[dest] == .GOAL) .MAN_ON_GOAL else .MAN;
            state[current] = if (state[current] == .MAN_ON_GOAL) .GOAL else .SPACE;
        } else if (state[dest] == .BLOCK or state[dest] == .BLOCK_ON_GOAL) {
            //  如果是箱子或者目的地上的箱子，需要考虑该方向上的第二个位置
            const index = @as(isize, @intCast(dest)) + delta;
            if (index < 0 or index > play.map.size()) return;

            const next = @as(usize, @intCast(index));
            if (state[next] == .SPACE or state[next] == .GOAL) {
                state[next] = if (state[next] == .GOAL) .BLOCK_ON_GOAL else .BLOCK;
                state[dest] = if (state[dest] == .BLOCK_ON_GOAL) .MAN_ON_GOAL else .MAN;
                state[current] = if (state[current] == .MAN_ON_GOAL) .GOAL else .SPACE;
            }
        }
    }

    pub fn draw(self: Play) void {
        for (0..self.map.height) |y| {
            for (0..self.map.width) |x| {
                const item = self.map.data[y * self.map.width + x];
                if (item != map.MapItem.WALL) {
                    self.drawCell(x, y, if (item.hasGoal()) .GOAL else .SPACE);
                }
                if (item != .SPACE) self.drawCell(x, y, item);
            }
        }
    }

    fn drawCell(play: Play, x: usize, y: usize, item: map.MapItem) void {
        var source = ray.Rectangle{ .width = 32, .height = 32 };
        source.x = item.toImageIndex() * source.width;
        const dest = ray.Rectangle{
            .x = @as(f32, @floatFromInt(x)) * source.width,
            .y = @as(f32, @floatFromInt(y)) * source.height,
            .width = source.width,
            .height = source.height,
        };

        ray.DrawTexturePro(play.box.texture, source, dest, .{}, 0, ray.WHITE);
    }

    pub fn deinit(self: Play) void {
        self.map.deinit();
    }
};
```

## popup.zig

```zig
const std = @import("std");
const map = @import("map.zig");
const file = @import("file.zig");
const ray = @import("raylib.zig");

pub const PopupType = enum { loading, menu, clear, quit, title };

pub const Popup = union(PopupType) {
    loading: Loading,
    menu: Menu,
    clear: Clear,
    quit: void,
    title: void,

    pub fn update(self: *Popup) ?PopupType {
        return switch (self.*) {
            .title, .quit => unreachable,
            inline else => |*case| case.update(),
        };
    }

    pub fn draw(self: Popup) void {
        switch (self) {
            .title, .quit => unreachable,
            inline else => |sequence| sequence.draw(),
        }
    }

    pub fn deinit(self: Popup) void {
        switch (self) {
            .loading => |sequence| sequence.deinit(),
            else => {},
        }
    }
};

pub const Loading = struct {
    texture: file.Texture,
    time: f64,

    pub fn init() Loading {
        return Loading{
            .texture = file.loadTexture("loading.dds"),
            .time = ray.GetTime(),
        };
    }

    fn update(self: Loading) ?PopupType {
        return if ((ray.GetTime() - self.time) > 1) return .quit else null;
    }

    fn draw(self: Loading) void {
        ray.DrawTexture(self.texture.texture, 0, 0, ray.WHITE);
    }

    fn deinit(self: Loading) void {
        self.texture.unload();
    }
};

pub const Menu = struct {
    texture: file.Texture,

    pub fn init() Menu {
        return Menu{ .texture = file.loadTexture("menu.dds") };
    }

    fn update(_: Menu) ?PopupType {
        return null;
    }

    fn draw(self: Menu) void {
        ray.DrawTexture(self.texture.texture, 0, 0, ray.WHITE);
    }

    fn deinit(self: Menu) void {
        self.texture.unload();
    }
};

pub const Clear = struct {
    texture: file.Texture,
    time: f64,

    pub fn init() Clear {
        return Clear{
            .texture = file.loadTexture("clear.dds"),
            .time = ray.GetTime(),
        };
    }

    fn update(self: Clear) ?PopupType {
        return if ((ray.GetTime() - self.time) > 1) return .title else null;
    }

    fn draw(self: Clear) void {
        ray.DrawTexture(self.texture.texture, 0, 0, ray.WHITE);
    }

    fn deinit(self: Clear) void {
        self.texture.unload();
    }
};
```

## 效果

![box13][1]

## 总结

在上一节的基础上，增加菜单画面的显示。

[1]: images/box13.png

## 附录
