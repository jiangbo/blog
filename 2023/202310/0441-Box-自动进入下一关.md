# 0441-Box-自动进入下一关

## 环境

- Time 2024-03-13
- Zig 0.12.0-dev.3152+90c1a2c41
- WSL-Ubuntu 22.04.3 LTS
- raylib 5.0

## 前言

### 说明

参考资料：

1. 《游戏开发：世嘉新人培训教材》
2. 图片素材：<https://www.ituring.com.cn/book/1742>

### 目标

当前关卡全部完成后，自动进入下一关。进行了一小部分的重构，调整结构和重命名。

## stage.zig

```zig
const std = @import("std");
const file = @import("file.zig");
const ray = @import("raylib.zig");
const popup = @import("popup.zig");
const play = @import("play.zig");

pub const SequenceType = enum { title, select, stage };
pub const SequenceData = union(SequenceType) {
    title: void,
    select: void,
    stage: usize,
};

pub fn init(allocator: std.mem.Allocator, level: usize, box: file.Texture) ?Stage {
    return Stage{
        .level = level,
        .current = play.init(allocator, level, box) orelse return null,
        .popup = .{ .loading = popup.Loading.init() },
    };
}

pub const Stage = struct {
    level: usize,
    current: play.Play,
    popup: ?popup.Popup = null,

    pub fn update(self: *Stage) ?SequenceData {
        if (self.popup) |*option| {
            const menu = option.update() orelse return null;
            defer option.deinit();
            switch (menu) {
                .title => return .title,
                .select => return .select,
                .reset => return .{ .stage = self.level },
                .next => return .{ .stage = self.level + 1 },
                .quit => self.popup = null,
            }
        }

        self.popup = switch (self.current.update() orelse return null) {
            .clear => .{ .clear = popup.Clear.init() },
            .menu => .{ .menu = popup.Menu.init() },
            .loading => .{ .loading = popup.Loading.init() },
        };

        return null;
    }

    pub fn draw(self: Stage) void {
        self.current.draw();

        // ray.TextFormat("", : ...)
        // ray.DrawText(text: [*c]const u8, posX: c_int, posY: c_int, fontSize: c_int, color: Color)
        if (self.popup) |option| option.draw();
    }

    pub fn deinit(self: Stage) void {
        if (self.popup) |option| option.deinit();
        self.current.deinit();
    }
};
```

## popup.zig

```zig
const std = @import("std");
const map = @import("map.zig");
const file = @import("file.zig");
const ray = @import("raylib.zig");

pub const MenuType = enum { quit, title, select, reset, next };
pub const PopupType = enum { loading, menu, clear };

pub const Popup = union(PopupType) {
    loading: Loading,
    menu: Menu,
    clear: Clear,

    pub fn update(self: *Popup) ?MenuType {
        return switch (self.*) {
            inline else => |*case| case.update(),
        };
    }

    pub fn draw(self: Popup) void {
        switch (self) {
            inline else => |sequence| sequence.draw(),
        }
    }

    pub fn deinit(self: Popup) void {
        switch (self) {
            inline else => |sequence| sequence.deinit(),
        }
    }
};

pub const Loading = struct {
    texture: file.Texture,
    time: f64,

    pub fn init() Loading {
        return Loading{
            .texture = file.loadTexture("loading.dds"),
            .time = ray.GetTime(),
        };
    }

    fn update(self: Loading) ?MenuType {
        return if ((ray.GetTime() - self.time) > 1) return .quit else null;
    }

    fn draw(self: Loading) void {
        self.texture.draw();
    }

    fn deinit(self: Loading) void {
        self.texture.unload();
    }
};

pub const Menu = struct {
    texture: file.Texture,

    pub fn init() Menu {
        return Menu{ .texture = file.loadTexture("menu.dds") };
    }

    fn update(_: Menu) ?MenuType {
        const char = ray.GetCharPressed();
        return switch (char) {
            '1' => .reset,
            '2' => .select,
            '3' => .title,
            '4' => .quit,
            else => null,
        };
    }

    fn draw(self: Menu) void {
        self.texture.draw();
    }

    fn deinit(self: Menu) void {
        self.texture.unload();
    }
};

pub const Clear = struct {
    texture: file.Texture,
    time: f64,

    pub fn init() Clear {
        return Clear{
            .texture = file.loadTexture("clear.dds"),
            .time = ray.GetTime(),
        };
    }

    fn update(self: Clear) ?MenuType {
        return if ((ray.GetTime() - self.time) > 1) return .next else null;
    }

    fn draw(self: Clear) void {
        self.texture.draw();
    }

    fn deinit(self: Clear) void {
        self.texture.unload();
    }
};
```

## 总结

当前关卡全部完成后，自动进入下一关。

## 附录
