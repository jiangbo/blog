# 0313-文本冒险中的对象

## 环境

- Time 2023-07-17
- Zig 0.10.0

## 前言

### 说明

参考：

- <https://helderman.github.io/htpataic/>

### 目标

定义了文本冒险中的各种对象，根据角色的移动，可以看到不同的对象。

## main.zig

```zig
const std = @import("std");
const location = @import("location.zig");
const world = @import("world.zig");
const print = std.debug.print;

pub fn main() !void {
    world.init();
    print("Welcome to Little Cave Adventure.\n", .{});
    const reader = std.io.getStdIn().reader();
    var buffer: [100]u8 = undefined;
    _ = location.lookAround();

    while (true) {
        print("--> ", .{});
        var input = try getInput(reader, buffer[0..]) orelse continue;
        if (std.mem.eql(u8, input, "quit")) {
            break;
        }
        parseAndExecute(input);
    }

    print("\nBye!\n", .{});
}

fn getInput(reader: anytype, buffer: []u8) !?[]const u8 {
    if (try reader.readUntilDelimiterOrEof(buffer, '\n')) |input| {
        if (@import("builtin").os.tag == .windows) {
            return std.mem.trimRight(u8, input, "\r");
        }
        return input;
    }
    return null;
}

pub fn parseAndExecute(input: []const u8) void {
    var iterator = std.mem.split(u8, input, " ");
    const verb = iterator.next() orelse return;
    const noun = iterator.next();

    if (std.mem.eql(u8, verb, "look")) {
        if (!location.executeLook(noun)) {
            print("I don't understand what you want to see.\n", .{});
        }
    } else if (std.mem.eql(u8, verb, "go")) {
        if (!location.executeGo(noun)) {
            print("I don't understand where you want to go.\n", .{});
        }
    } else {
        print("I don't know how to {s}.\n", .{verb});
    }
}
```

## location

```zig
const std = @import("std");
const world = @import("world.zig");
const getVisible = @import("noun.zig").getVisible;
const print = std.debug.print;

pub fn executeLook(input: ?[]const u8) bool {
    const noun = input orelse return false;
    if (std.mem.eql(u8, noun, "around")) {
        const location = world.player.location.?;
        print("You are in {s}.\n", .{location.desc});
        listAtLocation(location);
        return true;
    }
    return false;
}

pub fn lookAround() bool {
    return executeLook("around");
}

pub fn executeGo(input: ?[]const u8) bool {
    const noun = input orelse return false;

    var item = world.getItem(noun) orelse return false;
    if (item.isLocation() and item.isPlayerNotIn()) {
        print("OK.\n", .{});
        world.player.location = item;
        return lookAround();
    } else {
        print("You can't get much closer than this.\n", .{});
        return true;
    }

    return false;
}

fn listAtLocation(location: *const world.Item) void {
    var count: i32 = 0;
    for (world.items) |item| {
        if (item.isNotPlayer() and item.isLocate(location)) {
            if (count == 0) {
                print("You see:\n", .{});
            }
            print("{s}\n", .{item.desc});
            count += 1;
        }
    }
}
```

## world.zig

```zig
const std = @import("std");
const print = std.debug.print;

const Type = enum { field, cave, silver, gold, guard, player };

pub const Item = struct {
    desc: []const u8,
    type: Type,
    location: ?*const Item = null,

    pub fn isNotPlayer(self: *const Item) bool {
        return self.type != .player;
    }

    pub fn isLocation(self: *const Item) bool {
        return self.type == .field or self.type == .cave;
    }

    pub fn isLocate(self: *const Item, location: *const Item) bool {
        return self.location == location;
    }

    pub fn isPlayerNotIn(self: *const Item) bool {
        return self != player.location.?;
    }
};

pub var items = [_]Item{
    .{ .desc = "an open field", .type = .field },
    .{ .desc = "a little cave", .type = .cave },
    .{ .desc = "a silver coin", .type = .silver },
    .{ .desc = "a gold coin", .type = .gold },
    .{ .desc = "a burly guard", .type = .guard },
    .{ .desc = "yourself", .type = .player },
};

fn toType(noun: []const u8) ?Type {
    return std.meta.stringToEnum(Type, noun);
}

pub fn getItem(noun: []const u8) ?*const Item {
    const itemType = toType(noun) orelse return null;
    for (items) |*value| {
        if (value.type == itemType) {
            return value;
        }
    }
    return null;
}

pub var player: *Item = &items[5];

pub fn init() void {
    items[2].location = &items[0];
    items[3].location = &items[1];
    items[4].location = &items[0];
    items[5].location = &items[0];
}
```

## 总结

实现了游戏中的各种对象的定义，根据角色的移动，可以看见不同的对象。

## 附录
