# 0311-封装屏幕显示逻辑

## 环境

- Time 2023-07-26
- Zig 0.11.0-dev.4191+1bf16b172
- SLD2 2.28.1

## 前言

### 说明

### 目标

在前一节的基础上，从主函数中分离屏幕的显示逻辑，放到单独的对象中。

## screen.zig

1. 定义了屏幕的缓存大小 BUFFER_SIZE 和放大的尺寸 scale。
2. 定义了 needRender 来判断是否需要重新渲染屏幕。
3. 只支持黑白两种颜色，所以用布尔来填充缓存。
4. 支持通过坐标和索引设置显示的缓存，返回当前坐标是否点亮。
5. 支持清屏命令

```zig
const c = @cImport(@cInclude("SDL.h"));

const WIDTH: c_int = 64;
const HEIGHT: c_int = 32;
const BUFFER_SIZE = WIDTH * HEIGHT;

pub const Screen = struct {
    scale: u8 = 10,
    buffer: [BUFFER_SIZE]bool = undefined,
    needRender: bool = true,
    window: *c.SDL_Window = undefined,
    renderer: *c.SDL_Renderer = undefined,
    texture: *c.SDL_Texture = undefined,

    pub fn init(self: *Screen) void {
        if (c.SDL_Init(c.SDL_INIT_EVERYTHING) < 0)
            @panic("sdl init failed");

        const center = c.SDL_WINDOWPOS_CENTERED;
        self.window = c.SDL_CreateWindow("chip8", center, center, //
            WIDTH * self.scale, HEIGHT * self.scale, c.SDL_WINDOW_SHOWN) //
        orelse @panic("create window failed");

        self.renderer = c.SDL_CreateRenderer(self.window, -1, 0) //
        orelse @panic("create renderer failed");

        self.texture = c.SDL_CreateTexture(self.renderer, //
            c.SDL_PIXELFORMAT_RGBA8888, c.SDL_TEXTUREACCESS_STREAMING, //
            WIDTH, HEIGHT) orelse @panic("create texture failed");

        _ = c.SDL_SetRenderTarget(self.renderer, self.texture);
        _ = c.SDL_RenderSetLogicalSize(self.renderer, WIDTH, HEIGHT);
    }

    pub fn update(self: *Screen, fps: u32) void {
        defer c.SDL_Delay(1000 / fps);
        if (!self.needRender) return;

        _ = c.SDL_SetRenderDrawColor(self.renderer, 0, 0, 0, 255);
        _ = c.SDL_RenderClear(self.renderer);
        _ = c.SDL_SetRenderDrawColor(self.renderer, 255, 255, 255, 255);

        for (self.buffer, 0..) |value, index| {
            if (value) {
                const x: c_int = @intCast(index % WIDTH);
                const y: c_int = @intCast(@divTrunc(index, WIDTH));
                _ = c.SDL_RenderDrawPoint(self.renderer, x, y);
            }
        }
        c.SDL_RenderPresent(self.renderer);
        self.needRender = false;
    }

    pub fn setIndex(self: *Screen, i: usize) bool {
        self.needRender = true;
        const index = if (i >= BUFFER_SIZE) i % BUFFER_SIZE else i;
        self.buffer[index] = !self.buffer[index];
        return self.buffer[index];
    }

    pub fn setPixel(self: *Screen, x: usize, y: usize) bool {
        return self.setIndex(x + y * WIDTH);
    }

    pub fn clear(self: *Screen) void {
        @memset(&self.buffer, false);
        self.needRender = true;
    }

    pub fn deinit(self: *Screen) void {
        c.SDL_DestroyTexture(self.texture);
        c.SDL_DestroyRenderer(self.renderer);
        c.SDL_DestroyWindow(self.window);
        c.SDL_Quit();
    }
};
```

## main.zig

```zig
const c = @cImport(@cInclude("SDL.h"));

const std = @import("std");
const Screen = @import("screen.zig").Screen;

const FPS = 60;

pub fn main() !void {
    var screen = Screen{};

    screen.init();
    defer screen.deinit();

    var index: usize = 0;
    mainloop: while (true) : (index += 1) {
        var event: c.SDL_Event = undefined;
        while (c.SDL_PollEvent(&event) > 0) {
            if (event.type == c.SDL_QUIT)
                break :mainloop;
        }
        if (index % 44 == 0) screen.clear();
        _ = screen.setIndex(index);
        screen.update(FPS);
    }
}
```

## 启动

`zig build run`

## 效果

![窗口][1]

## 总结

将屏幕的显示逻辑抽取到了一个单独的文件中，用来处理显示相关的逻辑。

[1]: images/screen.png

## 附录
