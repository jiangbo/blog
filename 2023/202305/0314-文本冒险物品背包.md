# 0314-文本冒险物品背包

## 环境

- Time 2023-07-18
- Zig 0.10.0

## 前言

### 说明

参考：

- <https://helderman.github.io/htpataic/>

### 目标

定义了文本冒险中的角色背包，可以拾取物品，丢弃物品，转移物品等。

## main.zig

```zig
const std = @import("std");
const location = @import("location.zig");
const world = @import("world.zig");
const inventory = @import("inventory.zig");
const print = std.debug.print;

pub fn main() !void {
    world.init();
    print("Welcome to Little Cave Adventure.\n", .{});
    const reader = std.io.getStdIn().reader();
    var buffer: [100]u8 = undefined;
    _ = location.lookAround();

    while (true) {
        print("--> ", .{});
        var input = try getInput(reader, buffer[0..]) orelse continue;
        if (std.mem.eql(u8, input, "quit")) {
            break;
        }
        parseAndExecute(input);
    }

    print("\nBye!\n", .{});
}

fn getInput(reader: anytype, buffer: []u8) !?[]const u8 {
    if (try reader.readUntilDelimiterOrEof(buffer, '\n')) |input| {
        if (@import("builtin").os.tag == .windows) {
            return std.mem.trimRight(u8, input, "\r");
        }
        return input;
    }
    return null;
}

pub fn parseAndExecute(input: []const u8) void {
    var iterator = std.mem.split(u8, input, " ");
    const verb = iterator.next() orelse return;
    const noun = iterator.next();

    if (std.mem.eql(u8, verb, "look")) {
        if (!location.executeLook(noun)) {
            print("I don't understand what you want to see.\n", .{});
        }
    } else if (std.mem.eql(u8, verb, "go")) {
        if (!location.executeGo(noun)) {
            print("I don't understand where you want to go.\n", .{});
        }
    } else if (std.mem.eql(u8, verb, "get")) {
        inventory.executeGet(noun);
    } else if (std.mem.eql(u8, verb, "drop")) {
        inventory.executeDrop(noun);
    } else if (std.mem.eql(u8, verb, "give")) {
        inventory.executeGive(noun);
    } else if (std.mem.eql(u8, verb, "ask")) {
        inventory.executeAsk(noun);
    } else if (std.mem.eql(u8, verb, "inventory")) {
        inventory.executeInventory();
    } else {
        print("I don't know how to {s}.\n", .{verb});
    }
}
```

## location

```zig
const std = @import("std");
const world = @import("world.zig");
const print = std.debug.print;

pub fn executeLook(input: ?[]const u8) bool {
    const noun = input orelse return false;
    if (std.mem.eql(u8, noun, "around")) {
        const location = world.player.location.?;
        print("You are in {s}.\n", .{location.desc});
        _ = world.listAtLocation(location);
        return true;
    }
    return false;
}

pub fn lookAround() bool {
    return executeLook("around");
}

pub fn executeGo(input: ?[]const u8) bool {
    const noun = input orelse return false;

    const intention = "where you want to go";
    var item = world.getVisible(intention, noun) orelse return true;
    if (item.isLocation() and !item.isPlayerIn()) {
        print("OK.\n", .{});
        world.player.location = item;
        return lookAround();
    } else {
        print("You can't get much closer than this.\n", .{});
        return true;
    }

    return false;
}
```

## world.zig

```zig
const std = @import("std");
const print = std.debug.print;

const Type = enum { field, cave, silver, gold, guard, player };

pub const Item = struct {
    desc: []const u8,
    type: Type,
    location: ?*Item = null,

    pub fn isPlayer(self: *Item) bool {
        return self.type == .player;
    }

    pub fn isLocation(self: *Item) bool {
        return self.location == null;
    }

    pub fn isLocate(self: *Item, location: *Item) bool {
        return self.location == location;
    }

    pub fn isPlayerIn(self: *Item) bool {
        return self == player.location;
    }

    pub fn isPlayerItem(self: *Item) bool {
        return self.location == player;
    }

    pub fn isNpcItem(self: *Item) bool {
        const location = self.location orelse return false;
        return location.type == .guard;
    }

    pub fn isWithPlayer(self: *Item) bool {
        return self.isLocate(player.location.?);
    }
};

pub var items = [_]Item{
    .{ .desc = "an open field", .type = .field },
    .{ .desc = "a little cave", .type = .cave },
    .{ .desc = "a silver coin", .type = .silver },
    .{ .desc = "a gold coin", .type = .gold },
    .{ .desc = "a burly guard", .type = .guard },
    .{ .desc = "yourself", .type = .player },
};

fn toType(noun: ?[]const u8) ?Type {
    return std.meta.stringToEnum(Type, noun orelse return null);
}

pub fn getItem(noun: ?[]const u8) ?*Item {
    const itemType = toType(noun) orelse return null;
    for (&items) |*value| {
        if (value.type == itemType) {
            return value;
        }
    }
    return null;
}

pub fn moveItem(obj: ?*Item, to: ?*Item) void {
    const from = obj orelse return;

    if (to == null) {
        return print("There is nobody here to give that to.\n", .{});
    }

    if (from.isLocation()) {
        return print("That is way too heavy.\n", .{});
    }

    describeMove(from, to.?);
    from.location = to;
}

fn describeMove(from: *Item, to: *Item) void {
    if (to == player.location) {
        print("You drop {s}.\n", .{from.desc});
    } else if (to != player) {
        if (to.type == .guard) {
            print("You give {s} to {s}.\n", .{ from.desc, to.desc });
        } else {
            print("You put {s} in {s}.\n", .{ from.desc, to.desc });
        }
    } else if (from.isWithPlayer()) {
        print("You pick up {s}.\n", .{from.desc});
    } else {
        print("You get {s} from {s}.\n", .{ from.desc, from.location.?.desc });
    }
}

pub fn getVisible(intention: []const u8, noun: ?[]const u8) ?*Item {
    const oitem = getItem(noun);
    if (oitem == null) {
        print("I don't understand {s}.\n", .{intention});
        return null;
    }
    const item = oitem.?;
    if (item.isPlayer() or item.isPlayerIn() or item.isPlayerItem() or
        //
        item.isWithPlayer() or item.isLocation() or
        //
        item.location.?.isPlayerItem() or item.location.?.isWithPlayer())
    {
        return item;
    }

    print("You don't see any {s} here.\n", .{noun.?});
    return null;
}

pub fn listAtLocation(location: *Item) usize {
    var count: usize = 0;
    for (&items) |*item| {
        if (!item.isPlayer() and item.isLocate(location)) {
            if (count == 0) {
                print("You see:\n", .{});
            }
            print("{s}\n", .{item.desc});
            count += 1;
        }
    }
    return count;
}

pub fn getPossession(from: ?*Item, verb: []const u8, noun: ?[]const u8) ?*Item {
    if (from == null) {
        print("I don't understand who you want to {s}.\n", .{verb});
        return null;
    }

    const item = getItem(noun) orelse {
        print("I don't understand what you want to {s}.\n", .{verb});
        return null;
    };

    if (item == from) {
        print("You should not be doing that to {s}.\n", .{item.desc});
        return null;
    } else if (item.location != from) {
        if (from == player) {
            print("You are not holding any {s}.\n", .{noun.?});
        } else {
            print("There appears to be no {s} you can get from {s}.\n", .{ noun.?, from.?.desc });
        }
        return null;
    }
    return item;
}

pub var player: *Item = &items[5];

pub fn init() void {
    items[2].location = &items[0];
    items[3].location = &items[1];
    items[4].location = &items[0];
    items[5].location = &items[0];
}
```

## inventory

```zig
const std = @import("std");
const world = @import("world.zig");
const print = std.debug.print;

pub fn executeGet(noun: ?[]const u8) void {
    const intention = "what you want to get";
    const item = world.getVisible(intention, noun) orelse return;
    if (item.isPlayer()) {
        print("You should not be doing that to yourself.\n", .{});
    } else if (item.isPlayerItem()) {
        print("You already have {s}.\n", .{item.desc});
    } else if (item.isNpcItem()) {
        print("You should ask {s} nicely.\n", .{item.location.?.desc});
    } else {
        world.moveItem(item, world.player);
    }
}

pub fn executeDrop(noun: ?[]const u8) void {
    const possession = world.getPossession(world.player, "drop", noun);
    world.moveItem(possession, world.player.location);
}
pub fn executeAsk(noun: ?[]const u8) void {
    const possession = world.getPossession(actorHere(), "ask", noun);
    world.moveItem(possession, world.player);
}
pub fn executeGive(noun: ?[]const u8) void {
    const possession = world.getPossession(world.player, "give", noun);
    world.moveItem(possession, actorHere());
}

pub fn executeInventory() void {
    if (world.listAtLocation(world.player) == 0) {
        print("You are empty-handed.\n", .{});
    }
}

fn actorHere() ?*world.Item {
    const location = world.player.location;
    for (&world.items) |*item| {
        if (item.location == location and item.type == .guard) {
            return item;
        }
    }
    return null;
}
```

## 总结

实现了游戏中角色的背包定义，可以拾取物品，丢弃物品，转移物品等。

## 附录
