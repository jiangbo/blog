# 0315-文本冒险中的通道

## 环境

- Time 2023-07-18
- Zig 0.11.0

## 前言

### 说明

参考：

- <https://helderman.github.io/htpataic/>

### 目标

定义了文本冒险中通道，可以从一个地方通过通道进入另一个地方，或者直接进入通道到达另一个地方。

## main.zig

```zig
const std = @import("std");
const location = @import("location.zig");
const world = @import("world.zig");
const inventory = @import("inventory.zig");
const print = std.debug.print;

pub fn main() !void {
    world.init();
    print("Welcome to Little Cave Adventure.\n", .{});
    const reader = std.io.getStdIn().reader();
    var buffer: [100]u8 = undefined;
    _ = location.lookAround();

    while (true) {
        print("--> ", .{});
        var input = try getInput(reader, buffer[0..]) orelse continue;
        if (std.mem.eql(u8, input, "quit")) {
            break;
        }
        parseAndExecute(input);
    }

    print("\nBye!\n", .{});
}

fn getInput(reader: anytype, buffer: []u8) !?[]const u8 {
    if (try reader.readUntilDelimiterOrEof(buffer, '\n')) |input| {
        if (@import("builtin").os.tag == .windows) {
            return std.mem.trimRight(u8, input, "\r");
        }
        return input;
    }
    return null;
}

pub fn parseAndExecute(input: []const u8) void {
    var iterator = std.mem.split(u8, input, " ");
    const verb = iterator.next() orelse return;
    const noun = iterator.next();

    if (std.mem.eql(u8, verb, "look")) {
        if (!location.executeLook(noun)) {
            print("I don't understand what you want to see.\n", .{});
        }
    } else if (std.mem.eql(u8, verb, "go")) {
        if (!location.executeGo(noun)) {
            print("I don't understand where you want to go.\n", .{});
        }
    } else if (std.mem.eql(u8, verb, "get")) {
        inventory.executeGet(noun);
    } else if (std.mem.eql(u8, verb, "drop")) {
        inventory.executeDrop(noun);
    } else if (std.mem.eql(u8, verb, "give")) {
        inventory.executeGive(noun);
    } else if (std.mem.eql(u8, verb, "ask")) {
        inventory.executeAsk(noun);
    } else if (std.mem.eql(u8, verb, "inventory")) {
        inventory.executeInventory();
    } else {
        print("I don't know how to {s}.\n", .{verb});
    }
}
```

## location

```zig
const std = @import("std");
const world = @import("world.zig");
const print = std.debug.print;

pub fn executeLook(input: ?[]const u8) bool {
    const noun = input orelse return false;
    if (std.mem.eql(u8, noun, "around")) {
        const location = world.player.location.?;
        print("You are in {s}.\n", .{location.desc});
        _ = world.listAtLocation(location);
        return true;
    }
    return false;
}

pub fn lookAround() bool {
    return executeLook("around");
}

pub fn executeGo(input: ?[]const u8) bool {
    const noun = input orelse return false;

    const intention = "where you want to go";
    var item = world.getVisible(intention, noun) orelse return true;

    if (world.getPassage(world.player.location, item) != null) {
        print("OK.\n", .{});
        world.player.location = item;
        return lookAround();
    } else if (!item.isWithPlayer()) {
        print("You don't see any{s} here.\n", .{noun});
    } else if (!item.isPlayerIn()) {
        print("OK.\n", .{});
        world.player.location = item.destination;
        return lookAround();
    } else {
        print("You can't get much closer than this.\n", .{});
        return true;
    }

    return false;
}
```

## world.zig

```zig
const std = @import("std");
const print = std.debug.print;

const Type = enum { field, cave, silver, gold, guard, player, entrance, exit };

pub const Item = struct {
    desc: []const u8,
    type: Type,
    location: ?*Item = null,
    destination: ?*Item = null,

    pub fn isPlayer(self: *Item) bool {
        return self.type == .player;
    }

    pub fn isLocation(self: *Item) bool {
        return self.location == null;
    }

    fn isLocate(self: *Item, location: *Item) bool {
        return self.location == location;
    }

    pub fn isPlayerIn(self: *Item) bool {
        return self == player.location;
    }

    pub fn isPlayerItem(self: *Item) bool {
        return self.location == player;
    }

    pub fn isNpcItem(self: *Item) bool {
        const location = self.location orelse return false;
        return location.type == .guard;
    }

    pub fn isWithPlayer(self: *Item) bool {
        return self.isLocate(player.location.?);
    }

    fn isPassageWith(self: *Item, item: ?*Item) bool {
        if (self.isLocation() and (item orelse return false).isLocation()) {
            return getPassage(self, item) != null;
        } else {
            return false;
        }
    }

    fn isPlayerLocationItem(self: *Item) bool {
        if (self.isPlayerItem()) {
            return true;
        }

        if (self.location) |item| {
            return item.isPlayerItem() or item.isWithPlayer();
        }

        return false;
    }
};

pub var items = [_]Item{
    .{ .desc = "an open field", .type = .field },
    .{ .desc = "a little cave", .type = .cave },
    .{ .desc = "a silver coin", .type = .silver },
    .{ .desc = "a gold coin", .type = .gold },
    .{ .desc = "a burly guard", .type = .guard },
    .{ .desc = "yourself", .type = .player },
    .{ .desc = "a cave entrance", .type = .entrance },
    .{ .desc = "an exit", .type = .exit },
};

fn toType(noun: ?[]const u8) ?Type {
    return std.meta.stringToEnum(Type, noun orelse return null);
}

pub fn getItem(noun: ?[]const u8) ?*Item {
    const itemType = toType(noun) orelse return null;
    for (&items) |*value| {
        if (value.type == itemType) {
            return value;
        }
    }
    return null;
}

pub fn getPassage(from: ?*Item, to: ?*Item) ?*Item {
    if (from != null and to != null) {
        for (&items) |*item| {
            if (item.location == from and item.destination == to) {
                return item;
            }
        }
    }
    return null;
}

pub fn getVisible(intention: []const u8, noun: ?[]const u8) ?*Item {
    const oitem = getItem(noun);
    if (oitem == null) {
        print("I don't understand {s}.\n", .{intention});
        return null;
    }
    const item = oitem.?;
    if (item.isPlayer() or item.isPlayerIn() or item.isWithPlayer() or
        item.isPassageWith(player.location) or item.isPlayerLocationItem())
    {
        return item;
    }

    print("You don't see any {s} here.\n", .{noun.?});
    return null;
}

pub fn listAtLocation(location: *Item) usize {
    var count: usize = 0;
    for (&items) |*item| {
        if (!item.isPlayer() and item.isLocate(location)) {
            if (count == 0) {
                print("You see:\n", .{});
            }
            print("{s}\n", .{item.desc});
            count += 1;
        }
    }
    return count;
}

pub var player: *Item = &items[5];

pub fn init() void {
    items[2].location = &items[0];
    items[3].location = &items[1];
    items[4].location = &items[0];
    items[5].location = &items[0];

    items[6].location = &items[0];
    items[6].destination = &items[1];

    items[7].location = &items[1];
    items[7].destination = &items[0];
}
```

## inventory

```zig
const world = @import("world.zig");
const system = @import("system.zig");
const print = @import("std").debug.print;

pub fn executeGet(noun: ?[]const u8) void {
    const intention = "what you want to get";
    const item = world.getVisible(intention, noun) orelse return;
    if (item.isPlayer()) {
        print("You should not be doing that to yourself.\n", .{});
    } else if (item.isPlayerItem()) {
        print("You already have {s}.\n", .{item.desc});
    } else if (item.isNpcItem()) {
        print("You should ask {s} nicely.\n", .{item.location.?.desc});
    } else {
        system.moveItem(item, world.player);
    }
}

pub fn executeDrop(noun: ?[]const u8) void {
    const possession = system.getPossession(world.player, "drop", noun);
    system.moveItem(possession, world.player.location);
}
pub fn executeAsk(noun: ?[]const u8) void {
    const possession = system.getPossession(actorHere(), "ask", noun);
    system.moveItem(possession, world.player);
}
pub fn executeGive(noun: ?[]const u8) void {
    const possession = system.getPossession(world.player, "give", noun);
    system.moveItem(possession, actorHere());
}

pub fn executeInventory() void {
    if (world.listAtLocation(world.player) == 0) {
        print("You are empty-handed.\n", .{});
    }
}

fn actorHere() ?*world.Item {
    const location = world.player.location;
    for (&world.items) |*item| {
        if (item.location == location and item.type == .guard) {
            return item;
        }
    }
    return null;
}
```

## system.zig

```zig
const world = @import("world.zig");
const print = @import("std").debug.print;

pub fn moveItem(obj: ?*world.Item, to: ?*world.Item) void {
    const from = obj orelse return;

    if (to == null) {
        return print("There is nobody here to give that to.\n", .{});
    }

    if (from.isLocation()) {
        return print("That is way too heavy.\n", .{});
    }

    describeMove(from, to.?);
    from.location = to;
}

fn describeMove(from: *world.Item, to: *world.Item) void {
    if (to == world.player.location) {
        print("You drop {s}.\n", .{from.desc});
    } else if (to != world.player) {
        if (to.type == .guard) {
            print("You give {s} to {s}.\n", .{ from.desc, to.desc });
        } else {
            print("You put {s} in {s}.\n", .{ from.desc, to.desc });
        }
    } else if (from.isWithPlayer()) {
        print("You pick up {s}.\n", .{from.desc});
    } else {
        print("You get {s} from {s}.\n", .{ from.desc, from.location.?.desc });
    }
}

pub fn getPossession(from: ?*world.Item, verb: []const u8, noun: ?[]const u8) ?*world.Item {
    if (from == null) {
        print("I don't understand who you want to {s}.\n", .{verb});
        return null;
    }

    const item = world.getItem(noun) orelse {
        print("I don't understand what you want to {s}.\n", .{verb});
        return null;
    };

    if (item == from) {
        print("You should not be doing that to {s}.\n", .{item.desc});
        return null;
    } else if (item.location != from) {
        if (from == world.player) {
            print("You are not holding any {s}.\n", .{noun.?});
        } else {
            print("There appears to be no {s} you can get from {s}.\n", .{ noun.?, from.?.desc });
        }
        return null;
    }
    return item;
}
```

## 总结

实现了游戏中的通道，角色可以通过通道到达另一个地点。

## 附录
