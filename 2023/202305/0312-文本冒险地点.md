# 0312-文本冒险地点

## 环境

- Time 2023-07-14
- Zig 0.11.0-dev

## 前言

### 说明

参考：

- <https://helderman.github.io/htpataic/>

### 目标

定义了两个不同的地点，有不同的描述，可以根据指令到不同的地点。

## main.zig

```zig
const std = @import("std");
const exec = @import("parsexec.zig");
const location = @import("location.zig");
const print = std.debug.print;

fn getInput(reader: anytype, buffer: []u8) !?[]const u8 {
    if (try reader.readUntilDelimiterOrEof(buffer, '\n')) |input| {
        if (@import("builtin").os.tag == .windows) {
            return std.mem.trimRight(u8, input, "\r");
        }
        return input;
    }
    return null;
}

pub fn main() !void {
    print("Welcome to Little Cave Adventure.\n", .{});
    const reader = std.io.getStdIn().reader();
    var buffer: [100]u8 = undefined;

    while (true) {
        print("--> ", .{});
        var input = try getInput(reader, buffer[0..]) orelse continue;
        if (std.mem.eql(u8, input, "quit")) {
            break;
        }
        exec.parseAndExecute(input);
    }

    print("\nBye!\n", .{});
}
```

## parsexec.zig

```zig
const std = @import("std");
const location = @import("location.zig");
const print = std.debug.print;

pub fn parseAndExecute(input: []const u8) void {
    var iterator = std.mem.split(u8, input, " ");
    const verb = iterator.next() orelse return;
    const noun = iterator.next();

    if (std.mem.eql(u8, verb, "look")) {
        location.executeLook(noun);
    } else if (std.mem.eql(u8, verb, "go")) {
        location.executeGo(noun);
    } else {
        print("I don't know how to {s}.\n", .{verb});
    }
}
```

## location

```zig
const std = @import("std");
const print = std.debug.print;

pub const Location = struct { desc: []const u8, tag: []const u8 };

const LOCATIONS = [_]Location{
    .{ .desc = "an open field", .tag = "field"[0..] },
    .{ .desc = "a little cave", .tag = "cave" },
};

pub var position: usize = 0;

pub fn executeLook(input: ?[]const u8) void {
    if (input) |noun| {
        if (std.mem.eql(u8, noun, "around")) {
            print("You are in {s}.\n", .{LOCATIONS[position].desc});
            return;
        }
    }
    print("I don't understand what you want to see.\n", .{});
}
pub fn executeGo(input: ?[]const u8) void {
    const noun = input orelse "";
    for (LOCATIONS, 0..) |item, index| {
        if (std.mem.eql(u8, noun, item.tag)) {
            if (index == position) {
                print("You can't get much closer than this.\n", .{});
            } else {
                print("OK.\n", .{});
                position = index;
                executeLook("around");
            }
            return;
        }
    }
    print("I don't understand where you want to go.\n", .{});
}
```

## 总结

实现了游戏的地点定义，可以根据不同的指令到不同的地方，查看不同地点的描述。

## 附录
