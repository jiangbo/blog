# 0428-Box-从文件读取场景

## 环境

- Time 2024-03-10
- Zig 0.12.0-dev.3152+90c1a2c41
- WSL-Ubuntu 22.04.3 LTS
- raylib 5.0

## 前言

### 说明

参考资料：

1. 《游戏开发：世嘉新人培训教材》
2. 图片素材：<https://www.ituring.com.cn/book/1742>

### 目标

之前的场景都是在程序中写死的，增加从文件中读取场景的功能。

## 场景结构

```zig
pub const Stage = struct {
    width: usize = 0,
    height: usize = 0,
    data: []MapItem = undefined,
    allocator: std.mem.Allocator = undefined,
}
```

需要从文件中解析对应的宽度和高度，需要将其转为地图数据，保存了内存分配器。

## 初始化函数

```zig
    pub fn init(allocator: std.mem.Allocator, level: usize) ?Stage {
        texture = ray.LoadTexture("images/box.png");
        return doInit(allocator, level) catch |e| {
            std.log.err("init stage error: {}", .{e});
            return null;
        };
    }

    fn doInit(allocator: std.mem.Allocator, level: usize) !?Stage {
        var buf: [30]u8 = undefined;
        const path = try std.fmt.bufPrint(&buf, "data/stage/{}.txt", .{level});

        std.log.info("load stage: {s}", .{path});
        const text = try file.readAll(allocator, path);
        defer allocator.free(text);
        std.log.info("{s} text: \n{s}", .{ path, text });
        return parse(allocator, text);
    }
```

实现了从文件中读取数据的功能，并将文件内容给了解析函数。

## 解析函数

```zig
    fn parse(allocator: std.mem.Allocator, text: []const u8) !?Stage {
        var stage = parseText(allocator, text) orelse return null;

        var index: usize = 0;
        stage.data = try allocator.alloc(MapItem, stage.width * stage.height);
        for (text) |char| {
            if (char == '\r' or char == '\n') continue;
            stage.data[index] = MapItem.fromU8(char);
            index += 1;
        }
        return stage;
    }

    fn parseText(allocator: std.mem.Allocator, text: []const u8) ?Stage {
        var stage = Stage{ .allocator = allocator };

        var width: usize = 0;
        for (text) |char| {
            if (char == '\r') continue;
            if (char != '\n') {
                width += 1;
                continue;
            }

            if (stage.height != 0 and stage.width != width) {
                std.log.err("stage width error, {} vs {}", .{ stage.width, width });
                return null;
            }
            stage.width = width;
            width = 0;
            stage.height += 1;
        }
        return stage;
    }
```

将文件中的内容解析为地图数据，得到了场景的宽度和高度，区分了不同平台换行符的区别。

## 其他函数

```zig
    pub fn hasBlock(self: Stage) bool {
        for (self.data) |value| {
            if (value == MapItem.BLOCK) {
                return true;
            }
        } else return false;
    }

    pub fn playerIndex(self: Stage) usize {
        // 角色当前位置
        return for (self.data, 0..) |value, index| {
            if (value == .MAN or value == .MAN_ON_GOAL) break index;
        } else 0;
    }

    pub fn deinit(self: Stage) void {
        ray.UnloadTexture(texture);
        self.allocator.free(self.data);
    }
```

## file.zig

```zig
const std = @import("std");

pub fn readAll(allocator: std.mem.Allocator, name: []const u8) ![]const u8 {
    const file = try std.fs.cwd().openFile(name, .{});
    defer file.close();

    return try file.readToEndAlloc(allocator, std.math.maxInt(usize));
}
```

## 效果

![box4][1]

## 总结

实现了从文件中加载场景的功能，可以根据指定的关卡数来加载场景。

[1]: images/box4.png

## 附录

### 源码

<https://github.com/jiangbo/game/tree/main/zig/box/box4>
