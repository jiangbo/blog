# 0433-Box-分离地图和场景

## 环境

- Time 2024-03-12
- Zig 0.12.0-dev.3152+90c1a2c41
- WSL-Ubuntu 22.04.3 LTS
- raylib 5.0

## 前言

### 说明

参考资料：

1. 《游戏开发：世嘉新人培训教材》
2. 图片素材：<https://www.ituring.com.cn/book/1742>

### 目标

分离地图 map 模块，新建场景 stage 模块，并进行一点重构，替换新的素材。

## stage.zig

```zig
const std = @import("std");
const map = @import("map.zig");
const file = @import("file.zig");
const ray = @import("raylib.zig");
const res = @import("res.zig");

const Allocator = std.mem.Allocator;

pub fn init(allocator: Allocator, level: usize) ?Stage {
    return doInit(allocator, level) catch |e| {
        std.log.err("init stage error: {}", .{e});
        return null;
    };
}

fn doInit(allocator: Allocator, level: usize) !?Stage {
    var buf: [30]u8 = undefined;
    const path = try std.fmt.bufPrint(&buf, "data/stage/{}.txt", .{level});

    std.log.info("load stage: {s}", .{path});
    const text = try file.readAll(allocator, path);
    defer allocator.free(text);
    std.log.info("{s} text: \n{s}", .{ path, text });
    return parse(allocator, text);
}

fn parse(allocator: Allocator, text: []const u8) !?Stage {
    var stage = parseText(text) orelse return null;

    var index: usize = 0;
    stage.data = try allocator.alloc(map.MapItem, stage.width * stage.height);
    for (text) |char| {
        if (char == '\r' or char == '\n') continue;
        stage.data[index] = map.MapItem.fromU8(char);
        index += 1;
    }
    stage.allocator = allocator;
    return stage;
}

fn parseText(text: []const u8) ?Stage {
    var stage = Stage{};

    var width: usize = 0;
    for (text) |char| {
        if (char == '\r') continue;
        if (char != '\n') {
            width += 1;
            continue;
        }

        if (stage.height != 0 and stage.width != width) {
            std.log.err("stage width error, {} vs {}", .{ stage.width, width });
            return null;
        }
        stage.width = width;
        width = 0;
        stage.height += 1;
    }
    return stage;
}

pub const Stage = struct {
    width: usize = 0,
    height: usize = 0,
    data: []map.MapItem = undefined,
    allocator: std.mem.Allocator = undefined,

    pub fn hasCleared(self: Stage) bool {
        for (self.data) |value| {
            if (value == map.MapItem.BLOCK) {
                return false;
            }
        } else return true;
    }

    pub fn playerIndex(self: Stage) usize {
        // 角色当前位置
        return for (self.data, 0..) |value, index| {
            if (value == .MAN or value == .MAN_ON_GOAL) break index;
        } else 0;
    }

    pub fn update(self: *Stage) void {
        // 操作角色移动的距离
        const delta: isize = switch (ray.GetKeyPressed()) {
            ray.KEY_W, ray.KEY_UP => -@as(isize, @intCast(self.width)),
            ray.KEY_S, ray.KEY_DOWN => @as(isize, @intCast(self.width)),
            ray.KEY_D, ray.KEY_RIGHT => 1,
            ray.KEY_A, ray.KEY_LEFT => -1,
            else => return,
        };

        const currentIndex = self.playerIndex();
        const index = @as(isize, @intCast(currentIndex)) + delta;
        if (index < 0 or index > self.width * self.height) return .none;

        // 角色欲前往的目的地
        const destIndex = @as(usize, @intCast(index));
        self.updatePlayer(currentIndex, destIndex, delta);
    }

    fn updatePlayer(stage: *Stage, current: usize, dest: usize, delta: isize) void {
        var state = stage.data;
        if (state[dest] == .SPACE or state[dest] == .GOAL) {
            // 如果是空地或者目标地，则可以移动
            state[dest] = if (state[dest] == .GOAL) .MAN_ON_GOAL else .MAN;
            state[current] = if (state[current] == .MAN_ON_GOAL) .GOAL else .SPACE;
        } else if (state[dest] == .BLOCK or state[dest] == .BLOCK_ON_GOAL) {
            //  如果是箱子或者目的地上的箱子，需要考虑该方向上的第二个位置
            const index = @as(isize, @intCast(dest)) + delta;
            if (index < 0 or index > stage.width * stage.height) return;

            const next = @as(usize, @intCast(index));
            if (state[next] == .SPACE or state[next] == .GOAL) {
                state[next] = if (state[next] == .GOAL) .BLOCK_ON_GOAL else .BLOCK;
                state[dest] = if (state[dest] == .BLOCK_ON_GOAL) .MAN_ON_GOAL else .MAN;
                state[current] = if (state[current] == .MAN_ON_GOAL) .GOAL else .SPACE;
            }
        }
    }

    pub fn draw(self: Stage) void {
        for (0..self.height) |y| {
            for (0..self.width) |x| {
                const item = self.data[y * self.width + x];
                if (item != map.MapItem.WALL) {
                    drawCell(x, y, if (item.hasGoal()) .GOAL else .SPACE);
                }
                if (item != .SPACE) drawCell(x, y, item);
            }
        }
    }

    fn drawCell(x: usize, y: usize, item: map.MapItem) void {
        var source = ray.Rectangle{ .width = 32, .height = 32 };
        source.x = item.toImageIndex() * source.width;
        const dest = ray.Rectangle{
            .x = @as(f32, @floatFromInt(x)) * source.width,
            .y = @as(f32, @floatFromInt(y)) * source.height,
            .width = source.width,
            .height = source.height,
        };

        ray.DrawTexturePro(res.box, source, dest, .{}, 0, ray.WHITE);
    }

    pub fn deinit(self: Stage) void {
        self.allocator.free(self.data);
    }
};
```

将 map.zig 中的 Stage 单独分离了出来，并且将 draw 相关方法移动到了 Stage 中。

## map.zig

```zig
const std = @import("std");

// 定义地图的类型
pub const MapItem = enum(u8) {
    SPACE = ' ',
    WALL = '#',
    GOAL = '.',
    BLOCK = 'o',
    BLOCK_ON_GOAL = 'O',
    MAN = 'p',
    MAN_ON_GOAL = 'P',

    pub fn fromU8(value: u8) MapItem {
        return @enumFromInt(value);
    }

    pub fn toU8(self: MapItem) u8 {
        return @intFromEnum(self);
    }

    pub fn hasGoal(self: MapItem) bool {
        return self == .BLOCK_ON_GOAL or self == .MAN_ON_GOAL;
    }

    pub fn toImageIndex(self: MapItem) f32 {
        return switch (self) {
            .SPACE => 4,
            .WALL => 1,
            .BLOCK => 2,
            .GOAL => 3,
            .BLOCK_ON_GOAL => 2,
            .MAN => 0,
            .MAN_ON_GOAL => 0,
        };
    }
};
```

map.zig 中简略了很多，将 mapItemToIndex 方法移动了枚举中，重命名为 toImageIndex。

## main.zig

```zig
const std = @import("std");
const ray = @import("raylib.zig");
const map = @import("map.zig");
const stg = @import("stage.zig");
const res = @import("res.zig");

const screenWidth = 320;
const screenHeight = 240;

pub fn main() void {
    ray.InitWindow(screenWidth, screenHeight, "推箱子");
    defer ray.CloseWindow();
    ray.SetTargetFPS(60);
    ray.SetExitKey(ray.KEY_NULL);

    var gpa = std.heap.GeneralPurposeAllocator(.{}){};
    defer _ = gpa.deinit();
    const allocator = gpa.allocator();

    res.init();
    defer res.deinit();

    var title = true;
    var ostage: ?stg.Stage = null;
    defer if (ostage) |stage| stage.deinit();

    while (!ray.WindowShouldClose()) {
        if (title) {
            if (ray.IsKeyPressed(ray.KEY_SPACE)) title = false;
        } else {
            // 根据输入更新游戏地图
            if (ostage == null) ostage = stg.init(allocator, 1);

            if (ostage) |stage| {
                update(@constCast(&stage));
                // 检查游戏胜利条件
                if (stage.hasCleared()) {
                    title = true;
                    stage.deinit();
                    ostage = null;
                }
            }
        }

        // 画出游戏地图
        ray.BeginDrawing();
        defer ray.EndDrawing();
        ray.ClearBackground(ray.WHITE);

        if (title)
            ray.DrawTexture(res.title, 0, 0, ray.WHITE)
        else if (ostage) |stage| stage.draw();
        ray.DrawFPS(screenWidth - 80, 10);
    }
}
```

主要是修改了 map 和 stage 的引用，其它地方无修改，相关的 update 方法未列出。

## 效果

替换了新的素材。

![box9][1]

## 总结

分离地图 map 模块，新建场景 stage 模块，并进行一点重构，替换新的素材。

[1]: images/box9.png

## 附录
