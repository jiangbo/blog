# 0436-Box-重构地图模块

## 环境

- Time 2024-03-13
- Zig 0.12.0-dev.3152+90c1a2c41
- WSL-Ubuntu 22.04.3 LTS
- raylib 5.0

## 前言

### 说明

参考资料：

1. 《游戏开发：世嘉新人培训教材》
2. 图片素材：<https://www.ituring.com.cn/book/1742>

### 目标

场景 stage 中的代码越来越多，有一部分属于 map 模块的代码，将其分离出来。

## file.zig

```zig
const std = @import("std");
const ray = @import("raylib.zig");

const maxPathLength = 30;

pub fn readStageText(allocator: std.mem.Allocator, level: usize) ![]const u8 {
    var buf: [maxPathLength]u8 = undefined;
    const path = try std.fmt.bufPrint(&buf, "data/stage/{}.txt", .{level});

    std.log.info("load stage: {s}", .{path});
    return try readAll(allocator, path);
}

fn readAll(allocator: std.mem.Allocator, name: []const u8) ![]const u8 {
    const file = try std.fs.cwd().openFile(name, .{});
    defer file.close();

    return try file.readToEndAlloc(allocator, std.math.maxInt(usize));
}

pub const Texture = struct {
    texture: ray.Texture2D,

    pub fn unload(self: Texture) void {
        ray.UnloadTexture(self.texture);
    }
};

pub fn loadTexture(name: []const u8) Texture {
    var buf: [maxPathLength]u8 = undefined;
    const path = std.fmt.bufPrintZ(&buf, "data/image/{s}", .{name}) catch |e| {
        std.log.err("load image error: {}", .{e});
        return Texture{ .texture = ray.Texture2D{} };
    };

    return Texture{ .texture = ray.LoadTexture(path) };
}
```

将文件读取的操作统一到文件 file 模块中。

## stage.zig

```zig
const std = @import("std");
const map = @import("map.zig");
const file = @import("file.zig");
const ray = @import("raylib.zig");

pub const SequenceType = enum { title, select, stage };
pub const SequenceData = union(SequenceType) {
    title: void,
    select: void,
    stage: usize,
};
const Allocator = std.mem.Allocator;

pub fn init(allocator: Allocator, level: usize, box: file.Texture) ?Stage {
    const m = map.Map.init(allocator, level) catch |e| {
        std.log.err("init stage error: {}", .{e});
        return null;
    } orelse return null;
    return Stage{ .map = m, .box = box };
}

pub const Stage = struct {
    map: map.Map,
    box: file.Texture,

    pub fn update(self: *Stage) ?SequenceData {
        // 操作角色移动的距离
        const delta: isize = switch (ray.GetKeyPressed()) {
            ray.KEY_W, ray.KEY_UP => -@as(isize, @intCast(self.map.width)),
            ray.KEY_S, ray.KEY_DOWN => @as(isize, @intCast(self.map.width)),
            ray.KEY_D, ray.KEY_RIGHT => 1,
            ray.KEY_A, ray.KEY_LEFT => -1,
            else => return null,
        };

        const currentIndex = self.map.playerIndex();
        const index = @as(isize, @intCast(currentIndex)) + delta;
        if (index < 0 or index > self.map.size()) return null;

        // 角色欲前往的目的地
        const destIndex = @as(usize, @intCast(index));
        self.updatePlayer(currentIndex, destIndex, delta);

        return if (self.map.hasCleared()) .title else null;
    }

    fn updatePlayer(stage: *Stage, current: usize, dest: usize, delta: isize) void {
        var state = stage.map.data;
        if (state[dest] == .SPACE or state[dest] == .GOAL) {
            // 如果是空地或者目标地，则可以移动
            state[dest] = if (state[dest] == .GOAL) .MAN_ON_GOAL else .MAN;
            state[current] = if (state[current] == .MAN_ON_GOAL) .GOAL else .SPACE;
        } else if (state[dest] == .BLOCK or state[dest] == .BLOCK_ON_GOAL) {
            //  如果是箱子或者目的地上的箱子，需要考虑该方向上的第二个位置
            const index = @as(isize, @intCast(dest)) + delta;
            if (index < 0 or index > stage.map.size()) return;

            const next = @as(usize, @intCast(index));
            if (state[next] == .SPACE or state[next] == .GOAL) {
                state[next] = if (state[next] == .GOAL) .BLOCK_ON_GOAL else .BLOCK;
                state[dest] = if (state[dest] == .BLOCK_ON_GOAL) .MAN_ON_GOAL else .MAN;
                state[current] = if (state[current] == .MAN_ON_GOAL) .GOAL else .SPACE;
            }
        }
    }

    pub fn draw(self: Stage) void {
        for (0..self.map.height) |y| {
            for (0..self.map.width) |x| {
                const item = self.map.data[y * self.map.width + x];
                if (item != map.MapItem.WALL) {
                    self.drawCell(x, y, if (item.hasGoal()) .GOAL else .SPACE);
                }
                if (item != .SPACE) self.drawCell(x, y, item);
            }
        }
    }

    fn drawCell(stage: Stage, x: usize, y: usize, item: map.MapItem) void {
        var source = ray.Rectangle{ .width = 32, .height = 32 };
        source.x = item.toImageIndex() * source.width;
        const dest = ray.Rectangle{
            .x = @as(f32, @floatFromInt(x)) * source.width,
            .y = @as(f32, @floatFromInt(y)) * source.height,
            .width = source.width,
            .height = source.height,
        };

        ray.DrawTexturePro(stage.box.texture, source, dest, .{}, 0, ray.WHITE);
    }

    pub fn deinit(self: Stage) void {
        self.map.deinit();
    }
};
```

将一部分地图和文件的功能，从 stage 中移除掉。

## map.zig

```zig
const std = @import("std");
const file = @import("file.zig");
const Allocator = std.mem.Allocator;

// 定义地图的类型
pub const MapItem = enum(u8) {
    SPACE = ' ',
    WALL = '#',
    GOAL = '.',
    BLOCK = 'o',
    BLOCK_ON_GOAL = 'O',
    MAN = 'p',
    MAN_ON_GOAL = 'P',

    pub fn fromU8(value: u8) MapItem {
        return @enumFromInt(value);
    }

    pub fn toU8(self: MapItem) u8 {
        return @intFromEnum(self);
    }

    pub fn hasGoal(self: MapItem) bool {
        return self == .BLOCK_ON_GOAL or self == .MAN_ON_GOAL;
    }

    pub fn toImageIndex(self: MapItem) f32 {
        return switch (self) {
            .SPACE => 4,
            .WALL => 1,
            .BLOCK => 2,
            .GOAL => 3,
            .BLOCK_ON_GOAL => 2,
            .MAN => 0,
            .MAN_ON_GOAL => 0,
        };
    }
};

pub const Map = struct {
    width: usize = 0,
    height: usize = 0,
    data: []MapItem = undefined,
    allocator: std.mem.Allocator = undefined,

    pub fn init(allocator: Allocator, level: usize) !?Map {
        const text = try file.readStageText(allocator, level);
        defer allocator.free(text);

        var map = parseText(text) orelse return null;

        var index: usize = 0;
        map.data = try allocator.alloc(MapItem, map.size());
        for (text) |char| {
            if (char == '\r' or char == '\n') continue;
            map.data[index] = MapItem.fromU8(char);
            index += 1;
        }
        map.allocator = allocator;
        return map;
    }

    fn parseText(text: []const u8) ?Map {
        var map = Map{};

        var width: usize = 0;
        for (text) |char| {
            if (char == '\r') continue;
            if (char != '\n') {
                width += 1;
                continue;
            }

            if (map.height != 0 and map.width != width) {
                std.log.err("width error, {} vs {}", .{ map.width, width });
                return null;
            }
            map.width = width;
            width = 0;
            map.height += 1;
        }
        return map;
    }

    pub fn size(self: Map) usize {
        return self.width * self.height;
    }

    pub fn hasCleared(self: Map) bool {
        for (self.data) |value| {
            if (value == MapItem.BLOCK) {
                return false;
            }
        } else return true;
    }

    pub fn playerIndex(self: Map) usize {
        return for (self.data, 0..) |value, index| {
            if (value == .MAN or value == .MAN_ON_GOAL) break index;
        } else 0;
    }

    pub fn deinit(self: Map) void {
        self.allocator.free(self.data);
    }
};
```

## 效果

和之前一致，重构代码，未改变逻辑。

![box10][1]

## 总结

场景 stage 中的代码越来越多，有一部分属于 map 模块的代码，将其分离出来。

[1]: images/box10.png

## 附录
