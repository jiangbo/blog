# 0434-Box-重构标题显示

## 环境

- Time 2024-03-12
- Zig 0.12.0-dev.3152+90c1a2c41
- WSL-Ubuntu 22.04.3 LTS
- raylib 5.0

## 前言

### 说明

参考资料：

1. 《游戏开发：世嘉新人培训教材》
2. 图片素材：<https://www.ituring.com.cn/book/1742>

### 目标

随着游戏中的场景增加，越来越复杂，重构场景切换的逻辑，其功能保持不变。

## state.zig

```zig
const std = @import("std");
const ray = @import("raylib.zig");
const stage = @import("stage.zig");
const SequenceType = stage.SequenceType;

pub const State = struct {
    current: Sequence,
    box: ray.Texture2D,
    allocator: std.mem.Allocator,

    pub fn init(allocator: std.mem.Allocator) State {
        return State{
            .current = Sequence{ .title = Title.init() },
            .box = ray.LoadTexture("data/image/box.dds"),
            .allocator = allocator,
        };
    }

    pub fn update(self: *State) void {
        const sequenceType = self.current.update() orelse return;

        const old = self.current;
        self.current = switch (sequenceType) {
            .title => Sequence{ .title = Title.init() },
            .stage => .{ .stage = stage.init(self.allocator, 1, self.box) orelse return },
        };
        old.deinit();
    }

    pub fn draw(self: State) void {
        self.current.draw();
    }

    pub fn deinit(self: State) void {
        self.current.deinit();
        ray.UnloadTexture(self.box);
    }
};

pub const Sequence = union(SequenceType) {
    title: Title,
    stage: stage.Stage,

    pub fn update(self: *Sequence) ?SequenceType {
        return switch (self.*) {
            inline else => |*case| case.update(),
        };
    }

    pub fn draw(self: Sequence) void {
        ray.BeginDrawing();
        defer ray.EndDrawing();
        defer ray.DrawFPS(235, 10);
        ray.ClearBackground(ray.WHITE);

        switch (self) {
            inline else => |sequence| sequence.draw(),
        }
    }

    pub fn deinit(self: Sequence) void {
        switch (self) {
            inline else => |case| case.deinit(),
        }
    }
};

const Title = struct {
    texture: ray.Texture2D,

    pub fn init() Title {
        return Title{ .texture = ray.LoadTexture("data/image/title.dds") };
    }

    pub fn update(_: Title) ?SequenceType {
        return if (ray.IsKeyPressed(ray.KEY_SPACE)) .stage else null;
    }

    pub fn draw(self: Title) void {
        ray.DrawTexture(self.texture, 0, 0, ray.WHITE);
    }

    pub fn deinit(self: Title) void {
        ray.UnloadTexture(self.texture);
    }
};
```

新增状态管理模块 state.zig，现在有两个状态，标题 title 和游戏场景 stage。

## main.zig

```zig
const std = @import("std");
const ray = @import("raylib.zig");
const map = @import("map.zig");
const state = @import("state.zig");

const screenWidth = 320;
const screenHeight = 240;

pub fn main() void {
    ray.InitWindow(screenWidth, screenHeight, "推箱子");
    defer ray.CloseWindow();
    ray.SetTargetFPS(60);
    ray.SetExitKey(ray.KEY_NULL);

    var gpa = std.heap.GeneralPurposeAllocator(.{}){};
    defer _ = gpa.deinit();

    var currentState = state.State.init(gpa.allocator());
    defer currentState.deinit();

    while (!ray.WindowShouldClose()) {
        currentState.update();
        currentState.draw();
    }
}
```

main.zig 简略了很多，把 update 相关的方法移动到了 stage.zig 模块中，删除 res.zig 模块。

## 效果

只是重构了代码，显示效果和之前一致。

![box9][1]

## 总结

随着游戏中的场景增加，越来越复杂，重构场景切换的逻辑，其功能保持不变。

[1]: images/box9.png

## 附录

### 源码

<https://github.com/jiangbo/game/tree/main/zig/box/box5>
