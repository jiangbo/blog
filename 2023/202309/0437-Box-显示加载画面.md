# 0437-Box-显示加载画面

## 环境

- Time 2024-03-13
- Zig 0.12.0-dev.3152+90c1a2c41
- WSL-Ubuntu 22.04.3 LTS
- raylib 5.0

## 前言

### 说明

参考资料：

1. 《游戏开发：世嘉新人培训教材》
2. 图片素材：<https://www.ituring.com.cn/book/1742>

### 目标

进入正式的游戏场景前，显示一个加载动画的页面。

## stage.zig

```zig
const std = @import("std");
const map = @import("map.zig");
const file = @import("file.zig");
const ray = @import("raylib.zig");
const play = @import("play.zig");

pub const SequenceType = enum { title, select, stage };
pub const SequenceData = union(SequenceType) {
    title: void,
    select: void,
    stage: usize,
};
const Allocator = std.mem.Allocator;
const PlayingType = play.PlayingType;

pub const Stage = struct {
    map: map.Map,
    box: file.Texture,
    current: Sequence,

    pub fn update(self: *Stage) ?SequenceData {
        const sequence = self.current.update() orelse return null;

        const old = self.current;
        defer old.deinit();

        self.current = switch (sequence) {
            .loading => .{ .loading = Loading.init() },
            .play => .{ .play = play.init(self.map, self.box) },
            .title => return .title,
        };

        return null;
    }

    pub fn draw(self: Stage) void {
        self.current.draw();
    }

    pub fn deinit(self: Stage) void {
        self.map.deinit();
    }
};

const Sequence = union(PlayingType) {
    loading: Loading,
    play: play.Play,
    title: void,

    fn update(self: *Sequence) ?PlayingType {
        return switch (self.*) {
            .title => unreachable,
            inline else => |*case| case.update(),
        };
    }

    fn draw(self: Sequence) void {
        switch (self) {
            .title => unreachable,
            inline else => |sequence| sequence.draw(),
        }
    }

    fn deinit(self: Sequence) void {
        switch (self) {
            .loading => |sequence| sequence.deinit(),
            else => {},
        }
    }
};

const Loading = struct {
    texture: file.Texture,
    time: f64,

    fn init() Loading {
        return Loading{
            .texture = file.loadTexture("loading.dds"),
            .time = ray.GetTime(),
        };
    }

    fn update(self: Loading) ?PlayingType {
        return if ((ray.GetTime() - self.time) > 1) return .play else null;
    }

    fn draw(self: Loading) void {
        ray.DrawTexture(self.texture.texture, 0, 0, ray.WHITE);
    }

    fn deinit(self: Loading) void {
        self.texture.unload();
    }
};

pub fn init(allocator: Allocator, level: usize, box: file.Texture) ?Stage {
    const m = map.Map.init(allocator, level) catch |err| {
        std.log.err("init stage error: {}", .{err});
        return null;
    } orelse return null;
    return Stage{ .map = m, .box = box, .current = .{ .loading = Loading.init() } };
}
```

仿照之前 state 模块的方式，定义了一个 Loading 的状态。

## play.zig

```zig
const std = @import("std");
const map = @import("map.zig");
const file = @import("file.zig");
const ray = @import("raylib.zig");

pub const PlayingType = enum { loading, play, title };

pub fn init(m: map.Map, box: file.Texture) Play {
    return .{ .map = m, .box = box };
}

pub const Play = struct {
    map: map.Map,
    box: file.Texture,

    pub fn update(self: *Play) ?PlayingType {

        // 操作角色移动的距离
        const delta: isize = switch (ray.GetKeyPressed()) {
            ray.KEY_W, ray.KEY_UP => -@as(isize, @intCast(self.map.width)),
            ray.KEY_S, ray.KEY_DOWN => @as(isize, @intCast(self.map.width)),
            ray.KEY_D, ray.KEY_RIGHT => 1,
            ray.KEY_A, ray.KEY_LEFT => -1,
            else => return null,
        };

        const currentIndex = self.map.playerIndex();
        const index = @as(isize, @intCast(currentIndex)) + delta;
        if (index < 0 or index > self.map.size()) return null;

        // 角色欲前往的目的地
        const destIndex = @as(usize, @intCast(index));
        self.updatePlayer(currentIndex, destIndex, delta);

        return if (self.map.hasCleared()) .title else null;
    }

    fn updatePlayer(play: *Play, current: usize, dest: usize, delta: isize) void {
        var state = play.map.data;
        if (state[dest] == .SPACE or state[dest] == .GOAL) {
            // 如果是空地或者目标地，则可以移动
            state[dest] = if (state[dest] == .GOAL) .MAN_ON_GOAL else .MAN;
            state[current] = if (state[current] == .MAN_ON_GOAL) .GOAL else .SPACE;
        } else if (state[dest] == .BLOCK or state[dest] == .BLOCK_ON_GOAL) {
            //  如果是箱子或者目的地上的箱子，需要考虑该方向上的第二个位置
            const index = @as(isize, @intCast(dest)) + delta;
            if (index < 0 or index > play.map.size()) return;

            const next = @as(usize, @intCast(index));
            if (state[next] == .SPACE or state[next] == .GOAL) {
                state[next] = if (state[next] == .GOAL) .BLOCK_ON_GOAL else .BLOCK;
                state[dest] = if (state[dest] == .BLOCK_ON_GOAL) .MAN_ON_GOAL else .MAN;
                state[current] = if (state[current] == .MAN_ON_GOAL) .GOAL else .SPACE;
            }
        }
    }

    pub fn draw(self: Play) void {
        for (0..self.map.height) |y| {
            for (0..self.map.width) |x| {
                const item = self.map.data[y * self.map.width + x];
                if (item != map.MapItem.WALL) {
                    self.drawCell(x, y, if (item.hasGoal()) .GOAL else .SPACE);
                }
                if (item != .SPACE) self.drawCell(x, y, item);
            }
        }
    }

    fn drawCell(play: Play, x: usize, y: usize, item: map.MapItem) void {
        var source = ray.Rectangle{ .width = 32, .height = 32 };
        source.x = item.toImageIndex() * source.width;
        const dest = ray.Rectangle{
            .x = @as(f32, @floatFromInt(x)) * source.width,
            .y = @as(f32, @floatFromInt(y)) * source.height,
            .width = source.width,
            .height = source.height,
        };

        ray.DrawTexturePro(play.box.texture, source, dest, .{}, 0, ray.WHITE);
    }
};
```

将游戏具体的玩法移动到了 play 模块中。

## 效果

![box11][1]

## 总结

进入正式的游戏场景前，显示一个加载动画的页面。

[1]: images/box11.png

## 附录
