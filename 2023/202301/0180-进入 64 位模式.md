# 0180-进入 64 位模式

## 环境

- Time 2022-11-12
- WSL-Ubuntu 22.04
- QEMU 6.2.0
- NASM 2.15.05

## 前言

### 说明

参考：<https://os.phil-opp.com/entering-longmode/>

### 目标

从保护模式切换到长模式。

## 定位代码段

因为当前还是执行的 32 的指令，所以需要执行跳转，重新选择 GDT，这里给代码段加了一个标记。

```asm
gdt64:
    dq 0 ; 和之前一样，第一段为 0
.code: equ $ - gdt64 ; 需要跳转到代码段
    ; 43 表示代码段，44 同样为 1，47 表示可用，53 表示 64 位
    dq (1<<43) | (1<<44) | (1<<47) | (1<<53) ; 代码段
```

## 跳转指令

和之前一样，如果模式切换了，需要执行远跳指令，刷新流水线，重新加载指令。

```asm
...
extern long_mode_start
...

; 远跳指令，清空流水线，执行 64 位指令
jmp gdt64.code:long_mode_start
```

同时增加了一个 extern 64 位的入口。

## 64 位汇编

```asm
global long_mode_start

section .text
bits 64
long_mode_start:

    ; 清空所有的段寄存器，因为当前为平坦模式，不需要段选择器
    mov ax, 0
    mov ss, ax
    mov ds, ax
    mov es, ax
    mov fs, ax
    mov gs, ax

    ; print `OKAY` to screen
    mov rax, 0x2f592f412f4b2f4f
    mov qword [0xb8000], rax
    hlt
```

## 修改编译和链接

```bash
#! /usr/bin/bash

nasm -f elf32 -g boot.asm
nasm -f elf32 -g long_mode.asm
ld -T linker.ld -m elf_i386 boot.o long_mode.o -o kernel.elf
qemu-system-x86_64 -kernel kernel.elf -display curses -s -S
```

## 效果

![切换到长模式][1]

## 总结

从之前的保护模式，经过一系列的操作，进入了长模式，即 64 位模式。

[1]:images/long-mode.png

## 附录

### long_mode.asm

```asm
global long_mode_start

section .text
bits 64
long_mode_start:

    ; 清空所有的段寄存器，因为当前为平坦模式，不需要段选择器
    mov ax, 0
    mov ss, ax
    mov ds, ax
    mov es, ax
    mov fs, ax
    mov gs, ax

    ; 打印 `OKAY` 到屏幕
    mov rax, 0x2f592f412f4b2f4f
    mov qword [0xb8000], rax
    hlt
```

### boot.asm

```asm
section .multiboot_header
header_start:
    dd 0x1BADB002  ; 魔法数字，固定值
    dd 0
    dd -0x1BADB002 ; 定义的这三个数字相加需要等于0
header_end:

global start
extern long_mode_start
section .text
bits 32
start:

    ; 栈是否高地址往低地址增长
    mov esp, stack_top

    call check_cpuid
    call check_long_mode

    call set_up_page_tables
    call enable_paging

    lgdt [gdt64.pointer]

    ; 远跳指令，清空流水线，执行 64 位指令
    jmp gdt64.code:long_mode_start
    ; print `OK` to screen
    mov dword [0xb8000], 0x2f4b2f4f
    hlt

check_cpuid:
    ; 检查 CPUID 是否支持可以通过翻转 ID 位，即第 21 位。
    ; 如果在 FLAGS 标志寄存器中，我们能够翻转它，CPUID 就是可用的。

    ; 通过栈拷贝 FLAGS 寄存器的值到 EAX 寄存器
    pushfd
    pop eax

    ; 将 EAX 的值拷贝到 ECX，后面要用
    mov ecx, eax

    ; 翻转第 21 位
    xor eax, 1 << 21

    ; 把 EAX 的值拷贝回 FLAGS 寄存器
    push eax
    popfd

    ; 拷贝 FLAGS 寄存器的值回 EAX 寄存器，检查是否翻转成功，成功翻转则支持 CPUID
    pushfd
    pop eax

    ; 通过 ECX 还原 EFLAGS 中的值
    push ecx
    popfd

    ; 比较，如果两个一样，则翻转不成功，不支持CPUID；如果翻转成功，则支持CPUID
    cmp eax, ecx
    je .no_cpuid
    ret
.no_cpuid:
    mov al, "1"
    jmp error

check_long_mode:

    ; 检查是否有扩展的处理器信息可用
    mov eax, 0x80000000    ; CPUID 的隐式参数
    cpuid                  ; 获取最高支持的参数
    cmp eax, 0x80000001    ; 如果支持长模式，至少是 0x80000001
    jb .no_long_mode       ; 如果小于，则不支持长模式

    ; 使用扩展信息验证是否支持长模式
    mov eax, 0x80000001    ; 扩展处理器参数信息
    cpuid                  ; 将各种特征标记位返回到 ECX 和 EDX
    test edx, 1 << 29      ; 第 29 位是 long mode 长模式标记位，检查是否支持
    jz .no_long_mode       ; 如果为 0，表示不支持长模式
    ret
.no_long_mode:
    mov al, "2"
    jmp error

set_up_page_tables:

    ; 将 P4 的第一个地址设置成 P3 的起始地址
    mov eax, p3_table
    or eax, 0b11 ; 二进制数，表示当前页存在，并且可写
    mov [p4_table], eax

    ; 将 P3 的第一个地址设置成 P2 的起始地址
    mov eax, p2_table
    or eax, 0b11 ; 二进制数，表示当前页存在，并且可写
    mov [p3_table], eax

    ; 将 P2 设置成 2M 的巨型页
    mov ecx, 0         ; 循环的计数器
.map_p2_table:
    ; 使用 EAX 初始化 P2 的每一项，并且映射到物理地址最低的 1G 空间
    mov eax, 0x200000  ; 2MiB
    mul ecx            ; 每一项对应的物理地址 EAX * counter
    or eax, 0b10000011 ; 存在，可写，巨型页
    mov [p2_table + ecx * 8], eax ; 将地址记录到 P2 的每一项

    inc ecx            ; 计数器加 1
    cmp ecx, 512       ; 是否存满，最大 512 项
    jne .map_p2_table  ; 不相等继续下次循环

    ret

enable_paging:
    ; 将 CR3 寄存器指向 P4 的起始地址
    mov eax, p4_table
    mov cr3, eax

    ; 在 CR4 中启用物理地址扩展(Physical Address Extension)，第五位
    mov eax, cr4
    or eax, 1 << 5
    mov cr4, eax

    ; 将 EFER MSR（model specific register）寄存器中的第八位设置成长模式
    mov ecx, 0xC0000080
    rdmsr
    or eax, 1 << 8
    wrmsr

    ; 将 CR0 的最高位分页开启位设置成 1
    mov eax, cr0
    or eax, 1 << 31
    mov cr0, eax

    ret

; 打印 `ERR: ` 和一个错误代码并停住。
; 错误代码在 al 寄存器中
error:
    mov dword [0xb8000], 0x4f524f45
    mov dword [0xb8004], 0x4f3a4f52
    mov dword [0xb8008], 0x4f204f20
    mov byte  [0xb800a], al
    hlt

section .rodata
gdt64:
    dq 0 ; 和之前一样，第一段为 0
.code: equ $ - gdt64 ; 需要跳转到代码段
    ; 43 表示代码段，44 同样为 1，47 表示可用，53 表示 64 位
    dq (1<<43) | (1<<44) | (1<<47) | (1<<53) ; 代码段

.pointer:
    dw $ - gdt64 - 1
    dq gdt64

section .bss
align 4096
p4_table:
    resb 4096
p3_table:
    resb 4096
p2_table:
    resb 4096
stack_bottom:
    resb 64
stack_top:
```
