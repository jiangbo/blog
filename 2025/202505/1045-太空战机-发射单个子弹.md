# 1045-太空战机-发射单个子弹

## 目标

角色可以发射子弹，目前先实现发射单个子弹的功能。

## 环境

- Time 2025-12-04
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1wSCFYQEyc>

## 想法

一次只能发射一个子弹，并且要等到消失后，才能发射新的子弹。

## player.zig

加入了发射子弹的逻辑。

```zig
const std = @import("std");
const zhu = @import("zhu");

const window = zhu.window;
const gfx = zhu.gfx;
const camera = zhu.camera;

const Bullet = struct {
    position: gfx.Vector, // 子弹的位置
    speed: f32 = 400, // 子弹的速度
};

const SPEED = 200; // 玩家的移动速度

var position: gfx.Vector = undefined; // 玩家的位置
var texture: gfx.Texture = undefined; // 玩家的纹理
var size: gfx.Vector = undefined; // 玩家的尺寸

var bulletTexture: gfx.Texture = undefined; // 子弹的纹理
var playerBullet: ?Bullet = null; // 子弹
var bulletSize: gfx.Vector = undefined; // 子弹的尺寸

pub fn init() void {
    texture = gfx.loadTexture("assets/image/SpaceShip.png", .init(241, 187));
    // 图片太大了，缩小到四分之一
    size = texture.size().scale(0.25);
    position = window.logicSize.sub(size).div(.init(2, 1));

    bulletTexture = gfx.loadTexture("assets/image/laser-1.png", .init(81, 126));
    bulletSize = bulletTexture.size().scale(0.25);

    std.log.info("player texture: {}, bullet texture: {}", .{ texture.view, bulletTexture.view });
}

pub fn update(delta: f32) void {
    // 玩家键盘控制
    const distance = SPEED * delta; // 根据时间调整移动距离
    if (window.isKeyDown(.A)) position.x -= distance;
    if (window.isKeyDown(.D)) position.x += distance;
    if (window.isKeyDown(.W)) position.y -= distance;
    if (window.isKeyDown(.S)) position.y += distance;

    // 发射子弹。按下了按键，并且当前没有发射子弹才进行发射
    if (window.isKeyDown(.J) and playerBullet == null) {

        // 发射的位置：和玩家的 Y 坐标一样，在玩家的 X 中间
        const pos = position.addX(size.x / 2).addX(-bulletSize.x / 2);
        playerBullet = .{ .position = pos };
    }

    // 限制玩家的移动边界
    position = position.clamp(.zero, window.logicSize.sub(size));

    // 子弹移动
    updateBullet(delta);
}

fn updateBullet(delta: f32) void {
    if (playerBullet) |*bullet| {
        // 子弹存在，才进行位置更新
        bullet.position.y -= bullet.speed * delta; // 向上移动
        // 判断子弹是否超出屏幕，不是 Y 到 0，而是完全超出
        if (bullet.position.y < -bulletSize.y) playerBullet = null;
    }
}

pub fn draw() void {

    // 先绘制子弹，再绘制玩家，让子弹在玩家的下面
    if (playerBullet) |bullet| {
        camera.drawOption(bulletTexture, bullet.position, .{
            .size = bulletSize,
        });
    }

    // 绘制玩家
    camera.drawOption(texture, position, .{ .size = size });
}
```

## 效果

![发射单个子弹][1]

[1]: images/太空战机06.gif

## 附录
