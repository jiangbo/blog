# 1019-地宫探险-游戏结束画面

## 目标

战斗的时候，检测血量，如果是玩家的血量为0，则结束游戏。

## 环境

- Time 2025-11-11
- Zig 0.15.1

## 参考

1. <https://pragprog.com/titles/hwrust/hands-on-rust/>
2. 《Rust游戏开发实战》

## 想法

显示文字的时候，实现文字的缩放。

## battle.zig

检测了是否是玩家。

```zig
pub fn attack() void {
    var view = ecs.w.view(.{WantToAttack});
    while (view.next()) |entity| {
        const target = view.get(entity, WantToAttack)[0];

        var health = ecs.w.getPtr(target, Health) orelse continue;
        health.current -|= 1;
        if (health.current == 0) {
            if (ecs.w.isIdentity(target, Player)) {
                ecs.w.addContext(TurnState.over);
            } else {
                ecs.w.destroyEntity(target);
            }
        }
    }
    ecs.w.clear(WantToAttack);
}
```

## hud.zig

实现了游戏结束画面的绘制。

```zig
const std = @import("std");
const zhu = @import("zhu");

const gfx = zhu.gfx;
const window = zhu.window;
const camera = zhu.camera;
const math = zhu.math;
const ecs = zhu.ecs;

const component = @import("component.zig");
const map = @import("map.zig");

const Player = component.Player;
const Health = component.Health;
const Name = component.Name;
const Position = component.Position;
const TurnState = component.TurnState;

var texture: gfx.Texture = undefined;
const healthForeground: math.Vector4 = .init(0.298, 0.735, 0.314, 1);
const healthBackground: math.Vector4 = .init(0.2, 0.2, 0.2, 1);

pub fn init() void {
    texture = gfx.loadTexture("assets/terminal8x8.png", .init(128, 128));
    camera.whiteTexture = texture.subTexture(.init(.init(88, 104), size));
}

pub fn draw() void {
    camera.mode = .local;
    defer camera.mode = .world;

    var pos: gfx.Vector = .init(window.logicSize.x / 2, 10);
    var healthSize: gfx.Vector = .init(200, 12);
    const healthPos = pos.sub(healthSize.scale(0.5));

    const health = ecs.w.getIdentity(Player, Health).?;
    var buffer: [50]u8 = undefined;
    const text = zhu.format(&buffer, "Health: {} / {}", //
        .{ health.current, health.max });

    camera.drawRect(.init(healthPos, healthSize), healthBackground);
    healthSize.x *= math.percentInt(health.current, health.max);
    camera.drawRect(.init(healthPos, healthSize), healthForeground);

    drawTextCenter(text, pos, .{});
    pos.y += size.x * 2;
    drawTextCenter("Explore the Dungeon. A/S/D/W to move.", pos, .{});

    drawNameAndHealthIfNeed();
    drawGameOverIfNeed();
}

fn drawNameAndHealthIfNeed() void {
    var buffer: [50]u8 = undefined;
    const mousePosition = camera.toWorld(window.mousePosition);

    var view = ecs.w.view(.{ Health, Name, Position });
    while (view.next()) |entity| {
        var position = view.get(entity, Position);
        const rect: gfx.Rect = .init(position, map.TILE_SIZE);

        if (!rect.contains(mousePosition)) continue;

        const health = view.get(entity, Health).current;
        const name = view.get(entity, Name)[0];

        const text = zhu.format(&buffer, "{s}: {}hp", .{ name, health });

        position = position.addXY(map.TILE_SIZE.x / 2, -size.y);
        drawTextCenter(text, camera.toWindow(position), .{});
    }
}

fn drawGameOverIfNeed() void {
    if (ecs.w.getContext(TurnState).? != .over) return;

    var pos: gfx.Vector = .init(window.logicSize.x / 2, 130);
    var text: []const u8 = "Your quest has ended.";
    drawTextCenter(text, pos, .{ .color = .red, .scale = 2 });

    text = "Slain by a monster, your hero's journey has come to a end.";
    pos = pos.addY(50);
    drawTextCenter(text, pos, .{});
    text = "The Amulet of Yala remains unclaimed," ++
        " and your home town is not saved.";
    pos = pos.addY(20);
    drawTextCenter(text, pos, .{});

    text = "Don't worry, you can always try again with a new hero.";
    pos = pos.addY(40);
    drawTextCenter(text, pos, .{ .color = .yellow });

    text = "Press 1 to play again.";
    pos = pos.addY(50);
    drawTextCenter(text, pos, .{ .color = .green, .scale = 2 });
}

const Options = struct { color: gfx.Color = .white, scale: f32 = 1 };
fn drawTextCenter(text: []const u8, pos: Position, opt: Options) void {
    const textSize = size.mul(.init(@floatFromInt(text.len), 1));
    drawText(text, pos.sub(textSize.scale(0.5).scale(opt.scale)), opt);
}

const size: Position = .init(8, 8);
fn drawText(text: []const u8, position: Position, opt: Options) void {
    var pos = position;

    for (text) |byte| {
        const x: f32 = @floatFromInt(byte % 16);
        const y: f32 = @floatFromInt(byte / 16);
        const charTexture = texture.subTexture(.{
            .min = size.mul(.init(x, y)),
            .size = size,
        });

        camera.drawOption(charTexture, pos, .{
            .color = opt.color,
            .size = size.scale(opt.scale),
        });
        pos.x += size.x * opt.scale;
    }
}
```

## 效果

![游戏结束画面][1]

[1]: images/地宫探险14.gif

## 附录
