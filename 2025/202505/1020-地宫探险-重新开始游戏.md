# 1020-地宫探险-重新开始游戏

## 目标

游戏结束后，可以按 1 来重新开始游戏。

## 环境

- Time 2025-11-11
- Zig 0.15.1

## 参考

1. <https://pragprog.com/titles/hwrust/hands-on-rust/>
2. 《Rust游戏开发实战》

## 想法

直接把之前的数据全部清空，然后再次执行一次初始化流程。

## scene.zig

主要将 init 方法中的一些逻辑移动到了 restart，并且检测了是否游戏结束。

```zig
const std = @import("std");
const zhu = @import("zhu");

const window = zhu.window;
const gfx = zhu.gfx;
const camera = zhu.camera;
const ecs = zhu.ecs;

const map = @import("map.zig");
const player = @import("player.zig");
const monster = @import("monster.zig");
const hud = @import("hud.zig");
const battle = @import("battle.zig");
const component = @import("component.zig");

const Player = component.Player;
const Position = component.Position;
const TilePosition = component.TilePosition;
const WantToMove = component.WantToMove;
const TurnState = component.TurnState;

var isHelp = false;
var isDebug = false;
const scale = 1;

pub fn init() void {
    window.initFont(.{
        .font = @import("zon/font.zon"),
        .texture = gfx.loadTexture("assets/font.png", .init(960, 960)),
    });

    camera.frameStats(true);
    camera.init(5000);
    camera.scale = .init(scale, scale);
    ecs.init(window.allocator);

    restart();
}

fn restart() void {
    ecs.clear();

    hud.init();
    map.init();
    player.init();
    cameraFollow();
    monster.init();
}

pub fn update(delta: f32) void {
    if (window.isKeyRelease(.H)) isHelp = !isHelp;
    if (window.isKeyRelease(.X)) isDebug = !isDebug;

    if (window.isKeyDown(.LEFT_ALT) and window.isKeyRelease(.ENTER)) {
        return window.toggleFullScreen();
    }

    const speed: f32 = std.math.round(100 * delta) / scale;
    if (window.isKeyDown(.UP)) camera.position.y -= speed;
    if (window.isKeyDown(.DOWN)) camera.position.y += speed;
    if (window.isKeyDown(.LEFT)) camera.position.x -= speed;
    if (window.isKeyDown(.RIGHT)) camera.position.x += speed;

    if (ecs.w.getContext(TurnState).? == .over) {
        return if (window.isKeyRelease(._1)) restart();
    }

    player.move();
    battle.checkPlayerAttack();
    battle.attack();
    const playerEntity = ecs.w.getIdentityEntity(Player).?;
    if (ecs.w.has(playerEntity, WantToMove)) {
        map.updateDistance(ecs.w.get(playerEntity, TilePosition).?);
    }
    monster.move();
    map.update(delta);

    cameraFollow();
    sceneCall("update", .{delta});
}

pub fn cameraFollow() void {
    const playerEntity = ecs.w.getIdentityEntity(Player).?;
    if (!ecs.w.has(playerEntity, WantToMove)) return;

    const position = ecs.w.get(playerEntity, Position).?;

    const scaleSize = window.logicSize.div(camera.scale);
    const half = scaleSize.scale(0.5);
    const max = map.size.sub(scaleSize).max(.zero);
    camera.position = position.sub(half).clamp(.zero, max);
}

pub fn draw() void {
    camera.beginDraw(.{});
    defer camera.endDraw();

    window.keepAspectRatio();
    sceneCall("draw", .{});
    map.draw();

    var view = ecs.w.view(.{ gfx.Texture, Position });
    while (view.next()) |entity| {
        const texture = view.get(entity, gfx.Texture);
        const position = view.get(entity, Position);
        camera.draw(texture, position);
    }

    hud.draw();

    if (isHelp) drawHelpInfo() else if (isDebug) drawDebugInfo();
}

fn drawHelpInfo() void {
    const text =
        \\按键说明：
        \\上：W，下：S，左：A，右：D
        \\确定：F，取消：Q，菜单：E
        \\帮助：H  按一次打开，再按一次关掉
    ;
    var iterator = std.unicode.Utf8View.initUnchecked(text).iterator();
    var count: u32 = 0;
    while (iterator.nextCodepoint()) |code| {
        if (code == '\n') continue;
        count += 1;
    }
    debutTextCount = count;

    camera.drawColorText(text, .init(10, 5), .green);
}

var debutTextCount: u32 = 0;
fn drawDebugInfo() void {
    var buffer: [1024]u8 = undefined;
    const format =
        \\后端：{s}
        \\帧率：{}
        \\帧时：{d:.2}
        \\用时：{d:.2}
        \\显存：{}
        \\常量：{}
        \\绘制：{}
        \\图片：{}
        \\文字：{}
        \\内存：{}
        \\鼠标：{d:.2}，{d:.2}
        \\相机：{d:.2}，{d:.2}
    ;

    const stats = camera.queryFrameStats();
    const text = zhu.format(&buffer, format, .{
        @tagName(camera.queryBackend()),
        window.frameRate,
        window.frameDeltaPerSecond,
        window.usedDeltaPerSecond,
        stats.size_append_buffer + stats.size_update_buffer,
        stats.size_apply_uniforms,
        stats.num_draw,
        camera.imageDrawCount(),
        // Debug 信息本身的次数也应该统计进去
        camera.textDrawCount() + debutTextCount,
        window.countingAllocator.used,
        window.mousePosition.x,
        window.mousePosition.y,
        camera.position.x,
        camera.position.y,
    });

    var iterator = std.unicode.Utf8View.initUnchecked(text).iterator();
    var count: u32 = 0;
    while (iterator.nextCodepoint()) |code| {
        if (code == '\n') continue;
        count += 1;
    }
    debutTextCount = count;

    camera.drawColorText(text, .init(10, 5), .green);
}

pub fn deinit() void {
    sceneCall("deinit", .{});
    ecs.deinit();
}

fn sceneCall(comptime function: []const u8, args: anytype) void {
    _ = function;
    _ = args;
}
```

## 效果

![重新开始游戏][1]

[1]: images/地宫探险15.gif

## 附录
