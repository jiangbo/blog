# 1036-地宫探险-数据定义物品

## 目标

将之前怪兽和物品的生成逻辑，修改成配置文件，类似数据驱动的形式。

## 环境

- Time 2025-11-25
- Zig 0.15.1

## 参考

1. <https://pragprog.com/titles/hwrust/hands-on-rust/>
2. 《Rust游戏开发实战》

## 想法

使用数据定义后，修改起来方便很多，而且要新增或者减少也很方便。

## templates.zon

定义了物品的模板。

```zig
.{
    .{
        .entityType = .item,
        .levels = .{ 1, 2, 3 },
        .frequency = 2,
        .name = "Weak Healing Potion",
        .tile = .heal,
        .value = 2,
    },
    .{
        .entityType = .item,
        .levels = .{ 1, 2, 3 },
        .frequency = 1,
        .name = "Healing Potion",
        .tile = .heal,
        .value = 4,
    },
    .{
        .entityType = .item,
        .levels = .{ 1, 2, 3 },
        .frequency = 1,
        .name = "Dungeon Map",
        .tile = .map,
        .value = 0,
    },
    .{
        .entityType = .enemy,
        .levels = .{ 1, 2, 3 },
        .frequency = 3,
        .name = "Goblin",
        .tile = .goblin,
        .value = 1,
    },
    .{
        .entityType = .enemy,
        .levels = .{ 1, 2, 3 },
        .frequency = 2,
        .name = "Orc",
        .tile = .orc,
        .value = 2,
    },
}
```

## monster.zig

定义了关卡，关卡中包含物品的模板。

```zig
const Template = struct {
    entityType: enum { enemy, item },
    levels: []const u8,
    frequency: u8,
    name: []const u8,
    tile: Tile,
    value: u8,
};
const templates: []const Template = @import("zon/templates.zon");
var frequencies: [templates.len]u8 = undefined;

pub fn init() void {
    for (templates, &frequencies) |template, *f| {
        const contains = std.mem.indexOfScalar;
        const found = contains(u8, template.levels, map.currentLevel);
        f.* = if (found == null) 0 else template.frequency;
    }

    const playerView = ecs.w.getIdentity(Player, ViewField).?[0];
    for (map.spawns[1..]) |pos| {
        const entity = ecs.w.createEntity();
        if (playerView.contains(pos)) ecs.w.add(entity, PlayerView{});

        const index = zhu.random().weightedIndex(u8, &frequencies);
        const template = &templates[index];
        ecs.w.add(entity, pos);
        ecs.w.add(entity, map.worldPosition(pos));
        ecs.w.add(entity, map.getTextureFromTile(template.tile));
        ecs.w.add(entity, Name{template.name});

        switch (templates[index].entityType) {
            .item => spawnItem(entity, template),
            .enemy => spawnMonster(entity, template),
        }
    }
}

fn spawnItem(entity: ecs.Entity, t: *const Template) void {
    ecs.w.add(entity, Item{});
    if (t.tile == .map) return;
    ecs.w.add(entity, Healing{ .amount = t.value });
}

fn spawnMonster(enemy: ecs.Entity, t: *const Template) void {
    const hp: i32 = @intCast(t.value);
    ecs.w.add(enemy, Health{ .current = hp, .max = hp });
    ecs.w.add(enemy, ChasePlayer{});
    ecs.w.add(enemy, Enemy{});
}
```

## 效果

![数据定义物品][1]

[1]: images/地宫探险31.gif

## 附录
