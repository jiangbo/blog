# 1026-地宫探险-元胞自动机地图

## 目标

之前使用的房间的方式生成地图，现在使用元胞自动机生成地图。

## 环境

- Time 2025-11-20
- Zig 0.15.1

## 参考

1. <https://pragprog.com/titles/hwrust/hands-on-rust/>
2. 《Rust游戏开发实战》

## 想法

好像这个算法生成的地图，并不能保证所有地方都是连通的，如果有没有连通的怎么办呢？

## map.zig

重构了地图模块，生成地图的代码移动到了新的模块。

```zig
const std = @import("std");
const zhu = @import("zhu");

const gfx = zhu.gfx;
const ecs = zhu.ecs;

const component = @import("component.zig");
const builder = @import("builder.zig");

const Position = component.Position;
const TilePosition = component.TilePosition;
const TileRect = component.TileRect;
const WantToMove = component.WantToMove;
const Player = component.Player;
const Tile = component.Tile;
const ViewField = component.ViewField;

const WIDTH = builder.WIDTH;
const HEIGHT = builder.HEIGHT;
pub const TILE_SIZE: gfx.Vector = .init(32, 32);
const TILE_PER_ROW = 16;

pub var size = gfx.Vector.init(WIDTH, HEIGHT).mul(TILE_SIZE);
pub var spawns: [builder.SPAWN_SIZE]TilePosition = undefined;
pub var amuletPos: TilePosition = undefined;

var tiles: [WIDTH * HEIGHT]Tile = undefined;
var texture: gfx.Texture = undefined;
var walks: [HEIGHT * WIDTH]bool = undefined;

pub fn init() void {
    texture = gfx.loadTexture("assets/dungeonfont.png", .init(512, 512));

    // builder.buildRooms(&tiles, &spawns);
    builder.buildAutometa(&tiles, &spawns);

    @memset(&walks, false);
    updateDistance(spawns[0]);

    var max: u8 = 0;
    for (&distances, 0..) |*line, y| {
        for (line, 0..) |value, x| {
            if (value == 0xFF or value <= max) continue;
            max = value;
            amuletPos = .{ .x = @intCast(x), .y = @intCast(y) };
        }
    }
}

pub fn getTextureFromTile(tile: Tile) gfx.Texture {
    const index: usize = @intFromEnum(tile);
    const row: f32 = @floatFromInt(index / TILE_PER_ROW);
    const col: f32 = @floatFromInt(index % TILE_PER_ROW);
    const pos = gfx.Vector.init(col, row).mul(TILE_SIZE);
    return texture.subTexture(.init(pos, TILE_SIZE));
}

fn getPositionFromIndex(index: usize) gfx.Vector {
    const row: f32 = @floatFromInt(index / WIDTH);
    const col: f32 = @floatFromInt(index % WIDTH);
    return gfx.Vector.init(col, row).mul(TILE_SIZE);
}

const indexUsize = builder.indexUsize;
pub fn indexTile(x: usize, y: usize) Tile {
    return tiles[indexUsize(x, y)];
}

pub fn worldPosition(pos: TilePosition) Position {
    return pos.toVector().mul(TILE_SIZE);
}

pub fn canMove(pos: TilePosition) bool {
    return pos.x < WIDTH and pos.y < HEIGHT //
    and indexTile(pos.x, pos.y) == .floor;
}

pub fn moveIfNeed() void {
    var view = ecs.w.view(.{ WantToMove, TilePosition });
    blk: while (view.next()) |entity| {
        const dest = view.get(entity, WantToMove)[0];
        if (!canMove(dest)) continue;

        for (ecs.w.raw(TilePosition)) |pos| {
            if (pos.equals(dest)) continue :blk;
        }

        view.getPtr(entity, TilePosition).* = dest;
        const pos = worldPosition(dest);
        view.getPtr(entity, Position).* = pos;
    }
}

var distances: [HEIGHT][WIDTH]u8 = undefined;
const Dequeue = std.PriorityDequeue(TilePosition, void, struct {
    fn compare(_: void, a: TilePosition, b: TilePosition) std.math.Order {
        return std.math.order(distances[a.y][a.x], distances[b.y][b.x]);
    }
}.compare);
const directions: [4]TilePosition = .{
    .{ .x = 1, .y = 0 }, .{ .x = 0xFF, .y = 0 },
    .{ .x = 0, .y = 1 }, .{ .x = 0, .y = 0xFF },
};
pub fn updateDistance(pos: TilePosition) void {
    for (&distances) |*row| @memset(row, 0xFF);

    var queue = Dequeue.init(zhu.window.allocator, {});
    defer queue.deinit();

    distances[pos.y][pos.x] = 0;
    queue.add(pos) catch unreachable;

    while (queue.removeMinOrNull()) |min| {
        const distance = distances[min.y][min.x];

        for (directions) |dir| {
            const x, const y = .{ min.x +% dir.x, min.y +% dir.y };
            if (x >= WIDTH or y >= HEIGHT) continue; // 超过地图
            if (tiles[indexUsize(x, y)] != .floor) continue; // 不可通过

            if (distance + 1 < distances[y][x]) {
                distances[y][x] = distance + 1;
                queue.add(.{ .x = x, .y = y }) catch unreachable;
            }
        }
    }
}

pub fn queryLessDistance(pos: TilePosition) ?TilePosition {
    const distance = distances[pos.y][pos.x];
    if (distance == 0) return null;

    var r1: ?TilePosition, var r2: ?TilePosition = .{ null, null };
    for (directions) |dir| {
        const x, const y = .{ pos.x +% dir.x, pos.y +% dir.y };
        if (x >= WIDTH or y >= HEIGHT) continue; // 超过地图

        if (distances[y][x] < distance) {
            const r = TilePosition{ .x = x, .y = y };
            if (distance > 4) return r; // 远距离直接返回
            if (r1 == null) r1 = r else r2 = r;
        }
    }
    if (r2 == null) return r1;
    return if (zhu.randomBool()) r1 else r2;
}

pub fn updatePlayerWalk() void {
    const viewField = ecs.w.getIdentity(Player, ViewField).?[0];

    for (viewField.y..viewField.y + viewField.h) |y| {
        for (viewField.x..viewField.x + viewField.w) |x| {
            walks[indexUsize(x, y)] = true;
        }
    }
}

pub fn draw() void {
    drawPlayerWalk();
    drawPlayerView();
}

fn drawPlayerWalk() void {
    const playerEntity = ecs.w.getIdentityEntity(Player).?;
    const viewField = ecs.w.get(playerEntity, ViewField).?[0];
    const playerPos = ecs.w.get(playerEntity, TilePosition).?;

    const x = playerPos.x -| 10;
    const y = playerPos.y -| 7;
    const windowView = TileRect{ .x = x, .y = y, .w = 20, .h = 13 };

    for (walks, 0..) |isWalk, index| {
        if (!isWalk) continue;
        const pos = TilePosition{
            .x = @intCast(index % WIDTH),
            .y = @intCast(index / WIDTH),
        };
        if (!windowView.contains(pos)) continue;
        if (viewField.contains(pos)) continue;

        const tex = getTextureFromTile(tiles[index]);
        zhu.camera.drawOption(tex, getPositionFromIndex(index), .{
            .color = .{ .x = 0.5, .y = 0.5, .z = 0.5, .w = 1 },
        });
    }
}

fn drawPlayerView() void {
    const viewField = ecs.w.getIdentity(Player, ViewField).?[0];

    for (viewField.x..viewField.x + viewField.w) |x| {
        for (viewField.y..viewField.y + viewField.h) |y| {
            const index = indexUsize(x, y);
            const tex = getTextureFromTile(tiles[index]);
            zhu.camera.draw(tex, getPositionFromIndex(index));
        }
    }
}
```

## builder.zig

将地图生成的代码移动到了这个模块。

```zig
const std = @import("std");
const zhu = @import("zhu");

const component = @import("component.zig");

const TileRect = component.TileRect;
const Tile = component.Tile;
const TilePosition = component.TilePosition;

pub const WIDTH: usize = 80;
pub const HEIGHT: usize = 50;
pub const SPAWN_SIZE = 20;

pub fn buildRooms(tiles: []Tile, spawns: []TilePosition) void {
    @memset(tiles, .wall);

    var rooms: [SPAWN_SIZE]TileRect = undefined;
    for (0..rooms.len) |roomIndex| {
        var room: TileRect = undefined;
        label: {
            room = TileRect{
                .x = zhu.randomInt(u8, 1, WIDTH - 10),
                .y = zhu.randomInt(u8, 1, HEIGHT - 10),
                .w = zhu.randomInt(u8, 2, 10),
                .h = zhu.randomInt(u8, 2, 10),
            };

            for (0..roomIndex) |idx| {
                if (rooms[idx].intersect(room)) break :label;
            }
        }

        for (room.y..room.y + room.h) |y| {
            for (room.x..room.x + room.w) |x| {
                setTile(tiles, x, y, .floor);
            }
        }
        rooms[roomIndex] = room;
    }

    std.mem.sort(TileRect, &rooms, {}, compare);
    buildCorridors(tiles, &rooms);
    for (spawns, 0..) |*value, i| value.* = rooms[i].center();
}

fn compare(_: void, r1: TileRect, r2: TileRect) bool {
    return if (r1.x == r2.x) r1.y < r2.y else r1.x < r2.x;
}

fn buildCorridors(tiles: []Tile, rooms: []TileRect) void {
    for (rooms[1..], 1..) |room, roomIndex| {
        const prev = rooms[roomIndex - 1].center();
        const new = room.center();
        if (zhu.randomInt(u8, 0, 2) == 1) {
            applyHorizontal(tiles, prev.x, new.x, prev.y);
            applyVertical(tiles, prev.y, new.y, new.x);
        } else {
            applyVertical(tiles, prev.y, new.y, prev.x);
            applyHorizontal(tiles, prev.x, new.x, new.y);
        }
    }
}

fn applyVertical(tiles: []Tile, y1: usize, y2: usize, x: usize) void {
    for (@min(y1, y2)..@max(y1, y2) + 1) |y| {
        setTile(tiles, x, y, .floor);
    }
}

fn applyHorizontal(tiles: []Tile, x1: usize, x2: usize, y: usize) void {
    for (@min(x1, x2)..@max(x1, x2) + 1) |x| {
        setTile(tiles, x, y, .floor);
    }
}

pub fn buildAutometa(tiles: []Tile, spawns: []TilePosition) void {
    for (tiles) |*value| {
        const roll = zhu.randomInt(u8, 0, 100);
        value.* = if (roll > 55) .floor else .wall;
    }

    for (0..10) |_| {
        var copy: [HEIGHT * WIDTH]Tile = undefined;
        @memcpy(&copy, tiles);
        for (1..HEIGHT - 1) |y| {
            for (1..WIDTH - 1) |x| {
                const dx: u8 = @intCast(x);
                const walls = countNeighborWall(&copy, dx, @intCast(y));
                const genWall = walls > 4 or walls == 0;
                tiles[indexUsize(x, y)] = if (genWall) .wall else .floor;
            }
        }
    }

    { // 放置玩家
        var x: u8, var y: u8 = .{ WIDTH / 2, WIDTH / 2 };
        blk: while (tiles[indexUsize(x, y)] == .wall) {
            for (neighborDir) |dir| {
                const dx, const dy = .{ x +% dir.x, y +% dir.y };
                if (tiles[indexUsize(dx, dy)] != .wall) {
                    spawns[0] = .{ .x = dx, .y = dy };
                    break :blk;
                }
            }
            const dir = neighborDir[zhu.randomIntMost(u8, 0, 7)];
            x, y = .{ x +% dir.x, y +% dir.y };
        } else spawns[0] = .{ .x = x, .y = y };
    }

    // 放置怪物
    for (spawns[1..]) |*value| {
        var pos = spawnRandomMonster(tiles);
        while (spawns[0].distanceSquared(pos) < 100) {
            pos = spawnRandomMonster(tiles);
        }
        value.* = pos;
    }
}

const neighborDir: [8]TilePosition = .{
    .{ .x = 0xFF, .y = 0xFF }, .{ .x = 0, .y = 0xFF },
    .{ .x = 1, .y = 0xFF },    .{ .x = 0xFF, .y = 0 },
    .{ .x = 1, .y = 0 },       .{ .x = 0xFF, .y = 1 },
    .{ .x = 0, .y = 1 },       .{ .x = 1, .y = 1 },
};
fn countNeighborWall(tiles: []Tile, x: u8, y: u8) u8 {
    var count: u8 = 0;
    for (&neighborDir) |dir| {
        const dx, const dy = .{ x +% dir.x, y +% dir.y };
        if (tiles[dy * WIDTH + dx] == .wall) count += 1;
    }
    return count;
}

fn spawnRandomMonster(tiles: []Tile) TilePosition {
    var roll = zhu.randomInt(u16, 0, HEIGHT * WIDTH);
    while (tiles[roll] == .wall) {
        roll = zhu.randomInt(u16, 0, HEIGHT * WIDTH);
    }
    const x, const y = .{ roll % WIDTH, roll / WIDTH };
    return .{ .x = @intCast(x), .y = @intCast(y) };
}

pub fn indexUsize(x: usize, y: usize) usize {
    return @min(x, WIDTH - 1) + @min(y, HEIGHT - 1) * WIDTH;
}

fn setTile(tiles: []Tile, x: usize, y: usize, tile: Tile) void {
    tiles[indexUsize(x, y)] = tile;
}
```

## 效果

![元胞自动机地图][1]

[1]: images/地宫探险21.gif

## 附录
