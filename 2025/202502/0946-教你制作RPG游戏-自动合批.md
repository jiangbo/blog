# 0946-教你制作RPG游戏-自动合批

## 目标

绘制的时候，如果是同一张纹理，合并到一个批量绘制中。

## 环境

- Time 2025-06-06
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

目前还存在一点问题，修改摄像机的坐标，会导致合批的绘制不准确，看看要怎么解决一下。

## gpu.zig

去掉了很多绘制的代码，旋转功能移除了，后面看需要再加上。

```zig
const std = @import("std");

const sk = @import("sokol");
const math = @import("math.zig");

pub const Rectangle = math.Rectangle;

pub const Texture = struct {
    image: sk.gfx.Image,
    area: Rectangle = .{},

    pub fn width(self: *const Texture) f32 {
        return self.size().x;
    }

    pub fn height(self: *const Texture) f32 {
        return self.size().y;
    }

    pub fn size(self: *const Texture) math.Vector {
        return self.area.size();
    }

    pub fn subTexture(self: *const Texture, area: Rectangle) Texture {
        return Texture{ .image = self.image, .area = .{
            .min = self.area.min.add(area.min),
            .max = self.area.min.add(area.max),
        } };
    }

    pub fn mapTexture(self: *const Texture, area: Rectangle) Texture {
        return Texture{ .image = self.image, .area = area };
    }

    pub fn deinit(self: *Texture) void {
        sk.gfx.destroyImage(self.image);
    }
};

pub fn queryTextureSize(image: sk.gfx.Image) math.Vector {
    return math.Vector{
        .x = @floatFromInt(sk.gfx.queryImageWidth(image)),
        .y = @floatFromInt(sk.gfx.queryImageHeight(image)),
    };
}

pub const asRange = sk.gfx.asRange;
pub const queryBackend = sk.gfx.queryBackend;
pub const Buffer = sk.gfx.Buffer;
pub const Color = sk.gfx.Color;
pub const Sampler = sk.gfx.Sampler;
pub const Shader = sk.gfx.Shader;
pub const VertexLayout = sk.gfx.VertexLayoutState;

pub fn createBuffer(desc: sk.gfx.BufferDesc) Buffer {
    return sk.gfx.makeBuffer(desc);
}

pub const RenderPipelineDesc = struct {
    shader: sk.gfx.Shader,
    vertexLayout: VertexLayout,
    primitive: sk.gfx.PrimitiveType = .TRIANGLES,
    color: sk.gfx.ColorTargetState = .{},
    index_type: sk.gfx.IndexType = .DEFAULT,
    depth: sk.gfx.DepthState = .{},
};

pub fn createRenderPipeline(desc: RenderPipelineDesc) RenderPipeline {
    return .{ .value = sk.gfx.makePipeline(.{
        .shader = desc.shader,
        .layout = desc.vertexLayout,
        .primitive_type = desc.primitive,
        .colors = init: {
            var c: [4]sk.gfx.ColorTargetState = @splat(.{});
            c[0] = desc.color;
            break :init c;
        },
        .index_type = desc.index_type,
        .depth = desc.depth,
    }) };
}

pub fn createShaderModule(desc: sk.gfx.ShaderDesc) sk.gfx.Shader {
    return sk.gfx.makeShader(desc);
}

pub fn createSampler(desc: sk.gfx.SamplerDesc) Sampler {
    return sk.gfx.makeSampler(desc);
}

pub fn appendBuffer(buffer: Buffer, data: anytype) void {
    _ = sk.gfx.appendBuffer(buffer, sk.gfx.asRange(data));
}

pub const BindGroup = struct {
    value: sk.gfx.Bindings = .{},

    pub fn setIndexBuffer(self: *BindGroup, buffer: Buffer) void {
        self.value.index_buffer = buffer;
    }

    pub fn setIndexOffset(self: *BindGroup, offset: u32) void {
        self.value.index_buffer_offset = @intCast(offset);
    }

    pub fn setVertexBuffer(self: *BindGroup, buffer: Buffer) void {
        self.value.vertex_buffers[0] = buffer;
    }

    pub fn setVertexOffset(self: *BindGroup, offset: u32) void {
        self.value.vertex_buffer_offsets[0] = @intCast(offset);
    }

    pub fn setTexture(self: *BindGroup, index: u32, texture: Texture) void {
        self.value.images[index] = texture.image;
    }

    pub fn setSampler(self: *BindGroup, index: u32, sampler: Sampler) void {
        self.value.samplers[index] = sampler;
    }
};

pub var commandEncoder: CommandEncoder = .{};
pub const CommandEncoder = struct {
    pub fn beginRenderPass(_: CommandEncoder, color: Color) RenderPassEncoder {
        var action = sk.gfx.PassAction{};
        action.colors[0] = .{ .load_action = .CLEAR, .clear_value = color };
        sk.gfx.beginPass(.{ .action = action, .swapchain = sk.glue.swapchain() });
        return RenderPassEncoder{};
    }

    pub fn submit(_: *CommandEncoder) void {
        sk.gfx.commit();
    }
};

pub const RenderPassEncoder = struct {
    pub fn setPipeline(self: *RenderPassEncoder, pipeline: RenderPipeline) void {
        _ = self;
        sk.gfx.applyPipeline(pipeline.value);
    }

    pub fn setBindGroup(self: *RenderPassEncoder, group: BindGroup) void {
        _ = self;
        sk.gfx.applyBindings(group.value);
    }

    pub fn setUniform(self: *RenderPassEncoder, index: u32, uniform: anytype) void {
        _ = self;
        sk.gfx.applyUniforms(index, sk.gfx.asRange(&uniform));
    }

    pub fn draw(self: *RenderPassEncoder, number: u32) void {
        _ = self;
        sk.gfx.draw(0, number, 1);
    }

    pub fn end(self: *RenderPassEncoder) void {
        _ = self;
        sk.gfx.endPass();
    }
};

pub const RenderPipeline = struct {
    value: sk.gfx.Pipeline,
};
```

## camera.zig

相机中实现了自动合批的功能，如果纹理一致则进行合并。

```zig
const std = @import("std");

const gpu = @import("gpu.zig");
const math = @import("math.zig");
const shader = @import("shader/2d.glsl.zig");
const window = @import("window.zig");

pub const Vertex = extern struct {
    position: math.Vector3, // 顶点坐标
    color: gpu.Color, // 顶点颜色
    uv: math.Vector2 = .zero, // 纹理坐标
};

pub var rect: math.Rectangle = undefined;
var border: math.Vector = undefined;

var viewMatrix: [16]f32 = undefined;
var renderPass: gpu.RenderPassEncoder = undefined;
var bindGroup: gpu.BindGroup = .{};
var pipeline: gpu.RenderPipeline = undefined;

var buffer: gpu.Buffer = undefined;
var needDrawCount: u32 = 0;
var totalDrawCount: u32 = 0;
var texture: gpu.Texture = .{ .image = .{} };

pub fn init(r: math.Rectangle, b: math.Vector, vertex: []Vertex, index: []u16) void {
    rect = r;
    border = b;

    const x, const y = .{ rect.size().x, rect.size().y };
    viewMatrix = .{
        2 / x, 0, 0, 0, 0,  2 / -y, 0, 0,
        0,     0, 1, 0, -1, 1,      0, 1,
    };

    bindGroup.setIndexBuffer(gpu.createBuffer(.{
        .type = .INDEXBUFFER,
        .data = gpu.asRange(index),
    }));

    buffer = gpu.createBuffer(.{
        .size = @sizeOf(Vertex) * vertex.len,
        .usage = .STREAM,
    });

    bindGroup.setSampler(shader.SMP_smp, gpu.createSampler(.{}));
    pipeline = initPipeline();
}

fn initPipeline() gpu.RenderPipeline {
    var vertexLayout = gpu.VertexLayout{};
    vertexLayout.attrs[shader.ATTR_single_position0].format = .FLOAT3;
    vertexLayout.attrs[shader.ATTR_single_color0].format = .FLOAT4;
    vertexLayout.attrs[shader.ATTR_single_texcoord0].format = .FLOAT2;

    const shaderDesc = shader.singleShaderDesc(gpu.queryBackend());
    return gpu.createRenderPipeline(.{
        .shader = gpu.createShaderModule(shaderDesc),
        .vertexLayout = vertexLayout,
        .color = .{ .blend = .{
            .enabled = true,
            .src_factor_rgb = .SRC_ALPHA,
            .dst_factor_rgb = .ONE_MINUS_SRC_ALPHA,
        } },
        .index_type = .UINT16,
        .depth = .{ .compare = .LESS_EQUAL, .write_enabled = true },
    });
}

pub fn lookAt(pos: math.Vector) void {
    const half = rect.size().scale(0.5);

    const max = border.sub(rect.size());
    const offset = pos.sub(half).clamp(.zero, max);

    rect = .init(offset, rect.size());
}

pub fn beginDraw(color: gpu.Color) void {
    renderPass = gpu.commandEncoder.beginRenderPass(color);
    totalDrawCount = 0;
}

pub fn draw(tex: gpu.Texture, position: math.Vector) void {
    drawFlipX(tex, position, false);
}

pub fn drawFlipX(tex: gpu.Texture, pos: math.Vector, flipX: bool) void {
    const target: math.Rectangle = .init(pos, tex.size());
    var src = tex.area;
    if (flipX) {
        src.min.x = tex.area.max.x;
        src.max.x = tex.area.min.x;
    }

    drawOptions(.{ .texture = tex, .source = src, .target = target });
}

const DrawOptions = struct {
    texture: gpu.Texture,
    source: math.Rectangle,
    target: math.Rectangle,
    color: gpu.Color = .{ .r = 1, .g = 1, .b = 1, .a = 1 },
};

pub fn drawOptions(options: DrawOptions) void {
    var vertexes = createVertexes(options.source, options.target);
    for (&vertexes) |*value| value.position.z = 0.5;
    for (&vertexes) |*value| value.color = options.color;
    gpu.appendBuffer(buffer, &vertexes);

    defer {
        needDrawCount += 1;
        totalDrawCount += 1;
        texture = options.texture;
    }

    if (totalDrawCount == 0) return; // 第一次绘制
    if (options.texture.image.id != texture.image.id) doDraw();
}

// pub fn drawText(text: []const u8, position: math.Vector) void {
//     var iterator = std.unicode.Utf8View.initUnchecked(text).iterator();

//     var pos = position;
//     while (iterator.nextCodepoint()) |code| {
//         const char = window.fonts.get(code).?;
//         const size = math.Vector.init(char.width, char.height);
//         const area = math.Rectangle.init(.init(char.x, char.y), size);
//         const tex = window.fontTexture.subTexture(area);
//         batchDraw(tex, pos);
//         pos = pos.addX(char.xAdvance);
//     }
// }

pub fn endDraw() void {
    if (needDrawCount != 0) doDraw();

    renderPass.end();
    gpu.commandEncoder.submit();
}

fn createVertexes(src: math.Rectangle, dst: math.Rectangle) [4]Vertex {
    var vertexes: [4]Vertex = undefined;

    vertexes[0].position = dst.min.addY(dst.size().y);
    vertexes[0].uv = .init(src.min.x, src.max.y);

    vertexes[1].position = dst.max;
    vertexes[1].uv = .init(src.max.x, src.max.y);

    vertexes[2].position = dst.min.addX(dst.size().x);
    vertexes[2].uv = .init(src.max.x, src.min.y);

    vertexes[3].position = dst.min;
    vertexes[3].uv = .init(src.min.x, src.min.y);
    return vertexes;
}

fn doDraw() void {

    // 绑定流水线
    renderPass.setPipeline(pipeline);

    // 处理 uniform 变量
    viewMatrix[12] = -1 - rect.min.x * viewMatrix[0];
    viewMatrix[13] = 1 - rect.min.y * viewMatrix[5];
    const size = gpu.queryTextureSize(texture.image);
    renderPass.setUniform(shader.UB_vs_params, .{
        .viewMatrix = viewMatrix,
        .textureVec = [4]f32{ size.x, size.y, 1, 1 },
    });

    // 绑定组
    bindGroup.setTexture(shader.IMG_tex, texture);
    bindGroup.setVertexBuffer(buffer);

    const offset = totalDrawCount - needDrawCount;
    bindGroup.setIndexOffset(offset * 6 * @sizeOf(u16));
    renderPass.setBindGroup(bindGroup);

    // 绘制
    renderPass.draw(needDrawCount * 6);
    needDrawCount = 0;
}
```

## 效果

和之前的效果一样，没有改变逻辑。

![多个文字][1]

[1]: images/教你制作RPG游戏48.webp

## 附录
