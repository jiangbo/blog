# 0929-教你制作RPG游戏-提示框

## 目标

和另一个 NPC 对话，显示一个信息提示框。

## 环境

- Time 2025-05-10

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

同样还是不处理文字，先把提示框显示出来。

## world.zig

新增了提示框的代码。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

pub const Player = @import("Player.zig");
pub const map = @import("map.zig");

const Dialog = struct {
    var background: gfx.Texture = undefined;
    face: gfx.Texture = undefined,
    left: bool = true,
    npc: *map.NPC = undefined,
};

const Tip = struct {
    var background: gfx.Texture = undefined;
};

pub var players: [3]Player = undefined;
pub var currentPlayer: *Player = &players[0];
pub var playerCamera: *gfx.Camera = undefined;

var dialog: ?Dialog = null;
var face: gfx.Texture = undefined;

var tip: ?Tip = null;

pub fn init(camera: *gfx.Camera) void {
    players[0] = .init("assets/r1.png", 0);
    players[1] = .init("assets/r2.png", 1);
    players[2] = .init("assets/r3.png", 2);

    Dialog.background = gfx.loadTexture("assets/msg.png", .init(790, 163));
    face = gfx.loadTexture("assets/face1_1.png", .init(307, 355));
    playerCamera = camera;

    Tip.background = gfx.loadTexture("assets/msgtip.png", .init(291, 42));

    map.init();
}

pub fn enter() void {
    playerCamera.lookAt(Player.position);
    window.playMusic("assets/1.ogg");
}

pub fn exit() void {
    playerCamera.lookAt(.zero);
    window.stopMusic();
}

pub fn update(delta: f32) void {
    if (dialog) |*d| {
        if (window.isKeyRelease(.SPACE)) {
            if (d.left) d.left = false else dialog = null;
        }
        return;
    }

    if (tip) |_| {
        if (window.isKeyRelease(.SPACE)) tip = null;
        return;
    }

    currentPlayer.update(delta);

    for (map.npcSlice()) |*npc| {
        if (npc.area.contains(Player.position)) {
            if (npc.keyTrigger) {
                if (window.isKeyRelease(.SPACE)) npc.action();
            } else npc.action();
        }

        map.updateNpc(npc, delta);
    }
}

pub fn render(camera: *gfx.Camera) void {
    map.drawBackground(camera);

    var playerNotDraw: bool = true;
    for (map.npcSlice()) |npc| {
        if (npc.position.y > Player.position.y and playerNotDraw) {
            currentPlayer.render(camera);
            playerNotDraw = false;
        }

        const npcPosition = npc.position.sub(.init(120, 220));

        if (npc.animation != null and !npc.animation.?.finished()) {
            camera.draw(npc.animation.?.currentTexture(), npcPosition);
        } else if (npc.texture) |texture| {
            camera.draw(texture, npcPosition);
        }

        // camera.drawRectangle(npc.area);
    }

    if (playerNotDraw) currentPlayer.render(camera);

    map.drawForeground(camera);

    if (dialog) |d| {
        camera.lookAt(.zero);
        camera.draw(Dialog.background, .init(0, 415));
        if (d.left) {
            camera.draw(d.face, .init(0, 245));
        } else {
            camera.draw(d.npc.face.?, .init(486, 245));
        }
        camera.lookAt(Player.position);
    }

    if (tip) |_| {
        camera.lookAt(.zero);
        camera.draw(Tip.background, .init(251, 200));
        camera.lookAt(Player.position);
    }

    window.showFrameRate();
}

pub fn showDialog(npc: *map.NPC) void {
    dialog = Dialog{ .face = face, .npc = npc };
}

pub fn showTip() void {
    tip = Tip{};
}
```

## map.zig

对话的触发事件。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const c = @import("../c.zig");
const world = @import("world.zig");

pub const SIZE: gfx.Vector = .init(1000, 800);
const PLAYER_OFFSET: gfx.Vector = .init(120, 220);
const NPC_SIZE: gfx.Vector = .init(240, 240);
const NPC_AREA: gfx.Vector = .init(80, 100);
const NPC_SPEED = 80;

const FrameAnimation = gfx.FixedFrameAnimation(4, 0.25);

var upAnimation: FrameAnimation = undefined;
var downAnimation: FrameAnimation = undefined;
var leftAnimation: FrameAnimation = undefined;
var rightAnimation: FrameAnimation = undefined;
var facing: gfx.FourDirection = .down;
var timer: window.Timer = .init(1.5);

const NPCType = enum { fixed, walk, fly };

const Action = *const fn () void;
pub const NPC = struct {
    position: gfx.Vector,
    texture: ?gfx.Texture = null,
    animation: ?FrameAnimation = null,
    area: gfx.Rectangle = .{},
    keyTrigger: bool = true,
    action: *const fn () void = undefined,
    type: NPCType = .fixed,
    face: ?gfx.Texture = null,

    pub fn init(x: f32, y: f32, path: ?[:0]const u8, action: Action) NPC {
        var self: NPC = .{ .position = .init(x, y), .action = action };

        if (path) |p| self.texture = window.loadTexture(p, NPC_SIZE);
        self.area = .init(self.position.sub(.init(40, 60)), NPC_AREA);
        return self;
    }
};

const Map = struct {
    map: gfx.Texture,
    mapShade: gfx.Texture,
    mapBack: ?gfx.Texture = null,
    mapBlock: ?std.StaticBitSet(SIZE.x * SIZE.y) = null,
    npcArray: [3]NPC = undefined,
};

var index: usize = maps.len - 1;
var maps: [2]Map = undefined;

fn npc1Action() void {
    world.showDialog(&maps[0].npcArray[0]);
}

fn npc2Action() void {
    world.showTip();
}

fn map2npc1Action() void {
    for (&maps[1].npcArray) |*npc| {
        if (npc.animation != null and npc.type == .fixed) {
            npc.animation.?.reset();
        }
    }
}

pub fn init() void {
    maps[0] = Map{
        .map = window.loadTexture("assets/map1.png", SIZE),
        .mapShade = window.loadTexture("assets/map1_shade.png", SIZE),
        .mapBack = window.loadTexture("assets/map1_back.png", SIZE),
        .npcArray = .{
            .init(800, 300, "assets/npc1.png", npc1Action),
            .init(700, 280, "assets/npc2.png", npc2Action),
            .init(0, 0, null, changeMap0),
        },
    };
    maps[0].npcArray[2].area = .init(.{ .y = 400 }, .init(20, 600));
    maps[0].npcArray[2].keyTrigger = false;
    const face = window.loadTexture("assets/face3_2.png", .init(307, 355));
    maps[0].npcArray[0].face = face;

    sortNPC(&maps[1].npcArray);

    // 地图二的具有动画的 NPC
    const anim = window.loadTexture("assets/Anm1.png", .init(480, 480));
    const animation = anim.subTexture(.init(.zero, .init(480, 240)));
    var anim2 = FrameAnimation.initWithCount(animation, 2);
    anim2.addFrame(.init(.init(0, 240), .init(240, 240)));
    anim2.stop();

    maps[1] = Map{
        .map = window.loadTexture("assets/map2.png", SIZE),
        .mapShade = window.loadTexture("assets/map2_shade.png", SIZE),
        .npcArray = .{
            .init(700, 300, "assets/npc3.png", map2npc1Action),
            .init(500, 280, null, npc2Action),
            .init(0, 0, null, changeMap1),
        },
    };
    maps[1].npcArray[0].animation = anim2;

    const npc4 = window.loadTexture("assets/npc4.png", .init(960, 960));
    const size: gfx.Vector = .init(960, 240);
    upAnimation = .init(npc4.subTexture(.init(.{ .y = 720 }, size)));
    downAnimation = .init(npc4.subTexture(.init(.{ .y = 0 }, size)));
    leftAnimation = .init(npc4.subTexture(.init(.{ .y = 240 }, size)));
    rightAnimation = .init(npc4.subTexture(.init(.{ .y = 480 }, size)));

    maps[1].npcArray[1].animation = downAnimation;
    maps[1].npcArray[1].type = .walk;

    maps[1].npcArray[2].area = .init(.init(980, 400), .init(20, 600));
    maps[1].npcArray[2].keyTrigger = false;
    sortNPC(&maps[1].npcArray);

    const file = window.File.load("assets/map1_block.png", 0, callback);
    if (file.state == .loaded) initMapBlock(file.data);

    changeMap();
}

fn changeMap0() void {
    changeMap();
    world.Player.position.x = SIZE.x - 25;
}

fn changeMap1() void {
    changeMap();
    world.Player.position.x = 25;
}

fn sortNPC(npcArray: []NPC) void {
    std.mem.sort(NPC, npcArray, {}, struct {
        fn lessThan(_: void, a: NPC, b: NPC) bool {
            return a.position.y < b.position.y;
        }
    }.lessThan);
}

pub fn changeMap() void {
    index = (index + 1) % maps.len;

    if (maps[index].mapBlock == null and index == 0) {
        const file = window.File.load("assets/map1_block.png", 0, callback);
        if (file.data.len != 0) initMapBlock(file.data);
    }

    if (maps[index].mapBlock == null and index == 1) {
        const file = window.File.load("assets/map2_block.png", 0, callback);
        if (file.data.len != 0) initMapBlock(file.data);
    }
}

pub fn canWalk(pos: gfx.Vector) bool {
    const x, const y = .{ @round(pos.x), @round(pos.y) };

    if (x < 0 or x >= SIZE.x or y < 0 or y >= SIZE.y) return false;
    if (maps[index].mapBlock) |block| {
        return !block.isSet(@intFromFloat(x + y * SIZE.x));
    } else return false;
}

pub fn npcSlice() []NPC {
    return maps[index].npcArray[0..];
}

pub fn updateNpc(npc: *NPC, delta: f32) void {
    if (npc.animation) |*animation| animation.update(delta);

    if (npc.type == .fixed) return;

    if (timer.isFinishedAfterUpdate(delta)) {
        facing = window.random().enumValue(gfx.FourDirection);
        npc.animation = switch (facing) {
            .up => upAnimation,
            .down => downAnimation,
            .left => leftAnimation,
            .right => rightAnimation,
        };
        timer.reset();
    }

    const velocity = facing.toVector().scale(delta * NPC_SPEED);
    const position = npc.position.add(velocity);
    if (npc.type == .walk and canWalk(position)) npc.position = position;
    if (npc.type == .fly) npc.position = position;

    npc.area = .init(npc.position.sub(.init(40, 60)), NPC_AREA);
}

fn callback(res: @import("../assets.zig").Response) []const u8 {
    const content, const allocator = .{ res.data, res.allocator };
    const image = c.stbImage.loadFromMemory(content) catch unreachable;
    defer c.stbImage.unload(image);

    const data = allocator.dupe(u8, image.data) catch unreachable;
    initMapBlock(data);
    return data;
}

fn initMapBlock(buffer: []const u8) void {
    const data: []const u32 = @ptrCast(@alignCast(buffer));
    std.debug.assert(data.len == SIZE.x * SIZE.y);

    var blocks: std.StaticBitSet(SIZE.x * SIZE.y) = .initEmpty();
    for (data, 0..) |color, i| if (color == 0xFF000000) blocks.set(i);

    maps[index].mapBlock = blocks;
}

pub fn drawBackground(camera: *gfx.Camera) void {
    if (maps[index].mapBack) |back| camera.draw(back, .zero);
    camera.draw(maps[index].map, .zero);
}

pub fn drawForeground(camera: *gfx.Camera) void {
    camera.draw(maps[index].mapShade, .zero);
}
```

## 效果

![提示框][1]

[1]: images/教你制作RPG游戏33.webp

## 附录
