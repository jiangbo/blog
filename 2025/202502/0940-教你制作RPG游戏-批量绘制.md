# 0940-教你制作RPG游戏-批量绘制

## 目标

目前绘制都是显示一张图就绘制一次，性能应该会受到影响，接下来慢慢接入批量绘制。

## 环境

- Time 2025-06-03
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

之前一张图绘制一次还能说得过去，接下来应该要接入文字显示了，如果文字显示还一个字绘制一次，应该不行了。

## gpu.zig

将顶点的信息封装成了一个对象。

```zig
...
pub const Vertex = extern struct {
    position: math.Vector3 = .zero, // 顶点坐标
    color: math.Vector4 = .init(1, 1, 1, 1), // 顶点颜色
    uv: math.Vector2 = .zero, // 纹理坐标
};
...
```

## Camera.zig

相机中增加了 indexBuffer，VertexBuffer，同时也增加了一个批量绘制的方法。

```zig
const std = @import("std");

const gpu = @import("gpu.zig");
const math = @import("math.zig");
const shader = @import("shader/single.glsl.zig");

const Camera = @This();

rect: math.Rectangle,
border: math.Vector,
matrix: [16]f32 = undefined,
renderPass: gpu.RenderPassEncoder = undefined,
bindGroup: gpu.BindGroup = .{},
pipeline: gpu.RenderPipeline = undefined,

vertexBuffer: []gpu.Vertex = undefined,
buffer: gpu.Buffer = undefined,

batchDrawCount: u32 = 0,
batchTexture: gpu.Texture = undefined,

pub fn init(rect: math.Rectangle, border: math.Vector, vertexBuffer: []gpu.Vertex, indexBuffer: []u16) Camera {
    var self: Camera = .{ .rect = rect, .border = border };

    self.matrix = .{
        2 / rect.size().x, 0.0,                0.0, 0.0,
        0.0,               2 / -rect.size().y, 0.0, 0.0,
        0.0,               0.0,                1,   0.0,
        -1,                1,                  0,   1.0,
    };

    self.bindGroup.bindIndexBuffer(gpu.createBuffer(.{
        .type = .INDEXBUFFER,
        .data = gpu.asRange(indexBuffer),
    }));

    self.buffer = gpu.createBuffer(.{
        .type = .VERTEXBUFFER,
        .size = @sizeOf(gpu.Vertex) * vertexBuffer.len,
        .usage = .STREAM,
    });

    self.vertexBuffer = vertexBuffer;

    self.bindGroup.bindSampler(shader.SMP_smp, gpu.createSampler(.{}));
    self.pipeline = initPipeline();
    return self;
}

fn initPipeline() gpu.RenderPipeline {
    var vertexLayout = gpu.VertexLayout{};
    vertexLayout.attrs[shader.ATTR_single_position].format = .FLOAT3;
    vertexLayout.attrs[shader.ATTR_single_color0].format = .FLOAT4;
    vertexLayout.attrs[shader.ATTR_single_texcoord0].format = .FLOAT2;

    const shaderDesc = shader.singleShaderDesc(gpu.queryBackend());
    return gpu.createRenderPipeline(.{
        .shader = gpu.createShaderModule(shaderDesc),
        .vertexLayout = vertexLayout,
        .color = .{ .blend = .{
            .enabled = true,
            .src_factor_rgb = .SRC_ALPHA,
            .dst_factor_rgb = .ONE_MINUS_SRC_ALPHA,
        } },
        .index_type = .UINT16,
        .depth = .{ .compare = .LESS_EQUAL, .write_enabled = true },
    });
}

pub fn lookAt(self: *Camera, pos: math.Vector) void {
    const half = self.rect.size().scale(0.5);

    const max = self.border.sub(self.rect.size());
    const offset = pos.sub(half).clamp(.zero, max);

    self.rect = .init(offset, self.rect.size());
}

pub fn beginDraw(self: *Camera, color: gpu.Color) void {
    self.renderPass = gpu.commandEncoder.beginRenderPass(color);
    self.batchDrawCount = 0;
}

pub fn draw(self: *Camera, tex: gpu.Texture, position: math.Vector) void {
    self.drawFlipX(tex, position, false);
}

pub fn drawFlipX(self: *Camera, tex: gpu.Texture, pos: math.Vector, flipX: bool) void {
    const target: math.Rectangle = .init(pos, tex.size());
    var src = tex.area;
    if (flipX) {
        src.min.x = tex.area.max.x;
        src.max.x = tex.area.min.x;
    }

    self.drawOptions(.{ .texture = tex, .sourceRect = src, .targetRect = target });
}

pub const DrawOptions = gpu.DrawOptions;
pub fn drawOptions(self: *Camera, options: DrawOptions) void {
    self.matrix[12] = -1 - self.rect.min.x * self.matrix[0];
    self.matrix[13] = 1 - self.rect.min.y * self.matrix[5];

    // var src = options.sourceRect;
    // if (src.min.approx(.zero) and src.max.approx(.zero)) {
    //     src = options.texture.area;
    // }

    self.renderPass.setPipeline(self.pipeline);
    self.renderPass.setUniform(shader.UB_vs_params, .{ .vp = self.matrix });
    self.bindGroup.bindTexture(shader.IMG_tex, options.texture);

    gpu.draw(&self.renderPass, &self.bindGroup, options);
}

pub fn batchDraw(self: *Camera, texture: gpu.Texture, position: math.Vector) void {
    const size = gpu.queryTextureSize(texture.image);
    if (size.approx(.zero)) return;

    const sourceRect = texture.area;
    const min = sourceRect.min.div(size);
    const max = sourceRect.max.div(size);

    self.vertexBuffer[self.batchDrawCount * 4 + 0] = .{
        .position = position.addY(texture.size().y),
        .uv = .init(min.x, max.y),
    };

    self.vertexBuffer[self.batchDrawCount * 4 + 1] = .{
        .position = position.add(texture.size()),
        .uv = .init(max.x, max.y),
    };

    self.vertexBuffer[self.batchDrawCount * 4 + 2] = .{
        .position = position.addX(texture.size().x),
        .uv = .init(max.x, min.y),
    };

    self.vertexBuffer[self.batchDrawCount * 4 + 3] = .{
        .position = position,
        .uv = .init(min.x, min.y),
    };

    self.batchTexture = texture;
    self.batchDrawCount += 1;
}

const sk = @import("sokol");
pub fn endDraw(self: *Camera) void {

    if (self.batchDrawCount != 0) {
        for (self.vertexBuffer) |*value| {
            value.position.z = 0;
        }

        sk.gfx.updateBuffer(self.buffer, sk.gfx.asRange(self.vertexBuffer));

        self.bindGroup.bindVertexBuffer(0, self.buffer);
        self.renderPass.setPipeline(self.pipeline);
        self.bindGroup.bindTexture(shader.IMG_tex, self.batchTexture);
        self.renderPass.setUniform(shader.UB_vs_params, .{ .vp = self.matrix });
        self.renderPass.setBindGroup(self.bindGroup);
        sk.gfx.draw(0, 6 * self.batchDrawCount, 1);
    }

    self.renderPass.end();
    gpu.commandEncoder.submit();
}
```

## scene.zig

测试一下批量绘制是否成功，同时也增加了批量绘制 Buffer 的初始化。

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");

const titleScene = @import("scene/title.zig");
const worldScene = @import("scene/world.zig");
const battleScene = @import("scene/battle.zig");

const SceneType = enum { title, world, battle };
var currentSceneType: SceneType = .battle;

const SIZE: gfx.Vector = .init(1000, 800);
pub var camera: gfx.Camera = undefined;
pub var cursor: gfx.Texture = undefined;
var cursorTexture: gfx.Texture = undefined;

const MAX_COUNT = 100;

var vertexBuffer: [MAX_COUNT * 4]gfx.Vertex = undefined;
var indexBuffer: [MAX_COUNT * 6]u16 = undefined;

var texture: gfx.Texture = undefined;

pub fn init() void {
    var index: u16 = 0;
    while (index < MAX_COUNT) : (index += 1) {
        indexBuffer[index * 6 + 0] = index * 4 + 0;
        indexBuffer[index * 6 + 1] = index * 4 + 1;
        indexBuffer[index * 6 + 2] = index * 4 + 2;
        indexBuffer[index * 6 + 3] = index * 4 + 0;
        indexBuffer[index * 6 + 4] = index * 4 + 2;
        indexBuffer[index * 6 + 5] = index * 4 + 3;
    }
    camera = .init(.init(.zero, window.size), SIZE, &vertexBuffer, &indexBuffer);

    titleScene.init();
    worldScene.init(&camera);
    battleScene.init();
    window.showCursor(false);
    cursorTexture = gfx.loadTexture("assets/mc_1.png", .init(32, 32));
    texture = gfx.loadTexture("assets/fight/p1.png", .init(960, 240));
    cursor = cursorTexture;
    enter();
}

pub fn enter() void {
    sceneCall("enter", .{});
}

pub fn exit() void {
    sceneCall("exit", .{});
}

pub fn changeScene() void {
    exit();
    const next: usize = @intFromEnum(currentSceneType);
    const len = std.enums.values(SceneType).len;
    currentSceneType = @enumFromInt((next + 1) % len);
    enter();
}

pub fn update(delta: f32) void {
    cursor = cursorTexture;
    _ = delta;
    // sceneCall("update", .{delta});
}

pub fn render() void {
    camera.beginDraw(.{ .a = 1 });
    defer camera.endDraw();

    // sceneCall("render", .{&camera});

    camera.draw(cursor, window.mousePosition.add(camera.rect.min));

    var tex = texture.subTexture(.init(.zero, .init(240, 240)));
    camera.batchDraw(tex, .init(0, 0));

    tex = texture.subTexture(.init(.init(240, 0), .init(240, 240)));
    camera.batchDraw(tex, .init(800 - 240, 0));

    tex = texture.subTexture(.init(.init(480, 0), .init(240, 240)));
    camera.batchDraw(tex, .init(0, 600 - 240));

    tex = texture.subTexture(.init(.init(720, 0), .init(240, 240)));
    camera.batchDraw(tex, .init(800 - 240, 600 - 240));
}

fn sceneCall(comptime function: []const u8, args: anytype) void {
    switch (currentSceneType) {
        .title => window.call(titleScene, function, args),
        .world => window.call(worldScene, function, args),
        .battle => window.call(battleScene, function, args),
    }
}
```

## 效果

![批量绘制][1]

[1]: images/教你制作RPG游戏44.webp

## 附录
