# 0937-教你制作RPG游戏-战斗场景

## 目标

新增一个战斗场景，角色和敌人在战斗场景中战斗。

## 环境

- Time 2025-05-19

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

先做战斗场景，状态面板先不做了，涉及到很多状态和文字，后面来完善。

## scene.zig

新增了一个场景的类型，叫做 battle 战斗场景。

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");

const titleScene = @import("scene/title.zig");
const worldScene = @import("scene/world.zig");
const battleScene = @import("scene/battle.zig");

const SceneType = enum { title, world, battle };
var currentSceneType: SceneType = .battle;

const SIZE: gfx.Vector = .init(1000, 800);
pub var camera: gfx.Camera = undefined;
pub var cursor: gfx.Texture = undefined;
var cursorTexture: gfx.Texture = undefined;

pub fn init() void {
    camera = .init(.init(.zero, window.size), SIZE);
    titleScene.init();
    worldScene.init(&camera);
    battleScene.init();
    window.showCursor(false);
    cursorTexture = gfx.loadTexture("assets/mc_1.png", .init(32, 32));
    cursor = cursorTexture;
    enter();
}

pub fn enter() void {
    sceneCall("enter", .{});
}

pub fn exit() void {
    sceneCall("exit", .{});
}

pub fn changeScene() void {
    exit();
    const next: usize = @intFromEnum(currentSceneType);
    const len = std.enums.values(SceneType).len;
    currentSceneType = @enumFromInt((next + 1) % len);
    enter();
}

pub fn update(delta: f32) void {
    cursor = cursorTexture;
    sceneCall("update", .{delta});
}

pub fn render() void {
    camera.beginDraw(.{ .a = 1 });
    defer camera.endDraw();
    sceneCall("render", .{&camera});

    camera.draw(cursor, window.mousePosition.add(camera.rect.min));
}

fn sceneCall(comptime function: []const u8, args: anytype) void {
    switch (currentSceneType) {
        .title => window.call(titleScene, function, args),
        .world => window.call(worldScene, function, args),
        .battle => window.call(battleScene, function, args),
    }
}
```

## Player.zig

角色中新增了战斗场景需要显示的纹理。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");

const Player = @This();
const FrameAnimation = gfx.FixedFrameAnimation(4, 0.15);
const PLAYER_SPEED = 150;
const PlayerState = enum { walk, talk };

pub var position: gfx.Vector = .init(800, 500);
pub var state: PlayerState = .walk;
var facing: gfx.FourDirection = .down;
var keyPressed: bool = false;
var velocity: gfx.Vector = .zero;

index: u8,
roleTexture: gfx.Texture,
upAnimation: FrameAnimation = undefined,
downAnimation: FrameAnimation = undefined,
leftAnimation: FrameAnimation = undefined,
rightAnimation: FrameAnimation = undefined,

statusTexture: gfx.Texture = undefined,
attack: ?gfx.Texture = null,
defend: ?gfx.Texture = null,

battleTexture: gfx.Texture = undefined,
attackTexture: gfx.Texture = undefined,

pub fn init(index: u8) Player {
    var player = switch (index) {
        0 => initPlayer1(),
        1 => initPlayer2(),
        2 => initPlayer3(),
        else => unreachable,
    };

    var size: gfx.Vector = .init(960, 240);

    var area = gfx.Rectangle.init(.{ .y = 720 }, size);
    player.upAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 0 }, size);
    player.downAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 240 }, size);
    player.leftAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 480 }, size);
    player.rightAnimation = .init(player.roleTexture.subTexture(area));

    size = .init(240, 240);
    area = gfx.Rectangle.init(.{ .y = 0 }, size);
    player.attackTexture = player.battleTexture.subTexture(area);

    return player;
}

fn initPlayer1() Player {
    const role = window.loadTexture("assets/r1.png", .init(960, 960));
    return Player{
        .index = 0,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face1.png", .init(357, 317)),
        .attack = window.loadTexture("assets/item/item3.png", .init(66, 66)),
        .defend = window.loadTexture("assets/item/item5.png", .init(66, 66)),
        .battleTexture = window.loadTexture("assets/fight/p1.png", .init(960, 240)),
    };
}

fn initPlayer2() Player {
    const role = window.loadTexture("assets/r2.png", .init(960, 960));

    return Player{
        .index = 1,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face2.png", .init(357, 317)),
        .battleTexture = window.loadTexture("assets/fight/p2.png", .init(960, 240)),
    };
}

fn initPlayer3() Player {
    const role = window.loadTexture("assets/r3.png", .init(960, 960));
    return Player{
        .index = 2,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face3.png", .init(357, 317)),
        .battleTexture = window.loadTexture("assets/fight/p3.png", .init(960, 240)),
    };
}

pub fn update(self: *Player, delta: f32) void {
    velocity = .zero;
    keyPressed = false;

    if (world.mouseTarget) |target| {
        velocity = target.sub(position).normalize();
        if (@abs(velocity.x) > @abs(velocity.y)) {
            facing = if (velocity.x > 0) .right else .left;
        } else {
            facing = if (velocity.y > 0) .down else .up;
        }
        keyPressed = true;
        const distance = target.sub(position);
        if (@abs(distance.x) < 16 and @abs(distance.y) < 16) {
            velocity = .zero;
            world.mouseTarget = null;
        }
    }

    if (window.isAnyKeyDown(&.{ .UP, .W })) updatePlayer(.up);
    if (window.isAnyKeyDown(&.{ .DOWN, .S })) updatePlayer(.down);
    if (window.isAnyKeyDown(&.{ .LEFT, .A })) updatePlayer(.left);
    if (window.isAnyKeyDown(&.{ .RIGHT, .D })) updatePlayer(.right);

    if (window.isKeyRelease(.TAB)) {
        const playerIndex = (self.index + 1) % world.players.len;
        world.currentPlayer = &world.players[playerIndex];
    }

    if (velocity.approx(.zero)) {
        self.current(facing).reset();
    } else {
        velocity = velocity.normalize().scale(delta * PLAYER_SPEED);
        const tempPosition = position.add(velocity);
        if (world.map.canWalk(tempPosition)) position = tempPosition;
        world.playerCamera.lookAt(position);
    }

    if (keyPressed) self.current(facing).update(delta);
}

fn updatePlayer(direction: gfx.FourDirection) void {
    facing = direction;
    keyPressed = true;
    velocity = velocity.add(direction.toVector());
    world.mouseTarget = null;
}

pub fn render(self: *Player, camera: *gfx.Camera) void {
    const playerTexture = self.current(facing).currentTexture();
    camera.draw(playerTexture, position.sub(.init(120, 220)));
}

fn current(self: *Player, face: gfx.FourDirection) *FrameAnimation {
    return switch (face) {
        .up => &self.upAnimation,
        .down => &self.downAnimation,
        .left => &self.leftAnimation,
        .right => &self.rightAnimation,
    };
}
```

## battle.zig

不清楚渲染的时候，为什么不直接使用坐标，需要加一个偏移，可能后面有用？后面再看。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");

var background: gfx.Texture = undefined;

pub fn init() void {
    background = gfx.loadTexture("assets/fight/f_scene.png", .init(800, 600));
}

pub fn enter() void {
    window.playMusic("assets/fight/fight.ogg");
}

pub fn exit() void {
    window.stopMusic();
}

pub fn update(delta: f32) void {
    _ = delta;
}

pub fn render(camera: *gfx.Camera) void {
    camera.draw(background, .init(0, 0));

    const offset = gfx.Vector.init(120, 120).scale(-1);

    const player1 = &world.players[0];
    camera.draw(player1.attackTexture, offset.add(.init(617, 258)));

    const player2 = &world.players[1];
    camera.draw(player2.attackTexture, offset.add(.init(695, 361)));

    const player3 = &world.players[2];
    camera.draw(player3.attackTexture, offset.add(.init(588, 417)));
}
```

## 效果

![战斗场景][1]

[1]: images/教你制作RPG游戏41.webp

## 附录
