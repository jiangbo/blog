# 0953-教你制作RPG游戏-状态信息

## 目标

定义角色的生命，攻击等信息，然后在状态面板进行显示。

## 环境

- Time 2025-06-06
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

又要来定位布局了，Zig 目前还没有热加载，每次修改需要重启，有点慢。

## Player.zig

定义角色信息。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");

const Player = @This();
const FrameAnimation = gfx.FixedFrameAnimation(4, 0.15);
const PLAYER_SPEED = 150;
const PlayerState = enum { walk, talk };

pub var position: gfx.Vector = .init(800, 500);
pub var state: PlayerState = .walk;
var facing: gfx.FourDirection = .down;
var keyPressed: bool = false;
var velocity: gfx.Vector = .zero;

index: u8,
roleTexture: gfx.Texture,
upAnimation: FrameAnimation = undefined,
downAnimation: FrameAnimation = undefined,
leftAnimation: FrameAnimation = undefined,
rightAnimation: FrameAnimation = undefined,

statusTexture: gfx.Texture = undefined,
attack: ?gfx.Texture = null,
defend: ?gfx.Texture = null,

battleTexture: gfx.Texture = undefined,
attackTexture: gfx.Texture = undefined,
battleFace: gfx.Texture = undefined,

maxHealth: u32 = 100,
health: u32 = 100,
maxMana: u32 = 100,
mana: u32 = 100,
maxAttack: u32 = 10,
maxDefend: u32 = 10,
maxSpeed: u32 = 10,
maxLuck: u32 = 10,

pub fn init(index: u8) Player {
    var player = switch (index) {
        0 => initPlayer1(),
        1 => initPlayer2(),
        2 => initPlayer3(),
        else => unreachable,
    };

    var size: gfx.Vector = .init(960, 240);

    var area = gfx.Rectangle.init(.{ .y = 720 }, size);
    player.upAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 0 }, size);
    player.downAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 240 }, size);
    player.leftAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 480 }, size);
    player.rightAnimation = .init(player.roleTexture.subTexture(area));

    size = .init(240, 240);
    area = gfx.Rectangle.init(.{ .y = 0 }, size);
    player.attackTexture = player.battleTexture.subTexture(area);

    return player;
}

fn initPlayer1() Player {
    const role = window.loadTexture("assets/r1.png", .init(960, 960));
    return Player{
        .index = 0,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face1.png", .init(357, 317)),
        .attack = window.loadTexture("assets/item/item3.png", .init(66, 66)),
        .defend = window.loadTexture("assets/item/item5.png", .init(66, 66)),
        .battleTexture = window.loadTexture("assets/fight/p1.png", .init(960, 240)),
        .battleFace = window.loadTexture("assets/fight/fm_face1.png", .init(319, 216)),
    };
}

fn initPlayer2() Player {
    const role = window.loadTexture("assets/r2.png", .init(960, 960));

    return Player{
        .index = 1,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face2.png", .init(357, 317)),
        .battleTexture = window.loadTexture("assets/fight/p2.png", .init(960, 240)),
        .battleFace = window.loadTexture("assets/fight/fm_face2.png", .init(319, 216)),
    };
}

fn initPlayer3() Player {
    const role = window.loadTexture("assets/r3.png", .init(960, 960));
    return Player{
        .index = 2,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face3.png", .init(357, 317)),
        .battleTexture = window.loadTexture("assets/fight/p3.png", .init(960, 240)),
        .battleFace = window.loadTexture("assets/fight/fm_face3.png", .init(319, 216)),
    };
}

pub fn update(self: *Player, delta: f32) void {
    velocity = .zero;
    keyPressed = false;

    if (world.mouseTarget) |target| {
        velocity = target.sub(position).normalize();
        if (@abs(velocity.x) > @abs(velocity.y)) {
            facing = if (velocity.x > 0) .right else .left;
        } else {
            facing = if (velocity.y > 0) .down else .up;
        }
        keyPressed = true;
        const distance = target.sub(position);
        if (@abs(distance.x) < 16 and @abs(distance.y) < 16) {
            velocity = .zero;
            world.mouseTarget = null;
        }
    }

    if (window.isAnyKeyDown(&.{ .UP, .W })) updatePlayer(.up);
    if (window.isAnyKeyDown(&.{ .DOWN, .S })) updatePlayer(.down);
    if (window.isAnyKeyDown(&.{ .LEFT, .A })) updatePlayer(.left);
    if (window.isAnyKeyDown(&.{ .RIGHT, .D })) updatePlayer(.right);

    if (window.isKeyRelease(.TAB)) {
        const playerIndex = (self.index + 1) % world.players.len;
        world.currentPlayer = &world.players[playerIndex];
    }

    if (velocity.approx(.zero)) {
        self.current(facing).reset();
    } else {
        velocity = velocity.normalize().scale(delta * PLAYER_SPEED);
        const tempPosition = position.add(velocity);
        if (world.map.canWalk(tempPosition)) position = tempPosition;
        camera.lookAt(position);
    }

    if (keyPressed) self.current(facing).update(delta);
}

fn updatePlayer(direction: gfx.FourDirection) void {
    facing = direction;
    keyPressed = true;
    velocity = velocity.add(direction.toVector());
    world.mouseTarget = null;
}

pub fn render(self: *Player) void {
    const playerTexture = self.current(facing).currentTexture();
    camera.draw(playerTexture, position.sub(.init(120, 220)));
}

fn current(self: *Player, face: gfx.FourDirection) *FrameAnimation {
    return switch (face) {
        .up => &self.upAnimation,
        .down => &self.downAnimation,
        .left => &self.leftAnimation,
        .right => &self.rightAnimation,
    };
}
```

## statusPopup.zig

渲染了角色的状态文本。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");

pub const MenuType = enum { item, skill };

pub var display: bool = false;
var position: gfx.Vector = undefined;
var background: gfx.Texture = undefined;
var selected: gfx.Texture = undefined;
var itemTexture: gfx.Texture = undefined;
var skillTexture: gfx.Texture = undefined;

var selectedPlayer: usize = 0;
var selectedItem: usize = 0;
var menuType: MenuType = .item;

pub fn init() void {
    position = .init(58, 71);
    background = gfx.loadTexture("assets/item/status_bg.png", .init(677, 428));
    selected = gfx.loadTexture("assets/item/sbt7_2.png", .init(273, 90));
    itemTexture = gfx.loadTexture("assets/item/sbt2_1.png", .init(62, 255));
    skillTexture = gfx.loadTexture("assets/item/sbt2_2.png", .init(62, 255));
}

pub fn update(delta: f32) void {
    if (window.isAnyKeyRelease(&.{ .ESCAPE, .Q, .E })) display = false;

    if (window.isAnyKeyRelease(&.{ .LEFT, .A })) {
        selectedPlayer += world.players.len;
        selectedPlayer = (selectedPlayer - 1) % world.players.len;
    } else if (window.isAnyKeyRelease(&.{ .RIGHT, .D })) {
        selectedPlayer = (selectedPlayer + 1) % world.players.len;
    }

    if (window.isKeyRelease(.TAB)) {
        menuType = if (menuType == .item) .skill else .item;
    }

    _ = delta;
}

pub fn render() void {
    if (!display) return;

    camera.draw(background, position);

    renderStatus();

    var items: []world.Item = undefined;

    if (menuType == .item) {
        items = &world.items;
        camera.draw(itemTexture, position.add(.init(629, 51)));
    } else {
        items = &world.skills;
        camera.draw(skillTexture, position.add(.init(629, 51)));
    }

    var showItemCount: usize = 0;
    for (items) |item| {
        if (item.count == 0) continue;

        const offset = position.add(.init(360, 48));
        const pos = offset.addY(@floatFromInt(96 * showItemCount));
        camera.draw(item.texture, pos);

        if (selectedItem == showItemCount) {
            camera.draw(selected, pos.sub(.init(10, 10)));
        }

        showItemCount += 1;
        if (showItemCount >= 3) break;
    }
}

fn renderStatus() void {
    const player = &world.players[selectedPlayer];
    camera.draw(player.statusTexture, position);

    if (player.attack) |attack| {
        camera.draw(attack, position.add(.init(41, 55)));
    }

    if (player.defend) |defend| {
        camera.draw(defend, position.add(.init(41, 136)));
    }

    var buffer: [32]u8 = undefined;

    drawText(&buffer, .{player.health}, .init(155, 411));
    drawText(&buffer, .{player.mana}, .init(290, 411));
    drawText(&buffer, .{player.maxAttack}, .init(155, 431));
    drawText(&buffer, .{player.maxDefend}, .init(290, 431));
    drawText(&buffer, .{player.maxSpeed}, .init(155, 451));
    drawText(&buffer, .{player.maxLuck}, .init(290, 451));
}

fn drawText(buffer: []u8, args: anytype, pos: gfx.Vector) void {
    const text = std.fmt.bufPrint(buffer, "{}", args);
    camera.drawTextOptions(.{
        .text = text catch unreachable,
        .position = pos,
        .color = .{ .r = 0.21, .g = 0.09, .b = 0.01, .a = 1 },
    });
}
```

## 效果

![状态信息][1]

[1]: images/教你制作RPG游戏55.webp

## 附录
