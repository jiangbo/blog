# 0954-教你制作RPG游戏-装备效果

## 目标

给角色装备物品，状态界面显示物品增加的效果。

## 环境

- Time 2025-06-06
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

对于物品，感觉应该单独创建一个模块，将其从 world 场景中独立出来，后面看看需不需要。

## Player.zig

新增了物品相关信息。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");

const Player = @This();
const FrameAnimation = gfx.FixedFrameAnimation(4, 0.15);
const PLAYER_SPEED = 150;
const PlayerState = enum { walk, talk };

pub var position: gfx.Vector = .init(800, 500);
pub var state: PlayerState = .walk;
var facing: gfx.FourDirection = .down;
var keyPressed: bool = false;
var velocity: gfx.Vector = .zero;

index: u8,
roleTexture: gfx.Texture,
upAnimation: FrameAnimation = undefined,
downAnimation: FrameAnimation = undefined,
leftAnimation: FrameAnimation = undefined,
rightAnimation: FrameAnimation = undefined,

statusTexture: gfx.Texture = undefined,
attackItem: ?world.Item = null,
defendItem: ?world.Item = null,
totalItem: world.Item = .{ .texture = undefined },

battleTexture: gfx.Texture = undefined,
attackTexture: gfx.Texture = undefined,
battleFace: gfx.Texture = undefined,

maxHealth: u32 = 100,
health: u32 = 100,
maxMana: u32 = 100,
mana: u32 = 100,
attack: u32 = 10,
defend: u32 = 10,
speed: u32 = 10,
luck: u32 = 10,

pub fn init(index: u8) Player {
    var player = switch (index) {
        0 => initPlayer1(),
        1 => initPlayer2(),
        2 => initPlayer3(),
        else => unreachable,
    };

    var size: gfx.Vector = .init(960, 240);

    var area = gfx.Rectangle.init(.{ .y = 720 }, size);
    player.upAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 0 }, size);
    player.downAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 240 }, size);
    player.leftAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 480 }, size);
    player.rightAnimation = .init(player.roleTexture.subTexture(area));

    size = .init(240, 240);
    area = gfx.Rectangle.init(.{ .y = 0 }, size);
    player.attackTexture = player.battleTexture.subTexture(area);

    return player;
}

fn initPlayer1() Player {
    const role = window.loadTexture("assets/r1.png", .init(960, 960));
    var player = Player{
        .index = 0,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face1.png", .init(357, 317)),
        .battleTexture = window.loadTexture("assets/fight/p1.png", .init(960, 240)),
        .battleFace = window.loadTexture("assets/fight/fm_face1.png", .init(319, 216)),
    };

    // .attack = window.loadTexture("assets/item/item3.png", .init(66, 66)),
    // .defend = window.loadTexture("assets/item/item5.png", .init(66, 66)),
    player.useItem(world.items[2]);
    return player;
}

fn initPlayer2() Player {
    const role = window.loadTexture("assets/r2.png", .init(960, 960));

    return Player{
        .index = 1,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face2.png", .init(357, 317)),
        .battleTexture = window.loadTexture("assets/fight/p2.png", .init(960, 240)),
        .battleFace = window.loadTexture("assets/fight/fm_face2.png", .init(319, 216)),
    };
}

fn initPlayer3() Player {
    const role = window.loadTexture("assets/r3.png", .init(960, 960));
    return Player{
        .index = 2,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face3.png", .init(357, 317)),
        .battleTexture = window.loadTexture("assets/fight/p3.png", .init(960, 240)),
        .battleFace = window.loadTexture("assets/fight/fm_face3.png", .init(319, 216)),
    };
}

pub fn useItem(self: *Player, item: world.Item) void {
    // 1 表示武器，2 表示防具
    if (1 == item.value1) {
        self.attackItem = item;
    } else if (2 == item.value1) {
        self.defendItem = item;
    }

    self.totalItem = .{ .texture = undefined };
    if (self.attackItem) |*i| self.totalItem.addValue(i);
    if (self.defendItem) |*i| self.totalItem.addValue(i);
}

pub fn update(self: *Player, delta: f32) void {
    velocity = .zero;
    keyPressed = false;

    if (world.mouseTarget) |target| {
        velocity = target.sub(position).normalize();
        if (@abs(velocity.x) > @abs(velocity.y)) {
            facing = if (velocity.x > 0) .right else .left;
        } else {
            facing = if (velocity.y > 0) .down else .up;
        }
        keyPressed = true;
        const distance = target.sub(position);
        if (@abs(distance.x) < 16 and @abs(distance.y) < 16) {
            velocity = .zero;
            world.mouseTarget = null;
        }
    }

    if (window.isAnyKeyDown(&.{ .UP, .W })) updatePlayer(.up);
    if (window.isAnyKeyDown(&.{ .DOWN, .S })) updatePlayer(.down);
    if (window.isAnyKeyDown(&.{ .LEFT, .A })) updatePlayer(.left);
    if (window.isAnyKeyDown(&.{ .RIGHT, .D })) updatePlayer(.right);

    if (window.isKeyRelease(.TAB)) {
        const playerIndex = (self.index + 1) % world.players.len;
        world.currentPlayer = &world.players[playerIndex];
    }

    if (velocity.approx(.zero)) {
        self.current(facing).reset();
    } else {
        velocity = velocity.normalize().scale(delta * PLAYER_SPEED);
        const tempPosition = position.add(velocity);
        if (world.map.canWalk(tempPosition)) position = tempPosition;
        camera.lookAt(position);
    }

    if (keyPressed) self.current(facing).update(delta);
}

fn updatePlayer(direction: gfx.FourDirection) void {
    facing = direction;
    keyPressed = true;
    velocity = velocity.add(direction.toVector());
    world.mouseTarget = null;
}

pub fn render(self: *Player) void {
    const playerTexture = self.current(facing).currentTexture();
    camera.draw(playerTexture, position.sub(.init(120, 220)));
}

fn current(self: *Player, face: gfx.FourDirection) *FrameAnimation {
    return switch (face) {
        .up => &self.upAnimation,
        .down => &self.downAnimation,
        .left => &self.leftAnimation,
        .right => &self.rightAnimation,
    };
}
```

## world.zig

物品新增了几个属性。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const camera = @import("../camera.zig");

pub const Player = @import("Player.zig");
pub const map = @import("map.zig");
const dialog = @import("dialog.zig");
const statusPopup = @import("statusPopup.zig");
const scene = @import("../scene.zig");

const Tip = struct {
    var background: gfx.Texture = undefined;
    content: []const u8,
};

pub const Item = struct {
    name: []const u8 = &.{},
    count: u32 = 0,
    texture: gfx.Texture,
    tip: []const u8 = &.{},
    value1: u32 = 0,
    value2: u32 = 0,
    value3: u32 = 0,
    value4: u32 = 0,
    value5: u32 = 0,

    pub fn addValue(self: *Item, other: *const Item) void {
        self.value2 += other.value2;
        self.value3 += other.value3;
        self.value4 += other.value4;
        self.value5 += other.value5;
    }
};
pub var items: [10]Item = undefined;
pub var skills: [10]Item = undefined;

pub var players: [3]Player = undefined;
pub var currentPlayer: *Player = &players[0];

var tip: ?Tip = null;
var talkTexture: gfx.Texture = undefined;

pub var mouseTarget: ?gfx.Vector = null;
var targetTexture: gfx.Texture = undefined;
var moveTimer: window.Timer = .init(0.4);
var moveDisplay: bool = true;

pub fn init() void {
    initItems();

    players[0] = Player.init(0);
    players[1] = Player.init(1);
    players[2] = Player.init(2);

    Tip.background = gfx.loadTexture("assets/msgtip.png", .init(291, 42));
    targetTexture = gfx.loadTexture("assets/move_flag.png", .init(33, 37));

    talkTexture = gfx.loadTexture("assets/mc_2.png", .init(30, 30));

    statusPopup.init();
    dialog.init();

    map.init();

    initSkills();
}

fn initItems() void {
    for (&items) |*item| item.count = 0;

    items[0] = .{
        .name = "红药水",
        .texture = gfx.loadTexture("assets/item/item1.png", .init(66, 66)),
        .tip = "恢复少量 HP",
        .count = 2,
    };

    items[1] = .{
        .name = "蓝药水",
        .texture = gfx.loadTexture("assets/item/item2.png", .init(66, 66)),
        .tip = "恢复少量 MP",
        .count = 3,
    };

    items[2] = .{
        .name = "短剑",
        .texture = gfx.loadTexture("assets/item/item3.png", .init(66, 66)),
        .tip = "一把钢制短剑",
        .count = 2,
        .value1 = 1,
        .value2 = 5,
        .value4 = 1,
    };
}

fn initSkills() void {
    for (&skills) |*skill| skill.count = 0;

    skills[0] = .{
        .name = "治疗术",
        .texture = gfx.loadTexture("assets/item/skill1.png", .init(66, 66)),
        .tip = "恢复少量 HP",
        .count = 20,
    };

    skills[1] = .{
        .name = "黑洞漩涡",
        .texture = gfx.loadTexture("assets/item/skill2.png", .init(66, 66)),
        .tip = "攻击型技能，将敌人吸入漩涡",
        .count = 20,
    };
}

pub fn enter() void {
    window.playMusic("assets/1.ogg");
}

pub fn exit() void {
    window.stopMusic();
}

pub fn update(delta: f32) void {
    const confirm = window.isAnyKeyRelease(&.{ .SPACE, .ENTER }) or
        window.isButtonRelease(.LEFT);

    if (dialog.active) return if (confirm) dialog.update(delta);

    if (tip) |_| {
        if (confirm) tip = null;
        return;
    }

    if (statusPopup.display) return statusPopup.update(delta);

    if (!statusPopup.display and (window.isAnyKeyRelease(&.{ .ESCAPE, .Q, .E }))) {
        statusPopup.display = true;
    }

    if (window.isButtonRelease(.LEFT)) {
        mouseTarget = camera.toWorldPosition(window.mousePosition);
    }

    if (mouseTarget != null) {
        if (moveTimer.isFinishedAfterUpdate(delta)) {
            moveDisplay = !moveDisplay;
            moveTimer.reset();
        }
    }

    currentPlayer.update(delta);

    for (map.npcSlice()) |*npc| {
        const contains = npc.area.contains(Player.position);
        if (contains) {
            if (npc.keyTrigger) {
                if (window.isAnyKeyRelease(&.{ .SPACE, .ENTER }))
                    npc.action();
            } else npc.action();
        }

        if (npc.texture != null) {
            const area = npc.area.move(camera.rect.min.neg());
            if (area.contains(window.mousePosition)) {
                scene.cursor = talkTexture;
                if (window.isButtonRelease(.LEFT) and contains) {
                    npc.action();
                }
            }
        }
        map.updateNpc(npc, delta);
    }
}

pub fn render() void {
    map.drawBackground();

    var playerNotDraw: bool = true;
    for (map.npcSlice()) |npc| {
        if (npc.position.y > Player.position.y and playerNotDraw) {
            currentPlayer.render();
            playerNotDraw = false;
        }

        const npcPosition = npc.position.sub(.init(120, 220));

        if (npc.animation != null and !npc.animation.?.finished()) {
            camera.draw(npc.animation.?.currentTexture(), npcPosition);
        } else if (npc.texture) |texture| {
            camera.draw(texture, npcPosition);
        }
    }

    if (playerNotDraw) currentPlayer.render();

    if (mouseTarget) |target| blk: {
        if (!moveDisplay) break :blk;
        const size = targetTexture.size();
        camera.draw(targetTexture, target.sub(.init(size.x / 2, size.y)));
    }

    map.drawForeground();
    renderPopup();

    window.showFrameRate();
}

fn renderPopup() void {
    camera.lookAt(.zero);

    if (dialog.active) dialog.render();

    if (tip) |t| {
        camera.draw(Tip.background, .init(251, 200));
        camera.drawText(t.content, .init(340, 207));
    }
    statusPopup.render();
    camera.lookAt(Player.position);
}

pub fn showDialog(npc: *map.NPC) void {
    dialog.show(npc);
}

pub fn showTip() void {
    tip = Tip{ .content = "遇到一个人" };
}
```

## statusPopup.zig

渲染了角色的物品效果。

```zig
...
fn renderStatus() void {
    const player = &world.players[selectedPlayer];
    camera.draw(player.statusTexture, position);

    var buffer: [32]u8 = undefined;
    if (player.attackItem) |item| {
        camera.draw(item.texture, position.add(.init(41, 55)));
    }

    if (player.defendItem) |item| {
        camera.draw(item.texture, position.add(.init(41, 136)));
    }

    drawText(&buffer, .{player.health}, .init(155, 411));
    drawText(&buffer, .{player.mana}, .init(290, 411));

    const item = &player.totalItem;
    drawText(&buffer, .{player.attack}, .init(155, 431));
    drawEffect(&buffer, .{item.value2}, .init(185, 431));
    drawText(&buffer, .{player.defend}, .init(290, 431));
    drawEffect(&buffer, .{item.value3}, .init(320, 431));

    drawText(&buffer, .{player.speed}, .init(155, 451));
    drawEffect(&buffer, .{item.value4}, .init(185, 451));
    drawText(&buffer, .{player.luck}, .init(290, 451));
    drawEffect(&buffer, .{item.value5}, .init(320, 451));
}

fn drawText(buffer: []u8, args: anytype, pos: gfx.Vector) void {
    const text = std.fmt.bufPrint(buffer, "{}", args);
    camera.drawTextOptions(.{
        .text = text catch unreachable,
        .position = pos,
        .color = .{ .r = 0.21, .g = 0.09, .b = 0.01, .a = 1 },
    });
}

fn drawEffect(buffer: []u8, args: anytype, pos: gfx.Vector) void {
    const text = std.fmt.bufPrint(buffer, "+{}", args);
    camera.drawTextOptions(.{
        .text = text catch unreachable,
        .position = pos,
        .color = .{ .r = 1, .a = 1 },
    });
}
...
```

## 效果

![状态信息][1]

[1]: images/教你制作RPG游戏56.webp

## 附录
