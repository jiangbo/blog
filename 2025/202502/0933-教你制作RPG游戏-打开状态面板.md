# 0933-教你制作RPG游戏-打开状态面板

## 目标

使用 Q 或者 E 键可以打开状态面板，查看人物的状态和物品。

## 环境

- Time 2025-05-13

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

还是没有实现文字的渲染，想先看看能不能把这个做完，后面来补充涉及到文字的地方。

## statusPopup.zig

新增了一个状态弹出框，用来显示人物的状态和物品信息。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

pub var display: bool = false;
var position: gfx.Vector = undefined;
var background: gfx.Texture = undefined;

pub fn init() void {
    position = .init(60, 60);
    background = gfx.loadTexture("assets/item/status_bg.png", .init(677, 428));
}

pub fn update(delta: f32) void {
    if (window.isAnyKeyRelease(&.{ .ESCAPE, .Q, .E })) display = false;

    _ = delta;
}

pub fn render(camera: *gfx.Camera) void {
    if (!display) return;

    camera.draw(background, position);
}
```

## world.zig

新增了状态弹出框的逻辑。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

pub const Player = @import("Player.zig");
pub const map = @import("map.zig");
const statusPopup = @import("statusPopup.zig");
const scene = @import("../scene.zig");

const Dialog = struct {
    var background: gfx.Texture = undefined;
    face: gfx.Texture = undefined,
    left: bool = true,
    npc: *map.NPC = undefined,
};

const Tip = struct {
    var background: gfx.Texture = undefined;
};

pub var players: [3]Player = undefined;
pub var currentPlayer: *Player = &players[0];
pub var playerCamera: *gfx.Camera = undefined;

var dialog: ?Dialog = null;
var face: gfx.Texture = undefined;

var tip: ?Tip = null;
var talkTexture: gfx.Texture = undefined;

pub var mouseTarget: ?gfx.Vector = null;
var targetTexture: gfx.Texture = undefined;
var moveTimer: window.Timer = .init(0.4);
var moveDisplay: bool = true;

pub fn init(camera: *gfx.Camera) void {
    players[0] = .init("assets/r1.png", 0);
    players[1] = .init("assets/r2.png", 1);
    players[2] = .init("assets/r3.png", 2);

    Dialog.background = gfx.loadTexture("assets/msg.png", .init(790, 163));
    face = gfx.loadTexture("assets/face1_1.png", .init(307, 355));
    playerCamera = camera;

    Tip.background = gfx.loadTexture("assets/msgtip.png", .init(291, 42));
    targetTexture = gfx.loadTexture("assets/move_flag.png", .init(33, 37));

    talkTexture = gfx.loadTexture("assets/mc_2.png", .init(30, 30));

    statusPopup.init();

    map.init();
}

pub fn enter() void {
    playerCamera.lookAt(Player.position);
    window.playMusic("assets/1.ogg");
}

pub fn exit() void {
    playerCamera.lookAt(.zero);
    window.stopMusic();
}

pub fn update(delta: f32) void {
    const confirm = window.isAnyKeyRelease(&.{ .SPACE, .ENTER }) or
        window.isButtonRelease(.LEFT);

    if (dialog) |*d| {
        if (confirm) {
            if (d.left) d.left = false else dialog = null;
        }
        return;
    }

    if (tip) |_| {
        if (confirm) tip = null;
        return;
    }

    if (statusPopup.display) return statusPopup.update(delta);

    if (!statusPopup.display and (window.isAnyKeyRelease(&.{ .ESCAPE, .Q, .E }))) {
        statusPopup.display = true;
    }

    if (window.isButtonRelease(.LEFT)) {
        mouseTarget = playerCamera.rect.min.add(window.mousePosition);
    }

    if (mouseTarget != null) {
        if (moveTimer.isFinishedAfterUpdate(delta)) {
            moveDisplay = !moveDisplay;
            moveTimer.reset();
        }
    }

    currentPlayer.update(delta);

    for (map.npcSlice()) |*npc| {
        const contains = npc.area.contains(Player.position);
        if (contains) {
            if (npc.keyTrigger) {
                if (window.isAnyKeyRelease(&.{ .SPACE, .ENTER }))
                    npc.action();
            } else npc.action();
        }

        if (npc.texture != null) {
            const area = npc.area.move(scene.camera.rect.min.neg());
            if (area.contains(window.mousePosition)) {
                scene.cursor = talkTexture;
                if (window.isButtonRelease(.LEFT) and contains) {
                    npc.action();
                }
            }
        }
        map.updateNpc(npc, delta);
    }
}

pub fn render(camera: *gfx.Camera) void {
    map.drawBackground(camera);

    var playerNotDraw: bool = true;
    for (map.npcSlice()) |npc| {
        if (npc.position.y > Player.position.y and playerNotDraw) {
            currentPlayer.render(camera);
            playerNotDraw = false;
        }

        const npcPosition = npc.position.sub(.init(120, 220));

        if (npc.animation != null and !npc.animation.?.finished()) {
            camera.draw(npc.animation.?.currentTexture(), npcPosition);
        } else if (npc.texture) |texture| {
            camera.draw(texture, npcPosition);
        }

        // camera.drawRectangle(npc.area);
    }

    if (playerNotDraw) currentPlayer.render(camera);

    if (mouseTarget) |target| blk: {
        if (!moveDisplay) break :blk;
        const size = targetTexture.size();
        camera.draw(targetTexture, target.sub(.init(size.x / 2, size.y)));
    }

    map.drawForeground(camera);
    renderPopup(camera);

    window.showFrameRate();
}

fn renderPopup(camera: *gfx.Camera) void {
    camera.lookAt(.zero);
    if (dialog) |d| {
        camera.draw(Dialog.background, .init(0, 415));
        if (d.left) {
            camera.draw(d.face, .init(0, 245));
        } else {
            camera.draw(d.npc.face.?, .init(486, 245));
        }
    }

    if (tip) |_| {
        camera.draw(Tip.background, .init(251, 200));
    }
    statusPopup.render(camera);
    camera.lookAt(Player.position);
}

pub fn showDialog(npc: *map.NPC) void {
    dialog = Dialog{ .face = face, .npc = npc };
}

pub fn showTip() void {
    tip = Tip{};
}
```

## 效果

![打开状态面板][1]

[1]: images/教你制作RPG游戏37.webp

## 附录
