# 0931-教你制作RPG游戏-鼠标移动

## 目标

在世界场景中，使用鼠标点击地图，角色可以寻路过去。

## 环境

- Time 2025-05-11

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

使用了最简单的直接移动的方式，如果之间有阻挡物，会导致卡住。

## world.zig

新增了目标坐标，以及对应的目标点的提示图片，寻路的过程中，目的地会进行闪烁。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

pub const Player = @import("Player.zig");
pub const map = @import("map.zig");

const Dialog = struct {
    var background: gfx.Texture = undefined;
    face: gfx.Texture = undefined,
    left: bool = true,
    npc: *map.NPC = undefined,
};

const Tip = struct {
    var background: gfx.Texture = undefined;
};

pub var players: [3]Player = undefined;
pub var currentPlayer: *Player = &players[0];
pub var playerCamera: *gfx.Camera = undefined;

var dialog: ?Dialog = null;
var face: gfx.Texture = undefined;

var tip: ?Tip = null;

pub var mouseTarget: ?gfx.Vector = null;
var targetTexture: gfx.Texture = undefined;
var moveTimer: window.Timer = .init(0.4);
var moveDisplay: bool = true;

pub fn init(camera: *gfx.Camera) void {
    players[0] = .init("assets/r1.png", 0);
    players[1] = .init("assets/r2.png", 1);
    players[2] = .init("assets/r3.png", 2);

    Dialog.background = gfx.loadTexture("assets/msg.png", .init(790, 163));
    face = gfx.loadTexture("assets/face1_1.png", .init(307, 355));
    playerCamera = camera;

    Tip.background = gfx.loadTexture("assets/msgtip.png", .init(291, 42));
    targetTexture = gfx.loadTexture("assets/move_flag.png", .init(33, 37));

    map.init();
}

pub fn enter() void {
    playerCamera.lookAt(Player.position);
    window.playMusic("assets/1.ogg");
}

pub fn exit() void {
    playerCamera.lookAt(.zero);
    window.stopMusic();
}

pub fn update(delta: f32) void {
    if (dialog) |*d| {
        if (window.isKeyRelease(.SPACE)) {
            if (d.left) d.left = false else dialog = null;
        }
        return;
    }

    if (tip) |_| {
        if (window.isKeyRelease(.SPACE)) tip = null;
        return;
    }

    if (window.isButtonRelease(.LEFT)) {
        mouseTarget = playerCamera.rect.min.add(window.mousePosition);
    }

    if (mouseTarget != null) {
        if (moveTimer.isFinishedAfterUpdate(delta)) {
            moveDisplay = !moveDisplay;
            moveTimer.reset();
        }
    }

    currentPlayer.update(delta);

    for (map.npcSlice()) |*npc| {
        if (npc.area.contains(Player.position)) {
            if (npc.keyTrigger) {
                if (window.isKeyRelease(.SPACE)) npc.action();
            } else npc.action();
        }

        map.updateNpc(npc, delta);
    }
}

pub fn render(camera: *gfx.Camera) void {
    map.drawBackground(camera);

    var playerNotDraw: bool = true;
    for (map.npcSlice()) |npc| {
        if (npc.position.y > Player.position.y and playerNotDraw) {
            currentPlayer.render(camera);
            playerNotDraw = false;
        }

        const npcPosition = npc.position.sub(.init(120, 220));

        if (npc.animation != null and !npc.animation.?.finished()) {
            camera.draw(npc.animation.?.currentTexture(), npcPosition);
        } else if (npc.texture) |texture| {
            camera.draw(texture, npcPosition);
        }

        // camera.drawRectangle(npc.area);
    }

    if (playerNotDraw) currentPlayer.render(camera);

    if (mouseTarget) |target| blk: {
        if (!moveDisplay) break :blk;
        const size = targetTexture.size();
        camera.draw(targetTexture, target.sub(.init(size.x / 2, size.y)));
    }

    map.drawForeground(camera);

    if (dialog) |d| {
        camera.lookAt(.zero);
        camera.draw(Dialog.background, .init(0, 415));
        if (d.left) {
            camera.draw(d.face, .init(0, 245));
        } else {
            camera.draw(d.npc.face.?, .init(486, 245));
        }
        camera.lookAt(Player.position);
    }

    if (tip) |_| {
        camera.lookAt(.zero);
        camera.draw(Tip.background, .init(251, 200));
        camera.lookAt(Player.position);
    }

    window.showFrameRate();
}

pub fn showDialog(npc: *map.NPC) void {
    dialog = Dialog{ .face = face, .npc = npc };
}

pub fn showTip() void {
    tip = Tip{};
}
```

## Player.zig

角色会自动行走到目标点，如果没有阻挡物的话。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");

const Player = @This();
const FrameAnimation = gfx.FixedFrameAnimation(4, 0.15);
const PLAYER_SPEED = 150;
const PlayerState = enum { walk, talk };

pub var position: gfx.Vector = .init(800, 500);
pub var state: PlayerState = .walk;
var facing: gfx.FourDirection = .down;
var keyPressed: bool = false;
var velocity: gfx.Vector = .zero;

index: u8,
upAnimation: FrameAnimation,
downAnimation: FrameAnimation,
leftAnimation: FrameAnimation,
rightAnimation: FrameAnimation,

pub fn init(path: [:0]const u8, index: u8) Player {
    const role = window.loadTexture(path, .init(960, 960));
    const size: gfx.Vector = .init(960, 240);

    return Player{
        .index = index,
        .upAnimation = .init(role.subTexture(.init(.{ .y = 720 }, size))),
        .downAnimation = .init(role.subTexture(.init(.{ .y = 0 }, size))),
        .leftAnimation = .init(role.subTexture(.init(.{ .y = 240 }, size))),
        .rightAnimation = .init(role.subTexture(.init(.{ .y = 480 }, size))),
    };
}

pub fn update(self: *Player, delta: f32) void {
    velocity = .zero;
    keyPressed = false;

    if (world.mouseTarget) |target| {
        velocity = target.sub(position).normalize();
        if (@abs(velocity.x) > @abs(velocity.y)) {
            facing = if (velocity.x > 0) .right else .left;
        } else {
            facing = if (velocity.y > 0) .down else .up;
        }
        keyPressed = true;
        const distance = target.sub(position);
        if (@abs(distance.x) < 16 and @abs(distance.y) < 16) {
            velocity = .zero;
            world.mouseTarget = null;
        }
    }

    if (window.isAnyKeyDown(&.{ .UP, .W })) updatePlayer(.up);
    if (window.isAnyKeyDown(&.{ .DOWN, .S })) updatePlayer(.down);
    if (window.isAnyKeyDown(&.{ .LEFT, .A })) updatePlayer(.left);
    if (window.isAnyKeyDown(&.{ .RIGHT, .D })) updatePlayer(.right);

    if (window.isKeyRelease(.TAB)) {
        const playerIndex = (self.index + 1) % world.players.len;
        world.currentPlayer = &world.players[playerIndex];
    }

    if (velocity.approx(.zero)) {
        self.current(facing).reset();
    } else {
        velocity = velocity.normalize().scale(delta * PLAYER_SPEED);
        const tempPosition = position.add(velocity);
        if (world.map.canWalk(tempPosition)) position = tempPosition;
        world.playerCamera.lookAt(position);
    }

    if (keyPressed) self.current(facing).update(delta);
}

fn updatePlayer(direction: gfx.FourDirection) void {
    facing = direction;
    keyPressed = true;
    velocity = velocity.add(direction.toVector());
    world.mouseTarget = null;
}

pub fn render(self: *Player, camera: *gfx.Camera) void {
    const playerTexture = self.current(facing).currentTexture();
    camera.draw(playerTexture, position.sub(.init(120, 220)));
}

fn current(self: *Player, face: gfx.FourDirection) *FrameAnimation {
    return switch (face) {
        .up => &self.upAnimation,
        .down => &self.downAnimation,
        .left => &self.leftAnimation,
        .right => &self.rightAnimation,
    };
}
```

## 效果

![鼠标移动][1]

[1]: images/教你制作RPG游戏35.webp

## 附录
