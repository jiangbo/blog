# 0927-教你制作RPG游戏-提取相机

## 目标

之前绘制的状态在 graphics.zig 和 gpu.zig 中，想将这些状态提取出来，统一到相机中。

## 环境

- Time 2025-05-10

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

把 debug 的框线绘制去掉了，后面看是再加上还是使用其它的办法。
提取了一个相机模块，将所有的绘制状态保存到里面，所以绘制的方法就不是直接调用 gfx 而是相机了。

## Camera.zig

提取一个相机模块来保存所有的绘制状态。

```zig
const std = @import("std");

const gpu = @import("gpu.zig");
const math = @import("math.zig");
const shader = @import("shader/single.glsl.zig");

const Camera = @This();
var commandEncoder: gpu.CommandEncoder = .{};

rect: math.Rectangle,
border: math.Vector,
matrix: [16]f32 = undefined,
renderPass: gpu.RenderPassEncoder = undefined,
bindGroup: gpu.BindGroup = .{},
pipeline: gpu.RenderPipeline = undefined,

pub fn init(rect: math.Rectangle, border: math.Vector) Camera {
    var self: Camera = .{ .rect = rect, .border = border };

    self.matrix = .{
        2 / rect.size().x, 0.0,                0.0, 0.0,
        0.0,               2 / -rect.size().y, 0.0, 0.0,
        0.0,               0.0,                1,   0.0,
        -1,                1,                  0,   1.0,
    };

    self.bindGroup.bindIndexBuffer(gpu.createBuffer(.{
        .type = .INDEXBUFFER,
        .data = gpu.asRange(&[_]u16{ 0, 1, 2, 0, 2, 3 }),
    }));

    self.bindGroup.bindSampler(shader.SMP_smp, gpu.createSampler(.{}));
    self.pipeline = initPipeline();
    return self;
}

fn initPipeline() gpu.RenderPipeline {
    var vertexLayout = gpu.VertexLayout{};
    vertexLayout.attrs[shader.ATTR_single_position].format = .FLOAT3;
    vertexLayout.attrs[shader.ATTR_single_color0].format = .FLOAT4;
    vertexLayout.attrs[shader.ATTR_single_texcoord0].format = .FLOAT2;

    const shaderDesc = shader.singleShaderDesc(gpu.queryBackend());
    return gpu.createRenderPipeline(.{
        .shader = gpu.createShaderModule(shaderDesc),
        .vertexLayout = vertexLayout,
        .color = .{ .blend = .{
            .enabled = true,
            .src_factor_rgb = .SRC_ALPHA,
            .dst_factor_rgb = .ONE_MINUS_SRC_ALPHA,
        } },
        .index_type = .UINT16,
        .depth = .{ .compare = .LESS_EQUAL, .write_enabled = true },
    });
}

pub fn lookAt(self: *Camera, pos: math.Vector) void {
    const half = self.rect.size().scale(0.5);

    const max = self.border.sub(self.rect.size());
    const offset = pos.sub(half).clamp(.zero, max);

    self.rect = .init(offset, self.rect.size());
}

pub fn beginDraw(self: *Camera, color: gpu.Color) void {
    self.renderPass = commandEncoder.beginRenderPass(color);
}

pub fn draw(self: *Camera, tex: gpu.Texture, position: math.Vector) void {
    self.drawFlipX(tex, position, false);
}

pub fn drawFlipX(self: *Camera, tex: gpu.Texture, pos: math.Vector, flipX: bool) void {
    const target: math.Rectangle = .init(pos, tex.size());
    var src = tex.area;
    if (flipX) {
        src.min.x = tex.area.max.x;
        src.max.x = tex.area.min.x;
    }

    self.drawOptions(.{ .texture = tex, .sourceRect = src, .targetRect = target });
}

pub const DrawOptions = gpu.DrawOptions;
pub fn drawOptions(self: *Camera, options: DrawOptions) void {
    self.matrix[12] = -1 - self.rect.min.x * self.matrix[0];
    self.matrix[13] = 1 - self.rect.min.y * self.matrix[5];

    // var src = options.sourceRect;
    // if (src.min.approx(.zero) and src.max.approx(.zero)) {
    //     src = options.texture.area;
    // }

    self.renderPass.setPipeline(self.pipeline);
    self.renderPass.setUniform(shader.UB_vs_params, .{ .vp = self.matrix });
    self.bindGroup.bindTexture(shader.IMG_tex, options.texture);

    gpu.draw(&self.renderPass, &self.bindGroup, options);
}

pub fn endDraw(self: *Camera) void {
    self.renderPass.end();
    commandEncoder.submit();
}
```

## gpu.zig

这个模块想的是存储 GPU 相关和平台相关的，比如 sokol，或者 DirectX 这些，想做成 WebGPU 类似的接口。

```zig
const std = @import("std");

const sk = @import("sokol");
const math = @import("math.zig");

pub const Rectangle = math.Rectangle;

pub const Texture = struct {
    image: sk.gfx.Image,
    area: Rectangle = .{},

    pub fn width(self: *const Texture) f32 {
        return self.size().x;
    }

    pub fn height(self: *const Texture) f32 {
        return self.size().y;
    }

    pub fn size(self: *const Texture) math.Vector {
        return self.area.size();
    }

    pub fn subTexture(self: *const Texture, area: Rectangle) Texture {
        return Texture{ .image = self.image, .area = .{
            .min = self.area.min.add(area.min),
            .max = self.area.min.add(area.max),
        } };
    }

    pub fn mapTexture(self: *const Texture, area: Rectangle) Texture {
        return Texture{ .image = self.image, .area = area };
    }

    pub fn deinit(self: *Texture) void {
        sk.gfx.destroyImage(self.image);
    }
};

fn queryTextureSize(image: sk.gfx.Image) math.Vector {
    return math.Vector{
        .x = @floatFromInt(sk.gfx.queryImageWidth(image)),
        .y = @floatFromInt(sk.gfx.queryImageHeight(image)),
    };
}

pub const asRange = sk.gfx.asRange;
pub const queryBackend = sk.gfx.queryBackend;
pub const Buffer = sk.gfx.Buffer;
pub const Color = sk.gfx.Color;
pub const Sampler = sk.gfx.Sampler;
pub const Shader = sk.gfx.Shader;
pub const VertexLayout = sk.gfx.VertexLayoutState;

pub fn createBuffer(desc: sk.gfx.BufferDesc) Buffer {
    return sk.gfx.makeBuffer(desc);
}

pub const RenderPipelineDesc = struct {
    shader: sk.gfx.Shader,
    vertexLayout: VertexLayout,
    primitive: sk.gfx.PrimitiveType = .TRIANGLES,
    color: sk.gfx.ColorTargetState = .{},
    index_type: sk.gfx.IndexType = .DEFAULT,
    depth: sk.gfx.DepthState = .{},
};

pub fn createRenderPipeline(desc: RenderPipelineDesc) RenderPipeline {
    return .{ .value = sk.gfx.makePipeline(.{
        .shader = desc.shader,
        .layout = desc.vertexLayout,
        .primitive_type = desc.primitive,
        .colors = init: {
            var c: [4]sk.gfx.ColorTargetState = @splat(.{});
            c[0] = desc.color;
            break :init c;
        },
        .index_type = desc.index_type,
        .depth = desc.depth,
    }) };
}

pub fn createShaderModule(desc: sk.gfx.ShaderDesc) sk.gfx.Shader {
    return sk.gfx.makeShader(desc);
}

pub fn createSampler(desc: sk.gfx.SamplerDesc) Sampler {
    return sk.gfx.makeSampler(desc);
}

pub const BindGroup = struct {
    value: sk.gfx.Bindings = .{},

    pub fn bindIndexBuffer(self: *BindGroup, buffer: Buffer) void {
        self.value.index_buffer = buffer;
    }

    pub fn bindVertexBuffer(self: *BindGroup, index: u32, buffer: Buffer) void {
        self.value.vertex_buffers[index] = buffer;
    }

    pub fn bindTexture(self: *BindGroup, index: u32, texture: Texture) void {
        self.value.images[index] = texture.image;
    }

    pub fn bindSampler(self: *BindGroup, index: u32, sampler: Sampler) void {
        self.value.samplers[index] = sampler;
    }
};

pub const CommandEncoder = struct {
    pub fn beginRenderPass(_: CommandEncoder, color: Color) RenderPassEncoder {
        var action = sk.gfx.PassAction{};
        action.colors[0] = .{ .load_action = .CLEAR, .clear_value = color };
        sk.gfx.beginPass(.{ .action = action, .swapchain = sk.glue.swapchain() });
        return RenderPassEncoder{};
    }

    pub fn submit(_: *CommandEncoder) void {
        sk.gfx.commit();
    }
};

pub const RenderPassEncoder = struct {
    pub fn setPipeline(self: *RenderPassEncoder, pipeline: RenderPipeline) void {
        _ = self;
        sk.gfx.applyPipeline(pipeline.value);
    }

    pub fn setBindGroup(self: *RenderPassEncoder, group: BindGroup) void {
        _ = self;
        sk.gfx.applyBindings(group.value);
    }

    pub fn setUniform(self: *RenderPassEncoder, index: u32, uniform: anytype) void {
        _ = self;
        sk.gfx.applyUniforms(index, sk.gfx.asRange(&uniform));
    }

    pub fn draw(self: *RenderPassEncoder, number: u32) void {
        _ = self;
        sk.gfx.draw(0, number, 1);
    }

    pub fn end(self: *RenderPassEncoder) void {
        _ = self;
        sk.gfx.endPass();
    }
};

pub const DrawOptions = struct {
    texture: Texture,
    sourceRect: Rectangle,
    targetRect: Rectangle,
    radians: f32 = 0,
    pivot: math.Vector = .zero,
    alpha: f32 = 1,
};

pub fn draw(renderPass: *RenderPassEncoder, bind: *BindGroup, options: DrawOptions) void {
    const dst = options.targetRect;

    const size = queryTextureSize(options.texture.image);
    if (size.approx(.zero)) return;

    const min = options.sourceRect.min.div(size);
    const max = options.sourceRect.max.div(size);

    var vertex = [_]math.Vector3{
        .{ .x = dst.min.x, .y = dst.max.y },
        .{ .x = dst.max.x, .y = dst.max.y },
        .{ .x = dst.max.x, .y = dst.min.y },
        .{ .x = dst.min.x, .y = dst.min.y },
    };

    if (options.radians != 0) {
        const percent = options.pivot.div(size);
        const pivot = dst.min.add(percent.mul(dst.size()));

        for (&vertex) |*point| {
            point.* = pivot.add(point.sub(pivot).rotate(options.radians));
        }
    }

    const vertexes = [_]f32{
        // 顶点和颜色
        vertex[0].x, vertex[0].y, 0.5, 1.0, 1.0, 1.0, options.alpha, min.x, max.y, // 左上
        vertex[1].x, vertex[1].y, 0.5, 1.0, 1.0, 1.0, options.alpha, max.x, max.y, // 右上
        vertex[2].x, vertex[2].y, 0.5, 1.0, 1.0, 1.0, options.alpha, max.x, min.y, // 右下
        vertex[3].x, vertex[3].y, 0.5, 1.0, 1.0, 1.0, options.alpha, min.x, min.y, // 左下
    };

    const vertexBuffer = sk.gfx.makeBuffer(.{
        .data = sk.gfx.asRange(&vertexes),
    });

    bind.bindVertexBuffer(0, vertexBuffer);
    renderPass.setBindGroup(bind.*);

    sk.gfx.draw(0, 6, 1);
    sk.gfx.destroyBuffer(vertexBuffer);
}

pub const RenderPipeline = struct {
    value: sk.gfx.Pipeline,
};
```

## 效果

其它涉及场景中绘制的代码需要传递相机，这块就不列出来了，后面肯定会列出来的。没有改变逻辑，效果不变。

![提取相机][1]

[1]: images/教你制作RPG游戏31.webp

## 附录
