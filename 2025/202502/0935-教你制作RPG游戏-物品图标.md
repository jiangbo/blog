# 0935-教你制作RPG游戏-物品图标

## 目标

将人物的装备图片还有物品栏中的物品的图标进行显示。

## 环境

- Time 2025-05-13

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

感觉 UI 界面显示的坐标定位也还需要研究一下，不过现在直接根据教程写就行。

## Player.zig

新增了一个攻击装备栏，一个防御装备栏。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");

const Player = @This();
const FrameAnimation = gfx.FixedFrameAnimation(4, 0.15);
const PLAYER_SPEED = 150;
const PlayerState = enum { walk, talk };

pub var position: gfx.Vector = .init(800, 500);
pub var state: PlayerState = .walk;
var facing: gfx.FourDirection = .down;
var keyPressed: bool = false;
var velocity: gfx.Vector = .zero;

index: u8,
roleTexture: gfx.Texture,
upAnimation: FrameAnimation = undefined,
downAnimation: FrameAnimation = undefined,
leftAnimation: FrameAnimation = undefined,
rightAnimation: FrameAnimation = undefined,

statusTexture: gfx.Texture = undefined,
attack: ?gfx.Texture = null,
defend: ?gfx.Texture = null,

pub fn init(index: u8) Player {
    var player = switch (index) {
        0 => initPlayer1(),
        1 => initPlayer2(),
        2 => initPlayer3(),
        else => unreachable,
    };

    const size: gfx.Vector = .init(960, 240);

    var area = gfx.Rectangle.init(.{ .y = 720 }, size);
    player.upAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 0 }, size);
    player.downAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 240 }, size);
    player.leftAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 480 }, size);
    player.rightAnimation = .init(player.roleTexture.subTexture(area));

    return player;
}

fn initPlayer1() Player {
    const role = window.loadTexture("assets/r1.png", .init(960, 960));
    return Player{
        .index = 0,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face1.png", .init(357, 317)),
        .attack = window.loadTexture("assets/item/item3.png", .init(66, 66)),
        .defend = window.loadTexture("assets/item/item5.png", .init(66, 66)),
    };
}

fn initPlayer2() Player {
    const role = window.loadTexture("assets/r2.png", .init(960, 960));

    return Player{
        .index = 1,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face2.png", .init(357, 317)),
    };
}

fn initPlayer3() Player {
    const role = window.loadTexture("assets/r3.png", .init(960, 960));
    return Player{
        .index = 2,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face3.png", .init(357, 317)),
    };
}

pub fn update(self: *Player, delta: f32) void {
    velocity = .zero;
    keyPressed = false;

    if (world.mouseTarget) |target| {
        velocity = target.sub(position).normalize();
        if (@abs(velocity.x) > @abs(velocity.y)) {
            facing = if (velocity.x > 0) .right else .left;
        } else {
            facing = if (velocity.y > 0) .down else .up;
        }
        keyPressed = true;
        const distance = target.sub(position);
        if (@abs(distance.x) < 16 and @abs(distance.y) < 16) {
            velocity = .zero;
            world.mouseTarget = null;
        }
    }

    if (window.isAnyKeyDown(&.{ .UP, .W })) updatePlayer(.up);
    if (window.isAnyKeyDown(&.{ .DOWN, .S })) updatePlayer(.down);
    if (window.isAnyKeyDown(&.{ .LEFT, .A })) updatePlayer(.left);
    if (window.isAnyKeyDown(&.{ .RIGHT, .D })) updatePlayer(.right);

    if (window.isKeyRelease(.TAB)) {
        const playerIndex = (self.index + 1) % world.players.len;
        world.currentPlayer = &world.players[playerIndex];
    }

    if (velocity.approx(.zero)) {
        self.current(facing).reset();
    } else {
        velocity = velocity.normalize().scale(delta * PLAYER_SPEED);
        const tempPosition = position.add(velocity);
        if (world.map.canWalk(tempPosition)) position = tempPosition;
        world.playerCamera.lookAt(position);
    }

    if (keyPressed) self.current(facing).update(delta);
}

fn updatePlayer(direction: gfx.FourDirection) void {
    facing = direction;
    keyPressed = true;
    velocity = velocity.add(direction.toVector());
    world.mouseTarget = null;
}

pub fn render(self: *Player, camera: *gfx.Camera) void {
    const playerTexture = self.current(facing).currentTexture();
    camera.draw(playerTexture, position.sub(.init(120, 220)));
}

fn current(self: *Player, face: gfx.FourDirection) *FrameAnimation {
    return switch (face) {
        .up => &self.upAnimation,
        .down => &self.downAnimation,
        .left => &self.leftAnimation,
        .right => &self.rightAnimation,
    };
}
```

## statusPopup.zig

实现了渲染装备和物品的方法。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");

pub var display: bool = false;
var position: gfx.Vector = undefined;
var background: gfx.Texture = undefined;
var selectedPlayer: usize = 0;

pub fn init() void {
    position = .init(58, 71);
    background = gfx.loadTexture("assets/item/status_bg.png", .init(677, 428));
}

pub fn update(delta: f32) void {
    if (window.isAnyKeyRelease(&.{ .ESCAPE, .Q, .E })) display = false;

    if (window.isAnyKeyRelease(&.{ .LEFT, .A })) {
        selectedPlayer += world.players.len;
        selectedPlayer = (selectedPlayer - 1) % world.players.len;
    } else if (window.isAnyKeyRelease(&.{ .RIGHT, .D })) {
        selectedPlayer = (selectedPlayer + 1) % world.players.len;
    }

    _ = delta;
}

pub fn render(camera: *gfx.Camera) void {
    if (!display) return;

    camera.draw(background, position);

    const player = &world.players[selectedPlayer];
    camera.draw(player.statusTexture, position);

    if (player.attack) |attack| {
        camera.draw(attack, position.add(.init(41, 55)));
    }

    if (player.defend) |defend| {
        camera.draw(defend, position.add(.init(41, 136)));
    }

    var showItemCount: f32 = 0;
    for (&world.items) |item| {
        if (item.count == 0) continue;

        const offset = position.add(.init(360, 48));
        camera.draw(item.texture, offset.addY(96 * showItemCount));
        showItemCount += 1;
        if (showItemCount >= 3) break;
    }
}
```

## world.zig

先将物品栏的信息放到场景中。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

pub const Player = @import("Player.zig");
pub const map = @import("map.zig");
const statusPopup = @import("statusPopup.zig");
const scene = @import("../scene.zig");

const Dialog = struct {
    var background: gfx.Texture = undefined;
    face: gfx.Texture = undefined,
    left: bool = true,
    npc: *map.NPC = undefined,
};

const Tip = struct {
    var background: gfx.Texture = undefined;
};

pub const Item = struct {
    name: []const u8,
    count: u32 = 0,
    texture: gfx.Texture,
    tip: []const u8,
};
pub var items: [10]Item = undefined;

pub var players: [3]Player = undefined;
pub var currentPlayer: *Player = &players[0];
pub var playerCamera: *gfx.Camera = undefined;

var dialog: ?Dialog = null;
var face: gfx.Texture = undefined;

var tip: ?Tip = null;
var talkTexture: gfx.Texture = undefined;

pub var mouseTarget: ?gfx.Vector = null;
var targetTexture: gfx.Texture = undefined;
var moveTimer: window.Timer = .init(0.4);
var moveDisplay: bool = true;

pub fn init(camera: *gfx.Camera) void {
    players[0] = Player.init(0);
    players[1] = Player.init(1);
    players[2] = Player.init(2);

    Dialog.background = gfx.loadTexture("assets/msg.png", .init(790, 163));
    face = gfx.loadTexture("assets/face1_1.png", .init(307, 355));
    playerCamera = camera;

    Tip.background = gfx.loadTexture("assets/msgtip.png", .init(291, 42));
    targetTexture = gfx.loadTexture("assets/move_flag.png", .init(33, 37));

    talkTexture = gfx.loadTexture("assets/mc_2.png", .init(30, 30));

    statusPopup.init();

    map.init();

    for (&items) |*item| item.count = 0;

    items[0] = .{
        .name = "红药水",
        .texture = gfx.loadTexture("assets/item/item1.png", .init(66, 66)),
        .tip = "恢复少量 HP",
        .count = 2,
    };

    items[1] = .{
        .name = "蓝药水",
        .texture = gfx.loadTexture("assets/item/item2.png", .init(66, 66)),
        .tip = "恢复少量 MP",
        .count = 3,
    };

    items[2] = .{
        .name = "短剑",
        .texture = gfx.loadTexture("assets/item/item3.png", .init(66, 66)),
        .tip = "一把钢制短剑",
        .count = 2,
    };
}

pub fn enter() void {
    playerCamera.lookAt(Player.position);
    window.playMusic("assets/1.ogg");
}

pub fn exit() void {
    playerCamera.lookAt(.zero);
    window.stopMusic();
}

pub fn update(delta: f32) void {
    const confirm = window.isAnyKeyRelease(&.{ .SPACE, .ENTER }) or
        window.isButtonRelease(.LEFT);

    if (dialog) |*d| {
        if (confirm) {
            if (d.left) d.left = false else dialog = null;
        }
        return;
    }

    if (tip) |_| {
        if (confirm) tip = null;
        return;
    }

    if (statusPopup.display) return statusPopup.update(delta);

    if (!statusPopup.display and (window.isAnyKeyRelease(&.{ .ESCAPE, .Q, .E }))) {
        statusPopup.display = true;
    }

    if (window.isButtonRelease(.LEFT)) {
        mouseTarget = playerCamera.rect.min.add(window.mousePosition);
    }

    if (mouseTarget != null) {
        if (moveTimer.isFinishedAfterUpdate(delta)) {
            moveDisplay = !moveDisplay;
            moveTimer.reset();
        }
    }

    currentPlayer.update(delta);

    for (map.npcSlice()) |*npc| {
        const contains = npc.area.contains(Player.position);
        if (contains) {
            if (npc.keyTrigger) {
                if (window.isAnyKeyRelease(&.{ .SPACE, .ENTER }))
                    npc.action();
            } else npc.action();
        }

        if (npc.texture != null) {
            const area = npc.area.move(scene.camera.rect.min.neg());
            if (area.contains(window.mousePosition)) {
                scene.cursor = talkTexture;
                if (window.isButtonRelease(.LEFT) and contains) {
                    npc.action();
                }
            }
        }
        map.updateNpc(npc, delta);
    }
}

pub fn render(camera: *gfx.Camera) void {
    map.drawBackground(camera);

    var playerNotDraw: bool = true;
    for (map.npcSlice()) |npc| {
        if (npc.position.y > Player.position.y and playerNotDraw) {
            currentPlayer.render(camera);
            playerNotDraw = false;
        }

        const npcPosition = npc.position.sub(.init(120, 220));

        if (npc.animation != null and !npc.animation.?.finished()) {
            camera.draw(npc.animation.?.currentTexture(), npcPosition);
        } else if (npc.texture) |texture| {
            camera.draw(texture, npcPosition);
        }

        // camera.drawRectangle(npc.area);
    }

    if (playerNotDraw) currentPlayer.render(camera);

    if (mouseTarget) |target| blk: {
        if (!moveDisplay) break :blk;
        const size = targetTexture.size();
        camera.draw(targetTexture, target.sub(.init(size.x / 2, size.y)));
    }

    map.drawForeground(camera);
    renderPopup(camera);

    window.showFrameRate();
}

fn renderPopup(camera: *gfx.Camera) void {
    camera.lookAt(.zero);
    if (dialog) |d| {
        camera.draw(Dialog.background, .init(0, 415));
        if (d.left) {
            camera.draw(d.face, .init(0, 245));
        } else {
            camera.draw(d.npc.face.?, .init(486, 245));
        }
    }

    if (tip) |_| {
        camera.draw(Tip.background, .init(251, 200));
    }
    statusPopup.render(camera);
    camera.lookAt(Player.position);
}

pub fn showDialog(npc: *map.NPC) void {
    dialog = Dialog{ .face = face, .npc = npc };
}

pub fn showTip() void {
    tip = Tip{};
}
```

## 效果

![物品图标][1]

[1]: images/教你制作RPG游戏39.webp

## 附录
