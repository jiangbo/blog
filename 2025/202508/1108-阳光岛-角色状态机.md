# 1108-阳光岛-角色状态机

## 目标

角色有几个状态，全部用 if/else 的话，太乱了，所以创建状态机来进行管理。

## 环境

- Time 2026-01-23
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1jf9XYQEhW>

## 想法

教程中使用的继承来实现状态机，而 Zig 不支持继承，所以需要用其它方式来实现。

## player.zig

新增了一个 IdleState 状态。

```zig
const std = @import("std");
const zhu = @import("zhu");

const moveForce = 200; // 移动力
const factor = 0.85; // 减速因子
const maxSpeed = 120; // 最大速度

const size: zhu.Vector2 = .xy(32, 32);
var image: zhu.graphics.Image = undefined;

var velocity: zhu.Vector2 = .zero;
var position: zhu.Vector2 = undefined;
var state: State = .idle;

pub fn init(pos: zhu.Vector2) void {
    position = pos;
    const foxy = zhu.getImage("textures/Actors/foxy.png");
    image = foxy.sub(.init(.zero, size));
    state.enter();
}

pub fn update(delta: f32) void {
    state.update(delta);
}

pub fn draw() void {
    zhu.batch.drawImage(image, position, .{
        .flipX = velocity.x < 0,
    });
}

const State = union(enum) {
    idle: IdleState,
    // walk: WalkState,
    // jump: JumpState,
    // fall: FallState,

    fn enter(self: State) void {
        switch (self) {
            inline else => |case| @TypeOf(case).enter(),
        }
    }

    fn update(self: State, delta: f32) void {
        switch (self) {
            inline else => |case| @TypeOf(case).update(delta),
        }
    }

    fn draw(self: State) void {
        switch (self) {
            inline else => |case| @TypeOf(case).draw(),
        }
    }
};

const IdleState = struct {
    fn enter() void {
        std.log.info("enter idle", .{});
    }

    fn update(delta: f32) void {
        if (zhu.window.isKeyDown(.A)) {
            if (velocity.x > 0) velocity.x = 0;
            velocity.x -= moveForce * delta;
        } else if (zhu.window.isKeyDown(.D)) {
            if (velocity.x < 0) velocity.x = 0;
            velocity.x += moveForce * delta;
        } else {
            // 没有按的时候，减少速度
            velocity.x *= factor;
        }

        velocity.x = std.math.clamp(velocity.x, -maxSpeed, maxSpeed);
        position = position.add(velocity.scale(delta));
    }
};

const WalkState = struct {};
const JumpState = struct {};
const FallState = struct {};
```

## 效果

没有改变任何逻辑，效果和之前一样。

![角色状态机][1]

[1]: images/阳光岛07.gif

## 附录
