# 1135-阳光岛-共享游戏数据

## 目标

角色的游戏数据可以保存，当开始游戏时加载之前的进度。

## 环境

- Time 2026-01-30
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1jf9XYQEhW>

## 想法

主要修改了场景模块中的保存和加载。

## scene.zig

```zig
const std = @import("std");
const zhu = @import("zhu");

const window = zhu.window;
const batch = zhu.batch;

const map = @import("map.zig");
const player = @import("player.zig");
const object = @import("object.zig");

var help = false;
var debug = false;

const Session = extern struct {
    level: u8 = 0,
    health: u8 = 3,
    score: u32 = 0,
    highScore: u32 = 0,
};

const savePath = "save/save.dat";
var level: u8 = 0;
var highScore: u32 = 0;

pub fn init() void {
    const session = loadSession();
    level = session.level;
    highScore = session.highScore;
    map.init(session.level);

    for (map.objects.items, 0..) |obj, index| {
        if (obj.type != .player) continue;
        player.init(obj.position, obj.size);
        player.health = session.health;
        player.score = session.score;
        _ = map.objects.swapRemove(index);
        break;
    }
    object.init(map.objects);

    zhu.audio.playMusic("assets/audio/hurry_up_and_run.ogg");
}

fn loadSession() Session {
    var buffer: [64]u8 = undefined;
    const content = zhu.window.readBuffer(savePath, &buffer) catch {
        return .{};
    };

    var reader = std.Io.Reader.fixed(content);
    return reader.takeStruct(Session, .little) catch unreachable;
}

fn saveSession() !void {
    const session = Session{
        .level = level,
        .health = player.health,
        .score = player.score,
        .highScore = @max(player.score, highScore),
    };

    var buffer: [64]u8 = undefined;
    var writer = std.Io.Writer.fixed(&buffer);
    try writer.writeStruct(session, .little);

    try zhu.window.saveAll(savePath, buffer[0..writer.end]);
}

pub fn changeNextLevel() void {
    level += 1;
    saveSession() catch unreachable;
    init();
}

pub fn deinit() void {
    map.deinit();
}

pub fn update(delta: f32) void {
    if (window.isKeyReleased(.H)) help = !help;
    if (window.isKeyReleased(.X)) debug = !debug;

    if (window.isKeyDown(.LEFT_ALT) and window.isKeyReleased(.ENTER)) {
        return window.toggleFullScreen();
    }

    player.update(delta);
    object.update(delta);
}

pub fn draw() void {
    zhu.batch.beginDraw(.black);
    defer zhu.batch.endDraw();

    map.draw();
    object.draw();
    player.draw();

    if (help) drawHelpInfo() else if (debug) window.drawDebugInfo();
}

fn drawHelpInfo() void {
    const text =
        \\按键说明：
        \\上：W，下：S，左：A，右：D
        \\确定：F，取消：Q，菜单：E
        \\帮助：H  按一次打开，再按一次关闭
    ;
    zhu.text.drawColor(text, .xy(10, 10), .green);
}
```

## 效果

![共享游戏数据][1]

[1]: images/阳光岛32.png

## 附录
