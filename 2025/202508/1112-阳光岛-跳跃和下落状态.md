# 1112-阳光岛-跳跃和下落状态

## 目标

给角色新增了两个状态，跳跃和下落状态。

## 环境

- Time 2026-01-24
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1jf9XYQEhW>

## 想法

先加上再说，不清楚有没有其它的问题，遇到再解决。

## player.zig

新增了跳跃和下落状态。

```zig
const std = @import("std");
const zhu = @import("zhu");

const tiled = zhu.extend.tiled;

const map = @import("map.zig");

const moveForce = 200; // 移动力
const factor = 0.85; // 减速因子
const maxSpeed = 120; // 最大速度
const gravity = 980; // 重力
const jumpForce = 350.0; // 跳跃力

const size: zhu.Vector2 = .xy(32, 32);
var image: zhu.graphics.Image = undefined;

var force: zhu.Vector2 = .xy(0, gravity);
var velocity: zhu.Vector2 = .zero;
pub var position: zhu.Vector2 = undefined;
var state: State = .idle;

pub fn init(pos: zhu.Vector2) void {
    position = pos;
    const foxy = zhu.getImage("textures/Actors/foxy.png");
    image = foxy.sub(.init(.zero, size));
    state.enter();
}

pub fn update(delta: f32) void {
    state.update(delta);

    velocity = velocity.add(force.scale(delta));
    velocity.x = std.math.clamp(velocity.x, -maxSpeed, maxSpeed);
    const toPosition = position.add(velocity.scale(delta));

    const clamped = map.clamp(position, toPosition, size);
    // std.log.info("old: {}, new: {}, clamped: {}", .{ position, toPosition, clamped });
    if (clamped.x == position.x) velocity.x = 0;
    if (clamped.y == position.y) velocity.y = 0;
    position = clamped;
}

pub fn draw() void {
    zhu.batch.drawImage(image, position, .{
        .flipX = velocity.x < 0,
    });
}

const State = union(enum) {
    idle: IdleState,
    walk: WalkState,
    jump: JumpState,
    fall: FallState,

    fn enter(self: State) void {
        switch (self) {
            inline else => |case| @TypeOf(case).enter(),
        }
    }

    fn update(self: State, delta: f32) void {
        switch (self) {
            inline else => |case| @TypeOf(case).update(delta),
        }
    }

    fn draw(self: State) void {
        switch (self) {
            inline else => |case| @TypeOf(case).draw(),
        }
    }
};

fn changeState(newState: State) void {
    state = newState;
    state.enter();
}

const IdleState = struct {
    fn enter() void {
        std.log.info("enter idle", .{});
    }

    fn update(_: f32) void {
        if (zhu.window.isAnyKeyPress(&.{ .W, .SPACE })) {
            changeState(.jump);
        } else if (zhu.window.isAnyKeyDown(&.{ .A, .D })) {
            changeState(.walk);
        } else velocity.x *= factor; // 减速
    }
};

const WalkState = struct {
    fn enter() void {
        std.log.info("enter walk", .{});
    }

    fn update(_: f32) void {
        if (zhu.window.isAnyKeyPress(&.{ .W, .SPACE })) {
            changeState(.jump);
        } else if (zhu.window.isKeyDown(.A)) {
            if (velocity.x > 0) velocity.x = 0;
            force.x = -moveForce;
        } else if (zhu.window.isKeyDown(.D)) {
            if (velocity.x < 0) velocity.x = 0;
            force.x = moveForce;
        } else {
            force.x = 0;
            changeState(.idle);
        }
    }
};
const JumpState = struct {
    fn enter() void {
        std.log.info("enter jump", .{});
        velocity.y = -jumpForce;
    }

    fn update(_: f32) void {
        if (velocity.y > 0) {
            changeState(.fall);
        }
    }
};
const FallState = struct {
    fn enter() void {
        std.log.info("enter fall", .{});
    }

    fn update(_: f32) void {
        if (velocity.y == 0) {
            changeState(.idle);
        }
    }
};
```

## 效果

![跳跃和下落状态][1]

[1]: images/阳光岛10.gif

## 附录
