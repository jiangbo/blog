# 1123-阳光岛-角色和对象碰撞

## 目标

角色可以和对象碰撞，有两种碰撞方式，上方碰撞就是踩踏，其它方向就是受伤。

## 环境

- Time 2026-01-27
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1jf9XYQEhW>

## 想法

根据教程中的方式，实现了区分踩踏和普通碰撞的功能。

## object.zig

区分了不同的碰撞方式，播放了敌人的死亡动画。

```zig
const std = @import("std");
const zhu = @import("zhu");

const batch = zhu.batch;
const tiled = zhu.extend.tiled;

const map = @import("map.zig");
const getImage = zhu.assets.getImage;
const player = @import("player.zig");

const gemFrames = zhu.graphics.framesX(5, .xy(15, 13), 0.2);
const cherryFrames = zhu.graphics.loopFramesX(5, .xy(21, 21), 0.2);
const opossumFrames = zhu.graphics.framesX(6, .xy(36, 28), 0.1);
const eagleFrames = zhu.graphics.framesX(4, .xy(40, 41), 0.15);
const itemFrames = zhu.graphics.framesX(5, .xy(32, 32), 0.1);
const deadFrames = zhu.graphics.framesX(6, .xy(40, 41), 0.1);

var gemAnimation: zhu.graphics.FrameAnimation = undefined;
var cherryAnimation: zhu.graphics.FrameAnimation = undefined;
var opossumAnimation: zhu.graphics.FrameAnimation = undefined;
var eagleAnimation: zhu.graphics.FrameAnimation = undefined;
var itemAnimation: zhu.graphics.FrameAnimation = undefined;
var deadAnimation: zhu.graphics.FrameAnimation = undefined;

const frogEnum = enum { idle, jump, fall };
const frogIdleFrames = zhu.graphics.framesX(4, .xy(35, 32), 0.3);
const frogJumpFrames: [1]zhu.graphics.Frame = .{
    .{ .area = .init(.xy(35, 32), .xy(35, 32)), .interval = 0.1 },
};
const frogFallFrames: [1]zhu.graphics.Frame = .{
    .{ .area = .init(.xy(70, 32), .xy(35, 32)), .interval = 0.1 },
};
var frogAnimations: zhu.graphics.EnumFrameAnimation(frogEnum) = undefined;
var frogState: frogEnum = .idle;

const Animation = struct {
    center: zhu.Vector2, // 需要播放的中心点
    effect: zhu.graphics.FrameAnimation,
};

var effectArray: [10]Animation = undefined;
var effectAnimations: std.ArrayList(Animation) = undefined;
var objects: std.ArrayList(map.Object) = undefined;

pub fn init(obj: std.ArrayList(map.Object)) void {
    objects = obj;

    const gemImage = getImage(@intFromEnum(map.ObjectEnum.gem));
    gemAnimation = .init(gemImage, &gemFrames);

    const cherryImage = getImage(@intFromEnum(map.ObjectEnum.cherry));
    cherryAnimation = .init(cherryImage, &cherryFrames);

    const opossumImage = getImage(@intFromEnum(map.ObjectEnum.opossum));
    opossumAnimation = .init(opossumImage, &opossumFrames);

    const eagleImage = getImage(@intFromEnum(map.ObjectEnum.eagle));
    eagleAnimation = .init(eagleImage, &eagleFrames);

    const frogImage = getImage(@intFromEnum(map.ObjectEnum.frog));
    frogAnimations.set(.idle, .init(frogImage, &frogIdleFrames));
    frogAnimations.set(.jump, .init(frogImage, &frogJumpFrames));
    frogAnimations.set(.fall, .init(frogImage, &frogFallFrames));

    const itemImage = zhu.getImage("textures/FX/item-feedback.png");
    itemAnimation = .init(itemImage, &itemFrames);
    const deadImage = zhu.getImage("textures/FX/enemy-deadth.png");
    deadAnimation = .init(deadImage, &deadFrames);
    effectAnimations = .initBuffer(&effectArray);
}

pub fn update(delta: f32) void {
    gemAnimation.loopUpdate(delta);
    cherryAnimation.loopUpdate(delta);
    opossumAnimation.loopUpdate(delta);
    eagleAnimation.loopUpdate(delta);
    frogAnimations.getPtr(frogState).loopUpdate(delta);

    { // 特效动画
        var iterator = std.mem.reverseIterator(effectAnimations.items);
        while (iterator.nextPtr()) |animation| {
            if (animation.effect.isFinishedOnceUpdate(delta)) {
                _ = effectAnimations.swapRemove(iterator.index);
            }
        }
    }

    if (player.state == .dead) return;

    const playerRect = player.collideRect();
    var iterator = std.mem.reverseIterator(objects.items);
    while (iterator.nextPtr()) |item| {
        // 检测碰撞框
        if (item.object == null) continue;
        const obj = item.object.?;
        const pos = item.position.add(obj.position);
        const rect = zhu.Rect.init(pos, obj.size); // 碰撞区域
        if (playerRect.intersect(rect)) {
            // 玩家与物体发生碰撞，根据不同对象，不同处理。
            const delete = blk: switch (item.type) {
                .gem, .cherry => collideItem(item, rect.center()),
                .opossum, .eagle, .frog => {
                    const area = playerRect.overlapArea(rect);
                    if (playerRect.center().y < area.center().y and
                        area.size.x > area.size.y)
                    {
                        // 从上方碰撞，击败敌人
                        break :blk collideEnemy(item, rect.center());
                    } else {
                        player.hurt();
                        break :blk false;
                    }
                },
                else => false,
            };
            if (delete) _ = objects.swapRemove(iterator.index);
        }
    }
}

fn collideItem(_: *map.Object, center: zhu.Vector2) bool {
    // 播放特效动画
    effectAnimations.appendAssumeCapacity(.{
        .center = center,
        .effect = itemAnimation,
    });
    return true;
}

fn collideEnemy(_: *map.Object, center: zhu.Vector2) bool {
    // 播放特效动画
    effectAnimations.appendAssumeCapacity(.{
        .center = center,
        .effect = deadAnimation,
    });
    player.velocity.y = -300; // 弹起
    return true;
}

pub fn draw() void {
    for (objects.items) |item| {
        const image: ?zhu.graphics.Image = switch (item.type) {
            .gem => gemAnimation.currentImage(),
            .cherry => cherryAnimation.currentImage(),
            .opossum => opossumAnimation.currentImage(),
            .eagle => eagleAnimation.currentImage(),
            .frog => frogAnimations.get(frogState).currentImage(),
            else => null,
        };

        if (image) |img| batch.drawImage(img, item.position, .{});

        // 显示碰撞框
        if (item.object == null) continue;
        const obj = item.object.?;
        batch.debugDraw(.init(item.position.add(obj.position), obj.size));
    }

    // 绘制特效动画
    for (effectAnimations.items) |animation| {
        const img = animation.effect.currentImage();
        batch.drawImage(img, animation.center, .{ .anchor = .center });
    }
}
```

## 效果

![角色和对象碰撞][1]

[1]: images/阳光岛21.gif
## 附录
