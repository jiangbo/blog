# 0956-教你制作RPG游戏-显示物品描述

## 目标

在物品栏中，将物品的名称，描述信息和数量显示出来。

## 环境

- Time 2025-06-07
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

目前很多文字还没有，后面点来补充缺少的文字。

## bag.zig

```zig
const std = @import("std");

const gfx = @import("../graphics.zig");

pub const Item = struct {
    info: *ItemInfo,
    count: u32 = 0,
};

pub const ItemInfo = struct {
    name: []const u8 = &.{},
    texture: gfx.Texture,
    tip: []const u8 = &.{},
    price: u32 = 0,
    value1: u32 = 0,
    value2: u32 = 0,
    value3: u32 = 0,
    value4: u32 = 0,
    value5: u32 = 0,

    pub fn addValue(self: *ItemInfo, other: *const ItemInfo) void {
        self.value2 += other.value2;
        self.value3 += other.value3;
        self.value4 += other.value4;
        self.value5 += other.value5;
    }
};

pub var infos: [8]ItemInfo = undefined;

pub var money: u32 = 143;
pub var items: [10]Item = undefined;
pub var skills: [10]Item = undefined;

pub fn init() void {
    initInfos();

    for (&items) |*item| item.count = 0;

    items[0] = .{ .info = &infos[0], .count = 2 };
    items[1] = .{ .info = &infos[1], .count = 3 };

    items[2] = .{ .info = &infos[2], .count = 2 };

    for (&skills) |*skill| skill.count = 0;

    skills[0] = .{ .info = &infos[6], .count = 20 };
    skills[1] = .{ .info = &infos[7], .count = 20 };
}

fn initInfos() void {
    infos[0] = .{
        .name = "红药水",
        .texture = gfx.loadTexture("assets/item/item1.png", .init(66, 66)),
        .tip = "恢复少量 HP",
        .price = 30,
    };
    infos[1] = .{
        .name = "蓝药水",
        .texture = gfx.loadTexture("assets/item/item2.png", .init(66, 66)),
        .tip = "恢复少量 MP",
        .price = 200,
    };
    infos[2] = .{
        .name = "短剑",
        .texture = gfx.loadTexture("assets/item/item3.png", .init(66, 66)),
        .tip = "一把钢制短剑",
        .price = 100,
        .value1 = 1,
        .value2 = 10,
        .value5 = 5,
    };
    infos[3] = .{
        .name = "斧头",
        .texture = gfx.loadTexture("assets/item/item4.png", .init(66, 66)),
        .tip = "传说这是一把能够劈开阴\n气的斧头，但无人亲眼见n过它的威力",
        .price = 100,
        .value1 = 1,
        .value2 = 3,
        .value5 = 50,
    };
    infos[4] = .{
        .name = "钢盾",
        .texture = gfx.loadTexture("assets/item/item5.png", .init(66, 66)),
        .tip = "钢质盾牌，没有矛可以穿\n破它",
        .price = 100,
        .value1 = 2,
        .value3 = 20,
        .value4 = 5,
    };
    infos[5] = .{
        .name = "魔法书",
        .texture = gfx.loadTexture("assets/item/item6.png", .init(66, 66)),
        .tip = "一本游记，记录世间\n奇事，可打开阅览",
        .price = 100,
    };

    infos[6] = .{
        .name = "治疗术",
        .texture = gfx.loadTexture("assets/item/skill1.png", .init(66, 66)),
        .tip = "恢复少量 HP",
        .price = 20,
    };

    infos[7] = .{
        .name = "黑洞漩涡",
        .texture = gfx.loadTexture("assets/item/skill2.png", .init(66, 66)),
        .tip = "攻击型技能，\n将敌人吸入漩涡",
        .price = 20,
    };
}
```

## statusPopup.zig

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");
const bag = @import("bag.zig");

pub const MenuType = enum { item, skill };

pub var display: bool = false;
var position: gfx.Vector = undefined;
var background: gfx.Texture = undefined;
var selected: gfx.Texture = undefined;
var itemTexture: gfx.Texture = undefined;
var skillTexture: gfx.Texture = undefined;

var selectedPlayer: usize = 0;
var selectedItem: usize = 0;
var menuType: MenuType = .item;

pub fn init() void {
    position = .init(58, 71);
    background = gfx.loadTexture("assets/item/status_bg.png", .init(677, 428));
    selected = gfx.loadTexture("assets/item/sbt7_2.png", .init(273, 90));
    itemTexture = gfx.loadTexture("assets/item/sbt2_1.png", .init(62, 255));
    skillTexture = gfx.loadTexture("assets/item/sbt2_2.png", .init(62, 255));
}

pub fn update(delta: f32) void {
    if (window.isAnyKeyRelease(&.{ .ESCAPE, .Q, .E })) display = false;

    if (window.isAnyKeyRelease(&.{ .LEFT, .A })) {
        selectedPlayer += world.players.len;
        selectedPlayer = (selectedPlayer - 1) % world.players.len;
    } else if (window.isAnyKeyRelease(&.{ .RIGHT, .D })) {
        selectedPlayer = (selectedPlayer + 1) % world.players.len;
    }

    if (window.isKeyRelease(.TAB)) {
        menuType = if (menuType == .item) .skill else .item;
    }

    _ = delta;
}

pub fn render() void {
    if (!display) return;

    camera.draw(background, position);

    renderStatus();

    var items: []bag.Item = undefined;

    if (menuType == .item) {
        items = &bag.items;
        camera.draw(itemTexture, position.add(.init(629, 51)));
    } else {
        items = &bag.skills;
        camera.draw(skillTexture, position.add(.init(629, 51)));
    }

    var showItemCount: usize = 0;
    for (items) |item| {
        if (item.count == 0) continue;

        const offset = position.add(.init(360, 48));
        const pos = offset.addY(@floatFromInt(96 * showItemCount));
        camera.draw(item.info.texture, pos);
        drawItemInfo(.{item.info.name}, pos.addX(80));
        drawItemInfo(.{item.info.tip}, pos.add(.init(90, 27)));
        drawCount(.{item.count}, pos.addX(160));

        if (selectedItem == showItemCount) {
            camera.draw(selected, pos.sub(.init(10, 10)));
        }

        showItemCount += 1;
        if (showItemCount >= 3) break;
    }

    drawStatusText(.{bag.money}, .init(525, 445));
}

fn renderStatus() void {
    const player = &world.players[selectedPlayer];
    camera.draw(player.statusTexture, position);

    if (player.attackItem) |item| {
        camera.draw(item.texture, position.add(.init(41, 55)));
    }

    if (player.defendItem) |item| {
        camera.draw(item.texture, position.add(.init(41, 136)));
    }

    drawStatusText(.{player.health}, .init(155, 411));
    drawStatusText(.{player.mana}, .init(290, 411));

    const item = &player.totalItem;
    drawStatusText(.{player.attack}, .init(155, 431));
    drawEffect(.{item.value2}, .init(185, 431));
    drawStatusText(.{player.defend}, .init(290, 431));
    drawEffect(.{item.value3}, .init(320, 431));

    drawStatusText(.{player.speed}, .init(155, 451));
    drawEffect(.{item.value4}, .init(185, 451));
    drawStatusText(.{player.luck}, .init(290, 451));
    drawEffect(.{item.value5}, .init(320, 451));
}

fn drawStatusText(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("{}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 0.21, .g = 0.09, .b = 0.01, .a = 1 },
    });
}

fn drawEffect(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("+{}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 1, .a = 1 },
    });
}

fn drawItemInfo(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("{s}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 0.75, .g = 0.89, .b = 0.26, .a = 1 },
    });
}

fn drawCount(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("X {}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 0.75, .g = 0.89, .b = 0.26, .a = 1 },
    });
}

fn drawTextOptions(comptime fmt: []const u8, options: anytype) void {
    var buffer: [256]u8 = undefined;
    const text = std.fmt.bufPrint(&buffer, fmt, options.args);
    camera.drawTextOptions(.{
        .text = text catch unreachable,
        .position = options.position,
        .color = options.color,
    });
}
```

## 效果

![显示物品描述][1]

[1]: images/教你制作RPG游戏58.webp

## 附录
