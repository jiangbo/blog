# 0983-英雄救美-显示菜单

## 目标

参考了之前标题界面的菜单，然后将代码复制过来，显示了游戏中的菜单。

## 环境

- Time 2025-06-21
- Zig 0.14.1

## 参考

1. 圣剑英雄传：英雄救美源码。

## 想法

不清楚是否有必要将菜单抽取成一个独立的模块，不过还没有想好怎么抽取菜单。

## world.zig

目前所有的菜单和对话的代码都还在 world 场景里，后面看需要是不是分开。

```zig
const std = @import("std");

const window = @import("zhu").window;
const gfx = @import("zhu").gfx;
const camera = @import("zhu").camera;

const player = @import("player.zig");
const map = @import("map.zig");

const Status = union(enum) { normal, talk: usize, menu };
var status: Status = .normal;

const Talk = struct {
    actor: u8 = 0,
    content: []const u8,
    format: enum { none, int } = .none,
    next: usize = 0,
};
const talks: []const Talk = @import("zon/talk.zon");
var talkTexture: gfx.Texture = undefined;
var talkNumber: usize = 0;
var buffer: [256]u8 = undefined;
var bufferIndex: usize = 0;

const Menu = struct {
    names: []const []const u8,
    areas: []const gfx.Rectangle = undefined,
    current: usize = 0,
    const color = gfx.Color{ .w = 1 };
};

var menu: Menu = .{
    .names = &.{
        "状　　态", "物　　品", "读取进度", "存储进度", //
        "关于游戏", "退　　出", "返回游戏",
    },
    .areas = &createAreas(7, .{ .x = 0 + 33, .y = 288 }),
};

fn createAreas(comptime num: u8, pos: gfx.Vector) [num]gfx.Rectangle {
    var areas: [num]gfx.Rectangle = undefined;
    for (&areas, 0..) |*area, i| {
        const offsetY: f32 = @floatFromInt(10 + i * 24);
        area.* = .init(pos.addY(offsetY), .init(85, 25));
    }
    return areas;
}

var menuTexture: gfx.Texture = undefined;

pub fn init() void {
    talkTexture = gfx.loadTexture("assets/pic/talkbar.png", .init(640, 96));
    menuTexture = gfx.loadTexture("assets/pic/mainmenu1.png", .init(150, 200));
    // status = .{ .talk = 1 };
    map.init();
    player.init();

    // window.playMusic("assets/voc/back.ogg");
}

pub fn event(ev: *const window.Event) void {
    if (ev.type != .MOUSE_MOVE) return;

    for (menu.areas, 0..) |area, i| {
        if (area.contains(window.mousePosition)) {
            menu.current = i;
        }
    }
}

pub fn update(delta: f32) void {
    switch (status) {
        .normal => {},
        .talk => |talkId| return updateTalk(talkId),
        .menu => return updateMenu(),
    }

    // 角色移动和碰撞检测
    const toPosition = player.toMove(delta);
    if (toPosition) |position| {
        if (map.canWalk(position.addXY(-8, -12)) and
            map.canWalk(position.addXY(-8, 2)) and
            map.canWalk(position.addXY(8, -12)) and
            map.canWalk(position.addXY(8, 2)))
            player.position = position;
    }

    // 交互检测
    if (window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER })) {
        const object = map.talk(player.position, player.facing());
        if (object != 0) handleObject(object);
    }

    // 打开菜单
    if (window.isAnyKeyRelease(&.{ .ESCAPE, .E })) status = .menu;

    player.update(delta);
}

fn handleObject(object: u16) void {
    if (object & 0x1000 != 0) handleChest(object);
}

fn handleChest(object: u16) void {
    if (object == 0x1000) {
        const gold = window.random().intRangeLessThanBiased(u8, 10, 100);
        player.money += gold;
        status = .{ .talk = 3 };
        talkNumber = gold;
    } else {
        status = .{ .talk = 4 };
    }
}

fn updateTalk(talkId: usize) void {
    if (!window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER })) return;

    bufferIndex = 0;
    const next = talks[talkId].next;
    status = if (next == 0) .normal else .{ .talk = next };
}

fn updateMenu() void {
    if (window.isAnyKeyRelease(&.{ .ESCAPE, .E })) status = .normal;

    if (window.isAnyKeyRelease(&.{ .DOWN, .S })) {
        menu.current = (menu.current + 1) % menu.names.len;
    }
    if (window.isAnyKeyRelease(&.{ .UP, .W })) {
        menu.current += menu.names.len;
        menu.current = (menu.current - 1) % menu.names.len;
    }

    var confirm = window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER });
    if (window.isButtonRelease(.LEFT)) {
        for (menu.areas, 0..) |area, i| {
            if (area.contains(window.mousePosition)) {
                menu.current = i;
                confirm = true;
            }
        }
    }
}

pub fn enter() void {}

pub fn exit() void {}

pub fn render() void {
    map.render();
    player.render();

    switch (status) {
        .normal => {},
        .talk => |talkId| renderTalk(talkId),
        .menu => renderMenu(),
    }
}

fn renderTalk(talkId: usize) void {
    camera.draw(talkTexture, .init(0, 384));

    const talk = talks[talkId];
    if (talk.actor == 0) player.renderTalk();

    var content = talk.content;
    if (talk.format == .int) {
        content = if (bufferIndex == 0)
            formatInt(content)
        else
            buffer[0..bufferIndex];
    }

    camera.drawColorText(content, .init(123, 403), .{ .w = 1 });
    camera.drawColorText(content, .init(120, 400), .one);
}

fn formatInt(content: []const u8) []const u8 {
    const index = std.fmt.bufPrint(buffer[240..], "{d}", .{talkNumber});
    const text = index catch unreachable;

    const times = std.mem.replace(u8, content, "{}", text, &buffer);
    std.debug.assert(times == 1);

    bufferIndex = content.len - 2 + text.len;
    return buffer[0..bufferIndex];
}

fn renderMenu() void {
    camera.draw(menuTexture, .init(0, 280));

    for (menu.areas, menu.names, 0..) |area, name, i| {
        if (i == menu.current) {
            camera.drawRectangle(area, Menu.color);
        }
        camera.drawText(name, area.min.addXY(5, -2));
    }
}
```

## 效果

![显示菜单][1]

[1]: images/英雄救美14.png

## 附录
