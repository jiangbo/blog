# 0972-英雄救美-淡入淡出效果

## 目标

绘制一张全黑的纹理来挡住窗口，然后将透明度慢慢改变来实现淡入淡出的效果。

## 环境

- Time 2025-06-14
- Zig 0.14.1

## 参考

1. 圣剑英雄传：英雄救美源码。

## 想法

除了使用一张纹理来实现，还考虑过在填入顶点时，将颜色中的透明度修改，两种不清楚哪种好。
截图效果看起来很奇怪，但是游戏中看起来正常的，所以暂停先这样，后面发现问题再修改。

## title.zig

增加淡入淡出的方法调用。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const scene = @import("../scene.zig");
const camera = @import("../camera.zig");

var background: gfx.Texture = undefined;

const Menu = struct {
    background: ?gfx.Texture = null,
    position: gfx.Vector,
    names: []const []const u8,
    areas: []const gfx.Rectangle = undefined,
    current: usize = 0,
    const color = gfx.color(0.73, 0.72, 0.53, 1);
};

var menu: *Menu = &mainMenu;
var displayHeader: bool = false;
var displayTimer: window.Timer = .init(0.08);
var textIndex: usize = 0;

var mainMenu: Menu = .{
    .position = .{ .x = 11, .y = 375 },
    .names = &.{ "新游戏", "读进度", "退　出" },
    .areas = &createAreas(3, .{ .x = 11, .y = 375 }),
};
var loadMenu: Menu = .{
    .position = .{ .x = 0, .y = 280 },
    .names = &.{ "进度一", "进度二", "进度三", "进度四", "进度五", "取　消" },
    .areas = &createAreas(6, .{ .x = 0 + 45, .y = 280 + 20 }),
};

fn createAreas(comptime num: u8, pos: gfx.Vector) [num]gfx.Rectangle {
    var areas: [num]gfx.Rectangle = undefined;
    for (&areas, 0..) |*area, i| {
        const offsetY: f32 = @floatFromInt(10 + i * 24);
        area.* = .init(pos.addY(offsetY), .init(58, 25));
    }
    return areas;
}

pub fn init() void {
    background = gfx.loadTexture("assets/pic/title.png", .init(640, 480));
    const path = "assets/pic/mainmenu.png";
    loadMenu.background = gfx.loadTexture(path, .init(150, 200));
}

pub fn event(ev: *const window.Event) void {
    if (ev.type != .MOUSE_MOVE) return;

    for (menu.areas, 0..) |area, i| {
        if (area.contains(window.mousePosition)) {
            menu.current = i;
        }
    }
}

pub fn enter() void {
    menu.current = 0;
    window.playMusic("assets/voc/title.ogg");
    displayHeader = false;
    textIndex = 0;
    scene.fadeIn();
}

pub fn exit() void {
    window.stopMusic();
}

pub fn update(delta: f32) void {
    if (displayHeader) return updateHeader(delta);

    if (window.isAnyKeyRelease(&.{ .DOWN, .S })) {
        menu.current = (menu.current + 1) % menu.names.len;
    }
    if (window.isAnyKeyRelease(&.{ .UP, .W })) {
        menu.current += menu.names.len;
        menu.current = (menu.current - 1) % menu.names.len;
    }

    var confirm = window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER });
    if (window.isButtonRelease(.LEFT)) {
        for (menu.areas, 0..) |area, i| {
            if (area.contains(window.mousePosition)) {
                menu.current = i;
                confirm = true;
            }
        }
    }

    if (confirm) {
        if (menu == &mainMenu) mainMenuSelected() else loadMenuSelected();
    }

    if (window.isAnyKeyRelease(&.{ .Q, .ESCAPE })) {
        menu = &mainMenu;
    }
}

fn updateHeader(delta: f32) void {
    if (window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER })) {
        scene.changeScene(.world);
        return;
    }

    if (displayTimer.isFinishedAfterUpdate(delta)) {
        if (textIndex >= text.len) return;
        const len = std.unicode.utf8ByteSequenceLength(text[textIndex]);
        textIndex += len catch unreachable;
        displayTimer.reset();
    }
}

const text = scene.talks[0].content;

fn mainMenuSelected() void {
    switch (menu.current) {
        0 => scene.fadeOut(struct {
            fn call() void {
                displayHeader = true;
            }
        }.call),
        1 => menu = &loadMenu,
        2 => window.exit(),
        else => unreachable(),
    }
}

fn loadMenuSelected() void {
    switch (menu.current) {
        0, 1, 2, 3, 4 => scene.changeScene(.world),
        5 => menu = &mainMenu,
        else => unreachable(),
    }
}

pub fn render() void {
    if (displayHeader) return renderHeader();
    camera.draw(background, .zero);

    if (menu.background) |bg| camera.draw(bg, menu.position);

    for (menu.areas, menu.names, 0..) |area, name, i| {
        if (i == menu.current) {
            camera.drawRectangle(area, Menu.color);
        }
        camera.drawText(name, area.min.addX(5));
    }
}

pub fn renderHeader() void {
    camera.drawText(text[0..textIndex], .init(60, 100));
}
```

## scene.zig

增加了淡入淡出的效果。

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const camera = @import("camera.zig");

const titleScene = @import("scene/title.zig");
const worldScene = @import("scene/world.zig");

const Talk = struct { content: []const u8 };
pub const talks: []const Talk = @import("talk.zon");

const SceneType = enum { title, world };
var currentSceneType: SceneType = .title;
var toSceneType: SceneType = .title;

var vertexBuffer: [100 * 4]camera.Vertex = undefined;

pub fn init() void {
    camera.init(.init(.zero, window.size), &vertexBuffer);

    titleScene.init();
    worldScene.init();
    window.fontTexture = gfx.loadTexture("assets/4_0.png", .init(512, 512));

    enter();
}

pub fn event(ev: *const window.Event) void {
    titleScene.event(ev);
}

pub fn enter() void {
    sceneCall("enter", .{});
}

pub fn exit() void {
    sceneCall("exit", .{});
}

pub fn changeScene(sceneType: SceneType) void {
    toSceneType = sceneType;
    fadeOut(doChangeScene);
}

fn doChangeScene() void {
    exit();
    currentSceneType = toSceneType;
    enter();
}

pub fn update(delta: f32) void {
    if (fadeTimer) |*timer| {
        if (timer.isRunningAfterUpdate(delta)) return;
        if (isFadeIn) {
            fadeTimer = null;
        } else {
            if (fadeOutCallback) |callback| callback();
            isFadeIn = true;
            timer.restart();
        }
        return;
    }
    sceneCall("update", .{delta});
}

pub fn render() void {
    camera.beginDraw(.{ .a = 1 });
    defer camera.endDraw();

    sceneCall("render", .{});

    if (fadeTimer) |*timer| {
        const percent = timer.elapsed / timer.duration;
        const a = if (isFadeIn) 1 - percent else percent;
        camera.drawRectangle(.init(.zero, window.size), .{ .a = a });
    }

    var buffer: [20]u8 = undefined;
    const text = std.fmt.bufPrint(&buffer, "FPS: {}", .{window.frameRate});
    camera.drawTextOptions(.{
        .text = text catch unreachable,
        .position = .init(10, 5),
        .color = .{ .r = 0, .g = 1, .b = 0, .a = 1 },
    });
}

var fadeTimer: ?window.Timer = null;
var isFadeIn: bool = false;
var fadeOutCallback: ?*const fn () void = null;

pub fn fadeIn() void {
    isFadeIn = true;
    fadeTimer = .init(2);
}

pub fn fadeOut(callback: ?*const fn () void) void {
    isFadeIn = false;
    fadeTimer = .init(2);
    fadeOutCallback = callback;
}

fn sceneCall(comptime function: []const u8, args: anytype) void {
    switch (currentSceneType) {
        .title => window.call(titleScene, function, args),
        .world => window.call(worldScene, function, args),
    }
}
```

## 效果

![淡入淡出效果][1]

[1]: images/英雄救美03.webp

## 附录
