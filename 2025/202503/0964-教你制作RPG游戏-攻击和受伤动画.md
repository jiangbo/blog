# 0964-教你制作RPG游戏-攻击和受伤动画

## 目标

实现人物的攻击和受伤动画，完善了战斗面板，可以根据血量百分比进行显示。

## 环境

- Time 2025-06-08
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

完成了攻击和受伤的动画，后面还要补充敌人的血量，攻击这些，才能计算伤害。

## panel.zig

完善了面板显示，可以显示百分比。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");
const battle = @import("battle.zig");

pub var active: bool = true;

var attack: gfx.Texture = undefined;
var attackHover: gfx.Texture = undefined;
var item: gfx.Texture = undefined;
var itemHover: gfx.Texture = undefined;
var skill: gfx.Texture = undefined;
var skillHover: gfx.Texture = undefined;
var background: gfx.Texture = undefined;
var health: gfx.Texture = undefined;
var mana: gfx.Texture = undefined;

var selectedType: usize = 0;
var selectedPlayer: usize = 0;

pub fn init() void {
    attack = gfx.loadTexture("assets/fight/fm_b1_1.png", .init(38, 36));
    attackHover = gfx.loadTexture("assets/fight/fm_b1_2.png", .init(38, 36));
    item = gfx.loadTexture("assets/fight/fm_b2_1.png", .init(38, 36));
    itemHover = gfx.loadTexture("assets/fight/fm_b2_2.png", .init(38, 36));
    skill = gfx.loadTexture("assets/fight/fm_b3_1.png", .init(38, 36));
    skillHover = gfx.loadTexture("assets/fight/fm_b3_2.png", .init(38, 36));
    background = gfx.loadTexture("assets/fight/fm_bg.png", .init(319, 216));
    health = gfx.loadTexture("assets/fight/fm_s1.png", .init(129, 17));
    mana = gfx.loadTexture("assets/fight/fm_s2.png", .init(129, 17));
}

pub fn update(_: f32) void {
    if (window.isAnyKeyRelease(&.{ .LEFT, .A })) {
        selectedType = (selectedType + 2) % 3;
    }
    if (window.isAnyKeyRelease(&.{ .RIGHT, .D })) {
        selectedType = (selectedType + 1) % 3;
    }

    if (window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER })) {
        battle.startAttack(selectedPlayer, 4);
    }

    if (window.isKeyRelease(.TAB)) {
        selectedPlayer = (selectedPlayer + 1) % 3;
        battle.selected = selectedPlayer;
    }
}

pub fn render() void {
    const offset = gfx.Vector.init(200, 385);
    camera.draw(background, offset);

    var texture = if (selectedType == 0) attackHover else attack;
    camera.draw(texture, offset.add(.init(142, 68)));

    texture = if (selectedType == 1) itemHover else item;
    camera.draw(texture, offset.add(.init(192, 68)));

    texture = if (selectedType == 2) skillHover else skill;
    camera.draw(texture, offset.add(.init(242, 68)));

    // 头像
    const player = &world.players[selectedPlayer];
    camera.draw(player.battleFace, offset);

    drawName(player.name, offset.add(.init(180, 114)));
    // 状态条
    var percent = computePercent(player.health, player.maxHealth);
    drawBar(percent, health, offset.add(.init(141, 145)));
    percent = computePercent(player.mana, player.maxMana);
    drawBar(percent, mana, offset.add(.init(141, 171)));
}

fn computePercent(current: usize, max: usize) f32 {
    if (max == 0) return 0;
    const cur: f32 = @floatFromInt(current);
    return cur / @as(f32, @floatFromInt(max));
}

fn drawBar(percent: f32, tex: gfx.Texture, pos: gfx.Vector) void {
    const width = tex.area.size().x * percent;
    camera.drawOptions(.{
        .texture = tex,
        .source = .init(.zero, .init(width, tex.area.size().y)),
        .target = .init(pos, .init(width, tex.area.size().y)),
    });
}

fn drawName(name: []const u8, pos: gfx.Vector) void {
    camera.drawTextOptions(.{
        .text = name,
        .position = pos,
        .color = gfx.Color{ .g = 0.05, .b = 0.16, .a = 1 },
    });
}

fn drawTextOptions(comptime fmt: []const u8, options: anytype) void {
    var buffer: [256]u8 = undefined;
    const text = std.fmt.bufPrint(&buffer, fmt, options.args);
    camera.drawTextOptions(.{
        .text = text catch unreachable,
        .position = options.position,
        .color = options.color,
    });
}
```

## battle.zig

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");
const panel = @import("panel.zig");

pub const Enemy = struct {
    active: bool = false,
    texture: gfx.Texture = undefined,
};

const statusEnum = enum { idle, attack, hurt, dead, none };

var background: gfx.Texture = undefined;
var enemyTexture: gfx.Texture = undefined;
var enemies: [3]Enemy = undefined;
var targetTexture: gfx.Texture = undefined;

var attackTimer: window.Timer = .init(0.4);
pub var selected: usize = 0;

var areas: [6]gfx.Rectangle = .{
    .init(.init(497, 138), .init(240, 240)),
    .init(.init(575, 241), .init(240, 240)),
    .init(.init(468, 297), .init(240, 240)),
    .init(.init(93, 130), .init(480, 240)),
    .init(.init(19, 225), .init(480, 240)),
    .init(.init(60, 321), .init(480, 240)),
};
var textures: [areas.len]gfx.Texture = undefined;
pub var status = [1]statusEnum{.idle} ** areas.len;

pub fn init() void {
    background = gfx.loadTexture("assets/fight/f_scene.png", .init(800, 600));
    enemyTexture = gfx.loadTexture("assets/fight/enemy.png", .init(1920, 240));
    targetTexture = gfx.loadTexture("assets/fight/fm_b4_2.png", .init(190, 186));
    panel.init();
    attackTimer.stop();

    textures[0] = gfx.loadTexture("assets/fight/p1.png", .init(960, 240));
    textures[1] = gfx.loadTexture("assets/fight/p2.png", .init(960, 240));
    textures[2] = gfx.loadTexture("assets/fight/p3.png", .init(960, 240));
    textures[3] = gfx.loadTexture("assets/fight/enemy.png", .init(1920, 240));
    textures[4] = textures[3];
    textures[5] = textures[3];
}

pub fn enter() void {
    window.playMusic("assets/fight/fight.ogg");
}

pub fn exit() void {
    window.stopMusic();
}

pub fn startAttack(attack: usize, hurt: usize) void {
    attackTimer.reset();
    status[attack] = .attack;
    status[hurt] = .hurt;
}

pub fn update(delta: f32) void {
    if (attackTimer.isFinishedAfterUpdate(delta)) {
        for (&status) |*value| {
            if (value.* == .attack or value.* == .hurt)
                value.* = .idle;
        }
    }
    if (panel.active) panel.update(delta);
}

pub fn render() void {
    camera.draw(background, .init(0, 0));

    for (areas, textures, status, 0..) |area, texture, s, index| {
        if (s == .none) continue;

        const size = area.size();
        const x: f32 = @floatFromInt(@intFromEnum(s));
        const sub = gfx.Rectangle.init(.init(x * size.x, 0), size);
        camera.draw(texture.subTexture(sub), area.min);

        if (!attackTimer.isRunning() and index == selected) {
            const offset = gfx.Vector.init(90 - size.x / 2, 40);
            camera.draw(targetTexture, area.min.sub(offset));
        }
    }

    if (panel.active) panel.render();

    // for (areas) |area| camera.debugDraw(area);
}
```

## 效果

![攻击和受伤动画][1]

[1]: images/教你制作RPG游戏66.webp

## 附录
