# 0981-英雄救美-打开金币宝箱

## 目标

角色可以在地图上进行移动，面向宝箱时，可以打开宝箱。

## 环境

- Time 2025-06-20
- Zig 0.14.1

## 参考

1. 圣剑英雄传：英雄救美源码。

## 想法

又把 player 和 map 部分代码移动到了 world.zig 中。

## world.zig

新增了事件控制的代码，对话中可以格式化数字到对话中。

```zig
const std = @import("std");

const window = @import("zhu").window;
const gfx = @import("zhu").gfx;
const camera = @import("zhu").camera;

const player = @import("player.zig");
const map = @import("map.zig");

const Status = union(enum) { normal, talk: usize };
var status: Status = .normal;

const Talk = struct {
    actor: u8 = 0,
    content: []const u8,
    format: enum { none, int } = .none,
    next: usize = 0,
};
const talks: []const Talk = @import("zon/talk.zon");
var talkTexture: gfx.Texture = undefined;
var talkNumber: usize = 0;
var buffer: [256]u8 = undefined;
var bufferIndex: usize = 0;

pub fn init() void {
    talkTexture = gfx.loadTexture("assets/pic/talkbar.png", .init(640, 96));
    // status = .{ .talk = 1 };
    map.init();
    player.init();

    // window.playMusic("assets/voc/back.ogg");
}

pub fn update(delta: f32) void {
    switch (status) {
        .normal => {},
        .talk => |talkId| return updateTalk(talkId),
    }

    // 角色移动和碰撞检测
    const toPosition = player.toMove(delta);
    if (toPosition) |position| {
        if (map.canWalk(position.addXY(-8, -12)) and
            map.canWalk(position.addXY(-8, 2)) and
            map.canWalk(position.addXY(8, -12)) and
            map.canWalk(position.addXY(8, 2)))
            player.position = position;
    }

    // 交互检测
    if (confirm()) {
        const object = map.talk(player.position, player.facing());
        if (object != 0) handleObject(object);
    }

    player.update(delta);
}

fn handleObject(object: u16) void {
    if (object == 301) {
        const gold = window.random().intRangeLessThanBiased(u8, 10, 100);
        player.money += gold;
        status = .{ .talk = 3 };
        talkNumber = gold;
    }
}

fn updateTalk(talkId: usize) void {
    if (!confirm()) return;

    bufferIndex = 0;
    const next = talks[talkId].next;
    status = if (next == 0) .normal else .{ .talk = next };
}

fn confirm() bool {
    return window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER });
}

pub fn enter() void {}

pub fn exit() void {}

pub fn render() void {
    map.render();
    player.render();

    switch (status) {
        .normal => {},
        .talk => |talkId| renderTalk(talkId),
    }
}

fn renderTalk(talkId: usize) void {
    camera.draw(talkTexture, .init(0, 384));

    const talk = talks[talkId];
    if (talk.actor == 0) player.renderTalk();

    var content = talk.content;
    if (talk.format == .int) {
        content = if (bufferIndex == 0)
            formatInt(content)
        else
            buffer[0..bufferIndex];
    }

    camera.drawColorText(content, .init(123, 403), .{ .w = 1 });
    camera.drawColorText(content, .init(120, 400), .one);
}

fn formatInt(content: []const u8) []const u8 {
    const index = std.fmt.bufPrint(buffer[240..], "{d}", .{talkNumber});
    const text = index catch unreachable;

    const times = std.mem.replace(u8, content, "{}", text, &buffer);
    std.debug.assert(times == 1);

    bufferIndex = content.len - 2 + text.len;
    return buffer[0..bufferIndex];
}
```

## map.zig

新增了对象层，并且可以和角色互动，改变显示的对象。

```zig
const std = @import("std");

const window = @import("zhu").window;
const gfx = @import("zhu").gfx;
const camera = @import("zhu").camera;
const math = @import("zhu").math;

var texture: gfx.Texture = undefined;

const Map = struct {
    width: u16,
    height: u16,
    ground1: []const u16,
    ground2: []const u16,
    object: []const u16,
};

const map: Map = @import("zon/map.zon");

var vertexBuffer: [500]camera.Vertex = undefined;
var vertexIndex: usize = 0;

var objectOffset: usize = 0;
var objectArray: [1000]u16 = undefined;

pub fn init() void {
    texture = gfx.loadTexture("assets/pic/maps.png", .init(640, 1536));

    buildVertexBuffer(map.ground1);
    buildVertexBuffer(map.ground2);
    objectOffset = vertexIndex;

    @memcpy(objectArray[0..map.object.len], map.object);
    buildObjectBuffer();
}

fn buildVertexBuffer(tiles: []const u16) void {
    for (tiles, 0..) |tileIndex, index| {
        if (tileIndex != 0) appendVertex(tileIndex, index);
    }
}

fn buildObjectBuffer() void {
    vertexIndex = objectOffset;
    for (objectArray[0..map.object.len], 0..) |tileIndex, index| {
        if (tileIndex > 256) appendVertex(tileIndex, index);
    }
}

fn appendVertex(tileIndex: usize, index: usize) void {
    const tile = texture.subTexture(getAreaFromIndex(tileIndex));
    vertexBuffer[vertexIndex] = .{
        .position = getAreaFromIndex(index).min,
        .size = .init(32, 32),
        .texture = tile.area.toVector4(),
    };
    vertexIndex += 1;
}

fn getAreaFromIndex(index: usize) gfx.Rectangle {
    const row: f32 = @floatFromInt(index / map.width);
    const col: f32 = @floatFromInt(index % map.width);
    return .init(.init(col * 32, row * 32), .init(32, 32));
}

pub fn talk(position: gfx.Vector, direction: math.FourDirection) u16 {
    const index: i32 = @intCast(positionIndex(position));
    const talkIndex: i32 = switch (direction) {
        .down => index + map.width,
        .left => index - 1,
        .right => index + 1,
        .up => index - map.width,
    };

    if (talkIndex < 0 or talkIndex > map.object.len) return 0;
    const talkObject = objectArray[@intCast(talkIndex)];
    if (talkObject == 0 or talkObject == 1) return 0;

    changeObjectIfNeed(@intCast(talkIndex), talkObject);
    return talkObject;
}

fn changeObjectIfNeed(index: usize, object: u16) void {
    objectArray[index] = switch (object) {
        301 => 302,
        else => return,
    };
    buildObjectBuffer();
}

pub fn positionIndex(position: gfx.Vector) usize {
    const x = @floor(position.x / 32);
    const y = @floor(position.y / 32);
    return @intFromFloat(x + y * map.width);
}

pub fn canWalk(position: gfx.Vector) bool {
    const index = positionIndex(position);
    if (index > map.object.len) return false;
    return objectArray[index] == 0;
}

pub fn render() void {
    camera.drawVertex(texture, vertexBuffer[0..vertexIndex]);
}
```

## player.zig

移动了一部分代码到 world 中，去除了 map 的依赖。

```zig
const std = @import("std");

const window = @import("zhu").window;
const gfx = @import("zhu").gfx;
const camera = @import("zhu").camera;
const math = @import("zhu").math;

const FrameAnimation = gfx.FixedFrameAnimation(3, 0.15);
const Animation = std.EnumArray(math.FourDirection, FrameAnimation);

const name = "小飞刀";
const speed = 100;
var texture: gfx.Texture = undefined;
var animation: Animation = undefined;

var moving: bool = false;
var direction: math.Vector = .zero;
var offset: math.Vector = .zero;
pub var position: math.Vector = .init(180, 164);
pub var money: usize = 0;

pub fn init() void {
    texture = gfx.loadTexture("assets/pic/player.png", .init(96, 192));

    offset = math.Vector{ .x = -16, .y = -45 };
    animation = Animation.initUndefined();

    var tex = texture.subTexture(.init(.zero, .init(96, 48)));
    animation.set(.down, FrameAnimation.init(tex));

    tex = texture.subTexture(tex.area.move(.init(0, 48)));
    animation.set(.left, FrameAnimation.init(tex));

    tex = texture.subTexture(tex.area.move(.init(0, 48)));
    animation.set(.right, FrameAnimation.init(tex));

    tex = texture.subTexture(tex.area.move(.init(0, 48)));
    animation.set(.up, FrameAnimation.init(tex));
}

pub fn update(delta: f32) void {
    if (moving) animation.getPtr(facing()).update(delta);
}

pub fn toMove(delta: f32) ?math.Vector {
    var dir = math.Vector.zero;
    if (window.isAnyKeyDown(&.{ .UP, .W })) dir.y -= 1;
    if (window.isAnyKeyDown(&.{ .DOWN, .S })) dir.y += 1;
    if (window.isAnyKeyDown(&.{ .LEFT, .A })) dir.x -= 1;
    if (window.isAnyKeyDown(&.{ .RIGHT, .D })) dir.x += 1;

    moving = !dir.approxEqual(.zero);
    if (moving) {
        direction = dir.normalize().scale(speed);
        return position.add(direction.scale(delta));
    } else return null;
}

pub fn render() void {
    const current = animation.get(facing());
    camera.draw(current.currentTexture(), position.add(offset));
}

pub fn facing() math.FourDirection {
    if (@abs(direction.x) > @abs(direction.y))
        return if (direction.x < 0) .left else .right
    else
        return if (direction.y < 0) .up else .down;
}

pub fn renderTalk() void {

    // 头像
    const down = animation.get(.down);
    const tex = down.texture.subTexture(down.frames[0]);
    camera.draw(tex, .init(30, 396));

    // 名字
    const nameColor = gfx.color(1, 1, 0, 1);
    camera.drawColorText(name, .init(18, 445), nameColor);
}
```

## 效果

![打开金币宝箱][1]

[1]: images/英雄救美12.png

## 附录
