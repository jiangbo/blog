# 0963-教你制作RPG游戏-战斗切换角色

## 目标

战斗时，应该根据速度自动选择角色。目前在调试阶段，所以新增一个 TAB 键可以切换角色的功能。

## 环境

- Time 2025-06-08
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

切换角色后，角色的头像和名称会变化。同时实现角色的攻击动作。

## panel.zig

切换角色后，渲染不同角色的头像，支持攻击按键。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");
const battle = @import("battle.zig");

pub var active: bool = true;

var attack: gfx.Texture = undefined;
var attackHover: gfx.Texture = undefined;
var item: gfx.Texture = undefined;
var itemHover: gfx.Texture = undefined;
var skill: gfx.Texture = undefined;
var skillHover: gfx.Texture = undefined;
var background: gfx.Texture = undefined;
var health: gfx.Texture = undefined;
var mana: gfx.Texture = undefined;

var selectedType: usize = 0;
var selectedPlayer: usize = 0;

pub fn init() void {
    attack = gfx.loadTexture("assets/fight/fm_b1_1.png", .init(38, 36));
    attackHover = gfx.loadTexture("assets/fight/fm_b1_2.png", .init(38, 36));
    item = gfx.loadTexture("assets/fight/fm_b2_1.png", .init(38, 36));
    itemHover = gfx.loadTexture("assets/fight/fm_b2_2.png", .init(38, 36));
    skill = gfx.loadTexture("assets/fight/fm_b3_1.png", .init(38, 36));
    skillHover = gfx.loadTexture("assets/fight/fm_b3_2.png", .init(38, 36));
    background = gfx.loadTexture("assets/fight/fm_bg.png", .init(319, 216));
    health = gfx.loadTexture("assets/fight/fm_s1.png", .init(129, 17));
    mana = gfx.loadTexture("assets/fight/fm_s2.png", .init(129, 17));
}

pub fn update(_: f32) void {
    if (window.isAnyKeyRelease(&.{ .LEFT, .A })) {
        selectedType = (selectedType + 2) % 3;
    }
    if (window.isAnyKeyRelease(&.{ .RIGHT, .D })) {
        selectedType = (selectedType + 1) % 3;
    }

    if (window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER })) {
        battle.startAttack(selectedPlayer);
    }

    if (window.isKeyRelease(.TAB)) {
        selectedPlayer = (selectedPlayer + 1) % 3;
        battle.attackIndex = selectedPlayer;
    }
}

pub fn render() void {
    const offset = gfx.Vector.init(200, 385);
    camera.draw(background, offset);

    var texture = if (selectedType == 0) attackHover else attack;
    camera.draw(texture, offset.add(.init(142, 68)));

    texture = if (selectedType == 1) itemHover else item;
    camera.draw(texture, offset.add(.init(192, 68)));

    texture = if (selectedType == 2) skillHover else skill;
    camera.draw(texture, offset.add(.init(242, 68)));

    // 头像
    const player = &world.players[selectedPlayer];
    camera.draw(player.battleFace, offset);

    // 状态条
    drawName(player.name, offset.add(.init(180, 114)));
    camera.draw(health, offset.add(.init(141, 145)));
    camera.draw(mana, offset.add(.init(141, 171)));
}

fn drawName(name: []const u8, pos: gfx.Vector) void {
    camera.drawTextOptions(.{
        .text = name,
        .position = pos,
        .color = gfx.Color{ .g = 0.05, .b = 0.16, .a = 1 },
    });
}

fn drawTextOptions(comptime fmt: []const u8, options: anytype) void {
    var buffer: [256]u8 = undefined;
    const text = std.fmt.bufPrint(&buffer, fmt, options.args);
    camera.drawTextOptions(.{
        .text = text catch unreachable,
        .position = options.position,
        .color = options.color,
    });
}
```

## battle.zig

显示攻击动画。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");
const panel = @import("panel.zig");

pub const Enemy = struct {
    active: bool = false,
    texture: gfx.Texture = undefined,
};

var background: gfx.Texture = undefined;
var enemyTexture: gfx.Texture = undefined;
var enemies: [3]Enemy = undefined;

var attackTimer: window.Timer = .init(0.4);
pub var attackIndex: usize = 0;

pub fn init() void {
    background = gfx.loadTexture("assets/fight/f_scene.png", .init(800, 600));
    enemyTexture = gfx.loadTexture("assets/fight/enemy.png", .init(1920, 240));
    panel.init();
    attackTimer.stop();
}

pub fn enter() void {
    for (&enemies) |*enemy| {
        enemy.active = true;
        const area = gfx.Rectangle.init(.zero, .init(480, 240));
        enemy.texture = enemyTexture.subTexture(area);
    }

    window.playMusic("assets/fight/fight.ogg");
}

pub fn exit() void {
    window.stopMusic();
}

pub fn startAttack(index: usize) void {
    attackTimer.reset();
    attackIndex = index;
}

pub fn update(delta: f32) void {
    attackTimer.update(delta);
    if (panel.active) panel.update(delta);
}

pub fn render() void {
    camera.draw(background, .init(0, 0));

    var offset = gfx.Vector.init(120, 120).scale(-1);

    const player1 = &world.players[0];
    renderAttack(0, player1.battleTexture, offset.add(.init(617, 258)));

    const player2 = &world.players[1];
    renderAttack(1, player2.battleTexture, offset.add(.init(695, 361)));

    const player3 = &world.players[2];
    renderAttack(2, player3.battleTexture, offset.add(.init(588, 417)));

    offset = gfx.Vector.init(-160, -120);
    camera.draw(enemies[0].texture, offset.add(.init(253, 250)));
    camera.draw(enemies[1].texture, offset.add(.init(179, 345)));
    camera.draw(enemies[2].texture, offset.add(.init(220, 441)));

    if (panel.active) panel.render();
}

fn renderAttack(index: usize, texture: gfx.Texture, pos: gfx.Vector) void {
    const size = gfx.Vector.init(240, 240);

    var area = gfx.Rectangle.init(.init(0, 0), size);
    if (attackTimer.isRunning() and attackIndex == index) {
        area = .init(.init(240, 0), size);
    }
    camera.draw(texture.subTexture(area), pos);
}
```

## 效果

![战斗切换角色][1]

[1]: images/教你制作RPG游戏65.webp

## 附录
