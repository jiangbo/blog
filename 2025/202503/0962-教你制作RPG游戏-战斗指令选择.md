# 0962-教你制作RPG游戏-战斗指令选择

## 目标

在战斗的场景中，打开面板的时候，可以按左右来选择面板上的战斗指令。

## 环境

- Time 2025-06-08
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

新增了一个 panel 模块，来处理战斗中打开面板时的操作。

## panel.zig

战斗面板。

```zig
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");

pub var active: bool = true;

var attack: gfx.Texture = undefined;
var attackHover: gfx.Texture = undefined;
var item: gfx.Texture = undefined;
var itemHover: gfx.Texture = undefined;
var skill: gfx.Texture = undefined;
var skillHover: gfx.Texture = undefined;
var background: gfx.Texture = undefined;
var health: gfx.Texture = undefined;
var mana: gfx.Texture = undefined;

var selectedType: usize = 0;

pub fn init() void {
    attack = gfx.loadTexture("assets/fight/fm_b1_1.png", .init(38, 36));
    attackHover = gfx.loadTexture("assets/fight/fm_b1_2.png", .init(38, 36));
    item = gfx.loadTexture("assets/fight/fm_b2_1.png", .init(38, 36));
    itemHover = gfx.loadTexture("assets/fight/fm_b2_2.png", .init(38, 36));
    skill = gfx.loadTexture("assets/fight/fm_b3_1.png", .init(38, 36));
    skillHover = gfx.loadTexture("assets/fight/fm_b3_2.png", .init(38, 36));
    background = gfx.loadTexture("assets/fight/fm_bg.png", .init(319, 216));
    health = gfx.loadTexture("assets/fight/fm_s1.png", .init(129, 17));
    mana = gfx.loadTexture("assets/fight/fm_s2.png", .init(129, 17));
}

pub fn update(_: f32) void {
    if (window.isAnyKeyRelease(&.{ .LEFT, .A })) {
        selectedType = (selectedType + 2) % 3;
    }
    if (window.isAnyKeyRelease(&.{ .RIGHT, .D })) {
        selectedType = (selectedType + 1) % 3;
    }
}

pub fn render() void {
    const offset = gfx.Vector.init(200, 385);
    camera.draw(background, offset);

    var texture = if (selectedType == 0) attackHover else attack;
    camera.draw(texture, offset.add(.init(142, 68)));

    texture = if (selectedType == 1) itemHover else item;
    camera.draw(texture, offset.add(.init(192, 68)));

    texture = if (selectedType == 2) skillHover else skill;
    camera.draw(texture, offset.add(.init(242, 68)));

    // 头像
    const player = &world.players[0];
    camera.draw(player.battleFace, offset);

    // 状态条
    camera.draw(health, offset.add(.init(141, 145)));
    camera.draw(mana, offset.add(.init(141, 171)));
}
```

## battle.zig

战斗场景。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");
const panel = @import("panel.zig");

pub const Enemy = struct {
    active: bool = false,
    texture: gfx.Texture = undefined,
};

var background: gfx.Texture = undefined;
var enemyTexture: gfx.Texture = undefined;
var enemies: [3]Enemy = undefined;

pub fn init() void {
    background = gfx.loadTexture("assets/fight/f_scene.png", .init(800, 600));
    enemyTexture = gfx.loadTexture("assets/fight/enemy.png", .init(1920, 240));
    panel.init();
}

pub fn enter() void {
    for (&enemies) |*enemy| {
        enemy.active = true;
        const area = gfx.Rectangle.init(.zero, .init(480, 240));
        enemy.texture = enemyTexture.subTexture(area);
    }

    window.playMusic("assets/fight/fight.ogg");
}

pub fn exit() void {
    window.stopMusic();
}

pub fn update(delta: f32) void {
    if (panel.active) panel.update(delta);
}

pub fn render() void {
    camera.draw(background, .init(0, 0));

    var offset = gfx.Vector.init(120, 120).scale(-1);

    const player1 = &world.players[0];
    camera.draw(player1.attackTexture, offset.add(.init(617, 258)));

    const player2 = &world.players[1];
    camera.draw(player2.attackTexture, offset.add(.init(695, 361)));

    const player3 = &world.players[2];
    camera.draw(player3.attackTexture, offset.add(.init(588, 417)));

    offset = gfx.Vector.init(-160, -120);
    camera.draw(enemies[0].texture, offset.add(.init(253, 250)));
    camera.draw(enemies[1].texture, offset.add(.init(179, 345)));
    camera.draw(enemies[2].texture, offset.add(.init(220, 441)));

    if (panel.active) panel.render();
}
```

## 效果

![战斗指令选择][1]

[1]: images/教你制作RPG游戏64.webp

## 附录
