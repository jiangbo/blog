# 0967-教你制作RPG游戏-失败退出游戏

## 目标

如果角色在战斗中失败，则直接退出游戏。

## 环境

- Time 2025-06-09
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

先对所有的逻辑做减法，教程中战斗失败可以结束还可以扣钱，这里直接退出游戏。

## battle.zig

实现了战斗失败的逻辑。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");
const panel = @import("panel.zig");
const math = @import("../math.zig");
const scene = @import("../scene.zig");

pub const Enemy = struct {
    active: bool = true,
    name: []const u8 = "老虎",
    health: u32 = 100,
    maxHealth: u32 = 100,
    attack: u32 = 90,
    defend: u32 = 10,
    speed: f32 = 10,
    luck: u32 = 10,
};

const statusEnum = enum { idle, attack, hurt, dead, none };

var background: gfx.Texture = undefined;
var enemyTexture: gfx.Texture = undefined;
var enemies: [3]Enemy = undefined;
var targetTexture: gfx.Texture = undefined;

var attackTimer: window.Timer = .init(0.5);
pub var selected: usize = 0;
pub var phase: enum { normal, prepare, select, battle } = .normal;

var areas: [6]gfx.Rectangle = .{
    .init(.init(497, 138), .init(240, 240)),
    .init(.init(575, 241), .init(240, 240)),
    .init(.init(468, 297), .init(240, 240)),
    .init(.init(93, 130), .init(480, 240)),
    .init(.init(19, 225), .init(480, 240)),
    .init(.init(60, 321), .init(480, 240)),
};

var hurtAnimation: gfx.FixedFrameAnimation(5, 0.1) = undefined;

var textures: [areas.len]gfx.Texture = undefined;
pub var status = [1]statusEnum{.idle} ** areas.len;
var actions: [areas.len]u8 = [1]u8{0} ** areas.len;
var timers: [areas.len]window.Timer = undefined;
var timerIndex: usize = 0;

var hurtBlood: ?u32 = null;

var delayTimer: ?window.Timer = null;
var popupPosition: math.Vector = .init(200, 200);
var winTexture: gfx.Texture = undefined;
var loseTexture: gfx.Texture = undefined;

pub fn init() void {
    background = gfx.loadTexture("assets/fight/f_scene.png", .init(800, 600));
    enemyTexture = gfx.loadTexture("assets/fight/enemy.png", .init(1920, 240));
    targetTexture = gfx.loadTexture("assets/fight/fm_b4_2.png", .init(190, 186));
    winTexture = gfx.loadTexture("assets/fight/win.png", .init(341, 217));
    loseTexture = gfx.loadTexture("assets/fight/lose1.png", .init(343, 217));

    hurtAnimation = createAnimation("assets/fight/anm_att.png");

    panel.init();
    attackTimer.stop();

    textures[0] = gfx.loadTexture("assets/fight/p1.png", .init(960, 240));
    textures[1] = gfx.loadTexture("assets/fight/p2.png", .init(960, 240));
    textures[2] = gfx.loadTexture("assets/fight/p3.png", .init(960, 240));
    textures[3] = gfx.loadTexture("assets/fight/enemy.png", .init(1920, 240));
    textures[4] = textures[3];
    textures[5] = textures[3];
}

const SPEED_TIME = 50;
pub fn enter() void {
    window.playMusic("assets/fight/fight.ogg");
    for (&world.players, 0..) |player, index| {
        const speed: f32 = @floatFromInt(player.speed);
        timers[index] = .init(SPEED_TIME / speed);
    }

    const enemyArray: [3]Enemy = .{
        .{},
        .{ .active = false },
        .{ .speed = 20 },
    };

    @memset(status[3..], .none);
    for (enemyArray, 0..) |enemy, index| {
        enemies[index] = enemy;
        timers[3 + index] = .init(SPEED_TIME / enemy.speed);
        if (enemy.active) status[3 + index] = .idle;
    }
}

pub fn exit() void {
    window.stopMusic();
}

pub fn selectFirstEnemy() void {
    for (status[3..], 3..) |s, index| {
        if (s == .idle) selected = index;
        break;
    }
}

pub fn selectAlivePlayer() usize {
    var index = math.randU8(0, 2);
    while (status[index] != .idle) {
        index = math.randU8(0, 2);
    }
    return index;
}

pub fn currentSelectEnemy() *Enemy {
    return &enemies[selected - 3];
}

pub fn selectPrevEnemy() void {
    selected -= 1;
    while (selected > 2) : (selected -= 1) {
        if (status[selected] == .idle) break;
    } else {
        selected = 6;
        selectPrevEnemy();
    }
}

pub fn selectNextEnemy() void {
    selected += 1;
    while (selected < 6) : (selected += 1) {
        if (status[selected] == .idle) break;
    } else {
        selected = 2;
        selectNextEnemy();
    }
}

pub fn startAttackSelected(attack: usize, use: u8) void {
    startAttack(attack, selected, use);
}

fn startAttack(attack: usize, hurt: usize, use: u8) void {
    attackTimer.reset();
    status[attack] = .attack;
    timers[attack].reset();
    actions[attack] = use;
    status[hurt] = .hurt;
    hurtAnimation.reset();
    selected = hurt;

    computeHurtBlood(attack, hurt);
    phase = .battle;
}

fn computeHurtBlood(attack: usize, hurt: usize) void {
    if (attack < 3) {
        const player = world.players[attack];
        const atk = player.attack + player.totalItem.value2;
        hurtBlood = atk -| enemies[hurt - 3].defend;
        enemies[hurt - 3].health -|= hurtBlood.?;
        if (enemies[hurt - 3].health == 0) status[hurt] = .dead;
    } else {
        const player = &world.players[hurt];
        const def = player.defend + player.totalItem.value3;
        hurtBlood = enemies[attack - 3].attack -| def;
        player.health -|= hurtBlood.?;
        if (player.health == 0) status[hurt] = .dead;
    }

    std.log.info("hurt blood: {?d}", .{hurtBlood});
}

var gameLost: bool = true;
pub fn update(delta: f32) void {
    hurtAnimation.update(delta);

    if (delayTimer) |*timer| {
        if (timer.isFinishedAfterUpdate(delta)) {
            delayTimer = null;
            if (gameLost) window.exit();
            return;
        }
        return;
    }

    if (lost()) {
        delayTimer = .init(3);
        gameLost = true;
        return;
    }

    if (win() or lost()) unreachable;

    if (phase == .prepare or phase == .select) {
        panel.update(delta);
        return;
    }

    if (attackTimer.isFinishedAfterUpdate(delta)) {
        for (&status) |*value| {
            if (value.* == .attack or value.* == .hurt)
                value.* = .idle;
        }
        if (phase == .battle) phase = .normal;
        hurtBlood = null;
    }

    if (phase == .battle) return;

    const i = if (timerIndex == 6) 0 else timerIndex;
    for (timers[i..], status[i..], i..) |*timer, s, index| {
        if (s != .idle) continue;

        timerIndex = index + 1;
        if (timer.isRunningAfterUpdate(delta)) continue;

        if (index >= 3) {
            break startAttack(index, selectAlivePlayer(), 0);
        }

        if (index == 0 or index == 1 or index == 2) {
            break panel.onPlayerTurn(index);
        }
    }
}

pub fn win() bool {
    for (status[3..]) |value| {
        if (value == .idle) return false;
    }
    return true;
}

pub fn lost() bool {
    for (status[0..3]) |value| {
        if (value != .dead) return false;
    }
    return true;
}

pub fn render() void {
    camera.draw(background, .init(0, 0));

    for (areas, textures, status) |area, texture, s| {
        if (s == .none) continue;

        const size = area.size();
        const x: f32 = @floatFromInt(@intFromEnum(s));
        const sub = gfx.Rectangle.init(.init(x * size.x, 0), size);
        camera.draw(texture.subTexture(sub), area.min);

        if (s == .hurt and !hurtAnimation.finished()) {
            camera.draw(hurtAnimation.currentTexture(), area.min);
        }
    }

    if (hurtBlood) |blood| {
        var buffer: [32]u8 = undefined;
        const text = std.fmt.bufPrint(&buffer, "- {d}", .{blood});
        camera.drawTextOptions(.{
            .text = text catch unreachable,
            .position = areas[selected].min.add(.init(100, 0)),
            .color = .{ .r = 1, .a = 1 },
        });
    }

    if (delayTimer != null and gameLost) {
        camera.draw(loseTexture, popupPosition);
    }

    if (phase == .battle or phase == .normal) return;

    renderTarget();
    panel.render();

    // for (areas) |area| camera.debugDraw(area);
    // camera.debugDraw(areas[3]);
}

fn renderTarget() void {
    for (areas, 0..) |area, index| {
        if (attackTimer.isRunning() or index != selected) continue;
        camera.draw(targetTexture, area.min.add(.init(40, -40)));
    }
}

fn createAnimation(path: [:0]const u8) gfx.FixedFrameAnimation(5, 0.1) {
    const animation = gfx.loadTexture(path, .init(960, 480));

    const tex = animation.subTexture(.init(.zero, .init(960, 240)));
    hurtAnimation = .initWithCount(tex, 4);
    hurtAnimation.frames[4] = .init(.init(0, 240), .init(240, 240));
    hurtAnimation.loop = false;
    hurtAnimation.stop();
    return hurtAnimation;
}
```

## panel.zig

选择敌人时，能看到敌人的状态信息。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");
const battle = @import("battle.zig");

var attack: gfx.Texture = undefined;
var attackHover: gfx.Texture = undefined;
var item: gfx.Texture = undefined;
var itemHover: gfx.Texture = undefined;
var skill: gfx.Texture = undefined;
var skillHover: gfx.Texture = undefined;
var background: gfx.Texture = undefined;
var health: gfx.Texture = undefined;
var mana: gfx.Texture = undefined;

var selected: enum { attack, item, skill } = .attack;
pub var selectedPlayer: usize = 0;

pub fn init() void {
    attack = gfx.loadTexture("assets/fight/fm_b1_1.png", .init(38, 36));
    attackHover = gfx.loadTexture("assets/fight/fm_b1_2.png", .init(38, 36));
    item = gfx.loadTexture("assets/fight/fm_b2_1.png", .init(38, 36));
    itemHover = gfx.loadTexture("assets/fight/fm_b2_2.png", .init(38, 36));
    skill = gfx.loadTexture("assets/fight/fm_b3_1.png", .init(38, 36));
    skillHover = gfx.loadTexture("assets/fight/fm_b3_2.png", .init(38, 36));
    background = gfx.loadTexture("assets/fight/fm_bg.png", .init(319, 216));
    health = gfx.loadTexture("assets/fight/fm_s1.png", .init(129, 17));
    mana = gfx.loadTexture("assets/fight/fm_s2.png", .init(129, 17));
}

pub fn update(_: f32) void {
    if (battle.phase == .normal) return;

    if (battle.phase == .prepare) {
        if (window.isAnyKeyRelease(&.{ .LEFT, .A })) {
            selected = prevEnum(selected);
        }
        if (window.isAnyKeyRelease(&.{ .RIGHT, .D })) {
            selected = nextEnum(selected);
        }

        updatePrepare();
    } else if (battle.phase == .select) {
        switch (selected) {
            .attack => updateSelectAttack(),
            .skill => updateSelectSkill(),
            .item => updateSelectItem(),
        }
    }
}

pub fn onPlayerTurn(index: usize) void {
    battle.phase = .prepare;
    selectedPlayer = index;
    battle.selected = selectedPlayer;
}

fn updatePrepare() void {
    switch (selected) {
        .attack => {
            if (window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER })) {
                battle.phase = .select;
                battle.selectFirstEnemy();
            }
        },
        .item, .skill => {},
    }
}

fn updateSelectAttack() void {
    if (window.isAnyKeyRelease(&.{ .D, .S, .DOWN, .RIGHT })) {
        battle.selectNextEnemy();
    }

    if (window.isAnyKeyRelease(&.{ .A, .W, .LEFT, .UP })) {
        battle.selectPrevEnemy();
    }

    if (window.isAnyKeyRelease(&.{ .ENTER, .SPACE, .F })) {
        battle.startAttackSelected(selectedPlayer, 1);
    }
}
fn updateSelectItem() void {}
fn updateSelectSkill() void {}

fn prevEnum(value: anytype) @TypeOf(value) {
    var number: usize = @intFromEnum(value);
    if (number == 0) number += enumLength(value);
    return @enumFromInt(number - 1);
}

fn nextEnum(value: anytype) @TypeOf(value) {
    const number: usize = @intFromEnum(value) + 1;
    return @enumFromInt(number % enumLength(value));
}

fn enumLength(value: anytype) usize {
    return @typeInfo(@TypeOf(value)).@"enum".fields.len;
}

const offset = gfx.Vector.init(200, 385);
pub fn render() void {
    camera.draw(background, offset);
    if (battle.selected < 3) renderPlayer() else renderEnemy();
}

fn renderPlayer() void {
    var texture = if (selected == .attack) attackHover else attack;
    camera.draw(texture, offset.add(.init(142, 68)));

    texture = if (selected == .item) itemHover else item;
    camera.draw(texture, offset.add(.init(192, 68)));

    texture = if (selected == .skill) skillHover else skill;
    camera.draw(texture, offset.add(.init(242, 68)));
    const player = &world.players[selectedPlayer];
    // 头像
    camera.draw(player.battleFace, offset);
    drawName(player.name, offset.add(.init(180, 114)));
    // 状态条
    var percent = computePercent(player.health, player.maxHealth);
    drawBar(percent, health, offset.add(.init(141, 145)));
    percent = computePercent(player.mana, player.maxMana);
    drawBar(percent, mana, offset.add(.init(141, 171)));
}

fn renderEnemy() void {
    const enemy = battle.currentSelectEnemy();
    drawName(enemy.name, offset.add(.init(180, 114)));
    // 状态条
    const percent = computePercent(enemy.health, enemy.maxHealth);
    drawBar(percent, health, offset.add(.init(141, 145)));
}

fn computePercent(current: usize, max: usize) f32 {
    if (max == 0) return 0;
    const cur: f32 = @floatFromInt(current);
    return cur / @as(f32, @floatFromInt(max));
}

fn drawBar(percent: f32, tex: gfx.Texture, pos: gfx.Vector) void {
    const width = tex.area.size().x * percent;
    camera.drawOptions(.{
        .texture = tex,
        .source = .init(.zero, .init(width, tex.area.size().y)),
        .target = .init(pos, .init(width, tex.area.size().y)),
    });
}

fn drawName(name: []const u8, pos: gfx.Vector) void {
    camera.drawTextOptions(.{
        .text = name,
        .position = pos,
        .color = gfx.Color{ .g = 0.05, .b = 0.16, .a = 1 },
    });
}

fn drawTextOptions(comptime fmt: []const u8, options: anytype) void {
    var buffer: [256]u8 = undefined;
    const text = std.fmt.bufPrint(&buffer, fmt, options.args);
    camera.drawTextOptions(.{
        .text = text catch unreachable,
        .position = options.position,
        .color = options.color,
    });
}
```

## 效果

![失败退出游戏][1]

[1]: images/教你制作RPG游戏69.webp

## 附录
