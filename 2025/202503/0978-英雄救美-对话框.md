# 0978-英雄救美-对话框

## 目标

人物开场的时候，有两句开场白。需要实现一个对话系统，先实现对话框。

## 环境

- Time 2025-06-20
- Zig 0.14.1

## 参考

1. 圣剑英雄传：英雄救美源码。

## 想法

目前使用的 ZON 来存储对话的，到时候需要的时候，格式应该会继续修改。

## talk.zon

```zig
.{
    .{ .name = "intro", .content =
    \\　　在很久很久以前，白云城的居民过着富足而安定的生活。不过
    \\一场巨大的灾难即将降临到这里……
    \\　　一天，我们故事的主人翁'小飞刀'一觉醒来，故事就从这里开
    \\始……　　[按回车键继续]
    },
    .{ .name = "小飞刀", .content = "……天亮了，今天天气真不错.", .next = 2 },
    .{ .name = "小飞刀", .content = "我该出去转转了，顺便弄点好吃的！嘻嘻……" },
}
```

## world.zig

新增了人物的渲染和对话框。

```zig
const std = @import("std");

const window = @import("zhu").window;
const gfx = @import("zhu").gfx;
const camera = @import("zhu").camera;
const math = @import("zhu").math;
const scene = @import("scene.zig");

var playerTexture: gfx.Texture = undefined;
var mapTexture: gfx.Texture = undefined;

const Map = struct {
    indexes: []const u16,
    items: []const struct { index: u16, item: u16 },
};

const map: Map = @import("zon/map.zon");

var tiles: [500]camera.Vertex = undefined;
var tileIndex: usize = 0;
const Status = union(enum) {
    normal,
    talk: usize,
};
var status: Status = .normal;

const FrameAnimation = gfx.FixedFrameAnimation(3, 0.1);
const Animation = std.EnumArray(math.FourDirection, FrameAnimation);

var playerAnimation: Animation = undefined;
var playerDirection: math.FourDirection = .up;
var playerPosition: math.Vector = .init(180, 164);

var talkTexture: gfx.Texture = undefined;

pub fn init() void {
    playerTexture = gfx.loadTexture("assets/pic/player.png", .init(96, 192));
    mapTexture = gfx.loadTexture("assets/pic/maps.png", .init(640, 1536));

    // 背景
    for (map.indexes, 0..) |mapIndex, index| {
        if (mapIndex == std.math.maxInt(u16)) continue;

        const tile = mapTexture.subTexture(getAreaFromIndex(mapIndex));
        tiles[tileIndex] = .{
            .position = getAreaFromIndex(index).min,
            .size = .init(32, 32),
            .texture = tile.area.toVector4(),
        };
        tileIndex += 1;
    }

    // 装饰
    for (map.items) |item| {
        const tile = mapTexture.subTexture(getAreaFromIndex(item.item));
        tiles[tileIndex] = .{
            .position = getAreaFromIndex(item.index).min,
            .size = .init(32, 32),
            .texture = tile.area.toVector4(),
        };
        tileIndex += 1;
    }

    playerAnimation = createAnimation("assets/pic/player.png");

    talkTexture = gfx.loadTexture("assets/pic/talkbar.png", .init(640, 96));

    status = .{ .talk = 1 };

    // window.playMusic("assets/voc/back.ogg");
}

fn getAreaFromIndex(index: usize) gfx.Rectangle {
    const row: f32 = @floatFromInt(index / 20);
    const col: f32 = @floatFromInt(index % 20);
    return .init(.init(col * 32, row * 32), .init(32, 32));
}

fn createAnimation(path: [:0]const u8) Animation {
    var animation = Animation.initUndefined();

    const texture = gfx.loadTexture(path, .init(96, 192));
    var tex = texture.subTexture(.init(.zero, .init(96, 48)));
    animation.set(.down, FrameAnimation.init(tex));

    tex = texture.subTexture(.init(.init(0, 48), .init(96, 48)));
    animation.set(.left, FrameAnimation.init(tex));

    tex = texture.subTexture(.init(.init(0, 96), .init(96, 48)));
    animation.set(.right, FrameAnimation.init(tex));

    tex = texture.subTexture(.init(.init(0, 144), .init(96, 48)));
    animation.set(.up, FrameAnimation.init(tex));
    return animation;
}

pub fn update(delta: f32) void {
    _ = delta;

    switch (status) {
        .normal => {},
        .talk => |talkId| updateTalk(talkId),
    }
}

fn updateTalk(talkId: usize) void {
    if (!confirm()) return;

    const next = scene.talks[talkId].next;
    status = if (next == 0) .normal else .{ .talk = next };
}

fn confirm() bool {
    return window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER });
}

pub fn enter() void {}

pub fn exit() void {}

pub fn render() void {
    camera.drawVertex(mapTexture, tiles[0..tileIndex]);

    const animation = playerAnimation.get(playerDirection);
    camera.draw(animation.currentTexture(), playerPosition);

    switch (status) {
        .normal => {},
        .talk => |talkId| renderTalk(talkId),
    }
}

fn renderTalk(talkId: usize) void {
    camera.draw(talkTexture, .init(0, 384));

    const downAnimation = playerAnimation.get(.down);
    const tex = downAnimation.texture.mapTexture(downAnimation.frames[0]);
    camera.draw(tex, .init(30, 396));

    const talk = scene.talks[talkId];
    const nameColor = gfx.color(1, 1, 0, 1);
    camera.drawColorText(talk.name, .init(18, 445), nameColor);

    camera.drawColorText(talk.content, .init(123, 403), .{ .w = 1 });
    camera.drawColorText(talk.content, .init(120, 400), .one);
}
```

## 效果

![对话框][1]

[1]: images/英雄救美09.png

## 附录
