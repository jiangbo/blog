# 0965-教你制作RPG游戏-战斗回合

## 目标

实现回合制攻击，根据每个角色和敌人的速度来决定攻击的快慢。

## 环境

- Time 2025-06-08
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

不清楚应该怎么设计回合制攻击，先乱搞出来，后面找教程学一下，教程中根本没有使用游戏循环。

## panel.zig

定义了角色准备和选择的阶段和动作。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");
const battle = @import("battle.zig");

var attack: gfx.Texture = undefined;
var attackHover: gfx.Texture = undefined;
var item: gfx.Texture = undefined;
var itemHover: gfx.Texture = undefined;
var skill: gfx.Texture = undefined;
var skillHover: gfx.Texture = undefined;
var background: gfx.Texture = undefined;
var health: gfx.Texture = undefined;
var mana: gfx.Texture = undefined;

var selected: enum { attack, item, skill } = .attack;
pub var selectedPlayer: usize = 0;

pub fn init() void {
    attack = gfx.loadTexture("assets/fight/fm_b1_1.png", .init(38, 36));
    attackHover = gfx.loadTexture("assets/fight/fm_b1_2.png", .init(38, 36));
    item = gfx.loadTexture("assets/fight/fm_b2_1.png", .init(38, 36));
    itemHover = gfx.loadTexture("assets/fight/fm_b2_2.png", .init(38, 36));
    skill = gfx.loadTexture("assets/fight/fm_b3_1.png", .init(38, 36));
    skillHover = gfx.loadTexture("assets/fight/fm_b3_2.png", .init(38, 36));
    background = gfx.loadTexture("assets/fight/fm_bg.png", .init(319, 216));
    health = gfx.loadTexture("assets/fight/fm_s1.png", .init(129, 17));
    mana = gfx.loadTexture("assets/fight/fm_s2.png", .init(129, 17));
}

pub fn update(_: f32) void {
    if (battle.phase == .normal) return;

    if (battle.phase == .prepare) {
        if (window.isAnyKeyRelease(&.{ .LEFT, .A })) {
            selected = prevEnum(selected);
        }
        if (window.isAnyKeyRelease(&.{ .RIGHT, .D })) {
            selected = nextEnum(selected);
        }

        updatePrepare();
    } else if (battle.phase == .select) {
        switch (selected) {
            .attack => updateSelectAttack(),
            .skill => updateSelectSkill(),
            .item => updateSelectItem(),
        }
    }
}

pub fn onPlayerTurn(index: usize) void {
    battle.phase = .prepare;
    selectedPlayer = index;
    battle.selected = selectedPlayer;
}

fn updatePrepare() void {
    switch (selected) {
        .attack => {
            if (window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER })) {
                battle.phase = .select;
                battle.selectFirstEnemy();
            }
        },
        .item, .skill => {},
    }
}

fn updateSelectAttack() void {
    if (window.isAnyKeyRelease(&.{ .D, .S, .DOWN, .RIGHT })) {
        battle.selectNextEnemy();
    }

    if (window.isAnyKeyRelease(&.{ .A, .W, .LEFT, .UP })) {
        battle.selectPrevEnemy();
    }

    if (window.isAnyKeyRelease(&.{ .ENTER, .SPACE, .F })) {
        battle.startAttackSelected(selectedPlayer, 1);
    }
}
fn updateSelectSkill() void {}
fn updateSelectItem() void {}

fn prevEnum(value: anytype) @TypeOf(value) {
    var number: usize = @intFromEnum(value);
    if (number == 0) number += enumLength(value);
    return @enumFromInt(number - 1);
}

fn nextEnum(value: anytype) @TypeOf(value) {
    const number: usize = @intFromEnum(value) + 1;
    return @enumFromInt(number % enumLength(value));
}

fn enumLength(value: anytype) usize {
    return @typeInfo(@TypeOf(value)).@"enum".fields.len;
}

pub fn render() void {
    const offset = gfx.Vector.init(200, 385);
    camera.draw(background, offset);

    var texture = if (selected == .attack) attackHover else attack;
    camera.draw(texture, offset.add(.init(142, 68)));

    texture = if (selected == .item) itemHover else item;
    camera.draw(texture, offset.add(.init(192, 68)));

    texture = if (selected == .skill) skillHover else skill;
    camera.draw(texture, offset.add(.init(242, 68)));

    // 头像
    const player = &world.players[selectedPlayer];
    camera.draw(player.battleFace, offset);

    drawName(player.name, offset.add(.init(180, 114)));
    // 状态条
    var percent = computePercent(player.health, player.maxHealth);
    drawBar(percent, health, offset.add(.init(141, 145)));
    percent = computePercent(player.mana, player.maxMana);
    drawBar(percent, mana, offset.add(.init(141, 171)));
}

fn computePercent(current: usize, max: usize) f32 {
    if (max == 0) return 0;
    const cur: f32 = @floatFromInt(current);
    return cur / @as(f32, @floatFromInt(max));
}

fn drawBar(percent: f32, tex: gfx.Texture, pos: gfx.Vector) void {
    const width = tex.area.size().x * percent;
    camera.drawOptions(.{
        .texture = tex,
        .source = .init(.zero, .init(width, tex.area.size().y)),
        .target = .init(pos, .init(width, tex.area.size().y)),
    });
}

fn drawName(name: []const u8, pos: gfx.Vector) void {
    camera.drawTextOptions(.{
        .text = name,
        .position = pos,
        .color = gfx.Color{ .g = 0.05, .b = 0.16, .a = 1 },
    });
}

fn drawTextOptions(comptime fmt: []const u8, options: anytype) void {
    var buffer: [256]u8 = undefined;
    const text = std.fmt.bufPrint(&buffer, fmt, options.args);
    camera.drawTextOptions(.{
        .text = text catch unreachable,
        .position = options.position,
        .color = options.color,
    });
}
```

## battle.zig

定义了回合制攻击顺序。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");
const panel = @import("panel.zig");
const math = @import("../math.zig");

pub const Enemy = struct {
    active: bool = true,
    health: u32 = 100,
    maxHealth: u32 = 100,
    attack: u32 = 10,
    defend: u32 = 10,
    speed: f32 = 10,
    luck: u32 = 10,
};

const statusEnum = enum { idle, attack, hurt, dead, none };

var background: gfx.Texture = undefined;
var enemyTexture: gfx.Texture = undefined;
var enemies: [3]Enemy = undefined;
var targetTexture: gfx.Texture = undefined;

var attackTimer: window.Timer = .init(0.4);
pub var selected: usize = 0;
pub var phase: enum { normal, prepare, select, battle } = .normal;

var areas: [6]gfx.Rectangle = .{
    .init(.init(497, 138), .init(240, 240)),
    .init(.init(575, 241), .init(240, 240)),
    .init(.init(468, 297), .init(240, 240)),
    .init(.init(93, 130), .init(480, 240)),
    .init(.init(19, 225), .init(480, 240)),
    .init(.init(60, 321), .init(480, 240)),
};
var textures: [areas.len]gfx.Texture = undefined;
pub var status = [1]statusEnum{.idle} ** areas.len;
var actions: [areas.len]u8 = [1]u8{0} ** areas.len;
var timers: [areas.len]window.Timer = undefined;
var timerIndex: usize = 0;

pub fn init() void {
    background = gfx.loadTexture("assets/fight/f_scene.png", .init(800, 600));
    enemyTexture = gfx.loadTexture("assets/fight/enemy.png", .init(1920, 240));
    targetTexture = gfx.loadTexture("assets/fight/fm_b4_2.png", .init(190, 186));
    panel.init();
    attackTimer.stop();

    textures[0] = gfx.loadTexture("assets/fight/p1.png", .init(960, 240));
    textures[1] = gfx.loadTexture("assets/fight/p2.png", .init(960, 240));
    textures[2] = gfx.loadTexture("assets/fight/p3.png", .init(960, 240));
    textures[3] = gfx.loadTexture("assets/fight/enemy.png", .init(1920, 240));
    textures[4] = textures[3];
    textures[5] = textures[3];
}

const SPEED_TIME = 50;
pub fn enter() void {
    window.playMusic("assets/fight/fight.ogg");
    for (&world.players, 0..) |player, index| {
        const speed: f32 = @floatFromInt(player.speed);
        timers[index] = .init(SPEED_TIME / speed);
    }

    const enemyArray: [3]Enemy = .{ .{}, .{ .active = false }, .{ .speed = 20 } };

    @memset(status[3..], .none);
    for (enemyArray, 0..) |enemy, index| {
        enemies[index] = enemy;
        timers[3 + index] = .init(SPEED_TIME / enemy.speed);
        if (enemy.active) status[3 + index] = .idle;
    }
}

pub fn exit() void {
    window.stopMusic();
}

pub fn selectFirstEnemy() void {
    for (status[3..], 3..) |s, index| {
        if (s == .idle) selected = index;
        break;
    }
}

pub fn selectPrevEnemy() void {
    selected -= 1;
    while (selected > 2) : (selected -= 1) {
        if (status[selected] == .idle) break;
    } else {
        selected = 6;
        selectPrevEnemy();
    }
}

pub fn selectNextEnemy() void {
    selected += 1;
    while (selected < 6) : (selected += 1) {
        if (status[selected] == .idle) break;
    } else {
        selected = 2;
        selectNextEnemy();
    }
}

pub fn startAttackSelected(attack: usize, use: u8) void {
    startAttack(attack, selected, use);
}

fn startAttack(attack: usize, hurt: usize, use: u8) void {
    attackTimer.reset();
    status[attack] = .attack;
    timers[attack].reset();
    actions[attack] = use;
    status[hurt] = .hurt;
    phase = .battle;
}

pub fn update(delta: f32) void {
    if (phase == .prepare or phase == .select) {
        panel.update(delta);
        return;
    }

    if (attackTimer.isFinishedAfterUpdate(delta)) {
        for (&status) |*value| {
            if (value.* == .attack or value.* == .hurt)
                value.* = .idle;
        }
        if (phase == .battle) phase = .normal;
    }

    if (phase == .battle) return;

    const i = if (timerIndex == 6) 0 else timerIndex;
    for (timers[i..], status[i..], i..) |*timer, s, index| {
        if (s != .idle) continue;

        timerIndex = index + 1;
        if (timer.isRunningAfterUpdate(delta)) continue;

        if (index == 3 or index == 4 or index == 5) {
            break startAttack(index, math.randU8(0, 2), 0);
        }

        if (index == 0 or index == 1 or index == 2) {
            break panel.onPlayerTurn(index);
        }
    }
}

pub fn render() void {
    camera.draw(background, .init(0, 0));

    for (areas, textures, status) |area, texture, s| {
        if (s == .none) continue;

        const size = area.size();
        const x: f32 = @floatFromInt(@intFromEnum(s));
        const sub = gfx.Rectangle.init(.init(x * size.x, 0), size);
        camera.draw(texture.subTexture(sub), area.min);
    }

    if (phase == .battle or phase == .normal) return;

    renderTarget();
    panel.render();

    // for (areas) |area| camera.debugDraw(area);
    // camera.debugDraw(areas[3]);
}

fn renderTarget() void {
    for (areas, 0..) |area, index| {
        if (attackTimer.isRunning() or index != selected) continue;
        camera.draw(targetTexture, area.min.add(.init(40, -40)));
    }
}
```

## 效果

![战斗回合][1]

[1]: images/教你制作RPG游戏67.webp

## 附录
