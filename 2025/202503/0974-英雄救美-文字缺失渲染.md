# 0974-英雄救美-文字缺失渲染

## 目标

如果文字不存在，则渲染方块字，表示文字找不到。

## 环境

- Time 2025-06-19
- Zig 0.14.1

## 参考

1. 圣剑英雄传：英雄救美源码。

## 想法

新增了一个调试绘制，统计了图片，文字，绘制调用的次数等。

## scene.zig

新增了调试绘制，将一些基本的信息显示出来。

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const camera = @import("camera.zig");

const titleScene = @import("scene/title.zig");
const worldScene = @import("scene/world.zig");

const Talk = struct { content: []const u8 };
pub const talks: []const Talk = @import("zon/talk.zon");

const SceneType = enum { title, world };
var currentSceneType: SceneType = .title;
var toSceneType: SceneType = .title;

var vertexBuffer: [100 * 4]camera.Vertex = undefined;
var fontVertexBuffer: [1000 * 4]camera.Vertex = undefined;

pub fn init() void {
    const fontTexture = gfx.loadTexture("assets/font.png", .init(832, 832));
    window.initFont(.{
        .font = @import("zon/font.zon"),
        .texture = fontTexture,
        .vertex = &fontVertexBuffer,
    });

    camera.init(&vertexBuffer);

    titleScene.init();
    worldScene.init();

    enter();
}

pub fn event(ev: *const window.Event) void {
    titleScene.event(ev);
}

pub fn enter() void {
    sceneCall("enter", .{});
}

pub fn exit() void {
    sceneCall("exit", .{});
}

pub fn changeScene(sceneType: SceneType) void {
    toSceneType = sceneType;
    fadeOut(doChangeScene);
}

fn doChangeScene() void {
    exit();
    currentSceneType = toSceneType;
    enter();
}

pub fn update(delta: f32) void {
    if (fadeTimer) |*timer| {
        if (timer.isRunningAfterUpdate(delta)) return;
        if (isFadeIn) {
            fadeTimer = null;
        } else {
            if (fadeOutCallback) |callback| callback();
            isFadeIn = true;
            timer.restart();
        }
        return;
    }
    sceneCall("update", .{delta});
}

pub fn render() void {
    camera.beginDraw();
    defer camera.endDraw();

    window.keepAspectRatio();

    sceneCall("render", .{});

    // 将文字先绘制上，后面的淡入淡出才会生效。
    camera.flush();
    if (fadeTimer) |*timer| {
        const percent = timer.elapsed / timer.duration;
        const alpha = if (isFadeIn) 1 - percent else percent;
        camera.drawRectangle(.init(.zero, window.size), .{ .w = alpha });
    }

    var buffer: [100]u8 = undefined;
    const format =
        \\帧率：{}
        \\图片：{}
        \\文字：{}
        \\绘制：{}
        \\迷榖
    ;

    const text = std.fmt.bufPrint(&buffer, format, .{
        window.frameRate,
        camera.imageDrawCount(),
        camera.textDrawCount() + debutTextCount,
        camera.gpuDrawCount() + 1,
    }) catch unreachable;

    var iterator = std.unicode.Utf8View.initUnchecked(text).iterator();
    var count: u32 = 0;
    while (iterator.nextCodepoint()) |_| count += 1;
    debutTextCount = count;

    camera.drawColorText(text, .init(10, 5), .green);
}

var debutTextCount: u32 = 0;

var fadeTimer: ?window.Timer = null;
var isFadeIn: bool = false;
var fadeOutCallback: ?*const fn () void = null;

pub fn fadeIn() void {
    isFadeIn = true;
    fadeTimer = .init(2);
}

pub fn fadeOut(callback: ?*const fn () void) void {
    isFadeIn = false;
    fadeTimer = .init(2);
    fadeOutCallback = callback;
}

fn sceneCall(comptime function: []const u8, args: anytype) void {
    switch (currentSceneType) {
        .title => window.call(titleScene, function, args),
        .world => window.call(worldScene, function, args),
    }
}
```

## font.zig

处理了文字找不到的情况，获取一个默认的文字。

```zig
const std = @import("std");

const gpu = @import("gpu.zig");
const math = @import("math.zig");
const shader = @import("shader/font.glsl.zig");
const window = @import("window.zig");

pub const Font = struct {
    atlas: struct {
        type: []const u8,
        distanceRange: u32,
        distanceRangeMiddle: u32,
        size: f32,
        width: u32,
        height: u32,
        yOrigin: []const u8,
    },
    metrics: struct {
        emSize: u32,
        lineHeight: f32,
        ascender: f32,
        descender: f32,
        underlineY: f32,
        underlineThickness: f32,
    },
    glyphs: []const Glyph,
    kerning: struct {},
};

const Glyph = struct {
    unicode: u32,
    advance: f32,
    planeBounds: Rect = .{},
    atlasBounds: Rect = .{},
};

const Rect = struct {
    left: f32 = 0,
    top: f32 = 0,
    right: f32 = 0,
    bottom: f32 = 0,

    fn toArea(self: Rect) math.Rectangle {
        return .{
            .min = .{ .x = self.left, .y = self.top },
            .max = .{ .x = self.right, .y = self.bottom },
        };
    }
};

var font: Font = undefined;
var texture: gpu.Texture = undefined;
var invalidUnicode: u32 = 0x25A0;
var invalidIndex: usize = 0;

var pipeline: gpu.RenderPipeline = undefined;
var bindGroup: gpu.BindGroup = .{};
var buffer: gpu.Buffer = undefined;
var needDrawCount: u32 = 0;
pub var totalDrawCount: u32 = 0;

const initOptions = struct {
    font: Font,
    texture: gpu.Texture,
    vertex: []gpu.QuadVertex,
};

fn binarySearch(unicode: u32) ?usize {
    return std.sort.binarySearch(Glyph, font.glyphs, unicode, compare);
}

pub fn init(options: initOptions) void {
    font = options.font;
    invalidIndex = binarySearch(invalidUnicode).?;
    texture = options.texture;

    buffer = gpu.createBuffer(.{
        .size = @sizeOf(gpu.QuadVertex) * options.vertex.len,
        .usage = .{ .vertex_buffer = true, .stream_update = true },
    });

    const shaderDesc = shader.fontShaderDesc(gpu.queryBackend());
    pipeline = gpu.createQuadPipeline(shaderDesc);
}

fn searchGlyph(code: u32) *const Glyph {
    return &font.glyphs[binarySearch(code) orelse invalidIndex];
}

fn compare(a: u32, b: Glyph) std.math.Order {
    if (a < b.unicode) return .lt;
    if (a > b.unicode) return .gt;
    return .eq;
}

pub fn beginDraw() void {
    totalDrawCount = 0;
}

pub fn drawText(text: []const u8, position: math.Vector) void {
    drawTextOptions(text, .{ .position = position });
}

pub const TextOptions = struct {
    size: f32 = 18,
    position: math.Vector,
    color: math.Vector4 = .one,
};

const Color = math.Vector4;
pub fn drawColorText(text: []const u8, pos: math.Vector, color: Color) void {
    drawTextOptions(text, .{ .position = pos, .color = color });
}

pub fn drawTextOptions(text: []const u8, options: TextOptions) void {
    const Utf8View = std.unicode.Utf8View;
    var iterator = Utf8View.initUnchecked(text).iterator();

    const offsetY = -font.metrics.ascender * options.size;
    var pos = options.position.addY(offsetY);

    while (iterator.nextCodepoint()) |code| {
        if (code == '\n') {
            const height = font.metrics.lineHeight * options.size;
            pos = .init(options.position.x, pos.y + height);
            continue;
        }
        const char = searchGlyph(code);

        const target = char.planeBounds.toArea();
        gpu.appendBuffer(buffer, &.{gpu.QuadVertex{
            .position = pos.add(target.min.scale(options.size)),
            .size = target.size().scale(options.size).toVector2(),
            .texture = char.atlasBounds.toArea().toVector4(),
            .color = options.color,
        }});
        needDrawCount += 1;
        totalDrawCount += 1;
        pos = pos.addX(char.advance * options.size);
    }
}

pub fn flush() void {
    if (needDrawCount == 0) return;

    // 绑定流水线
    gpu.setPipeline(pipeline);

    // 处理 uniform 变量
    const x, const y = .{ window.size.x, window.size.y };
    const viewMatrix = [16]f32{
        2 / x, 0, 0, 0, 0,  2 / -y, 0, 0,
        0,     0, 1, 0, -1, 1,      0, 1,
    };
    const size = gpu.queryTextureSize(texture.image);
    gpu.setUniform(shader.UB_vs_params, .{
        .viewMatrix = viewMatrix,
        .textureVec = [4]f32{ size.x, size.y, 1, 1 },
    });

    // 绑定组
    bindGroup.setSampler(gpu.linearSampler);
    bindGroup.setTexture(texture);
    bindGroup.setVertexBuffer(buffer);
    const vertexOffset = totalDrawCount - needDrawCount;
    bindGroup.setVertexOffset(vertexOffset * @sizeOf(gpu.QuadVertex));
    gpu.setBindGroup(bindGroup);

    // 绘制
    gpu.drawInstanced(needDrawCount);
    needDrawCount = 0;
}
```

## 效果

![文字缺失渲染][1]

[1]: images/英雄救美05.png

## 附录
