# 0958-教你制作RPG游戏-调试绘制

## 目标

有时候在界面上定义了一个区域，但是没有绘制任何东西，就不太清楚位置在哪里，所以新增一个调试绘制。

## 环境

- Time 2025-06-07
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

调试绘制可以在指定的区域绘制一个半透明的颜色。

## gpu.zig

新增了一个创建纹理的方法。

```zig
...
pub fn createTexture(size: math.Vector, data: []const u8) Texture {
    return Texture{
        .image = sk.gfx.makeImage(.{
            .data = init: {
                var imageData = sk.gfx.ImageData{};
                imageData.subimage[0][0] = sk.gfx.asRange(data);
                break :init imageData;
            },
            .width = @intFromFloat(size.x),
            .height = @intFromFloat(size.y),
            .pixel_format = .RGBA8,
        }),
        .area = .init(.zero, size),
    };
}
...
```

## camera.zig

调试绘制的方法。

```zig
const std = @import("std");

const gpu = @import("gpu.zig");
const math = @import("math.zig");
const shader = @import("shader/texture.glsl.zig");
const window = @import("window.zig");

pub const Vertex = extern struct {
    position: math.Vector3, // 顶点坐标
    color: gpu.Color, // 顶点颜色
    uv: math.Vector2 = .zero, // 纹理坐标
};

pub var rect: math.Rectangle = undefined;
var border: math.Vector = undefined;

var viewMatrix: [16]f32 = undefined;
var renderPass: gpu.RenderPassEncoder = undefined;
var bindGroup: gpu.BindGroup = .{};
var pipeline: gpu.RenderPipeline = undefined;

var buffer: gpu.Buffer = undefined;
var needDrawCount: u32 = 0;
var totalDrawCount: u32 = 0;
var texture: gpu.Texture = .{ .image = .{} };
var debugTexture: gpu.Texture = undefined;

pub fn init(r: math.Rectangle, b: math.Vector, vertex: []Vertex, index: []u16) void {
    rect = r;
    border = b;

    const x, const y = .{ rect.size().x, rect.size().y };
    viewMatrix = .{
        2 / x, 0, 0, 0, 0,  2 / -y, 0, 0,
        0,     0, 1, 0, -1, 1,      0, 1,
    };

    bindGroup.setIndexBuffer(gpu.createBuffer(.{
        .type = .INDEXBUFFER,
        .data = gpu.asRange(index),
    }));

    buffer = gpu.createBuffer(.{
        .size = @sizeOf(Vertex) * vertex.len,
        .usage = .STREAM,
    });

    bindGroup.setSampler(shader.SMP_smp, gpu.createSampler(.{}));
    pipeline = initPipeline();

    const data: [64]u8 = [1]u8{0xFF} ** 64;
    debugTexture = gpu.createTexture(.init(4, 4), &data);
}

fn initPipeline() gpu.RenderPipeline {
    var vertexLayout = gpu.VertexLayout{};
    vertexLayout.attrs[shader.ATTR_texture_position0].format = .FLOAT3;
    vertexLayout.attrs[shader.ATTR_texture_color0].format = .FLOAT4;
    vertexLayout.attrs[shader.ATTR_texture_texcoord0].format = .FLOAT2;

    const shaderDesc = shader.textureShaderDesc(gpu.queryBackend());
    return gpu.createRenderPipeline(.{
        .shader = gpu.createShaderModule(shaderDesc),
        .vertexLayout = vertexLayout,
        .color = .{ .blend = .{
            .enabled = true,
            .src_factor_rgb = .SRC_ALPHA,
            .dst_factor_rgb = .ONE_MINUS_SRC_ALPHA,
        } },
        .index_type = .UINT16,
        .depth = .{ .compare = .LESS_EQUAL, .write_enabled = true },
    });
}

pub fn lookAt(pos: math.Vector) void {
    if (needDrawCount != 0) doDraw();

    const half = rect.size().scale(0.5);

    const max = border.sub(rect.size());
    const offset = pos.sub(half).clamp(.zero, max);

    rect = .init(offset, rect.size());
}

pub fn toWorldPosition(position: math.Vector) math.Vector {
    return position.add(rect.min);
}

pub fn toWindowPosition(position: math.Vector) math.Vector {
    return position.sub(rect.min);
}

pub fn beginDraw(color: gpu.Color) void {
    renderPass = gpu.commandEncoder.beginRenderPass(color);
    totalDrawCount = 0;
}

pub fn debugDraw(area: math.Rectangle) void {
    drawOptions(.{
        .texture = debugTexture,
        .source = debugTexture.area,
        .target = area,
        .color = .{ .r = 1, .b = 1, .a = 0.1 },
    });
}

pub fn draw(tex: gpu.Texture, position: math.Vector) void {
    drawFlipX(tex, position, false);
}

pub fn drawFlipX(tex: gpu.Texture, pos: math.Vector, flipX: bool) void {
    const target: math.Rectangle = .init(pos, tex.size());
    var src = tex.area;
    if (flipX) {
        src.min.x = tex.area.max.x;
        src.max.x = tex.area.min.x;
    }

    drawOptions(.{ .texture = tex, .source = src, .target = target });
}

const DrawOptions = struct {
    texture: gpu.Texture,
    source: math.Rectangle,
    target: math.Rectangle,
    radians: f32 = 0,
    pivot: math.Vector = .zero,
    color: gpu.Color = .{ .r = 1, .g = 1, .b = 1, .a = 1 },
};

pub fn drawOptions(options: DrawOptions) void {
    var vertexes = createVertexes(options.source, options.target);
    for (&vertexes) |*value| value.position.z = 0.5;
    for (&vertexes) |*value| value.color = options.color;
    gpu.appendBuffer(buffer, &vertexes);

    defer {
        needDrawCount += 1;
        totalDrawCount += 1;
        texture = options.texture;
    }

    if (totalDrawCount == 0) return; // 第一次绘制
    if (options.texture.image.id != texture.image.id) doDraw();
}

pub fn drawText(text: []const u8, position: math.Vector) void {
    drawTextOptions(.{ .text = text, .position = position });
}

const TextOptions = struct {
    text: []const u8,
    position: math.Vector,
    color: gpu.Color = .{ .r = 1, .g = 1, .b = 1, .a = 1 },
};

pub fn drawTextOptions(options: TextOptions) void {
    const Utf8View = std.unicode.Utf8View;
    var iterator = Utf8View.initUnchecked(options.text).iterator();

    var pos = options.position;
    var line: f32 = 1;
    while (iterator.nextCodepoint()) |code| {
        if (code == '\n') {
            pos = options.position.addY(line * window.lineHeight);
            line += 1;
            continue;
        }

        const char = window.fonts.get(code) orelse
            window.fonts.get(std.math.maxInt(u32)).?;
        const size = math.Vector.init(char.width, char.height);
        const area = math.Rectangle.init(.init(char.x, char.y), size);
        const tex = window.fontTexture.subTexture(area);
        drawOptions(.{
            .texture = tex,
            .source = area,
            .target = .init(pos.add(.init(char.xOffset, char.yOffset)), size),
            .color = options.color,
        });
        pos = pos.addX(char.xAdvance);
    }
}

pub fn endDraw() void {
    if (needDrawCount != 0) doDraw();

    renderPass.end();
    gpu.commandEncoder.submit();
}

fn createVertexes(src: math.Rectangle, dst: math.Rectangle) [4]Vertex {
    var vertexes: [4]Vertex = undefined;

    vertexes[0].position = dst.min.addY(dst.size().y);
    vertexes[0].uv = .init(src.min.x, src.max.y);

    vertexes[1].position = dst.max;
    vertexes[1].uv = .init(src.max.x, src.max.y);

    vertexes[2].position = dst.min.addX(dst.size().x);
    vertexes[2].uv = .init(src.max.x, src.min.y);

    vertexes[3].position = dst.min;
    vertexes[3].uv = .init(src.min.x, src.min.y);
    return vertexes;
}

fn doDraw() void {

    // 绑定流水线
    renderPass.setPipeline(pipeline);

    // 处理 uniform 变量
    viewMatrix[12] = -1 - rect.min.x * viewMatrix[0];
    viewMatrix[13] = 1 - rect.min.y * viewMatrix[5];
    const size = gpu.queryTextureSize(texture.image);
    renderPass.setUniform(shader.UB_vs_params, .{
        .viewMatrix = viewMatrix,
        .textureVec = [4]f32{ size.x, size.y, 1, 1 },
    });

    // 绑定组
    bindGroup.setTexture(shader.IMG_tex, texture);
    bindGroup.setVertexBuffer(buffer);

    const offset = totalDrawCount - needDrawCount;
    bindGroup.setIndexOffset(offset * 6 * @sizeOf(u16));
    renderPass.setBindGroup(bindGroup);

    // 绘制
    renderPass.draw(needDrawCount * 6);
    needDrawCount = 0;
}
```

## statusPopup.zig

```zig
...
for (&buttons) |value| camera.debugDraw(value);
...
```

## 效果

![调试绘制][1]

[1]: images/教你制作RPG游戏60.webp

## 附录
