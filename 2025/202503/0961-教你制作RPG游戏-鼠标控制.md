# 0961-教你制作RPG游戏-鼠标控制

## 目标

继续完善之前的商店，之前只能键盘控制，现在新增了鼠标控制。

## 环境

- Time 2025-06-07
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

感觉界面的布局，还需要继续了解一下才行，对于布局，现在都是写死的。

## shop.zig

完善界面布局与鼠标控制。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");
const bag = @import("bag.zig");

pub var active: bool = false;
var background: gfx.Texture = undefined;
const position = gfx.Vector.init(190, 70);

var items: [6]bag.Item = undefined;
var selected: usize = 0;
var selectedTexture: gfx.Texture = undefined;

var closeTexture: gfx.Texture = undefined;
var closeHover: gfx.Texture = undefined;
var prePageHover: gfx.Texture = undefined;
var buyHover: gfx.Texture = undefined;
var nextPageHover: gfx.Texture = undefined;
var buttons: [7]gfx.Rectangle = undefined;
var hover: [buttons.len]bool = undefined;

pub fn init() void {
    items[0] = bag.Item{ .info = &bag.infos[0], .count = 1 };
    items[1] = bag.Item{ .info = &bag.infos[1], .count = 1 };
    items[2] = bag.Item{ .info = &bag.infos[2], .count = 1 };
    items[3] = bag.Item{ .info = &bag.infos[3], .count = 1 };
    items[4] = bag.Item{ .info = &bag.infos[4], .count = 1 };

    background = gfx.loadTexture("assets/item/shop_bg.png", .init(367, 437));
    selectedTexture = gfx.loadTexture("assets/item/sbt7_2.png", .init(273, 90));

    closeTexture = gfx.loadTexture("assets/item/sbt6_1.png", .init(30, 31));
    closeHover = gfx.loadTexture("assets/item/sbt6_2.png", .init(30, 31));

    prePageHover = gfx.loadTexture("assets/item/sbt3_2.png", .init(67, 28));
    buyHover = gfx.loadTexture("assets/item/sbt8_2.png", .init(67, 28));
    nextPageHover = gfx.loadTexture("assets/item/sbt5_2.png", .init(67, 28));

    buttons[0] = .init(position.add(.init(62, 55)), .init(250, 70));
    buttons[1] = .init(position.add(.init(62, 150)), .init(250, 70));
    buttons[2] = .init(position.add(.init(62, 245)), .init(250, 70));
    buttons[3] = .init(position.add(.init(79, 335)), .init(67, 28));
    buttons[4] = .init(position.add(.init(154, 335)), .init(67, 28));
    buttons[5] = .init(position.add(.init(230, 335)), .init(67, 28));
    buttons[6] = .init(position.add(.init(328, 16)), .init(30, 31));
}

pub fn update(delta: f32) void {
    _ = delta;
    @memset(&hover, false);

    if (window.isAnyKeyRelease(&.{ .Q, .ESCAPE })) active = false;

    if (window.isAnyKeyRelease(&.{ .W, .UP })) {
        if (selected == 0) selected = items.len;
        selected -= 1;
    }

    if (window.isAnyKeyRelease(&.{ .A, .LEFT })) prePage();
    if (window.isAnyKeyRelease(&.{ .D, .RIGHT })) nextPage();

    if (window.isAnyKeyRelease(&.{ .S, .DOWN })) {
        selected += 1;
        selected = selected % items.len;
    }

    if (window.isAnyKeyRelease(&.{ .SPACE, .ENTER, .F })) buyItem();

    for (&buttons, 0..) |area, index| {
        if (area.contains(window.mousePosition)) {
            if (window.isButtonRelease(.LEFT))
                leftClickButton(index)
            else if (window.isButtonRelease(.RIGHT)) {
                rightClickButton(index);
            } else hover[index] = true;
            break;
        }
    }
}

fn buyItem() void {
    if (items[selected].info.price <= bag.money) {
        bag.addItem(items[selected].info);
        bag.money -= items[selected].info.price;
    }
}

fn prePage() void {
    if (selected / 3 == 0) selected += items.len;
    selected -= 3;
}

fn nextPage() void {
    selected = (selected + 3) % items.len;
}

fn leftClickButton(index: usize) void {
    if (index == 0 or index == 1 or index == 2) {
        selected = selected / 3 * 3 + index;
    }

    if (index == 3) prePage();
    if (index == 4) buyItem();
    if (index == 5) nextPage();
    if (index == 6) active = false;
}

fn rightClickButton(index: usize) void {
    if (index == 0 or index == 1 or index == 2) {
        selected = selected / 3 * 3 + index;
        buyItem();
    }
}

pub fn render() void {
    camera.draw(background, position);

    const page = selected / 3;
    for (items[page * 3 ..][0..3], 0..) |item, index| {
        const offset = position.add(.init(62, 54));
        const pos = offset.addY(@floatFromInt(96 * index));
        if (selected == index + page * 3) {
            camera.draw(selectedTexture, pos.sub(.init(10, 10)));
        }

        if (item.count == 0) continue;
        camera.draw(item.info.texture, pos);
        drawItemInfo(.{item.info.name}, pos.addX(80));
        drawItemInfo(.{item.info.tip}, pos.add(.init(80, 25)));
        drawPriceText(.{item.info.price}, pos.add(.init(180, 0)));
    }

    drawMoneyText(.{bag.money}, position.add(.init(180, 382)));

    if (hover[3]) camera.draw(prePageHover, buttons[3].min);
    if (hover[4]) camera.draw(buyHover, buttons[4].min);
    if (hover[5]) camera.draw(nextPageHover, buttons[5].min);

    const close = if (hover[6]) closeHover else closeTexture;
    camera.draw(close, buttons[6].min);

    for (&buttons) |button| camera.debugDraw(button);
}

fn drawPriceText(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("$ {}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 0.75, .g = 0.89, .b = 0.26, .a = 1 },
    });
}

fn drawMoneyText(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("{}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 0.21, .g = 0.09, .b = 0.01, .a = 1 },
    });
}

fn drawItemInfo(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("{s}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 0.75, .g = 0.89, .b = 0.26, .a = 1 },
    });
}

fn drawTextOptions(comptime fmt: []const u8, options: anytype) void {
    var buffer: [256]u8 = undefined;
    const text = std.fmt.bufPrint(&buffer, fmt, options.args);
    camera.drawTextOptions(.{
        .text = text catch unreachable,
        .position = options.position,
        .color = options.color,
    });
}
```

## 效果

![鼠标控制][1]

[1]: images/教你制作RPG游戏63.webp

## 附录
