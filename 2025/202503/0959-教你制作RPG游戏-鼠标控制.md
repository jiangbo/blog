# 0959-教你制作RPG游戏-鼠标控制

## 目标

使用鼠标来操作状态界面。

## 环境

- Time 2025-06-07
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

之前定义了调试绘制，可以比较好地看到鼠标可以反馈的位置和大小。

## statusPopup.zig

现在可以使用鼠标来操作了。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");
const bag = @import("bag.zig");

pub const MenuType = enum { item, skill };

pub var display: bool = true;
var position: gfx.Vector = undefined;
var background: gfx.Texture = undefined;
var selected: gfx.Texture = undefined;
var itemTexture: gfx.Texture = undefined;
var skillTexture: gfx.Texture = undefined;

var selectedPlayer: usize = 0;
var selectedItem: usize = 0;
var menuType: MenuType = .item;

var closeTexture: gfx.Texture = undefined;
var closeHover: gfx.Texture = undefined;
var nextPlayerHover: gfx.Texture = undefined;
var prePageHover: gfx.Texture = undefined;
var useHover: gfx.Texture = undefined;
var nextPageHover: gfx.Texture = undefined;
var buttons: [12]gfx.Rectangle = undefined;
var hover: [buttons.len]bool = undefined;

pub fn init() void {
    position = .init(58, 71);
    background = gfx.loadTexture("assets/item/status_bg.png", .init(677, 428));
    selected = gfx.loadTexture("assets/item/sbt7_2.png", .init(273, 90));
    itemTexture = gfx.loadTexture("assets/item/sbt2_1.png", .init(62, 255));
    skillTexture = gfx.loadTexture("assets/item/sbt2_2.png", .init(62, 255));

    nextPlayerHover = gfx.loadTexture("assets/item/sbt1_2.png", .init(23, 27));
    closeTexture = gfx.loadTexture("assets/item/sbt6_1.png", .init(30, 31));
    closeHover = gfx.loadTexture("assets/item/sbt6_2.png", .init(30, 31));

    prePageHover = gfx.loadTexture("assets/item/sbt3_2.png", .init(67, 28));
    useHover = gfx.loadTexture("assets/item/sbt4_2.png", .init(67, 28));
    nextPageHover = gfx.loadTexture("assets/item/sbt5_2.png", .init(67, 28));

    buttons[0] = .init(.init(360, 363), .init(33, 33));
    buttons[1] = .init(.init(99, 126), .init(66, 66));
    buttons[2] = .init(.init(99, 208), .init(66, 66));
    buttons[3] = .init(.init(419, 120), .init(250, 70));
    buttons[4] = .init(.init(419, 215), .init(250, 70));
    buttons[5] = .init(.init(419, 310), .init(250, 70));
    buttons[6] = .init(.init(431, 397), .init(67, 28));
    buttons[7] = .init(.init(505, 396), .init(67, 28));
    buttons[8] = .init(.init(581, 396), .init(67, 28));
    buttons[9] = .init(.init(693, 136), .init(40, 100));
    buttons[10] = .init(.init(693, 252), .init(40, 100));
    buttons[11] = .init(.init(685, 75), .init(30, 31));
}

pub fn update(delta: f32) void {
    @memset(&hover, false);

    if (window.isAnyKeyRelease(&.{ .ESCAPE, .Q, .E })) display = false;

    if (window.isAnyKeyRelease(&.{ .LEFT, .A })) {
        selectedPlayer += world.players.len;
        selectedPlayer = (selectedPlayer - 1) % world.players.len;
    } else if (window.isAnyKeyRelease(&.{ .RIGHT, .D })) {
        selectedPlayer = (selectedPlayer + 1) % world.players.len;
    }

    if (window.isKeyRelease(.TAB)) {
        menuType = if (menuType == .item) .skill else .item;
        selectedItem = 0;
    }

    if (window.isAnyKeyRelease(&.{ .W, .UP })) {
        if (selectedItem == 0) selectedItem = bag.items.len;
        selectedItem -= 1;
    }

    if (window.isAnyKeyRelease(&.{ .S, .DOWN })) {
        selectedItem += 1;
        selectedItem = selectedItem % bag.items.len;
    }

    if (window.isAnyKeyRelease(&.{ .SPACE, .F, .ENTER })) {
        world.players[selectedPlayer].useItem(&bag.items[selectedItem]);
    }

    for (&buttons, 0..) |area, index| {
        if (area.contains(window.mousePosition)) {
            if (window.isButtonRelease(.LEFT))
                leftClickButton(index)
            else if (window.isButtonRelease(.RIGHT)) {
                rightClickButton(index);
            } else hover[index] = true;
            break;
        }
    }

    _ = delta;
}

fn leftClickButton(index: usize) void {
    if (index == 0) {
        selectedPlayer = (selectedPlayer + 1) % world.players.len;
    }

    if (index == 3 or index == 4 or index == 5) {
        selectedItem = selectedItem / 3 * 3 + index % 3;
    }

    if (index == 6) {
        if (selectedItem / 3 == 0) selectedItem += bag.items.len;
        selectedItem -= 3;
    }

    if (index == 7) {
        world.players[selectedPlayer].useItem(&bag.items[selectedItem]);
    }

    if (index == 8) selectedItem = (selectedItem + 3) % bag.items.len;

    if (index == 9) menuType = .item;
    if (index == 10) menuType = .skill;

    if (index == 11) display = false;
}

fn rightClickButton(index: usize) void {
    if (index == 1 or index == 2) {
        world.players[selectedPlayer].removeItem(index);
    }

    if (index == 3 or index == 4 or index == 5) {
        selectedItem = selectedItem / 3 * 3 + index % 3;
        world.players[selectedPlayer].useItem(&bag.items[selectedItem]);
    }
}

pub fn render() void {
    if (!display) return;

    camera.draw(background, position);

    renderStatus();

    var items: []bag.Item = undefined;

    if (menuType == .item) {
        items = &bag.items;
        camera.draw(itemTexture, position.add(.init(629, 51)));
    } else {
        items = &bag.skills;
        camera.draw(skillTexture, position.add(.init(629, 51)));
    }

    const page = selectedItem / 3;
    for (items[page * 3 ..][0..3], 0..) |item, index| {
        const offset = position.add(.init(360, 48));
        const pos = offset.addY(@floatFromInt(96 * index));
        if (selectedItem == index + page * 3) {
            camera.draw(selected, pos.sub(.init(10, 10)));
        }
        if (item.count == 0) continue;

        camera.draw(item.info.texture, pos);
        drawItemInfo(.{item.info.name}, pos.addX(80));
        drawItemInfo(.{item.info.tip}, pos.add(.init(80, 25)));
        drawCount(.{item.count}, pos.addX(160));
    }

    drawStatusText(.{bag.money}, .init(525, 445));

    if (hover[6]) camera.draw(prePageHover, buttons[6].min);
    if (hover[7]) camera.draw(useHover, buttons[7].min);
    if (hover[8]) camera.draw(nextPageHover, buttons[8].min);

    const close = if (hover[11]) closeHover else closeTexture;
    camera.draw(close, buttons[11].min);

    for (&buttons) |value| camera.debugDraw(value);
}

fn renderStatus() void {
    if (hover[0]) camera.draw(nextPlayerHover, .init(362, 368));

    const player = &world.players[selectedPlayer];
    camera.draw(player.statusTexture, position);

    if (player.attackItem) |item| {
        camera.draw(item.texture, position.add(.init(41, 55)));
    }

    if (player.defendItem) |item| {
        camera.draw(item.texture, position.add(.init(41, 136)));
    }

    drawStatusText(.{player.health}, .init(155, 411));
    drawStatusText(.{player.mana}, .init(290, 411));

    const item = &player.totalItem;
    drawStatusText(.{player.attack}, .init(155, 431));
    drawEffect(.{item.value2}, .init(185, 431));
    drawStatusText(.{player.defend}, .init(290, 431));
    drawEffect(.{item.value3}, .init(320, 431));

    drawStatusText(.{player.speed}, .init(155, 451));
    drawEffect(.{item.value4}, .init(185, 451));
    drawStatusText(.{player.luck}, .init(290, 451));
    drawEffect(.{item.value5}, .init(320, 451));
}

fn drawStatusText(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("{}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 0.21, .g = 0.09, .b = 0.01, .a = 1 },
    });
}

fn drawEffect(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("+{}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 1, .a = 1 },
    });
}

fn drawItemInfo(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("{s}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 0.75, .g = 0.89, .b = 0.26, .a = 1 },
    });
}

fn drawCount(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("X {}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 0.75, .g = 0.89, .b = 0.26, .a = 1 },
    });
}

fn drawTextOptions(comptime fmt: []const u8, options: anytype) void {
    var buffer: [256]u8 = undefined;
    const text = std.fmt.bufPrint(&buffer, fmt, options.args);
    camera.drawTextOptions(.{
        .text = text catch unreachable,
        .position = options.position,
        .color = options.color,
    });
}
```

## 效果

![鼠标控制][1]

[1]: images/教你制作RPG游戏61.webp

## 附录
