# 0979-英雄救美-新增角色模块

## 目标

角色是玩家控制的，新增一个角色模块，接受键盘控制，可以在地图上移动。

## 环境

- Time 2025-06-20
- Zig 0.14.1

## 参考

1. 圣剑英雄传：英雄救美源码。

## 想法

将 world.zig 中的部分代码移动到了 player.zig 模块中。

## world.zig

```zig
const std = @import("std");

const window = @import("zhu").window;
const gfx = @import("zhu").gfx;
const camera = @import("zhu").camera;

const player = @import("player.zig");

var mapTexture: gfx.Texture = undefined;

const Map = struct {
    indexes: []const u16,
    items: []const struct { index: u16, item: u16 },
};

const map: Map = @import("zon/map.zon");

var tiles: [500]camera.Vertex = undefined;
var tileIndex: usize = 0;
const Status = union(enum) { normal, talk: usize };
var status: Status = .normal;

const Talk = struct {
    actor: u8 = 0,
    content: []const u8,
    next: usize = 0,
};
const talks: []const Talk = @import("zon/talk.zon");
var talkTexture: gfx.Texture = undefined;

pub fn init() void {
    mapTexture = gfx.loadTexture("assets/pic/maps.png", .init(640, 1536));

    // 背景
    for (map.indexes, 0..) |mapIndex, index| {
        if (mapIndex == std.math.maxInt(u16)) continue;

        const tile = mapTexture.subTexture(getAreaFromIndex(mapIndex));
        tiles[tileIndex] = .{
            .position = getAreaFromIndex(index).min,
            .size = .init(32, 32),
            .texture = tile.area.toVector4(),
        };
        tileIndex += 1;
    }

    // 装饰
    for (map.items) |item| {
        const tile = mapTexture.subTexture(getAreaFromIndex(item.item));
        tiles[tileIndex] = .{
            .position = getAreaFromIndex(item.index).min,
            .size = .init(32, 32),
            .texture = tile.area.toVector4(),
        };
        tileIndex += 1;
    }

    talkTexture = gfx.loadTexture("assets/pic/talkbar.png", .init(640, 96));
    status = .{ .talk = 1 };
    player.init();

    // window.playMusic("assets/voc/back.ogg");
}

fn getAreaFromIndex(index: usize) gfx.Rectangle {
    const row: f32 = @floatFromInt(index / 20);
    const col: f32 = @floatFromInt(index % 20);
    return .init(.init(col * 32, row * 32), .init(32, 32));
}

pub fn update(delta: f32) void {
    switch (status) {
        .normal => {},
        .talk => |talkId| return updateTalk(talkId),
    }

    player.update(delta);
}

fn updateTalk(talkId: usize) void {
    if (!confirm()) return;

    const next = talks[talkId].next;
    status = if (next == 0) .normal else .{ .talk = next };
}

fn confirm() bool {
    return window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER });
}

pub fn enter() void {}

pub fn exit() void {}

pub fn render() void {
    camera.drawVertex(mapTexture, tiles[0..tileIndex]);
    player.render();

    switch (status) {
        .normal => {},
        .talk => |talkId| renderTalk(talkId),
    }
}

fn renderTalk(talkId: usize) void {
    camera.draw(talkTexture, .init(0, 384));

    const talk = talks[talkId];
    if (talk.actor == 0) player.renderTalk();

    camera.drawColorText(talk.content, .init(123, 403), .{ .w = 1 });
    camera.drawColorText(talk.content, .init(120, 400), .one);
}
```

## player.zig

新增了人物的控制。

```zig
const std = @import("std");

const window = @import("zhu").window;
const gfx = @import("zhu").gfx;
const camera = @import("zhu").camera;
const math = @import("zhu").math;

const FrameAnimation = gfx.FixedFrameAnimation(3, 0.15);
const Animation = std.EnumArray(math.FourDirection, FrameAnimation);

const name = "小飞刀";
const speed = 100;
var texture: gfx.Texture = undefined;
var animation: Animation = undefined;

var moving: bool = false;
var direction: math.Vector = .zero;
var position: math.Vector = .init(180, 164);

pub fn init() void {
    texture = gfx.loadTexture("assets/pic/player.png", .init(96, 192));

    animation = Animation.initUndefined();

    var tex = texture.subTexture(.init(.zero, .init(96, 48)));
    animation.set(.down, FrameAnimation.init(tex));

    tex = texture.subTexture(tex.area.move(.init(0, 48)));
    animation.set(.left, FrameAnimation.init(tex));

    tex = texture.subTexture(tex.area.move(.init(0, 48)));
    animation.set(.right, FrameAnimation.init(tex));

    tex = texture.subTexture(tex.area.move(.init(0, 48)));
    animation.set(.up, FrameAnimation.init(tex));
}

pub fn update(delta: f32) void {
    move(delta);

    if (moving) animation.getPtr(facing()).update(delta);
}

fn move(delta: f32) void {
    var dir = math.Vector.zero;
    if (window.isAnyKeyDown(&.{ .UP, .W })) dir.y -= 1;
    if (window.isAnyKeyDown(&.{ .DOWN, .S })) dir.y += 1;
    if (window.isAnyKeyDown(&.{ .LEFT, .A })) dir.x -= 1;
    if (window.isAnyKeyDown(&.{ .RIGHT, .D })) dir.x += 1;

    if (dir.approxEqual(.zero)) {
        moving = false;
    } else {
        moving = true;
        direction = dir.normalize().scale(speed);
        position = position.add(direction.scale(delta));
    }
}

pub fn render() void {
    const current = animation.get(facing());
    camera.draw(current.currentTexture(), position);
}

fn facing() math.FourDirection {
    if (@abs(direction.x) > @abs(direction.y))
        return if (direction.x < 0) .left else .right
    else
        return if (direction.y < 0) .up else .down;
}

pub fn renderTalk() void {

    // 头像
    const down = animation.get(.down);
    const tex = down.texture.subTexture(down.frames[0]);
    camera.draw(tex, .init(30, 396));

    // 名字
    const nameColor = gfx.color(1, 1, 0, 1);
    camera.drawColorText(name, .init(18, 445), nameColor);
}
```

## 效果

![新增角色模块][1]

[1]: images/英雄救美10.png

## 附录
