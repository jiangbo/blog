# 0984-英雄救美-关于菜单显示

## 目标

制作关于菜单，里面需要显示很多的文字，并且文字要实现滚动的效果，超出边界自动消失。

## 环境

- Time 2025-06-21
- Zig 0.14.1

## 参考

1. 圣剑英雄传：英雄救美源码。

## 想法

这里遇到几个难点：

1. 文字很多，没有全部显示，而是根据界面的大小计算了显示的行数，只显示需要的文本。
2. 滚动的效果只需要修改坐标，但是需要根据偏移的坐标计算行数，这个也不好计算。
3. 超出边界自动消失，使用的裁剪，之前没有使用过这个。

## about.zig

对于关于菜单，单独制作了一个模块来进行显示。

```zig
const std = @import("std");

const window = @import("zhu").window;
const gfx = @import("zhu").gfx;
const camera = @import("zhu").camera;

var texture: gfx.Texture = undefined;

pub var roll: bool = false;
var timer: window.Timer = .init(0.05);

pub fn init() void {
    texture = gfx.loadTexture("assets/pic/sbar.png", .init(420, 320));
}

pub fn update(delta: f32) void {
    if (!roll) return;

    if (timer.isFinishedAfterUpdate(delta)) {
        if (end) return;
        timer.restart();
        rollOffset += 1;
    }
}

pub fn render() void {
    const position = gfx.Vector.init(120, 90);
    camera.draw(texture, position.addXY(-10, -10));

    if (roll) return renderRoll(position);

    var text: []const u8 = "圣剑英雄传--英雄救美（测试版）";
    camera.drawColorText(text, position.addXY(62, 17), .{ .w = 1 });
    camera.drawText(text, position.addXY(60, 15));

    text =
        \\　　这是我们的第一个RPG游戏，本来只是
        \\想练一练手而已，不过做到一半时才发现自
        \\己错了：既然做了就应该把它做好！
        \\　　现今，国内游戏界还普遍存在着急功近
        \\利、粗制滥造的现象，希望制作者们用实际
        \\行动来改变它吧！我们的宗旨是“不求极品，
        \\但求精品！”;
    ;
    camera.drawColorText(text, position.addXY(25, 52), .{ .w = 1 });
    camera.drawText(text, position.addXY(23, 50));

    text =
        \\成都金点工作组 E-mail: wj77@163.net
        \\　　网站 http://goldpoint.126.com
    ;

    camera.drawColorText(text, position.addXY(25, 248), .{ .w = 1 });
    camera.drawText(text, position.addXY(23, 246));
}

const intro =
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　《圣剑英雄传》制作群　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　《英雄救美》是一款微型的中文RPG
    \\游戏，由成都金点工作组成员 softboy
    \\和 EVA编写，游戏中出现的图片主要由网
    \\友 qinyong、 Daimy和cuigod提供。
    \\这是一个自由游戏软件，你可以任意复制
    \\并传播。如果愿意还可以自由更改，我们
    \\提供源程序。　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　====游戏运行要求====：　　
    \\　　　　　　　　　　　　　　　　　
    \\主机：INTEL兼容芯片，奔腾100以上CPU
    \\内存：8 兆以上　　　　　　　　　　
    \\显卡：SVGA 640*480*256　　　　　　
    \\声卡：WINDOWS 95兼容卡（可选）　　
    \\控制：键盘　　　　　　　　　　　　
    \\平台：WIN 95／98 + DirectX 5.0　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　======键盘定义======：　　
    \\　　　　　　　　　　　　　　　　　
    \\上、下、左、右 ---------- 行走
    \\　　　　　Ctrl ---------- 对话
    \\　　　　Enter  ---------- 确认
    \\　　　　Escape ---------- 调主菜单
    \\
    \\        ======文件清单======：
    \\
    \\  rpg.exe--------主程序
    \\  readme.txt-----说明/帮助
    \\  log.txt--------游戏制作日志
    \\  maps\*.*-------地图数据/NPC数据
    \\  pic\*.*--------游戏中使用的图片
    \\  text\*.*-------对白/物品数据
    \\  voc\*.*--------声音
    \\
    \\   最后，祝大家快乐！
    \\
    \\        敬礼！
    \\
    \\    =========制作成员=========：
    \\
    \\softboy -- 程序     wj77@163.net
    \\李为EVA -- 美工     eva@188.net
    \\qinyong -- 图片提供 qinyong@163.net
    \\  daimy -- 图片提供 daimy@163.net
    \\ cuigod -- 图片提供 cuiin@263.net
    \\   孔雀 -- 剧情支持 kclamp@21cn.com
    \\
    \\    =========联系方法=========
    \\
    \\汪疆(softboy)
    \\Mail:wj77@163.net
    \\主页:http://goldpoint.126.com
    \\ Tel:(028-4318564)
    \\成都电子科技大学 95080-5 [610054]
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　成都金点工作组
    \\　　　　　　一九九九年六月
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
    \\　　　　　　　　　　　　　　　　　
;

var rollOffset: usize = 0;
const lineHeight = 26;
var start: usize = 0;
var end: bool = false;

pub fn resetRoll() void {
    roll = false;
    rollOffset = 0;
    end = false;
    timer.reset();
}

fn renderRoll(position: gfx.Vector) void {
    defer camera.resetScissor();

    const size = gfx.Vector.init(380, 280);
    camera.scissor(.init(position.addXY(20, 12), size));

    const offsetY: f32 = @floatFromInt(rollOffset % lineHeight);

    if (end) {
        camera.drawText(intro[start..], position.addXY(25, -offsetY));
        return;
    }

    const startLine = rollOffset / lineHeight;
    var line: u8 = 0;

    var iter = std.unicode.Utf8View.initUnchecked(intro).iterator();
    if (startLine == 0)
        line = 0
    else while (iter.nextCodepoint()) |unicode| {
        if (unicode != '\n') continue;
        line += 1;
        if (line >= startLine) break;
    }

    start = iter.i;
    while (iter.nextCodepoint()) |unicode| {
        if (unicode != '\n') continue;
        line += 1;
        if (line >= startLine + 12) break;
    } else end = true;

    camera.drawText(intro[start..iter.i], position.addXY(25, -offsetY));
}
```

## world.zig

world 主要是流程和状态的控制。

```zig
const std = @import("std");

const window = @import("zhu").window;
const gfx = @import("zhu").gfx;
const camera = @import("zhu").camera;

const player = @import("player.zig");
const map = @import("map.zig");
const talk = @import("talk.zig");
const about = @import("about.zig");

const Status = union(enum) { normal, talk: usize, menu, about };
var status: Status = .normal;

const Menu = struct {
    names: []const []const u8,
    areas: []const gfx.Rectangle = undefined,
    current: usize = 0,
    const color = gfx.Color{ .w = 1 };
};

var menu: Menu = .{
    .names = &.{
        "状　　态", "物　　品", "读取进度", "存储进度", //
        "关于游戏", "退　　出", "返回游戏",
    },
    .areas = &createAreas(7, .{ .x = 0 + 33, .y = 288 }),
};

fn createAreas(comptime num: u8, pos: gfx.Vector) [num]gfx.Rectangle {
    var areas: [num]gfx.Rectangle = undefined;
    for (&areas, 0..) |*area, i| {
        const offsetY: f32 = @floatFromInt(10 + i * 24);
        area.* = .init(pos.addY(offsetY), .init(85, 25));
    }
    return areas;
}

var menuTexture: gfx.Texture = undefined;

pub fn init() void {
    menuTexture = gfx.loadTexture("assets/pic/mainmenu1.png", .init(150, 200));
    talk.init();
    about.init();
    map.init();
    player.init();

    // window.playMusic("assets/voc/back.ogg");
    // status = .{ .talk = 1 };
    // status = .about;
    // about.roll = true;
}

pub fn event(ev: *const window.Event) void {
    if (ev.type != .MOUSE_MOVE) return;

    for (menu.areas, 0..) |area, i| {
        if (area.contains(window.mousePosition)) {
            menu.current = i;
        }
    }
}

pub fn update(delta: f32) void {
    switch (status) {
        .normal => {},
        .talk => |talkId| updateTalk(talkId),
        .menu => return updateMenu(),
        .about => return updateAbout(delta),
    }

    // 角色移动和碰撞检测
    const toPosition = player.toMove(delta);
    if (toPosition) |position| {
        if (map.canWalk(position.addXY(-8, -12)) and
            map.canWalk(position.addXY(-8, 2)) and
            map.canWalk(position.addXY(8, -12)) and
            map.canWalk(position.addXY(8, 2)))
            player.position = position;
    }

    // 交互检测
    if (window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER })) {
        const object = map.talk(player.position, player.facing());
        if (object != 0) handleObject(object);
    }

    // 打开菜单
    if (window.isAnyKeyRelease(&.{ .ESCAPE, .E })) status = .menu;

    player.update(delta);
}

fn updateTalk(talkId: usize) void {
    const next = talk.update(talkId);
    status = if (next == 0) .normal else .{ .talk = next };
}

fn updateAbout(delta: f32) void {
    if (window.isAnyKeyRelease(&.{ .ESCAPE, .Q })) {
        status = .normal;
        return;
    }

    if (about.roll) {
        about.update(delta);
    } else {
        if (window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER })) {
            about.roll = true;
        }
    }
}

fn handleObject(object: u16) void {
    if (object & 0x1000 != 0) handleChest(object);
}

fn handleChest(object: u16) void {
    if (object == 0x1000) {
        const gold = window.random().intRangeLessThanBiased(u8, 10, 100);
        player.money += gold;
        status = .{ .talk = 3 };
        talk.talkNumber = gold;
    } else {
        status = .{ .talk = 4 };
    }
}

fn updateMenu() void {
    if (window.isAnyKeyRelease(&.{ .ESCAPE, .E, .Q })) status = .normal;

    if (window.isAnyKeyRelease(&.{ .DOWN, .S })) {
        menu.current = (menu.current + 1) % menu.names.len;
    }
    if (window.isAnyKeyRelease(&.{ .UP, .W })) {
        menu.current += menu.names.len;
        menu.current = (menu.current - 1) % menu.names.len;
    }

    var confirm = window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER });
    if (window.isButtonRelease(.LEFT)) {
        for (menu.areas, 0..) |area, i| {
            if (area.contains(window.mousePosition)) {
                menu.current = i;
                confirm = true;
            }
        }
    }

    if (confirm) menuSelected();
}

fn menuSelected() void {
    switch (menu.current) {
        0...3 => status = .normal,
        4 => {
            status = .about;
            about.resetRoll();
        },
        5 => window.exit(),
        6 => status = .normal,
        else => {},
    }
}

pub fn enter() void {}

pub fn exit() void {}

pub fn render() void {
    map.render();
    player.render();

    switch (status) {
        .normal => {},
        .talk => |talkId| talk.render(talkId),
        .menu => renderMenu(),
        .about => about.render(),
    }
}

fn renderMenu() void {
    camera.draw(menuTexture, .init(0, 280));

    for (menu.areas, menu.names, 0..) |area, name, i| {
        if (i == menu.current) {
            camera.drawRectangle(area, Menu.color);
        }
        camera.drawText(name, area.min.addXY(5, -2));
    }
}
```

## 效果

![关于菜单显示][1]

[1]: images/英雄救美15.png

## 附录
