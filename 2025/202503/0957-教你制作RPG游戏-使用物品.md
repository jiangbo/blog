# 0957-教你制作RPG游戏-使用物品

## 目标

可以浏览背包，并且选择其中的物品进行使用，如果是装备，则进行穿戴，存在时则替换。

## 环境

- Time 2025-06-07
- Zig 0.14.1

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

实现物品的使用和装备功能。

## bag.zig

新增了一个增加物品的方法。

```zig
const std = @import("std");

const gfx = @import("../graphics.zig");

pub const Item = struct {
    info: *ItemInfo,
    count: u32 = 0,
};

pub const ItemInfo = struct {
    name: []const u8 = &.{},
    texture: gfx.Texture,
    tip: []const u8 = &.{},
    price: u32 = 0,
    value1: u32 = 0,
    value2: u32 = 0,
    value3: u32 = 0,
    value4: u32 = 0,
    value5: u32 = 0,

    pub fn addValue(self: *ItemInfo, other: *const ItemInfo) void {
        self.value2 += other.value2;
        self.value3 += other.value3;
        self.value4 += other.value4;
        self.value5 += other.value5;
    }
};

pub var infos: [8]ItemInfo = undefined;

pub var money: u32 = 143;
pub var items: [9]Item = undefined;
pub var skills: [9]Item = undefined;

pub fn init() void {
    initInfos();

    for (&items) |*item| item.count = 0;

    items[0] = .{ .info = &infos[0], .count = 2 };
    items[1] = .{ .info = &infos[1], .count = 3 };
    items[2] = .{ .info = &infos[2], .count = 4 };
    items[3] = .{ .info = &infos[3], .count = 8 };
    items[4] = .{ .info = &infos[4], .count = 6 };

    for (&skills) |*skill| skill.count = 0;

    skills[0] = .{ .info = &infos[6], .count = 20 };
    skills[1] = .{ .info = &infos[7], .count = 20 };
}

pub fn addItem(itemInfo: *ItemInfo) void {
    for (&items) |*item| {
        if (item.info == itemInfo) {
            item.count += 1;
            return;
        }
    }

    for (&items) |*item| {
        if (item.count == 0) {
            item.info = itemInfo;
            item.count = 1;
            return;
        }
    }
}

fn initInfos() void {
    infos[0] = .{
        .name = "红药水",
        .texture = gfx.loadTexture("assets/item/item1.png", .init(66, 66)),
        .tip = "恢复少量 HP",
        .price = 30,
        .value1 = 3,
        .value2 = 30,
    };
    infos[1] = .{
        .name = "蓝药水",
        .texture = gfx.loadTexture("assets/item/item2.png", .init(66, 66)),
        .tip = "恢复少量 MP",
        .price = 200,
        .value1 = 4,
        .value2 = 20,
    };
    infos[2] = .{
        .name = "短剑",
        .texture = gfx.loadTexture("assets/item/item3.png", .init(66, 66)),
        .tip = "一把钢制短剑",
        .price = 100,
        .value1 = 1,
        .value2 = 10,
        .value5 = 5,
    };
    infos[3] = .{
        .name = "斧头",
        .texture = gfx.loadTexture("assets/item/item4.png", .init(66, 66)),
        .tip = "传说这是一把能够劈开\n阴气的斧头",
        .price = 100,
        .value1 = 1,
        .value2 = 3,
        .value5 = 50,
    };
    infos[4] = .{
        .name = "钢盾",
        .texture = gfx.loadTexture("assets/item/item5.png", .init(66, 66)),
        .tip = "钢质盾牌，没有矛可以\n穿破它",
        .price = 100,
        .value1 = 2,
        .value3 = 20,
        .value4 = 5,
    };
    infos[5] = .{
        .name = "魔法书",
        .texture = gfx.loadTexture("assets/item/item6.png", .init(66, 66)),
        .tip = "一本游记，记录世间\n奇事，可打开阅览",
        .price = 100,
    };

    infos[6] = .{
        .name = "治疗术",
        .texture = gfx.loadTexture("assets/item/skill1.png", .init(66, 66)),
        .tip = "恢复少量 HP",
        .price = 20,
    };

    infos[7] = .{
        .name = "黑洞漩涡",
        .texture = gfx.loadTexture("assets/item/skill2.png", .init(66, 66)),
        .tip = "攻击型技能，\n将敌人吸入漩涡",
        .price = 20,
    };
}
```

## statusPopup.zig

可以浏览和使用物品。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const camera = @import("../camera.zig");
const bag = @import("bag.zig");

pub const MenuType = enum { item, skill };

pub var display: bool = false;
var position: gfx.Vector = undefined;
var background: gfx.Texture = undefined;
var selected: gfx.Texture = undefined;
var itemTexture: gfx.Texture = undefined;
var skillTexture: gfx.Texture = undefined;

var selectedPlayer: usize = 0;
var selectedItem: usize = 0;
var menuType: MenuType = .item;

pub fn init() void {
    position = .init(58, 71);
    background = gfx.loadTexture("assets/item/status_bg.png", .init(677, 428));
    selected = gfx.loadTexture("assets/item/sbt7_2.png", .init(273, 90));
    itemTexture = gfx.loadTexture("assets/item/sbt2_1.png", .init(62, 255));
    skillTexture = gfx.loadTexture("assets/item/sbt2_2.png", .init(62, 255));
}

pub fn update(delta: f32) void {
    if (window.isAnyKeyRelease(&.{ .ESCAPE, .Q, .E })) display = false;

    if (window.isAnyKeyRelease(&.{ .LEFT, .A })) {
        selectedPlayer += world.players.len;
        selectedPlayer = (selectedPlayer - 1) % world.players.len;
    } else if (window.isAnyKeyRelease(&.{ .RIGHT, .D })) {
        selectedPlayer = (selectedPlayer + 1) % world.players.len;
    }

    if (window.isKeyRelease(.TAB)) {
        menuType = if (menuType == .item) .skill else .item;
        selectedItem = 0;
    }

    if (window.isAnyKeyRelease(&.{ .W, .UP })) {
        if (selectedItem == 0) selectedItem = bag.items.len;
        selectedItem -= 1;
    }

    if (window.isAnyKeyRelease(&.{ .S, .DOWN })) {
        selectedItem += 1;
        selectedItem = selectedItem % bag.items.len;
    }

    if (window.isAnyKeyRelease(&.{ .SPACE, .F, .ENTER })) {
        world.players[selectedPlayer].useItem(&bag.items[selectedItem]);
    }

    _ = delta;
}

pub fn render() void {
    if (!display) return;

    camera.draw(background, position);

    renderStatus();

    var items: []bag.Item = undefined;

    if (menuType == .item) {
        items = &bag.items;
        camera.draw(itemTexture, position.add(.init(629, 51)));
    } else {
        items = &bag.skills;
        camera.draw(skillTexture, position.add(.init(629, 51)));
    }

    const page = selectedItem / 3;
    for (items[page * 3 ..][0..3], 0..) |item, index| {
        const offset = position.add(.init(360, 48));
        const pos = offset.addY(@floatFromInt(96 * index));
        if (selectedItem == index + page * 3) {
            camera.draw(selected, pos.sub(.init(10, 10)));
        }
        if (item.count == 0) continue;

        camera.draw(item.info.texture, pos);
        drawItemInfo(.{item.info.name}, pos.addX(80));
        drawItemInfo(.{item.info.tip}, pos.add(.init(80, 25)));
        drawCount(.{item.count}, pos.addX(160));
    }

    drawStatusText(.{bag.money}, .init(525, 445));
}

fn renderStatus() void {
    const player = &world.players[selectedPlayer];
    camera.draw(player.statusTexture, position);

    if (player.attackItem) |item| {
        camera.draw(item.texture, position.add(.init(41, 55)));
    }

    if (player.defendItem) |item| {
        camera.draw(item.texture, position.add(.init(41, 136)));
    }

    drawStatusText(.{player.health}, .init(155, 411));
    drawStatusText(.{player.mana}, .init(290, 411));

    const item = &player.totalItem;
    drawStatusText(.{player.attack}, .init(155, 431));
    drawEffect(.{item.value2}, .init(185, 431));
    drawStatusText(.{player.defend}, .init(290, 431));
    drawEffect(.{item.value3}, .init(320, 431));

    drawStatusText(.{player.speed}, .init(155, 451));
    drawEffect(.{item.value4}, .init(185, 451));
    drawStatusText(.{player.luck}, .init(290, 451));
    drawEffect(.{item.value5}, .init(320, 451));
}

fn drawStatusText(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("{}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 0.21, .g = 0.09, .b = 0.01, .a = 1 },
    });
}

fn drawEffect(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("+{}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 1, .a = 1 },
    });
}

fn drawItemInfo(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("{s}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 0.75, .g = 0.89, .b = 0.26, .a = 1 },
    });
}

fn drawCount(args: anytype, pos: gfx.Vector) void {
    drawTextOptions("X {}", .{
        .args = args,
        .position = pos,
        .color = gfx.Color{ .r = 0.75, .g = 0.89, .b = 0.26, .a = 1 },
    });
}

fn drawTextOptions(comptime fmt: []const u8, options: anytype) void {
    var buffer: [256]u8 = undefined;
    const text = std.fmt.bufPrint(&buffer, fmt, options.args);
    camera.drawTextOptions(.{
        .text = text catch unreachable,
        .position = options.position,
        .color = options.color,
    });
}
```

## Player.zig

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const world = @import("world.zig");
const bag = @import("bag.zig");
const camera = @import("../camera.zig");

const Player = @This();
const FrameAnimation = gfx.FixedFrameAnimation(4, 0.15);
const PLAYER_SPEED = 150;
const PlayerState = enum { walk, talk };

pub var position: gfx.Vector = .init(800, 500);
pub var state: PlayerState = .walk;
var facing: gfx.FourDirection = .down;
var keyPressed: bool = false;
var velocity: gfx.Vector = .zero;

index: u8,
roleTexture: gfx.Texture,
upAnimation: FrameAnimation = undefined,
downAnimation: FrameAnimation = undefined,
leftAnimation: FrameAnimation = undefined,
rightAnimation: FrameAnimation = undefined,

statusTexture: gfx.Texture = undefined,
attackItem: ?*bag.ItemInfo = null,
defendItem: ?*bag.ItemInfo = null,
totalItem: bag.ItemInfo = .{ .texture = undefined },

battleTexture: gfx.Texture = undefined,
attackTexture: gfx.Texture = undefined,
battleFace: gfx.Texture = undefined,

maxHealth: u32 = 100,
health: u32 = 100,
maxMana: u32 = 100,
mana: u32 = 100,
attack: u32 = 10,
defend: u32 = 10,
speed: u32 = 10,
luck: u32 = 10,

pub fn init(index: u8) Player {
    var player = switch (index) {
        0 => initPlayer1(),
        1 => initPlayer2(),
        2 => initPlayer3(),
        else => unreachable,
    };

    var size: gfx.Vector = .init(960, 240);

    var area = gfx.Rectangle.init(.{ .y = 720 }, size);
    player.upAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 0 }, size);
    player.downAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 240 }, size);
    player.leftAnimation = .init(player.roleTexture.subTexture(area));

    area = gfx.Rectangle.init(.{ .y = 480 }, size);
    player.rightAnimation = .init(player.roleTexture.subTexture(area));

    size = .init(240, 240);
    area = gfx.Rectangle.init(.{ .y = 0 }, size);
    player.attackTexture = player.battleTexture.subTexture(area);

    return player;
}

fn initPlayer1() Player {
    const role = window.loadTexture("assets/r1.png", .init(960, 960));
    return Player{
        .index = 0,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face1.png", .init(357, 317)),
        .battleTexture = window.loadTexture("assets/fight/p1.png", .init(960, 240)),
        .battleFace = window.loadTexture("assets/fight/fm_face1.png", .init(319, 216)),
        .health = 20,
        .mana = 20,
    };
}

fn initPlayer2() Player {
    const role = window.loadTexture("assets/r2.png", .init(960, 960));

    return Player{
        .index = 1,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face2.png", .init(357, 317)),
        .battleTexture = window.loadTexture("assets/fight/p2.png", .init(960, 240)),
        .battleFace = window.loadTexture("assets/fight/fm_face2.png", .init(319, 216)),
    };
}

fn initPlayer3() Player {
    const role = window.loadTexture("assets/r3.png", .init(960, 960));
    return Player{
        .index = 2,
        .roleTexture = role,
        .statusTexture = window.loadTexture("assets/item/face3.png", .init(357, 317)),
        .battleTexture = window.loadTexture("assets/fight/p3.png", .init(960, 240)),
        .battleFace = window.loadTexture("assets/fight/fm_face3.png", .init(319, 216)),
    };
}

pub fn useItem(self: *Player, item: *bag.Item) void {
    if (item.count == 0) return;
    // 1 表示武器，2 表示防具
    if (1 == item.info.value1) {
        if (self.attackItem != null) self.removeItem(1);
        self.attackItem = item.info;
        self.computeTotalItem();
    } else if (2 == item.info.value1) {
        if (self.defendItem != null) self.removeItem(2);
        self.defendItem = item.info;
        self.computeTotalItem();
    } else if (3 == item.info.value1) {
        if (self.health == self.maxHealth) return;
        self.health += item.info.value2;
        self.health = std.math.clamp(self.health, 0, self.maxHealth);
    } else if (4 == item.info.value1) {
        if (self.mana == self.maxMana) return;
        self.mana += item.info.value2;
        self.mana = std.math.clamp(self.mana, 0, self.maxMana);
    }
    item.count -= 1;
}

pub fn removeItem(self: *Player, itemType: u32) void {
    if (itemType == 1) {
        if (self.attackItem) |attackItem| {
            bag.addItem(attackItem);
            self.attackItem = null;
        }
    } else if (itemType == 2) {
        if (self.defendItem) |defendItem| {
            bag.addItem(defendItem);
            self.defendItem = null;
        }
    }
    self.computeTotalItem();
}

fn computeTotalItem(self: *Player) void {
    self.totalItem = .{ .texture = undefined };
    if (self.attackItem) |i| self.totalItem.addValue(i);
    if (self.defendItem) |i| self.totalItem.addValue(i);
}

pub fn update(self: *Player, delta: f32) void {
    velocity = .zero;
    keyPressed = false;

    if (world.mouseTarget) |target| {
        velocity = target.sub(position).normalize();
        if (@abs(velocity.x) > @abs(velocity.y)) {
            facing = if (velocity.x > 0) .right else .left;
        } else {
            facing = if (velocity.y > 0) .down else .up;
        }
        keyPressed = true;
        const distance = target.sub(position);
        if (@abs(distance.x) < 16 and @abs(distance.y) < 16) {
            velocity = .zero;
            world.mouseTarget = null;
        }
    }

    if (window.isAnyKeyDown(&.{ .UP, .W })) updatePlayer(.up);
    if (window.isAnyKeyDown(&.{ .DOWN, .S })) updatePlayer(.down);
    if (window.isAnyKeyDown(&.{ .LEFT, .A })) updatePlayer(.left);
    if (window.isAnyKeyDown(&.{ .RIGHT, .D })) updatePlayer(.right);

    if (window.isKeyRelease(.TAB)) {
        const playerIndex = (self.index + 1) % world.players.len;
        world.currentPlayer = &world.players[playerIndex];
    }

    if (velocity.approx(.zero)) {
        self.current(facing).reset();
    } else {
        velocity = velocity.normalize().scale(delta * PLAYER_SPEED);
        const tempPosition = position.add(velocity);
        if (world.map.canWalk(tempPosition)) position = tempPosition;
        camera.lookAt(position);
    }

    if (keyPressed) self.current(facing).update(delta);
}

fn updatePlayer(direction: gfx.FourDirection) void {
    facing = direction;
    keyPressed = true;
    velocity = velocity.add(direction.toVector());
    world.mouseTarget = null;
}

pub fn render(self: *Player) void {
    const playerTexture = self.current(facing).currentTexture();
    camera.draw(playerTexture, position.sub(.init(120, 220)));
}

fn current(self: *Player, face: gfx.FourDirection) *FrameAnimation {
    return switch (face) {
        .up => &self.upAnimation,
        .down => &self.downAnimation,
        .left => &self.leftAnimation,
        .right => &self.rightAnimation,
    };
}
```

## 效果

![使用物品][1]

[1]: images/教你制作RPG游戏59.webp

## 附录
