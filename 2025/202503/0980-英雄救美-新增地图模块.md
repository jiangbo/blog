# 0980-英雄救美-新增地图模块

## 目标

地图模块控制地图的渲染，检测角色移动时是否发生碰撞。

## 环境

- Time 2025-06-20
- Zig 0.14.1

## 参考

1. 圣剑英雄传：英雄救美源码。

## 想法

将 world.zig 中的部分代码移动到了 player.zig 模块中。

## world.zig

把地图相关的代码移动到了 map.zig 文件中。

```zig
const std = @import("std");

const window = @import("zhu").window;
const gfx = @import("zhu").gfx;
const camera = @import("zhu").camera;

const player = @import("player.zig");
const map = @import("map.zig");

const Status = union(enum) { normal, talk: usize };
var status: Status = .normal;

const Talk = struct {
    actor: u8 = 0,
    content: []const u8,
    next: usize = 0,
};
const talks: []const Talk = @import("zon/talk.zon");
var talkTexture: gfx.Texture = undefined;

pub fn init() void {
    talkTexture = gfx.loadTexture("assets/pic/talkbar.png", .init(640, 96));
    // status = .{ .talk = 1 };
    map.init();
    player.init();

    // window.playMusic("assets/voc/back.ogg");
}

pub fn update(delta: f32) void {
    switch (status) {
        .normal => {},
        .talk => |talkId| return updateTalk(talkId),
    }

    player.update(delta);
}

fn updateTalk(talkId: usize) void {
    if (!confirm()) return;

    const next = talks[talkId].next;
    status = if (next == 0) .normal else .{ .talk = next };
}

fn confirm() bool {
    return window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER });
}

pub fn enter() void {}

pub fn exit() void {}

pub fn render() void {
    map.render();
    player.render();

    switch (status) {
        .normal => {},
        .talk => |talkId| renderTalk(talkId),
    }
}

fn renderTalk(talkId: usize) void {
    camera.draw(talkTexture, .init(0, 384));

    const talk = talks[talkId];
    if (talk.actor == 0) player.renderTalk();

    camera.drawColorText(talk.content, .init(123, 403), .{ .w = 1 });
    camera.drawColorText(talk.content, .init(120, 400), .one);
}
```

## map.zig

新增了地图的层级，并且实现了碰撞的检测。

```zig
const std = @import("std");

const window = @import("zhu").window;
const gfx = @import("zhu").gfx;
const camera = @import("zhu").camera;

var texture: gfx.Texture = undefined;

const Map = struct {
    width: u16,
    height: u16,
    ground: []const u16,
    prop: []const u16,
    object: []const u16,
};

const map: Map = @import("zon/map.zon");

var vertexBuffer: [500]camera.Vertex = undefined;
var vertexIndex: usize = 0;

pub fn init() void {
    texture = gfx.loadTexture("assets/pic/maps.png", .init(640, 1536));

    buildVertexBuffer(map.ground);
    buildVertexBuffer(map.prop);
}

fn buildVertexBuffer(tiles: []const u16) void {
    for (tiles, 0..) |tileIndex, index| {
        const x: u32 = 4;
        const y: u32 = 4;
        if (index == x + y * 20) {
            std.log.info("index: {}, tileIndex: {}", .{ index, tileIndex });
        }

        if (tileIndex == 0) continue;

        const tile = texture.subTexture(getAreaFromIndex(tileIndex));
        vertexBuffer[vertexIndex] = .{
            .position = getAreaFromIndex(index).min,
            .size = .init(32, 32),
            .texture = tile.area.toVector4(),
        };
        vertexIndex += 1;
    }
}

fn getAreaFromIndex(index: usize) gfx.Rectangle {
    const row: f32 = @floatFromInt(index / 20);
    const col: f32 = @floatFromInt(index % 20);
    return .init(.init(col * 32, row * 32), .init(32, 32));
}

pub fn canWalk(position: gfx.Vector) bool {
    const x = @floor(position.x / 32);
    const y = @floor(position.y / 32);

    const index: usize = @intFromFloat(x + y * map.width);
    if (index > map.object.len) return false;
    return map.object[index] == 0;
}

pub fn render() void {
    camera.drawVertex(texture, vertexBuffer[0..vertexIndex]);
}
```

## 效果

![新增地图模块][1]

[1]: images/英雄救美11.png

## 附录
