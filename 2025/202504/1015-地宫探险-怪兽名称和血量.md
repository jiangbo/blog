# 1015-地宫探险-怪兽名称和血量

## 目标

给怪兽添加一个生命值组件。

## 环境

- Time 2025-10-30
- Zig 0.15.1

## 参考

1. <https://pragprog.com/titles/hwrust/hands-on-rust/>
2. 《Rust游戏开发实战》

## 想法

给怪兽增加名称和血量组件，然后在鼠标移动到怪兽身上时，显示怪兽信息。

## monster.zig

怪兽新增了名称和血量组件，生成的几率也进行了修改。

```zig
const std = @import("std");
const zhu = @import("zhu");

const ecs = zhu.ecs;
const gfx = zhu.gfx;

const map = @import("map.zig");
const battle = @import("battle.zig");

const MovingRandomly = struct {};

pub fn init() void {
    for (map.rooms[1..]) |room| {
        const enemy = ecs.w.createEntity();

        const center = map.center(room);
        ecs.w.add(enemy, center);
        ecs.w.add(enemy, map.worldPosition(center));

        const enemyTile = switch (zhu.randomIntMost(u8, 1, 10)) {
            0...8 => map.Tile.goblin,
            else => map.Tile.orc,
        };

        const hp: i32 = switch (enemyTile) {
            map.Tile.goblin => 1,
            map.Tile.orc => 2,
            else => unreachable,
        };
        ecs.w.add(enemy, battle.Health{ .current = hp, .max = hp });
        ecs.w.add(enemy, battle.Name{@tagName(enemyTile)});

        ecs.w.add(enemy, map.getTextureFromTile(enemyTile));
        ecs.w.add(enemy, MovingRandomly{});
    }
}

pub fn move() void {
    if (ecs.w.getContext(battle.TurnState).?.* != .player) return;

    ecs.w.addContext(battle.TurnState.monster);
    var view = ecs.w.view(.{ MovingRandomly, map.Vec });
    while (view.next()) |entity| {
        var pos = view.get(entity, map.Vec);
        switch (zhu.randomIntMost(u8, 0, 3)) {
            0 => pos.x += 1,
            1 => pos.y += 1,
            2 => pos.x -= 1,
            else => pos.y -= 1,
        }
        view.add(entity, map.WantsToMove{pos});
    }
}

pub fn checkCollision(playerPosition: map.Vec) void {
    var view = ecs.w.view(.{ MovingRandomly, map.Vec });
    while (view.next()) |entity| {
        const enemyPos = view.getPtr(entity, map.Vec);
        if (enemyPos.equals(playerPosition)) {
            ecs.w.destroyEntity(ecs.w.getEntity(entity).?);
            ecs.w.addContext(battle.TurnState.player);
            return;
        }
    }
}
```

## hud.zig

绘制了人物的血量框，绘制了怪兽的名称和血量。

```zig
const std = @import("std");
const zhu = @import("zhu");

const gfx = zhu.gfx;
const window = zhu.window;
const camera = zhu.camera;
const math = zhu.math;
const ecs = zhu.ecs;

const player = @import("player.zig");
const battle = @import("battle.zig");
const map = @import("map.zig");

var texture: gfx.Texture = undefined;
const healthForeground: math.Vector4 = .init(0.298, 0.735, 0.314, 1);
const healthBackground: math.Vector4 = .init(0.2, 0.2, 0.2, 1);

pub fn init() void {
    texture = gfx.loadTexture("assets/terminal8x8.png", .init(128, 128));
    camera.whiteTexture = texture.subTexture(.init(.init(88, 104), size));
}

pub fn draw() void {
    camera.mode = .local;
    defer camera.mode = .world;

    var pos: gfx.Vector = .init(window.logicSize.x / 2, 10);
    var healthSize: gfx.Vector = .init(200, 12);
    const healthPos = pos.sub(healthSize.scale(0.5));

    const health = ecs.w.get(player.entity, battle.Health).?;
    var buffer: [50]u8 = undefined;
    const text = zhu.format(&buffer, "Health: {} / {}", //
        .{ health.current, health.max });

    camera.drawRect(.init(healthPos, healthSize), healthBackground);
    healthSize.x *= math.percentInt(health.current, health.max);
    camera.drawRect(.init(healthPos, healthSize), healthForeground);

    drawTextCenter(text, pos);
    pos.y += size.x * 2;
    drawTextCenter("Explore the Dungeon. A/S/D/W to move.", pos);

    drawNameAndHealthIfNeed();
}

fn drawNameAndHealthIfNeed() void {
    var buffer: [50]u8 = undefined;
    const mousePosition = camera.toWorld(window.mousePosition);

    var view = ecs.w.view(.{ battle.Health, battle.Name, gfx.Vector });
    while (view.next()) |entity| {
        var position = view.get(entity, gfx.Vector);
        const rect: gfx.Rect = .init(position, map.TILE_SIZE);

        if (!rect.contains(mousePosition)) continue;

        const health = view.get(entity, battle.Health).current;
        const name = view.get(entity, battle.Name)[0];

        const text = zhu.format(&buffer, "{s}: {}hp", //
            .{ name, health });

        position = position.addXY(map.TILE_SIZE.x / 2, -size.y);
        drawTextCenter(text, camera.toWindow(position));
    }
}

fn drawTextCenter(text: []const u8, position: gfx.Vector) void {
    const textSize = size.mul(.init(@floatFromInt(text.len), 1));
    drawText(text, position.sub(textSize.scale(0.5)));
}
const size: gfx.Vector = .init(8, 8);
fn drawText(text: []const u8, position: gfx.Vector) void {
    var pos = position;

    for (text) |byte| {
        const x: f32 = @floatFromInt(byte % 16);
        const y: f32 = @floatFromInt(byte / 16);
        const charTexture = texture.subTexture(.{
            .min = size.mul(.init(x, y)),
            .size = size,
        });

        camera.draw(charTexture, pos);
        pos.x += size.x;
    }
}
```

## 效果

![怪兽名称和血量][1]

[1]: images/地宫探险10.gif

## 附录
