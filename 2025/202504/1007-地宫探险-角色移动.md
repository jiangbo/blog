# 1007-地宫探险-角色移动

## 目标

将角色放置到地图上，并且支持按键进行移动。

## 环境

- Time 2025-10-23
- Zig 0.15.2

## 参考

1. <https://pragprog.com/titles/hwrust/hands-on-rust/>
2. 《Rust游戏开发实战》

## 想法

本来想在相机增加了一个缩放参数，但是移动相机的时候，屏幕会出现闪烁，不清楚怎么解决。

## scene.zig

由于移动相机要闪烁，所以去除了相机的控制逻辑。

```zig
const std = @import("std");
const zhu = @import("zhu");

const window = zhu.window;
const gfx = zhu.gfx;
const camera = zhu.camera;

const map = @import("map.zig");
const player = @import("player.zig");

var isHelp: bool = false;
var isDebug: bool = false;
const scale = 0.2;

pub fn init() void {
    window.initFont(.{
        .font = @import("zon/font.zon"),
        .texture = gfx.loadTexture("assets/font.png", .init(960, 960)),
    });

    camera.frameStats(true);
    camera.init(5000);
    camera.scale = .init(scale, scale);

    map.init();
    player.init();

    sceneCall("enter", .{});
}

pub fn update(delta: f32) void {
    window.keepAspectRatio();
    if (window.isKeyRelease(.H)) isHelp = !isHelp;
    if (window.isKeyRelease(.X)) isDebug = !isDebug;

    if (window.isKeyDown(.LEFT_ALT) and window.isKeyRelease(.ENTER)) {
        return window.toggleFullScreen();
    }

    if (window.isKeyRelease(.Z)) camera.position = .zero;

    player.update(delta);

    sceneCall("update", .{delta});
}

pub fn draw() void {
    camera.beginDraw();
    defer camera.endDraw();

    sceneCall("draw", .{});
    map.draw();
    player.draw();
    if (isHelp) drawHelpInfo() else if (isDebug) drawDebugInfo();
}

fn drawHelpInfo() void {
    const text =
        \\按键说明：
        \\上：W，下：S，左：A，右：D
        \\确定：F，取消：Q，菜单：E
        \\帮助：H  按一次打开，再按一次关掉
    ;
    var iterator = std.unicode.Utf8View.initUnchecked(text).iterator();
    var count: u32 = 0;
    while (iterator.nextCodepoint()) |code| {
        if (code == '\n') continue;
        count += 1;
    }
    debutTextCount = count;

    camera.drawColorText(text, .init(10, 5), .green);
}

var debutTextCount: u32 = 0;
fn drawDebugInfo() void {
    var buffer: [1024]u8 = undefined;
    const format =
        \\后端：{s}
        \\帧率：{}
        \\帧时：{d:.2}
        \\用时：{d:.2}
        \\显存：{}
        \\常量：{}
        \\绘制：{}
        \\图片：{}
        \\文字：{}
        \\内存：{}
        \\鼠标：{d:.2}，{d:.2}
        \\相机：{d:.2}，{d:.2}
    ;

    const stats = camera.queryFrameStats();
    const text = zhu.format(&buffer, format, .{
        @tagName(camera.queryBackend()),
        window.frameRate,
        window.frameDeltaPerSecond,
        window.usedDeltaPerSecond,
        stats.size_append_buffer + stats.size_update_buffer,
        stats.size_apply_uniforms,
        stats.num_draw,
        camera.imageDrawCount(),
        // Debug 信息本身的次数也应该统计进去
        camera.textDrawCount() + debutTextCount,
        window.countingAllocator.used,
        window.mousePosition.x,
        window.mousePosition.y,
        camera.position.x,
        camera.position.y,
    });

    var iterator = std.unicode.Utf8View.initUnchecked(text).iterator();
    var count: u32 = 0;
    while (iterator.nextCodepoint()) |code| {
        if (code == '\n') continue;
        count += 1;
    }
    debutTextCount = count;

    camera.drawColorText(text, .init(10, 5), .green);
}

pub fn deinit() void {
    sceneCall("deinit", .{});
}

fn sceneCall(comptime function: []const u8, args: anytype) void {
    _ = function;
    _ = args;
}
```

## player.zig

新增了角色模块。

```zig
const std = @import("std");
const zhu = @import("zhu");

const gfx = zhu.gfx;
const window = zhu.window;
const camera = zhu.camera;

const map = @import("map.zig");

var texture: gfx.Texture = undefined;
var tilePosition: map.Vec = undefined;
var position: gfx.Vector = undefined;

pub fn init() void {
    tilePosition = map.playerStartPosition();
    computePosition();
    texture = map.getTextureFromTile(.player);
}

pub fn computePosition() void {
    position = map.playerWorldPosition(tilePosition);

    const half = window.logicSize.scale(0.5);
    const max = map.size.sub(window.logicSize);
    camera.position = position.sub(half).clamp(.zero, max);
}

pub fn update(_: f32) void {
    var tilePos = tilePosition;
    if (window.isKeyRelease(.W)) tilePos.y -|= 1;
    if (window.isKeyRelease(.S)) tilePos.y += 1;
    if (window.isKeyRelease(.A)) tilePos.x -|= 1;
    if (window.isKeyRelease(.D)) tilePos.x += 1;

    const moved = tilePos.x != tilePosition.x or
        tilePos.y != tilePosition.y;

    if (moved and map.canEnter(tilePos)) {
        tilePosition = tilePos;
        computePosition();
    }
}

pub fn draw() void {
    camera.draw(texture, position);
}
```

## 效果

![角色移动][1]

[1]: images/地宫探险03.gif

## 附录
