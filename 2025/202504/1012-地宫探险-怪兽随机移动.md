# 1012-地宫探险-怪兽随机移动

## 目标

给怪兽添加一个随便移动的组件，然后在角色移动时，怪兽向随机方向移动。

## 环境

- Time 2025-10-25
- Zig 0.15.2

## 参考

1. <https://pragprog.com/titles/hwrust/hands-on-rust/>
2. 《Rust游戏开发实战》

## 想法

自己做的 ECS 系统问题还挺多的，完成功能过程中慢慢修复。

## monster.zig

怪兽目前有一个随机移动的组件，可以进行移动了。

```zig
const std = @import("std");
const zhu = @import("zhu");

const ecs = zhu.ecs;
const gfx = zhu.gfx;

const map = @import("map.zig");
const battle = @import("battle.zig");

const MovingRandomly = struct {};

pub fn init() void {
    for (map.rooms[1..]) |room| {
        const enemy = ecs.w.createEntity();

        const center = map.center(room);
        ecs.w.add(enemy, center);
        ecs.w.add(enemy, map.worldPosition(center));

        const enemyTile = switch (zhu.randomInt(u8, 0, 4)) {
            0 => map.Tile.ettin,
            1 => map.Tile.ogre,
            2 => map.Tile.orc,
            else => map.Tile.goblin,
        };

        ecs.w.add(enemy, map.getTextureFromTile(enemyTile));
        ecs.w.add(enemy, MovingRandomly{});
    }
}

pub fn move() void {
    if (ecs.w.getContext(battle.TurnState).?.* != .player) return;

    ecs.w.addContext(battle.TurnState.monster);
    var view = ecs.w.view(.{ MovingRandomly, map.Vec });
    while (view.next()) |entity| {
        const ptr = view.getPtr(entity, map.Vec);
        var pos = ptr.*;
        switch (zhu.randomIntMost(u8, 0, 3)) {
            0 => pos.x += 1,
            1 => pos.y += 1,
            2 => pos.x -= 1,
            3 => pos.y -= 1,
            else => unreachable,
        }

        if (map.canEnter(pos)) {
            ptr.* = pos;
            ecs.w.add(entity, map.worldPosition(pos));
        }
    }
}

pub fn checkCollision(playerPosition: map.Vec) void {
    for (ecs.w.raw(map.Vec), ecs.w.data(map.Vec)) |enemyPos, index| {
        if (playerPosition.x == enemyPos.x and
            playerPosition.y == enemyPos.y)
        {
            const entity = ecs.w.getEntity(index).?;
            ecs.w.destroyEntity(entity);
            break;
        }
    }
}
```

## scene.zig

场景中新增了怪兽移动功能。

```zig
pub fn update(delta: f32) void {
    window.keepAspectRatio();
    if (window.isKeyRelease(.H)) isHelp = !isHelp;
    if (window.isKeyRelease(.X)) isDebug = !isDebug;

    if (window.isKeyDown(.LEFT_ALT) and window.isKeyRelease(.ENTER)) {
        return window.toggleFullScreen();
    }

    const speed: f32 = std.math.round(100 * delta) / scale;
    if (window.isKeyDown(.UP)) camera.position.y -= speed;
    if (window.isKeyDown(.DOWN)) camera.position.y += speed;
    if (window.isKeyDown(.LEFT)) camera.position.x -= speed;
    if (window.isKeyDown(.RIGHT)) camera.position.x += speed;

    player.update(delta);
    monster.checkCollision(player.tilePosition);
    monster.move();
    monster.checkCollision(player.tilePosition);

    sceneCall("update", .{delta});
}
```

## battle.zig

暂时不清楚这个放到哪里比较好，先放这里。

```zig
const std = @import("std");
const zhu = @import("zhu");

const ecs = zhu.ecs;

pub const TurnState = enum { wait, player, monster };
```

## 效果

![怪兽随机移动][1]

[1]: images/地宫探险08.gif

## 附录
