# 0998-英雄救美-提示信息

## 目标

遇到不能够执行的操作，应该打印提示信息，比如购买物品时背包已满。

## 环境

- Time 2025-08-17
- Zig 0.14.1

## 参考

1. 圣剑英雄传：英雄救美源码。

## 想法

完善购买物品时，背包不够和金币不够的提示信息。

## world.zig

```zig
const std = @import("std");
const zhu = @import("zhu");

const window = zhu.window;
const gfx = zhu.gfx;
const camera = zhu.camera;

const menu = @import("menu.zig");
const player = @import("player.zig");
const map = @import("map.zig");
const talk = @import("talk.zig");
const about = @import("about.zig");
const item = @import("item.zig");
const npc = @import("npc.zig");

const Status = union(enum) { talk, menu, about, status, item, shop };
var status: ?Status = null;

var menuTexture: gfx.Texture = undefined;
var arenaAllocator: std.heap.ArenaAllocator = undefined;

const Shop = struct {
    items: [16]u8,
    current: u8 = 0,

    pub fn update(self: *Shop) void {
        self.current = item.update(self.items.len, self.current);

        if (window.isAnyKeyRelease(&.{ .LEFT_CONTROL, .F, .ENTER })) {
            const itemIndex = self.items[self.current];
            if (itemIndex != 0) buy(itemIndex);
        }

        if (window.isKeyRelease(.Q)) status = null;
    }

    fn buy(itemIndex: u8) void {
        const buyItem = item.zon[itemIndex];

        if (buyItem.money > player.money) {
            tip = "兄弟，你的钱不够！";
            return;
        }

        const bagEnough = player.addItem(itemIndex);
        if (!bagEnough) {
            tip = "你已经带满了！";
            return;
        }
        player.money -= buyItem.money;
    }

    pub fn draw(self: *const Shop) void {
        item.draw(&self.items, self.current);
        var buffer: [20]u8 = undefined;
        // 金币，操作说明
        camera.drawText("（金=", item.position.addXY(10, 270));
        const moneyStr = zhu.format(&buffer, "{d}）", .{player.money});
        camera.drawText(moneyStr, item.position.addXY(60, 270));
        const text = "CTRL=购买　　ESC=退出";
        camera.drawText(text, item.position.addXY(118, 270));
    }
};
var weaponShop: Shop = .{
    .items = .{
        12, 12, 13, 13, 14, 14, 9, 9, //
        10, 10, 8,  8,  16, 16, 0, 0,
    },
};

var tip: []const u8 = &.{};

pub fn init() void {
    arenaAllocator = std.heap.ArenaAllocator.init(window.allocator);
    menuTexture = gfx.loadTexture("assets/pic/mainmenu1.png", .init(150, 200));

    item.init();
    talk.init();
    about.init();
    map.init();
    player.init();
    npc.init();

    // status = .item;
}

pub fn deinit() void {
    npc.deinit();
    arenaAllocator.deinit();
    window.stopMusic();
}

pub fn enter() void {
    const playerPosition = map.enter();
    player.enter(playerPosition);
    npc.enter();
    menu.active = 6;
    // window.playMusic("assets/voc/back.ogg");

    // talk.active = 4;
    // status = .talk;

}

pub fn changeMap() void {
    const playerPosition = map.enter();
    player.enter(playerPosition);
    npc.enter();
}

pub fn exit() void {}

pub fn update(delta: f32) void {
    reloadIfChanged();

    if (tip.len != 0) {
        if (window.isAnyRelease()) tip = &.{};
        return;
    }

    if (status == null or status.? != .menu) {
        if (window.isMouseRelease(.RIGHT) or
            window.isAnyKeyRelease(&.{ .ESCAPE, .E }))
        {
            status = .menu;
            menu.active = 6;
            return;
        }
    }

    if (status) |pop| {
        switch (pop) {
            .talk => return updateTalk(),
            .item => return updateItem(),
            .shop => return weaponShop.update(),
            .status => {
                return if (window.isMouseRelease(.RIGHT) or
                    window.isAnyKeyRelease(&.{ .ESCAPE, .Q, .SPACE }))
                {
                    status = null;
                };
            },
            .menu => return updateMenu(),
            .about => return updateAbout(delta),
        }
    }

    npc.update(delta);
    player.update(delta);

    // 交互检测
    const confirm = window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER });
    if (confirm) {
        // 开启宝箱
        const object = map.talk(player.position, player.facing);
        if (object) |pickupIndex| openChest(pickupIndex);
    }

    if (confirm) {
        // 和 NPC 对话
        if (npc.talk(player.talkCollider(), player.facing)) |talkId| {
            talk.active = talkId;
            status = .talk;
        }
    }
}

var modifyTime: i64 = 0;
fn reloadIfChanged() void {
    const menuTime = window.statFileTime("src/zon/menu.zon");
    const linkTime = window.statFileTime("src/zon/link.zon");
    const time = @max(menuTime, linkTime);

    if (time > modifyTime) {
        _ = arenaAllocator.reset(.retain_capacity);
        menu.reload(arenaAllocator.allocator());
        player.position = map.reload(arenaAllocator.allocator());
        modifyTime = time;
    }
}

fn updateTalk() void {
    const talkEvent = talk.update();
    if (talkEvent) |event| {
        switch (event) {
            0 => status = null,
            4 => status = .shop,
            else => unreachable,
        }
    }
}

fn updateItem() void {
    if (window.isAnyKeyRelease(&.{ .ESCAPE, .Q, .E })) {
        status = null;
        return;
    }
    player.updateItem();
}

fn updateAbout(delta: f32) void {
    if (window.isAnyKeyRelease(&.{ .ESCAPE, .Q }) or
        window.isMouseRelease(.RIGHT))
    {
        status = null;
        return;
    }

    if (about.roll) {
        about.update(delta);
    } else {
        if (window.isAnyKeyRelease(&.{ .F, .SPACE, .ENTER }) or
            window.isMouseRelease(.LEFT))
        {
            about.roll = true;
        }
    }
}

fn openChest(pickIndex: u16) void {
    const object = item.pickupZon[pickIndex];

    if (object.itemIndex == 0 and object.count == 0) {
        const gold = window.random().intRangeLessThanBiased(u8, 10, 100);
        player.money += gold;
        talk.activeNumber(2, gold);
        status = .talk;
    } else {
        const added = player.addItem(object.itemIndex);
        if (!added) {
            tip = "你已经带满了！";
            return;
        }
        talk.activeText(3, item.zon[object.itemIndex].name);
        status = .talk;
        map.openChest(pickIndex);
    }
}

fn updateMenu() void {
    const menuEvent = menu.update();
    if (menuEvent) |event| menuSelected(event);

    if (window.isAnyKeyRelease(&.{ .ESCAPE, .Q, .E }) or
        window.isMouseRelease(.RIGHT))
    {
        status = null;
    }
}

fn menuSelected(index: usize) void {
    switch (index) {
        0 => status = .status,
        1 => status = .item,
        2...3 => status = null,
        4 => {
            status = .about;
            about.resetRoll();
        },
        5 => window.exit(),
        6 => status = null,
        else => unreachable,
    }
}

pub fn draw() void {
    map.draw();
    npc.draw();
    player.draw();

    camera.mode = .local;
    defer camera.mode = .world;

    if (status) |pop| {
        switch (pop) {
            .talk => talk.draw(),
            .status => player.drawStatus(),
            .item => player.drawItem(),
            .shop => weaponShop.draw(),
            .menu => {
                camera.draw(menuTexture, .init(0, 280));
                menu.draw();
            },
            .about => about.draw(),
        }
    }

    if (tip.len != 0) {
        camera.drawColorText(tip, .init(242, 442), .black);
        camera.drawColorText(tip, .init(240, 440), .yellow);
    }
}
```

## 效果

![提示信息][1]

[1]: images/英雄救美29.png

## 附录
