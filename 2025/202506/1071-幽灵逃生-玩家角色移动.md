# 1071-幽灵逃生-玩家角色移动

## 目标

实现了键盘控制玩家移动，并且相机跟随玩家一起移动。

## 环境

- Time 2026-01-08
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1jf9XYQEhW>

## 想法

对自己的小引擎修改了一下，可以支持纹理图集渲染了，将多张图片打包到一起。

## scene.zig

主要加入了 player 模块。

```zig
const std = @import("std");
const zhu = @import("zhu");

const window = zhu.window;
const graphics = zhu.graphics;
const camera = zhu.camera;

const player = @import("player.zig");

var isHelp = false;
var isDebug = false;
var vertexBuffer: []graphics.Vertex = undefined;

const atlas: graphics.Atlas = @import("zon/atlas.zon");

pub var worldSize: zhu.Vector2 = undefined; // 世界大小

pub fn init() void {
    window.initText(@import("zon/font.zon"), 24);

    vertexBuffer = window.alloc(graphics.Vertex, 5000);
    graphics.frameStats(true);
    graphics.initWithWhiteTexture(window.logicSize, vertexBuffer);

    graphics.loadAtlas(atlas);
    worldSize = window.logicSize.scale(3); // 设置世界大小

    player.init(worldSize.scale(0.5)); // 将玩家移动到世界中心
}

pub fn update(delta: f32) void {
    if (window.isKeyRelease(.H)) isHelp = !isHelp;
    if (window.isKeyRelease(.X)) isDebug = !isDebug;

    if (window.isKeyDown(.LEFT_ALT) and window.isKeyRelease(.ENTER)) {
        return window.toggleFullScreen();
    }

    const speed: f32 = std.math.round(400 * delta);
    if (window.isKeyDown(.UP)) camera.position.y -= speed;
    if (window.isKeyDown(.DOWN)) camera.position.y += speed;
    if (window.isKeyDown(.LEFT)) camera.position.x -= speed;
    if (window.isKeyDown(.RIGHT)) camera.position.x += speed;

    player.update(delta);
}

pub fn draw() void {
    camera.beginDraw(.{});
    defer camera.endDraw();
    window.keepAspectRatio();

    const gridColor = graphics.rgb(0.5, 0.5, 0.5);
    const area = zhu.Rect.init(.zero, worldSize);
    drawGrid(area, 80, gridColor);
    camera.drawRectBorder(area, 10, .white);

    player.draw(); // 玩家绘制

    camera.mode = .local;
    defer camera.mode = .world;

    if (isHelp) drawHelpInfo() else if (isDebug) drawDebugInfo();
}

fn drawGrid(area: zhu.Rect, width: f32, lineColor: zhu.Color) void {
    const max = area.max();
    const color = camera.LineOption{ .color = lineColor };

    var min = area.min;
    while (min.x < max.x) : (min.x += width) {
        camera.drawAxisLine(min, .init(min.x, max.y), color);
    }

    min = area.min;
    while (min.y < max.y) : (min.y += width) {
        camera.drawAxisLine(min, .init(max.x, min.y), color);
    }
}

fn drawHelpInfo() void {
    const text =
        \\按键说明：
        \\上：W，下：S，左：A，右：D
        \\确定：F，取消：Q，菜单：E
        \\帮助：H  按一次打开，再按一次关闭
    ;
    debutTextCount = zhu.text.computeTextCount(text);
    zhu.text.drawColor(text, .init(10, 10), .green);
}

var debutTextCount: u32 = 0;
fn drawDebugInfo() void {
    var buffer: [1024]u8 = undefined;
    const format =
        \\后端：{s}
        \\帧率：{}
        \\平滑：{d:.2}
        \\帧时：{d:.2}
        \\用时：{d:.2}
        \\显存：{}
        \\常量：{}
        \\绘制：{}
        \\图片：{}
        \\文字：{}
        \\内存：{}
        \\鼠标：{d:.2}，{d:.2}
        \\相机：{d:.2}，{d:.2}
    ;

    const stats = graphics.queryFrameStats();
    const text = zhu.format(&buffer, format, .{
        @tagName(graphics.queryBackend()),
        window.frameRate,
        window.currentSmoothTime * 1000,
        window.frameDeltaPerSecond,
        window.usedDeltaPerSecond,
        stats.size_append_buffer + stats.size_update_buffer,
        stats.size_apply_uniforms,
        stats.num_draw,
        camera.imageDrawCount(),
        // Debug 信息本身的次数也应该统计进去
        camera.textDrawCount() + debutTextCount,
        window.countingAllocator.used,
        window.mousePosition.x,
        window.mousePosition.y,
        camera.position.x,
        camera.position.y,
    });

    debutTextCount = zhu.text.computeTextCount(text);
    zhu.text.drawColor(text, .init(10, 10), .green);
}

pub fn deinit() void {
    window.free(vertexBuffer);
}
```

## player.zig

新增了角色模块，目前可以支持角色移动。

```zig
const std = @import("std");
const zhu = @import("zhu");

const camera = zhu.camera;
const window = zhu.window;

const scene = @import("scene.zig");

const maxSpeed = 500;

var position: zhu.Vector2 = undefined;
const size: zhu.Vector2 = .square(20);
var velocity: zhu.Vector2 = .zero;

pub fn init(initPosition: zhu.Vector2) void {
    position = initPosition;
    cameraFollow(initPosition); // 将相机移动到世界中心
}

pub fn update(delta: f32) void {
    if (window.isKeyPress(.A)) velocity = .init(-maxSpeed, 0);
    if (window.isKeyPress(.D)) velocity = .init(maxSpeed, 0);
    if (window.isKeyPress(.W)) velocity = .init(0, -maxSpeed);
    if (window.isKeyPress(.S)) velocity = .init(0, maxSpeed);

    velocity = velocity.scale(0.9);
    move(delta);
    position.clamp(.zero, scene.worldSize.sub(size));
    cameraFollow(position);
}

fn move(delta: f32) void {
    position = position.add(velocity.scale(delta));
}

fn cameraFollow(pos: zhu.Vector2) void {
    // const scaleSize = window.logicSize.div(camera.scale);
    // const half = scaleSize.scale(0.5);
    const max = scene.worldSize.sub(window.logicSize).max(.zero);
    const halfWindowSize = window.logicSize.scale(0.5);
    const square: zhu.Vector2 = .square(30);
    camera.position = pos.sub(halfWindowSize);
    camera.position.clamp(square.scale(-1), max.add(square));
}

pub fn draw() void {
    camera.drawRectBorder(.init(position, size), 5, .red);
}
```

## 效果

![玩家角色移动][1]

[1]: images/幽灵逃生04.gif

## 附录
