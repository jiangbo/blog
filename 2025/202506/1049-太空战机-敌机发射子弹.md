# 1049-太空战机-敌机发射子弹

## 目标

敌机在移动的过程中，也可以发射子弹，目前子弹只往下走。

## 环境

- Time 2025-12-04
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1wSCFYQEyc>

## 想法

目前只实现了敌机子弹只能往下移动的逻辑。

## enemy.zig

根据编写角色的套路，来编写敌机，基本上都差不多。

```zig
const std = @import("std");
const zhu = @import("zhu");

const gfx = zhu.gfx;
const window = zhu.window;
const camera = zhu.camera;

const Enemy = struct {
    position: gfx.Vector, // 敌机的位置
    shotTime: u64 = 0, // 敌机开火的时间
};

const Bullet = struct {
    position: gfx.Vector, // 子弹的位置
    direction: gfx.Vector, // 子弹的方向
};

const ENEMY_SPEED = 200; // 敌机的移动速度
const BULLET_SPEED = 400; // 子弹的移动速度
const SHOOT_INTERVAL = std.time.ns_per_s; // 敌机开火间隔

var texture: gfx.Texture = undefined; // 敌机的纹理
var size: gfx.Vector = undefined; // 敌机的大小

var enemies: std.ArrayList(Enemy) = .empty;
var spawnTimer: window.Timer = .init(1); // 生成敌机的定时器

var bulletTexture: gfx.Texture = undefined; // 子弹的纹理
var bulletSize: gfx.Vector = undefined; // 子弹的大小
var bullets: std.ArrayList(Bullet) = .empty;

pub fn init() void {
    texture = gfx.loadTexture("assets/image/insect-2.png", .init(182, 160));
    size = texture.size().scale(0.25);

    bulletTexture = gfx.loadTexture("assets/image/bullet-1.png", .init(14, 42));
    bulletSize = bulletTexture.size().scale(0.25);
}

pub fn update(delta: f32) void {
    if (spawnTimer.isFinishedAfterUpdate(delta)) { // 每秒生成一个
        spawnTimer.reset();
        spawnEnemy();
    }

    const shotTime = window.relativeTime() -| SHOOT_INTERVAL;
    var iterator = std.mem.reverseIterator(enemies.items);
    while (iterator.nextPtr()) |enemy| {
        enemy.position.y += ENEMY_SPEED * delta; // 敌机向下移动
        if (enemy.position.y > window.logicSize.y) {
            // 移动到屏幕外了，删除
            _ = enemies.swapRemove(iterator.index);
        } else if (enemy.shotTime < shotTime) {
            // 到达开火时间
            enemy.shotTime = window.relativeTime();
            spawnBullet(enemy);
        }
    }

    updateBullets(delta);
}

pub fn spawnEnemy() void {
    // 在 X 轴上随机生成敌机，Y 固定。
    const x = zhu.randomF32(0, window.logicSize.x - size.x);
    enemies.append(window.allocator, .{
        .position = .init(x, -size.y),
        .shotTime = window.relativeTime(),
    }) catch unreachable;
}

pub fn spawnBullet(enemy: *Enemy) void {
    const offset = size.sub(bulletSize).scale(0.5);
    bullets.append(window.allocator, .{
        .position = enemy.position.add(offset),
        .direction = .init(0, 1),
    }) catch unreachable;
}

pub fn updateBullets(delta: f32) void {
    var iterator = std.mem.reverseIterator(bullets.items);
    while (iterator.nextPtr()) |bullet| {
        bullet.position.y += BULLET_SPEED * delta; // 子弹向下移动
        if (bullet.position.y > window.logicSize.y) {
            // 移动到屏幕外了，删除
            _ = bullets.swapRemove(iterator.index);
        }
    }
}

pub fn draw() void {
    // 绘制子弹
    for (bullets.items) |bullet| {
        camera.drawOption(bulletTexture, bullet.position, .{
            .size = bulletSize,
        });
    }
    // 绘制敌机
    for (enemies.items) |enemy| {
        camera.drawOption(texture, enemy.position, .{ .size = size });
    }
}

pub fn deinit() void {
    enemies.deinit(window.allocator);
    bullets.deinit(window.allocator);
}
```

## 效果

![敌机发射子弹][1]

[1]: images/太空战机10.gif

## 附录
