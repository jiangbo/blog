# 1047-太空战机-发射多个子弹

## 目标

发射的子弹，可以不使用分配器，因为角色发射的子弹根据时间可以算出来。

## 环境

- Time 2025-12-04
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1wSCFYQEyc>

## 想法

之前还考虑倒序遍历这些，不使用分配器，直接使用数组不用考虑这个。就是每次需要遍历，不过不多。

## player.zig

角色可以发射多个子弹，并且没有使用内存分配器。

```zig
const std = @import("std");
const zhu = @import("zhu");

const window = zhu.window;
const gfx = zhu.gfx;
const camera = zhu.camera;

const Bullet = struct {
    position: gfx.Vector, // 子弹的位置
    speed: f32 = 400, // 子弹的速度
    dead: bool = true, // 子弹是否死亡
};

const SPEED = 200; // 玩家的移动速度
const SCALE = 0.25; // 图片素材太大，缩小到四分之一

var position: gfx.Vector = undefined; // 玩家的位置
var texture: gfx.Texture = undefined; // 玩家的纹理
var size: gfx.Vector = undefined; // 玩家的尺寸

var bulletTexture: gfx.Texture = undefined; // 子弹的纹理
var bullets: [10]Bullet = undefined; // 子弹数组
var bulletSize: gfx.Vector = undefined; // 子弹的尺寸

// 子弹发射的间隔，每 0.5 秒可以发射一次
var bulletTimer: zhu.window.Timer = .init(0.5);

pub fn init() void {
    texture = gfx.loadTexture("assets/image/SpaceShip.png", .init(241, 187));
    // 图片太大了，缩小到四分之一
    size = texture.size().scale(SCALE);
    position = window.logicSize.sub(size).div(.init(2, 1));

    bulletTexture = gfx.loadTexture("assets/image/laser-1.png", .init(81, 126));
    bulletSize = bulletTexture.size().scale(SCALE);
    bulletTimer.stop(); // 游戏开始就可以发射子弹
    for (&bullets) |*bullet| bullet.dead = true; //初始时所有子弹都可用
}

pub fn update(delta: f32) void {
    // 玩家键盘控制
    const distance = SPEED * delta; // 根据时间调整移动距离
    if (window.isKeyDown(.A)) position.x -= distance;
    if (window.isKeyDown(.D)) position.x += distance;
    if (window.isKeyDown(.W)) position.y -= distance;
    if (window.isKeyDown(.S)) position.y += distance;

    // 发射子弹。按下了按键，使用时间来控制发射子弹的间隔
    if (bulletTimer.isFinishedAfterUpdate(delta) and window.isKeyDown(.J)) {
        // 发射的位置：和玩家的 Y 坐标一样，在玩家的 X 中间
        const pos = position.addX(size.x / 2).addX(-bulletSize.x / 2);
        for (&bullets) |*bullet| {
            if (bullet.dead) { // 找到一个未使用的子弹
                bullet.* = .{ .position = pos, .dead = false };
                break;
            }
        }
        bulletTimer.reset();
    }

    // 限制玩家的移动边界
    position = position.clamp(.zero, window.logicSize.sub(size));

    // 子弹移动
    updateBullet(delta);
}

fn updateBullet(delta: f32) void {
    for (&bullets) |*bullet| {
        if (bullet.dead) continue;
        // 子弹存活，才进行位置更新
        bullet.position.y -= bullet.speed * delta; // 向上移动
        // 判断子弹是否超出屏幕，不是 Y 到 0，而是完全超出
        if (bullet.position.y < -bulletSize.y) bullet.dead = true;
    }
}

pub fn draw() void {
    // 先绘制子弹，再绘制玩家，让子弹在玩家的下面
    for (&bullets) |bullet| {
        if (bullet.dead) continue; // 子弹存活才绘制
        camera.drawOption(bulletTexture, bullet.position, .{
            .size = bulletSize,
        });
    }

    // 绘制玩家
    camera.drawOption(texture, position, .{ .size = size });
}

pub fn deinit() void {}
```

## 效果

![发射多个子弹][1]

[1]: images/太空战机08.gif

## 附录
