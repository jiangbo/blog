# 1052-太空战机-碰撞检测-玩家

## 目标

实现敌机子弹和玩家的碰撞，如果玩家生命值为 0，则玩家消失。

## 环境

- Time 2025-12-05
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1wSCFYQEyc>

## 想法

玩家生命值为 0，需要单独进行判断处理。

## player.zig

实现了和敌人子弹的碰撞逻辑。

```zig
const std = @import("std");
const zhu = @import("zhu");

const window = zhu.window;
const gfx = zhu.gfx;
const camera = zhu.camera;

const enemy = @import("enemy.zig");

const Bullet = struct {
    position: gfx.Vector, // 子弹的位置
    dead: bool = true, // 子弹是否死亡
};

const SPEED = 300; // 玩家的移动速度
const BULLET_SPEED = 600; // 子弹的速度
const SCALE = 0.25; // 图片素材太大，缩小到四分之一

var position: gfx.Vector = undefined; // 玩家的位置
var texture: gfx.Texture = undefined; // 玩家的纹理
var size: gfx.Vector = undefined; // 玩家的尺寸
var health: u8 = 3; // 玩家生命值

var bulletTexture: gfx.Texture = undefined; // 子弹的纹理
var bulletSize: gfx.Vector = undefined; // 子弹的尺寸
var bullets: [10]Bullet = undefined; // 子弹数组

// 子弹发射的间隔，每 0.3 秒可以发射一次
var bulletTimer: zhu.window.Timer = .init(0.3);

pub fn init() void {
    texture = gfx.loadTexture("assets/image/SpaceShip.png", .init(241, 187));
    // 图片太大了，缩小到四分之一
    size = texture.size().scale(SCALE);
    position = window.logicSize.sub(size).div(.init(2, 1));

    bulletTexture = gfx.loadTexture("assets/image/laser-1.png", .init(81, 126));
    bulletSize = bulletTexture.size().scale(SCALE);
    bulletTimer.stop(); // 游戏开始就可以发射子弹
    for (&bullets) |*bullet| bullet.dead = true; //初始时所有子弹都可用
}

pub fn update(delta: f32) void {
    // 子弹移动
    updateBullets(delta);
    if (health == 0) return; // 玩家死亡，不进行任何操作

    // 玩家键盘控制
    const distance = SPEED * delta; // 根据时间调整移动距离
    if (window.isKeyDown(.A)) position.x -= distance;
    if (window.isKeyDown(.D)) position.x += distance;
    if (window.isKeyDown(.W)) position.y -= distance;
    if (window.isKeyDown(.S)) position.y += distance;

    // 发射子弹。按下了按键，使用时间来控制发射子弹的间隔
    if (bulletTimer.isFinishedAfterUpdate(delta) and window.isKeyDown(.J)) {
        // 发射的位置：和玩家的 Y 坐标一样，在玩家的 X 中间
        const pos = position.addX(size.x / 2).addX(-bulletSize.x / 2);
        for (&bullets) |*bullet| {
            if (bullet.dead) { // 找到一个未使用的子弹
                bullet.* = .{ .position = pos, .dead = false };
                break;
            }
        }
        bulletTimer.reset(); // 重置计时器，玩家需要等到计时器结束才可以发射。
    }

    // 限制玩家的移动边界
    position = position.clamp(.zero, window.logicSize.sub(size));
}

fn updateBullets(delta: f32) void {
    for (&bullets) |*bullet| {
        if (bullet.dead) continue;

        // 子弹存活，才进行位置更新
        bullet.position.y -= BULLET_SPEED * delta; // 向上移动
        // 判断子弹是否超出屏幕，不是 Y 到 0，而是完全超出
        if (bullet.position.y < -bulletSize.y) {
            bullet.dead = true;
            continue;
        }

        // 玩家子弹的碰撞检测，用子弹的中心点检测感觉效果好一点
        const bulletCenter = bullet.position.add(bulletSize.scale(0.5));
        if (collideEnemy(bulletCenter)) bullet.dead = true;
    }
}

pub fn collideEnemy(bullet: gfx.Vector) bool {
    // 反向遍历，支持遍历时删除
    var iterator = std.mem.reverseIterator(enemy.enemies.items);
    while (iterator.nextPtr()) |ptr| {
        const rect: gfx.Rect = .init(ptr.position, enemy.size);
        if (!rect.contains(bullet)) continue; // 不相交，检测下一个

        ptr.health -|= 1; // 碰撞，减少一点血量
        if (ptr.health == 0) { // 血量为 0 ，进行销毁。
            _ = enemy.enemies.swapRemove(iterator.index);
        }
        return true;
    }
    return false;
}

pub fn center() gfx.Vector {
    // 玩家的中心位置
    return gfx.Rect.init(position, size).center();
}

pub fn collidePlayer(enemyBulletPosition: gfx.Vector) bool {
    if (health == 0) return false; // 玩家死亡，不碰撞
    // 玩家的碰撞矩形
    const rect = gfx.Rect.init(position, size);
    if (rect.contains(enemyBulletPosition)) {
        health -= 1; // 碰撞，减少一点血量
        return true;
    }
    return false;
}

pub fn draw() void {
    // 先绘制子弹，再绘制玩家，让子弹在玩家的下面
    for (&bullets) |bullet| {
        if (bullet.dead) continue; // 子弹存活才绘制
        camera.drawOption(bulletTexture, bullet.position, .{
            .size = bulletSize,
        });
    }

    if (health != 0) {
        // 绘制玩家
        camera.drawOption(texture, position, .{ .size = size });
    }
}

pub fn deinit() void {}
```

## enemy.zig

敌机的子弹检测。

```zig
fn updateBullets(delta: f32) void {
    var iterator = std.mem.reverseIterator(bullets.items);
    while (iterator.nextPtr()) |bullet| {
        const offset = bullet.direction.scale(BULLET_SPEED * delta);
        bullet.position = bullet.position.add(offset);
        if (!bulletBound.contains(bullet.position)) {
            // 移动到屏幕外了，删除
            _ = bullets.swapRemove(iterator.index);
            continue;
        }
        // 检测是否击中玩家
        const center = bullet.position.add(bulletSize.scale(0.5));
        if (player.collidePlayer(center)) {
            _ = bullets.swapRemove(iterator.index);
        }
    }
}
```

## 效果

![碰撞检测-玩家][1]

[1]: images/太空战机13.gif

## 附录
