# 1153-怪物战争-设定移动路径

## 目标

设定敌人的移动路径，确保敌人能够沿着预定路线移动。

## 环境

- Time 2026-02-03
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1jf9XYQEhW>

## 想法

增加了一个遵循路径的系统，根据敌人是否达到下一个点来改变速度方向。

## scene.zig

生成了更多的敌人，并且设定了它们的移动路径。

```zig
const std = @import("std");
const zhu = @import("zhu");

const ecs = zhu.ecs;

const map = @import("map.zig");
const com = @import("component.zig");

var registry: ecs.Registry = undefined;
var timer: zhu.Timer = .init(2);

pub fn init() void {
    registry = .init(zhu.assets.allocator);
    map.init();
}

fn spawnEnemy() void {
    var image = zhu.assets.loadImage("assets/textures/Enemy/wolf.png", .xy(5760, 768));
    image = image.sub(.init(.zero, .xy(192, 192)));

    for (map.startPaths) |startId| {
        if (startId == 0) break;

        const start = map.paths.get(startId).?;
        const enemy = registry.createEntity();
        registry.add(enemy, start.point);
        registry.add(enemy, com.Velocity{ .v = .zero });
        registry.add(enemy, com.Enemy{ .target = start, .speed = 40 });

        registry.add(enemy, com.Sprite{
            .image = image,
            .offset = .xy(-96, -128),
            .flip = false,
        });
    }
}

pub fn deinit() void {
    map.deinit();
    registry.deinit();
}

pub fn update(delta: f32) void {
    if (timer.isFinishedLoopUpdate(delta)) spawnEnemy();

    map.update(delta);

    followPath();

    var view = registry.view(.{ com.Position, com.Velocity });
    while (view.next()) |entity| {
        const position = view.getPtr(entity, com.Position);
        const velocity = view.get(entity, com.Velocity);
        position.* = position.*.add(velocity.v.scale(delta));
    }

    // 处理到达终点的敌人
    for (registry.getEvents(ecs.Entity)) |entity| {
        std.log.info("destroying entity: {}", .{entity});
        registry.destroyEntity(entity);
    }
    registry.clearEvent(ecs.Entity);
}

pub fn followPath() void {
    var view = registry.view(.{ com.Position, com.Enemy });
    while (view.next()) |entity| {
        // 当前位置和目标位置是否足够靠近
        const enemy = view.getPtr(entity, com.Enemy);
        const pos = view.get(entity, com.Position);
        if (enemy.target.point.sub(pos).length2() > 25) continue;

        // 到达目标位置，转向，即更新速度
        const nextPathId = enemy.target.randomNext();
        if (nextPathId == 0) { // 到达终点，销毁实体
            registry.addEvent(view.toEntity(entity));
            continue;
        }
        enemy.target = map.paths.get(nextPathId).?;
        const velocity = view.getPtr(entity, com.Velocity);
        const direction = enemy.target.point.sub(pos).normalize();
        velocity.v = direction.scale(enemy.speed);
    }
}

pub fn draw() void {
    map.draw();

    var view = registry.view(.{ com.Sprite, com.Position, com.Velocity });
    while (view.next()) |entity| {
        const sprite = view.get(entity, com.Sprite);
        const position = view.get(entity, com.Position);
        const velocity = view.get(entity, com.Velocity);
        const pos = position.add(sprite.offset);
        zhu.batch.drawImage(sprite.image, pos, .{
            .flipX = (velocity.v.x < 0) == sprite.flip,
        });
    }

    for (map.startPaths) |start| {
        if (start == 0) break;

        var prev = map.paths.get(start).?;
        while (prev.next != 0) {
            const next = map.paths.get(prev.next).?;
            zhu.batch.drawLine(prev.point, next.point, .{
                .color = .red,
                .width = 4,
            });
            prev = next;
        }
    }
}
```

## component.zig

```zig
const std = @import("std");
const zhu = @import("zhu");

pub const Image = zhu.graphics.Image;
pub const Sprite = struct {
    image: Image,
    offset: zhu.Vector2,
    flip: bool = false,
};
pub const Position = zhu.Vector2;
pub const Velocity = struct { v: zhu.Vector2 };

pub const Path = struct {
    point: zhu.Vector2, // 路径点位置
    next: u8 = 0, // 终点没有下一个路径点
    next2: u8 = 0, // 可选的第二条分支路径

    pub fn randomNext(self: Path) u8 {
        if (self.next2 == 0) return self.next;
        return if (zhu.randomBool()) self.next else self.next2;
    }
};

pub const Enemy = struct { target: Path, speed: f32 };

```

## 效果

![设定移动路径][1]

[1]: images/怪物战争11.png

## 附录
