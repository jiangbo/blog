# 1160-怪物战争-显示血量条

## 目标

显示受伤角色和怪兽的血量条。

## 环境

- Time 2026-02-10
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1jf9XYQEhW>

## 想法

将属性系统中的血量属性，显示在角色和怪兽的下方。

## health.zig

```zig
const std = @import("std");
const zhu = @import("zhu");

const com = @import("../component.zig");

pub fn update(reg: *zhu.ecs.Registry, _: f32) void {
    var view = reg.view(.{ com.attack.Hit, com.attack.Target });
    while (view.next()) |entity| {
        const target = view.get(entity, com.attack.Target).v;
        if (!reg.validEntity(target)) continue; // 目标已经死了

        const attack = view.getPtr(entity, com.Stats).attack;
        const stats = view.getPtr(target.index, com.Stats);

        if (attack < 0) { // 治疗
            stats.health -= attack;
            if (stats.health >= stats.maxHealth) {
                stats.health = stats.maxHealth;
                reg.remove(target, com.attack.Injured); // 移除受伤标签
            }
            const msg = "entity: {} heal target: {}, health: {}";
            std.log.debug(msg, .{ entity, target, stats.health });
            continue;
        }

        // 伤害
        const damage = @max(attack - stats.defense, 10);
        stats.health -= damage;
        const msg = "entity: {} hit target: {}, damage: {}, health: {}";
        std.log.debug(msg, .{ entity, target, damage, stats.health });

        view.add(target.index, com.attack.Injured{}); // 目标受伤了
        if (stats.health <= 0) {
            view.add(target.index, com.Dead{}); // 目标死了
        }
    }

    reg.clear(com.attack.Hit);
}

const percentInt = zhu.math.percentInt;
pub fn draw(reg: *zhu.ecs.Registry) void {
    const size: zhu.Vector2 = .xy(40, 10);

    var view = reg.view(.{ com.attack.Injured, com.Stats });
    while (view.next()) |entity| {
        const stats = view.getPtr(entity, com.Stats);
        const percent = percentInt(stats.health, stats.maxHealth);

        var pos = view.get(entity, com.Position);
        pos = pos.addXY(-size.x / 2, size.y);

        var color = zhu.graphics.Color.red;
        if (percent > 0.7) color = .green //
        else if (percent > 0.3) color = .yellow;

        var rect = zhu.math.Rect{ .min = pos, .size = size };
        zhu.batch.drawRectBorder(rect, 2, color);
        rect.size.x *= percent;
        zhu.batch.drawRect(rect, .{ .color = color });
    }
}
```

## 效果

![显示血量条][1]

[1]: images/怪物战争18.png

## 附录
