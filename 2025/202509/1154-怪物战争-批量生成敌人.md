# 1154-怪物战争-批量生成敌人

## 目标

定义敌人类型的模板，然后批量生成敌人，并让它们沿着设定的路径移动。

## 环境

- Time 2026-02-03
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1jf9XYQEhW>

## 想法

增加一个配置文件，定义敌人的属性，然后根据配置来生成敌人。

## scene.zig

这里有一个批量渲染的问题，素材的图片也太大了，不能合批，导致批次太多。

```zig
const std = @import("std");
const zhu = @import("zhu");

const ecs = zhu.ecs;

const map = @import("map.zig");
const com = @import("component.zig");
const enemy = @import("enemy.zig");

var registry: ecs.Registry = undefined;
// var timer: zhu.Timer = .init(10);

pub fn init() void {
    registry = .init(zhu.assets.allocator);
    map.init();
    enemy.spawn(&registry);
}

pub fn deinit() void {
    map.deinit();
    registry.deinit();
}

pub fn update(delta: f32) void {
    // if (timer.isFinishedLoopUpdate(delta)) enemy.spawn(&registry);

    map.update(delta);

    enemy.followPath(&registry);

    var view = registry.view(.{ com.Position, com.Velocity });
    while (view.next()) |entity| {
        const position = view.getPtr(entity, com.Position);
        const velocity = view.get(entity, com.Velocity);
        position.* = position.*.add(velocity.v.scale(delta));
    }

    // 处理到达终点的敌人
    for (registry.getEvents(ecs.Entity)) |entity| {
        registry.destroyEntity(entity);
    }
    registry.clearEvent(ecs.Entity);
}

pub fn draw() void {
    map.draw();

    // registry.sort(com.Sprite, struct {
    //     pub fn lessThan(a: com.Sprite, b: com.Sprite) bool {
    //         return a.image.texture.id < b.image.texture.id;
    //     }
    // }.lessThan);

    var view = registry.view(.{ com.Sprite, com.Position, com.Velocity });
    while (view.next()) |entity| {
        const sprite = view.get(entity, com.Sprite);
        const position = view.get(entity, com.Position);
        const velocity = view.get(entity, com.Velocity);
        const pos = position.add(sprite.offset);
        zhu.batch.drawImage(sprite.image, pos, .{
            .flipX = (velocity.v.x < 0) == sprite.flip,
        });
    }

    for (map.startPaths) |start| {
        if (start == 0) break;

        var previous = map.paths.get(start).?;
        while (previous.next != 0) {
            const next = map.paths.get(previous.next).?;
            zhu.batch.drawLine(previous.point, next.point, .{
                .color = .red,
                .width = 4,
            });
            previous = next;
        }
    }

    std.log.info("command len: {}", .{zhu.batch.imageDrawCount()});
}
```

## enemy.zig

把敌人相关的部分，移动到 enemy.zig 文件中。

```zig
const std = @import("std");
const zhu = @import("zhu");

const ecs = zhu.ecs;

const com = @import("component.zig");
const map = @import("map.zig");

const Enemy = struct {
    enemyEnum: enum { slime, wolf, goblin, darkWitch },
    name: []const u8,
    health: u32,
    attack: u32,
    defense: u32,
    range: f32,
    interval: f32,
    speed: f32,
    ranged: bool,
    image: struct { path: [:0]const u8, size: zhu.Vector2 },
    faceRight: bool,
    size: zhu.Vector2,
    offset: zhu.Vector2,
};

const zon: []const Enemy = @import("zon/enemy.zon");

pub fn spawn(registry: *ecs.Registry) void {
    for (map.startPaths) |startId| {
        if (startId == 0) break;

        const start = map.paths.get(startId).?;
        for (zon) |value| {
            const enemy = registry.createEntity();
            registry.add(enemy, start.point);
            registry.add(enemy, com.Velocity{ .v = .zero });
            registry.add(enemy, com.Enemy{
                .target = start,
                .speed = value.speed,
            });

            const path = value.image.path;
            const image = zhu.assets.loadImage(path, value.image.size);
            registry.add(enemy, com.Sprite{
                .image = image.sub(.init(.zero, value.size)),
                .offset = value.offset,
                .flip = value.faceRight,
            });
        }
    }
}

pub fn followPath(registry: *ecs.Registry) void {
    var view = registry.view(.{ com.Position, com.Enemy });
    while (view.next()) |entity| {
        // 当前位置和目标位置是否足够靠近
        const enemy = view.getPtr(entity, com.Enemy);
        const pos = view.get(entity, com.Position);
        if (enemy.target.point.sub(pos).length2() > 25) continue;

        // 到达目标位置，转向，即更新速度
        const nextPathId = enemy.target.randomNext();
        if (nextPathId == 0) { // 到达终点，销毁实体
            registry.addEvent(view.toEntity(entity));
            continue;
        }
        enemy.target = map.paths.get(nextPathId).?;
        const velocity = view.getPtr(entity, com.Velocity);
        const direction = enemy.target.point.sub(pos).normalize();
        velocity.v = direction.scale(enemy.speed);
    }
}
```

## 效果

![批量生成敌人][1]

[1]: images/怪物战争12.png

## 附录
