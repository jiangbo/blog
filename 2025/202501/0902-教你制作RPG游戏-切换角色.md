# 0902-教你制作RPG游戏-切换角色

## 目标

可以有三个角色，按下 TAB 键可以切换到下一个角色。

## 环境

- Time 2025-04-28

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

按道理角色的坐标应该属于角色自身，但是三个角色应该共享坐标和朝向，所以还是放到场景中。

## Player.zig

把角色单独提取到了一个文件中。

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const assets = @import("assets.zig");

const Player = @This();
const FrameAnimation = gfx.FixedFrameAnimation(4, 0.25);

index: u8,
upAnimation: FrameAnimation,
downAnimation: FrameAnimation,
leftAnimation: FrameAnimation,
rightAnimation: FrameAnimation,

pub fn init(path: [:0]const u8, index: u8) Player {
    const role = assets.loadTexture(path, .init(960, 960));
    const size: math.Vector = .init(960, 240);

    return Player{
        .index = index,
        .upAnimation = .init(role.subTexture(.init(.{ .y = 720 }, size))),
        .downAnimation = .init(role.subTexture(.init(.{ .y = 0 }, size))),
        .leftAnimation = .init(role.subTexture(.init(.{ .y = 240 }, size))),
        .rightAnimation = .init(role.subTexture(.init(.{ .y = 480 }, size))),
    };
}

pub fn current(self: *Player, face: math.FourDirection) *FrameAnimation {
    return switch (face) {
        .up => &self.upAnimation,
        .down => &self.downAnimation,
        .left => &self.leftAnimation,
        .right => &self.rightAnimation,
    };
}
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const assets = @import("assets.zig");

const Player = @import("Player.zig");
const PLAYER_SPEED = 100;

var players: [3]Player = undefined;
var currentPlayer: *Player = &players[0];
var position: math.Vector = .zero;
var facing: math.FourDirection = .down;

pub fn init() void {
    players[0] = .init("assets/r1.png", 0);
    players[1] = .init("assets/r2.png", 1);
    players[2] = .init("assets/r3.png", 2);
}

pub fn event(ev: *const window.Event) void {
    if (ev.type == .KEY_UP and ev.key_code == .TAB) {
        currentPlayer = &players[(currentPlayer.index + 1) % players.len];
    }
}

pub fn update(delta: f32) void {
    updatePlayer(delta);

    currentPlayer.current(facing).update(delta);
}

fn updatePlayer(delta: f32) void {
    var velocity: math.Vector = .zero;

    if (window.isKeyDown(.UP) or window.isKeyDown(.W)) {
        facing = .up;
        velocity = velocity.add(facing.toVector());
    }

    if (window.isKeyDown(.DOWN) or window.isKeyDown(.S)) {
        facing = .down;
        velocity = velocity.add(facing.toVector());
    }

    if (window.isKeyDown(.LEFT) or window.isKeyDown(.A)) {
        facing = .left;
        velocity = velocity.add(facing.toVector());
    }

    if (window.isKeyDown(.RIGHT) or window.isKeyDown(.D)) {
        facing = .right;
        velocity = velocity.add(facing.toVector());
    }

    if (!velocity.approx(.zero)) {
        velocity = velocity.normalize().scale(delta * PLAYER_SPEED);
        position = position.add(velocity);
    }
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    gfx.draw(currentPlayer.current(facing).current(), position);
}
```

## main.zig

把事件传播到了场景中。

```zig
const std = @import("std");

const assets = @import("assets.zig");
const window = @import("window.zig");
const math = @import("math.zig");
const gfx = @import("graphics.zig");
const audio = @import("audio.zig");
const scene = @import("scene.zig");

var soundBuffer: [20]audio.Sound = undefined;

export fn init() void {
    assets.init(allocator);
    gfx.init(window.size);
    audio.init(&soundBuffer);

    math.setRandomSeed(timer.lap());
    scene.init();
}

export fn event(ev: ?*const window.Event) void {
    if (ev) |e| {
        window.event(e);
        scene.event(e);
    }
}

export fn frame() void {
    const delta: f32 = @floatFromInt(timer.lap());
    assets.loading();
    scene.update(delta / std.time.ns_per_s);
    scene.render();
}

export fn deinit() void {
    audio.deinit();
    gfx.deinit();
    assets.deinit();
}

var allocator: std.mem.Allocator = undefined;
var timer: std.time.Timer = undefined;

pub fn main() void {
    var debugAllocator: std.heap.DebugAllocator(.{}) = undefined;
    if (@import("builtin").mode == .Debug) {
        debugAllocator = std.heap.DebugAllocator(.{}).init;
        allocator = debugAllocator.allocator();
    } else {
        allocator = std.heap.c_allocator;
    }

    defer if (@import("builtin").mode == .Debug) {
        _ = debugAllocator.deinit();
    };

    window.size = .{ .x = 640, .y = 480 };
    timer = std.time.Timer.start() catch unreachable;

    window.run(.{
        .window_title = "教你制作RPG游戏",
        .width = @as(i32, @intFromFloat(window.size.x)),
        .height = @as(i32, @intFromFloat(window.size.y)),
        .high_dpi = true,
        .init_cb = init,
        .event_cb = event,
        .frame_cb = frame,
        .cleanup_cb = deinit,
        .logger = .{ .func = window.log },
    });
}
```

## 效果

![切换角色][1]

[1]: images/教你制作RPG游戏07.webp

## 附录
