# 0909-教你制作RPG游戏-地图碰撞层

## 目标

有些地图元素不能通过，所以需要加一个碰撞层。教程中给出的是一张图片来描述的。

## 环境

- Time 2025-04-29

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

直接使用一张图片来决定碰撞层，可以描述得很精细，就是比较耗内存，每一个像素都需要存储。

## assets.zig

暴露出了加载文件后的回调方法。

```zig
const std = @import("std");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const c = @import("c.zig");
const sk = @import("sokol");

var allocator: std.mem.Allocator = undefined;

pub fn init(alloc: std.mem.Allocator) void {
    allocator = alloc;
    sk.fetch.setup(.{ .logger = .{ .func = sk.log.func } });
}

pub fn deinit() void {
    Texture.cache.deinit(allocator);
    Sound.deinit();
    sk.fetch.shutdown();
}

pub fn loading() void {
    sk.fetch.dowork();
}

var loadingBuffer: [1.5 * 1024 * 1024]u8 = undefined;

pub fn loadFile(path: [:0]const u8) void {
    loadCallback(path, callback);
}

pub const Callback = *const fn ([*c]const sk.fetch.Response) callconv(.C) void;
pub const Response = sk.fetch.Response;
pub fn loadCallback(path: [:0]const u8, cb: Callback) void {
    std.log.info("loading {s}", .{path});

    _ = sk.fetch.send(.{
        .path = path,
        .callback = cb,
        .buffer = sk.fetch.asRange(&loadingBuffer),
    });
}

fn callback(responses: [*c]const sk.fetch.Response) callconv(.C) void {
    const response = responses[0];

    if (response.failed) {
        std.debug.panic("failed to load assets, path: {s}", .{response.path});
    }

    const path = std.mem.span(response.path);
    if (std.mem.endsWith(u8, path, ".png")) {
        std.log.info("loaded texture from: {s}", .{path});
        Texture.init(path, rangeToSlice(response.data));
    } else if (std.mem.endsWith(u8, path, "bgm.ogg")) {
        std.log.info("loaded bgm from: {s}", .{path});
        const data = rangeToSlice(response.data);
        Music.init(path, allocator.dupe(u8, data) catch unreachable);
    } else if (std.mem.endsWith(u8, path, ".ogg")) {
        std.log.info("loaded ogg from: {s}", .{path});
        Sound.init(path, rangeToSlice(response.data));
    }
}

pub fn rangeToSlice(range: sk.fetch.Range) []const u8 {
    return @as([*]const u8, @ptrCast(range.ptr))[0..range.size];
}

pub fn loadTexture(path: [:0]const u8, size: math.Vector) gfx.Texture {
    return Texture.load(path, size);
}

pub const Texture = struct {
    var cache: std.StringHashMapUnmanaged(gfx.Texture) = .empty;

    pub fn load(path: [:0]const u8, size: math.Vector) gfx.Texture {
        const entry = cache.getOrPut(allocator, path) catch unreachable;
        if (entry.found_existing) return entry.value_ptr.*;

        loadFile(path);

        const image = sk.gfx.allocImage();
        entry.value_ptr.* = .{ .image = image, .area = .init(.zero, size) };
        return entry.value_ptr.*;
    }

    fn init(path: [:0]const u8, data: []const u8) void {
        const image = c.stbImage.loadFromMemory(data) catch unreachable;
        defer c.stbImage.unload(image);
        const texture = cache.getPtr(path).?;

        sk.gfx.initImage(texture.image, .{
            .width = image.width,
            .height = image.height,
            .data = init: {
                var imageData = sk.gfx.ImageData{};
                imageData.subimage[0][0] = sk.gfx.asRange(image.data);
                break :init imageData;
            },
        });
    }
};

const audio = @import("audio.zig");
pub const Sound = struct {
    var cache: std.StringHashMapUnmanaged(audio.Sound) = .empty;

    pub fn load(path: [:0]const u8) audio.Sound {
        const entry = cache.getOrPut(allocator, path) catch unreachable;
        if (entry.found_existing) return entry.value_ptr.*;

        loadFile(path);
        entry.value_ptr.* = .{ .source = undefined };
        entry.key_ptr.* = path;

        return entry.value_ptr.*;
    }

    pub fn init(path: [:0]const u8, data: []const u8) void {
        const stbAudio = c.stbAudio.loadFromMemory(data) catch unreachable;
        const info = c.stbAudio.getInfo(stbAudio);

        var sound = cache.getPtr(path).?;

        sound.channels = @intCast(info.channels);
        sound.sampleRate = @intCast(info.sample_rate);

        const size = c.stbAudio.getSampleCount(stbAudio) * sound.channels;
        sound.source = allocator.alloc(f32, size) catch unreachable;

        _ = c.stbAudio.fillSamples(stbAudio, sound.source, sound.channels);
        sound.valid = true;
    }

    pub fn deinit() void {
        var iterator = cache.valueIterator();
        while (iterator.next()) |value| allocator.free(value.source);
        cache.deinit(allocator);
    }
};

pub const Music = struct {
    pub fn load(path: [:0]const u8, loop: bool) audio.Music {
        if (audio.music) |m| {
            if (std.mem.eql(u8, m.path, path)) return audio.music.?;
        }

        _ = loadFile(path);
        return .{ .path = path, .loop = loop };
    }

    pub fn init(path: [:0]const u8, data: []const u8) void {
        const stbAudio = c.stbAudio.loadFromMemory(data) catch unreachable;
        const info = c.stbAudio.getInfo(stbAudio);
        const args = .{ info.sample_rate, info.channels, path };
        std.log.info("music sampleRate: {}, channels: {d}, path: {s}", args);
        audio.music.?.source = stbAudio;
        audio.music.?.data = data;
        audio.music.?.valid = true;
    }

    pub fn unload() void {
        c.stbAudio.unload(audio.music.?.source);
        audio.music.?.valid = false;
        if (audio.music.?.data.len != 0) {
            allocator.free(audio.music.?.data);
        }
    }
};
```

## map.zig

将地图相关的部分提取到了一个单独的文件中。

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const assets = @import("assets.zig");
const c = @import("c.zig");

pub const SIZE: math.Vector = .init(1000, 800);

var map: gfx.Texture = undefined;
var mapShade: gfx.Texture = undefined;
var mapBack: gfx.Texture = undefined;
var mapBlock: ?std.StaticBitSet(SIZE.x * SIZE.y) = null;

pub fn init() void {
    map = assets.loadTexture("assets/map1.png", SIZE);
    mapShade = assets.loadTexture("assets/map1_shade.png", SIZE);
    mapBack = assets.loadTexture("assets/map1_back.png", SIZE);

    _ = assets.loadCallback("assets/map1_block.png", callback);
}

pub fn canWalk(pos: math.Vector) bool {
    const x, const y = .{ @round(pos.x), @round(pos.y) };

    if (x < 0 or x >= SIZE.x or y < 0 or y >= SIZE.y) return false;
    if (mapBlock) |block| {
        return !block.isSet(@intFromFloat(x + y * SIZE.x));
    } else return false;
}

fn callback(responses: [*c]const assets.Response) callconv(.C) void {
    if (responses[0].failed) {
        @panic("failed to load map block");
    }

    const buffer = assets.rangeToSlice(responses[0].data);
    const image = c.stbImage.loadFromMemory(buffer) catch unreachable;
    defer c.stbImage.unload(image);

    const data: []const u32 = @ptrCast(@alignCast(image.data));
    std.debug.assert(data.len == SIZE.x * SIZE.y);

    var blocks: std.StaticBitSet(SIZE.x * SIZE.y) = .initEmpty();
    for (data, 0..) |color, index| {
        if (color == 0xFF000000) blocks.set(index);
    }
    mapBlock = blocks;
}

pub fn drawBackground() void {
    gfx.draw(mapBack, .zero);
    gfx.draw(map, .zero);
}

pub fn drawForeground() void {
    gfx.draw(mapShade, .zero);
}
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const assets = @import("assets.zig");

const Player = @import("Player.zig");
const map = @import("map.zig");
const PLAYER_SPEED = 100;

var players: [3]Player = undefined;
var currentPlayer: *Player = &players[0];
var position: math.Vector = .init(30, 500);
var facing: math.FourDirection = .down;
var keyPressed: bool = false;
var velocity: math.Vector = .zero;

const PLAYER_SIZE: math.Vector = .init(96, 96);

pub fn init() void {
    gfx.camera = .{ .rect = .init(.zero, window.size), .border = map.SIZE };
    gfx.camera.lookAt(position);

    players[0] = .init("assets/r1.png", 0);
    players[1] = .init("assets/r2.png", 1);
    players[2] = .init("assets/r3.png", 2);

    map.init();
}

pub fn event(ev: *const window.Event) void {
    if (ev.type == .KEY_UP and ev.key_code == .TAB) {
        currentPlayer = &players[(currentPlayer.index + 1) % players.len];
    }
}

pub fn update(delta: f32) void {
    velocity = .zero;
    keyPressed = false;

    if (window.isKeyDown(.UP) or window.isKeyDown(.W)) updatePlayer(.up);

    if (window.isKeyDown(.DOWN) or window.isKeyDown(.S)) updatePlayer(.down);

    if (window.isKeyDown(.LEFT) or window.isKeyDown(.A)) updatePlayer(.left);

    if (window.isKeyDown(.RIGHT) or window.isKeyDown(.D)) updatePlayer(.right);

    if (velocity.approx(.zero)) {
        currentPlayer.current(facing).reset();
    } else {
        velocity = velocity.normalize().scale(delta * PLAYER_SPEED);
        const tempPosition = position.add(velocity);
        if (map.canWalk(tempPosition)) position = tempPosition;
        gfx.camera.lookAt(position);
    }

    if (keyPressed) currentPlayer.current(facing).update(delta);
}

fn updatePlayer(direction: math.FourDirection) void {
    facing = direction;
    keyPressed = true;
    velocity = velocity.add(direction.toVector());
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    map.drawBackground();

    gfx.drawOptions(currentPlayer.current(facing).current(), .{
        .targetRect = .init(position, PLAYER_SIZE),
    });

    map.drawForeground();

    gfx.drawRectangle(.init(position, PLAYER_SIZE));
}
```

## 效果

![地图碰撞层][1]

[1]: images/教你制作RPG游戏14.webp

## 附录
