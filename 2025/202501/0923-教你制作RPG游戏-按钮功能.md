# 0923-教你制作RPG游戏-按钮功能

## 目标

实现开始游戏和退出游戏两个按钮的功能，剩下的一个暂时不清楚怎么实现，先放着。

## 环境

- Time 2025-05-09

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

还是使用的主动检测按键来实现的按钮的功能，没有实现回调。教程中好像使用的 C# 的委托，不会这个。

## 切换场景的方法

之前在场景管理中定义了切换场景的功能，把它提取成方法，可以在不同的场景中调用。

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");

const titleScene = @import("scene/title.zig");
const worldScene = @import("scene/world.zig");

const SceneType = enum { title, world };

var currentSceneType: SceneType = .title;

pub fn init() void {
    titleScene.init();
    worldScene.init();
    enter();
}

pub fn enter() void {
    sceneCall("enter", .{});
}

pub fn exit() void {
    sceneCall("exit", .{});
}

pub fn changeScene() void {
    exit();
    const next: usize = @intFromEnum(currentSceneType);
    const len = std.enums.values(SceneType).len;
    currentSceneType = @enumFromInt((next + 1) % len);
    enter();
}

pub fn update(delta: f32) void {
    sceneCall("update", .{delta});
}

pub fn render() void {
    sceneCall("render", .{});
}

fn sceneCall(comptime function: []const u8, args: anytype) void {
    switch (currentSceneType) {
        .title => @call(.auto, @field(titleScene, function), args),
        .world => @call(.auto, @field(worldScene, function), args),
    }
}
```

## 实现按钮功能

只实现了两个按钮的功能，还有一个只打印了日志。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const scene = @import("../scene.zig");

var background1: gfx.Texture = undefined;

var start1: gfx.Texture = undefined;
var start2: gfx.Texture = undefined;

var load1: gfx.Texture = undefined;
var load2: gfx.Texture = undefined;

var exit1: gfx.Texture = undefined;
var exit2: gfx.Texture = undefined;

const Button = struct {
    normal: gfx.Texture,
    hover: gfx.Texture,
};

var buttons: [3]Button = undefined;
var currentButton: u8 = 0;

pub fn init() void {
    background1 = gfx.loadTexture("assets/T_bg1.png", .init(800, 600));

    buttons[0] = .{
        .normal = gfx.loadTexture("assets/T_start_1.png", .init(142, 36)),
        .hover = gfx.loadTexture("assets/T_start_2.png", .init(142, 36)),
    };

    buttons[1] = .{
        .normal = gfx.loadTexture("assets/T_load_1.png", .init(142, 36)),
        .hover = gfx.loadTexture("assets/T_load_2.png", .init(142, 36)),
    };

    buttons[2] = .{
        .normal = gfx.loadTexture("assets/T_exit_1.png", .init(142, 36)),
        .hover = gfx.loadTexture("assets/T_exit_2.png", .init(142, 36)),
    };
}

pub fn enter() void {
    currentButton = 0;
    window.playMusic("assets/2.ogg");
}

pub fn exit() void {
    window.stopMusic();
}

pub fn update(delta: f32) void {
    _ = delta;

    if (window.isAnyKeyRelease(&.{ .W, .UP })) currentButton -|= 1;
    if (window.isAnyKeyRelease(&.{ .S, .DOWN })) currentButton += 1;
    currentButton = @min(currentButton, buttons.len - 1);

    if (window.isAnyKeyRelease(&.{ .ENTER, .SPACE })) {
        switch (currentButton) {
            0 => scene.changeScene(),
            1 => std.log.info("load game", .{}),
            2 => window.exit(),
            else => unreachable,
        }
    }
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    gfx.draw(background1, .zero);

    for (buttons, 0..) |button, index| {
        const offsetY: f32 = @floatFromInt(350 + index * 50);
        if (currentButton == index) {
            gfx.draw(button.hover, .init(325, offsetY));
        } else {
            gfx.draw(button.normal, .init(325, offsetY));
        }
    }
}
```

## 效果

![按钮功能][1]

[1]: images/教你制作RPG游戏27.webp

## 附录
