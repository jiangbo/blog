# 0901-教你制作RPG游戏-固定帧动画

## 目标

前面是将帧信息存储到 cache 中，这里进行简化，直接存储到动画类自己里面。

## 环境

- Time 2025-04-28

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

将 cache.zig 重命名为 assets.zig 并且删除里面很多不必要的，需要的时候再加。

## animation.zig

定义了一个泛型方法。

```zig
const std = @import("std");

const window = @import("window.zig");
const assets = @import("assets.zig");
const math = @import("math.zig");
const Texture = @import("gpu.zig").Texture;

pub const FrameAnimation = FixedFrameAnimation(4, 0.1);

pub fn FixedFrameAnimation(count: u8, time: f32) type {
    return struct {
        timer: window.Timer = .init(time),
        index: usize = 0,
        loop: bool = true,
        texture: Texture,
        frames: [count]math.Rectangle,
        offset: math.Vector = .zero,

        const Animation = @This();

        pub fn init(texture: Texture) Animation {
            var frames: [count]math.Rectangle = undefined;

            const width = @divExact(texture.width(), count);
            const size: math.Vector = .{ .x = width, .y = texture.height() };

            for (0..frames.len) |index| {
                const x = @as(f32, @floatFromInt(index)) * width;
                frames[index] = .init(.init(x, texture.area.min.y), size);
            }

            return .{ .texture = texture, .frames = frames };
        }

        pub fn current(self: *const Animation) Texture {
            return self.texture.mapTexture(self.frames[self.index]);
        }

        pub fn update(self: *Animation, delta: f32) void {
            if (self.timer.isRunningAfterUpdate(delta)) return;

            if (self.index == self.frames.len - 1) {
                if (self.loop) self.reset();
            } else {
                self.timer.reset();
                self.index += 1;
            }
        }

        pub fn anchor(self: *Animation, direction: math.EightDirection) void {
            const tex = self.texture;
            self.offset = switch (direction) {
                .down => .{ .x = -tex.width() / 2, .y = -tex.height() },
                else => unreachable,
            };
        }

        pub fn anchorCenter(self: *Animation) void {
            self.offset.x = -self.texture.width() / 2;
            self.offset.y = -self.texture.height() / 2;
        }

        pub fn reset(self: *Animation) void {
            self.timer.reset();
            self.index = 0;
        }

        pub fn finished(self: *const Animation) bool {
            return self.timer.finished and !self.loop;
        }
    };
}
```

## assets.zig

```zig
const std = @import("std");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const c = @import("c.zig");
const sk = @import("sokol");

var allocator: std.mem.Allocator = undefined;

pub fn init(alloc: std.mem.Allocator) void {
    allocator = alloc;
    sk.fetch.setup(.{ .logger = .{ .func = sk.log.func } });
}

pub fn deinit() void {
    Texture.cache.deinit(allocator);
    Sound.deinit();
    sk.fetch.shutdown();
}

pub fn loading() void {
    sk.fetch.dowork();
}

var loadingBuffer: [1.5 * 1024 * 1024]u8 = undefined;

pub fn send(path: [:0]const u8) void {
    std.log.info("loading {s}", .{path});

    _ = sk.fetch.send(.{
        .path = path,
        .callback = callback,
        .buffer = sk.fetch.asRange(&loadingBuffer),
    });
}

fn callback(responses: [*c]const sk.fetch.Response) callconv(.C) void {
    const response = responses[0];

    if (response.failed) {
        std.log.info("failed to load assets, path: {s}", .{response.path});
        return;
    }

    const path = std.mem.span(response.path);
    if (std.mem.endsWith(u8, path, ".png")) {
        std.log.info("loaded texture from: {s}", .{path});
        Texture.init(path, rangeToSlice(response.data));
    } else if (std.mem.endsWith(u8, path, "bgm.ogg")) {
        std.log.info("loaded bgm from: {s}", .{path});
        const data = rangeToSlice(response.data);
        Music.init(path, allocator.dupe(u8, data) catch unreachable);
    } else if (std.mem.endsWith(u8, path, ".ogg")) {
        std.log.info("loaded ogg from: {s}", .{path});
        Sound.init(path, rangeToSlice(response.data));
    }
}

fn rangeToSlice(range: sk.fetch.Range) []const u8 {
    return @as([*]const u8, @ptrCast(range.ptr))[0..range.size];
}

pub fn loadTexture(path: [:0]const u8, size: math.Vector) gfx.Texture {
    return Texture.load(path, size);
}

pub const Texture = struct {
    var cache: std.StringHashMapUnmanaged(gfx.Texture) = .empty;

    pub fn load(path: [:0]const u8, size: math.Vector) gfx.Texture {
        const entry = cache.getOrPut(allocator, path) catch unreachable;
        if (entry.found_existing) return entry.value_ptr.*;

        send(path);

        const image = sk.gfx.allocImage();
        entry.value_ptr.* = .{ .image = image, .area = .init(.zero, size) };
        return entry.value_ptr.*;
    }

    fn init(path: [:0]const u8, data: []const u8) void {
        const image = c.stbImage.loadFromMemory(data) catch unreachable;
        defer c.stbImage.unload(image);
        const texture = cache.getPtr(path).?;

        sk.gfx.initImage(texture.image, .{
            .width = image.width,
            .height = image.height,
            .data = init: {
                var imageData = sk.gfx.ImageData{};
                imageData.subimage[0][0] = sk.gfx.asRange(image.data);
                break :init imageData;
            },
        });
    }
};

const audio = @import("audio.zig");
pub const Sound = struct {
    var cache: std.StringHashMapUnmanaged(audio.Sound) = .empty;

    pub fn load(path: [:0]const u8) audio.Sound {
        const entry = cache.getOrPut(allocator, path) catch unreachable;
        if (entry.found_existing) return entry.value_ptr.*;

        send(path);
        entry.value_ptr.* = .{ .source = undefined };
        entry.key_ptr.* = path;

        return entry.value_ptr.*;
    }

    pub fn init(path: [:0]const u8, data: []const u8) void {
        const stbAudio = c.stbAudio.loadFromMemory(data) catch unreachable;
        const info = c.stbAudio.getInfo(stbAudio);

        var sound = cache.getPtr(path).?;

        sound.channels = @intCast(info.channels);
        sound.sampleRate = @intCast(info.sample_rate);

        const size = c.stbAudio.getSampleCount(stbAudio) * sound.channels;
        sound.source = allocator.alloc(f32, size) catch unreachable;

        _ = c.stbAudio.fillSamples(stbAudio, sound.source, sound.channels);
        sound.valid = true;
    }

    pub fn deinit() void {
        var iterator = cache.valueIterator();
        while (iterator.next()) |value| allocator.free(value.source);
        cache.deinit(allocator);
    }
};

pub const Music = struct {
    pub fn load(path: [:0]const u8, loop: bool) audio.Music {
        if (audio.music) |m| {
            if (std.mem.eql(u8, m.path, path)) return audio.music.?;
        }

        send(path);
        return .{ .path = path, .loop = loop };
    }

    pub fn init(path: [:0]const u8, data: []const u8) void {
        const stbAudio = c.stbAudio.loadFromMemory(data) catch unreachable;
        const info = c.stbAudio.getInfo(stbAudio);
        const args = .{ info.sample_rate, info.channels, path };
        std.log.info("music sampleRate: {}, channels: {d}, path: {s}", args);
        audio.music.?.source = stbAudio;
        audio.music.?.data = data;
        audio.music.?.valid = true;
    }

    pub fn unload() void {
        c.stbAudio.unload(audio.music.?.source);
        audio.music.?.valid = false;
        if (audio.music.?.data.len != 0) {
            allocator.free(audio.music.?.data);
        }
    }
};
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const assets = @import("assets.zig");

const FrameAnimation = gfx.FixedFrameAnimation(4, 0.25);

const SPEED = 100;
var position: math.Vector = .zero;
var facing: math.FourDirection = .down;

var upAnimation: FrameAnimation = undefined;
var downAnimation: FrameAnimation = undefined;
var leftAnimation: FrameAnimation = undefined;
var rightAnimation: FrameAnimation = undefined;

var roleTexture: gfx.Texture = undefined;

pub fn init() void {
    roleTexture = assets.loadTexture("assets/r1.png", .init(960, 960));

    const size: math.Vector = .init(960, 240);
    upAnimation = .init(roleTexture.subTexture(.init(.{ .y = 720 }, size)));

    downAnimation = .init(roleTexture.subTexture(.init(.{ .y = 0 }, size)));

    leftAnimation = .init(roleTexture.subTexture(.init(.{ .y = 240 }, size)));

    rightAnimation = .init(roleTexture.subTexture(.init(.{ .y = 480 }, size)));
}

pub fn update(delta: f32) void {
    updatePlayer(delta);

    switch (facing) {
        .up => upAnimation.update(delta),
        .down => downAnimation.update(delta),
        .left => leftAnimation.update(delta),
        .right => rightAnimation.update(delta),
    }
}

fn updatePlayer(delta: f32) void {
    var velocity: math.Vector = .zero;

    if (window.isKeyDown(.UP) or window.isKeyDown(.W)) {
        facing = .up;
        velocity = velocity.add(facing.toVector());
    }

    if (window.isKeyDown(.DOWN) or window.isKeyDown(.S)) {
        facing = .down;
        velocity = velocity.add(facing.toVector());
    }

    if (window.isKeyDown(.LEFT) or window.isKeyDown(.A)) {
        facing = .left;
        velocity = velocity.add(facing.toVector());
    }

    if (window.isKeyDown(.RIGHT) or window.isKeyDown(.D)) {
        facing = .right;
        velocity = velocity.add(facing.toVector());
    }

    if (!velocity.approx(.zero)) {
        velocity = velocity.normalize().scale(delta * SPEED);
        position = position.add(velocity);
    }
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    switch (facing) {
        .up => gfx.draw(upAnimation.current(), position),
        .down => gfx.draw(downAnimation.current(), position),
        .left => gfx.draw(leftAnimation.current(), position),
        .right => gfx.draw(rightAnimation.current(), position),
    }
}
```

## 效果

![固定帧动画][1]

[1]: images/教你制作RPG游戏06.webp

## 附录
