# 0912-教你制作RPG游戏-新增地图

## 目标

一共有两张地图，已经实现了一张地图了，封装一下，然后实现第二张地图。

## 环境

- Time 2025-04-30

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

在实现了第一张地图的基础上，新增第二张地图不是很复杂。

## map.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const assets = @import("assets.zig");
const c = @import("c.zig");

pub const SIZE: math.Vector = .init(1000, 800);

const Map = struct {
    map: gfx.Texture,
    mapShade: gfx.Texture,
    mapBack: ?gfx.Texture = null,
    mapBlock: ?std.StaticBitSet(SIZE.x * SIZE.y) = null,
};

var index: usize = maps.len - 1;
var maps: [2]Map = undefined;

pub fn init() void {
    maps[0] = Map{
        .map = assets.loadTexture("assets/map1.png", SIZE),
        .mapShade = assets.loadTexture("assets/map1_shade.png", SIZE),
        .mapBack = assets.loadTexture("assets/map1_back.png", SIZE),
    };

    maps[1] = Map{
        .map = assets.loadTexture("assets/map2.png", SIZE),
        .mapShade = assets.loadTexture("assets/map2_shade.png", SIZE),
    };

    const file = assets.File.load("assets/map1_block.png", callback);
    if (file.data.len != 0) initMapBlock(file.data);

    changeMap();
}

pub fn changeMap() void {
    index = (index + 1) % maps.len;
    switch (index) {
        0 => audio.playMusic("assets/1.ogg"),
        1 => audio.playMusic("assets/2.ogg"),
        else => unreachable,
    }

    if (maps[index].mapBlock == null and index == 0) {
        const file = assets.File.load("assets/map1_block.png", callback);
        if (file.data.len != 0) initMapBlock(file.data);
    }

    if (maps[index].mapBlock == null and index == 1) {
        const file = assets.File.load("assets/map2_block.png", callback);
        if (file.data.len != 0) initMapBlock(file.data);
    }
}

pub fn canWalk(pos: math.Vector) bool {
    const x, const y = .{ @round(pos.x), @round(pos.y) };

    if (x < 0 or x >= SIZE.x or y < 0 or y >= SIZE.y) return false;
    if (maps[index].mapBlock) |block| {
        return !block.isSet(@intFromFloat(x + y * SIZE.x));
    } else return false;
}

fn callback(allocator: std.mem.Allocator, buffer: *[]const u8) void {
    const image = c.stbImage.loadFromMemory(buffer.*) catch unreachable;
    defer c.stbImage.unload(image);

    buffer.* = allocator.dupe(u8, image.data) catch unreachable;
    initMapBlock(buffer.*);
}

fn initMapBlock(buffer: []const u8) void {
    const data: []const u32 = @ptrCast(@alignCast(buffer));
    std.debug.assert(data.len == SIZE.x * SIZE.y);

    var blocks: std.StaticBitSet(SIZE.x * SIZE.y) = .initEmpty();
    for (data, 0..) |color, i| {
        if (color == 0xFF000000) blocks.set(i);
    }
    maps[index].mapBlock = blocks;
}

pub fn drawBackground() void {
    if (maps[index].mapBack) |back| gfx.draw(back, .zero);
    gfx.draw(maps[index].map, .zero);
}

pub fn drawForeground() void {
    gfx.draw(maps[index].mapShade, .zero);
}
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const assets = @import("assets.zig");

const Player = @import("Player.zig");
const map = @import("map.zig");
const PLAYER_SPEED = 150;
const PLAYER_OFFSET: math.Vector = .init(120, 220);

var players: [3]Player = undefined;
var currentPlayer: *Player = &players[0];
var position: math.Vector = .init(30, 500);
var facing: math.FourDirection = .down;
var keyPressed: bool = false;
var velocity: math.Vector = .zero;

pub fn init() void {
    gfx.camera = .{ .rect = .init(.zero, window.size), .border = map.SIZE };
    gfx.camera.lookAt(position);

    players[0] = .init("assets/r1.png", 0);
    players[1] = .init("assets/r2.png", 1);
    players[2] = .init("assets/r3.png", 2);

    map.init();
}

pub fn event(ev: *const window.Event) void {
    if (ev.type == .KEY_UP and ev.key_code == .TAB) {
        currentPlayer = &players[(currentPlayer.index + 1) % players.len];
    }

    if (ev.type == .KEY_UP and ev.key_code == .SPACE) {
        map.changeMap();
    }
}

pub fn update(delta: f32) void {
    velocity = .zero;
    keyPressed = false;

    if (window.isKeyDown(.UP) or window.isKeyDown(.W)) updatePlayer(.up);

    if (window.isKeyDown(.DOWN) or window.isKeyDown(.S)) updatePlayer(.down);

    if (window.isKeyDown(.LEFT) or window.isKeyDown(.A)) updatePlayer(.left);

    if (window.isKeyDown(.RIGHT) or window.isKeyDown(.D)) updatePlayer(.right);

    if (velocity.approx(.zero)) {
        currentPlayer.current(facing).reset();
    } else {
        velocity = velocity.normalize().scale(delta * PLAYER_SPEED);
        const tempPosition = position.add(velocity);
        if (map.canWalk(tempPosition)) position = tempPosition;
        gfx.camera.lookAt(position);
    }

    if (keyPressed) currentPlayer.current(facing).update(delta);
}

fn updatePlayer(direction: math.FourDirection) void {
    facing = direction;
    keyPressed = true;
    velocity = velocity.add(direction.toVector());
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    map.drawBackground();

    const playerTexture = currentPlayer.current(facing).currentTexture();
    gfx.draw(playerTexture, position.sub(PLAYER_OFFSET));

    map.drawForeground();
}
```

## 效果

![新增地图][1]

[1]: images/教你制作RPG游戏16.webp

## 附录
