# 0897-教你制作RPG游戏-控制主角

## 目标

通过上下左右键来控制角色的移动。

## 环境

- Time 2025-04-27

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

将按键的状态存储到 window.zig 模块中，需要的时候，直接检测。

## build.zig

```zig
const std = @import("std");
const sk = @import("sokol");
const math = @import("math.zig");

pub const Event = sk.app.Event;

pub const Timer = struct {
    finished: bool = false,
    duration: f32,
    elapsed: f32 = 0,

    pub fn init(duration: f32) Timer {
        return Timer{ .duration = duration };
    }

    pub fn update(self: *Timer, delta: f32) void {
        if (self.finished) return;
        self.elapsed += delta;
        if (self.elapsed >= self.duration) self.finished = true;
    }

    pub fn isRunningAfterUpdate(self: *Timer, delta: f32) bool {
        return !self.isFinishedAfterUpdate(delta);
    }

    pub fn isFinishedAfterUpdate(self: *Timer, delta: f32) bool {
        self.update(delta);
        return self.finished;
    }

    pub fn reset(self: *Timer) void {
        self.finished = false;
        self.elapsed = 0;
    }

    pub fn isRunning(self: *const Timer) bool {
        return !self.finished;
    }
};

pub var size: math.Vector = .zero;
var keyState: std.StaticBitSet(512) = .initEmpty();

pub fn event(ev: *const Event) void {
    switch (ev.type) {
        .KEY_DOWN => keyState.set(@intCast(@intFromEnum(ev.key_code))),
        .KEY_UP => keyState.unset(@intCast(@intFromEnum(ev.key_code))),
        else => {},
    }
}

pub fn isKeyDown(keyCode: KeyCode) bool {
    return keyState.isSet(@intCast(@intFromEnum(keyCode)));
}

pub fn isKeyUp(keyCode: KeyCode) bool {
    return !isKeyDown(keyCode);
}

pub fn showCursor(show: bool) void {
    sk.app.showMouse(show);
}

pub fn displayText(x: f32, y: f32, text: [:0]const u8) void {
    sk.debugtext.canvas(sk.app.widthf() * 0.4, sk.app.heightf() * 0.4);
    sk.debugtext.origin(x, y);
    sk.debugtext.home();

    sk.debugtext.font(0);
    sk.debugtext.color3b(0xff, 0xff, 0xff);
    sk.debugtext.puts(text);
}

pub fn endDisplayText() void {
    sk.debugtext.draw();
}

pub fn exit() void {
    sk.app.quit();
}
pub const run = sk.app.run;
pub const KeyCode = sk.app.Keycode;
pub const log = sk.log.func;
```

## main.zig

```zig
const std = @import("std");

const cache = @import("cache.zig");
const window = @import("window.zig");
const math = @import("math.zig");
const gfx = @import("graphics.zig");
const audio = @import("audio.zig");
const scene = @import("scene.zig");

var soundBuffer: [20]audio.Sound = undefined;

export fn init() void {
    cache.init(allocator);
    gfx.init(window.size);
    audio.init(&soundBuffer);

    math.rand = .init(timer.lap());
    scene.init();
}

export fn event(ev: ?*const window.Event) void {
    if (ev) |e| window.event(e);
}

export fn frame() void {
    const delta: f32 = @floatFromInt(timer.lap());
    cache.loading();
    scene.update(delta / std.time.ns_per_s);
    scene.render();
}

export fn deinit() void {
    audio.deinit();
    gfx.deinit();
    cache.deinit();
}

var allocator: std.mem.Allocator = undefined;
var timer: std.time.Timer = undefined;

pub fn main() void {
    var debugAllocator: std.heap.DebugAllocator(.{}) = .init;
    defer _ = debugAllocator.deinit();
    allocator = debugAllocator.allocator();

    window.size = .{ .x = 640, .y = 480 };
    timer = std.time.Timer.start() catch unreachable;

    window.run(.{
        .window_title = "教你制作RPG游戏",
        .width = @as(i32, @intFromFloat(window.size.x)),
        .height = @as(i32, @intFromFloat(window.size.y)),
        .high_dpi = true,
        .init_cb = init,
        .event_cb = event,
        .frame_cb = frame,
        .cleanup_cb = deinit,
        .logger = .{ .func = window.log },
    });
}
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");

const SPEED = 100;
var position: math.Vector = .zero;

pub fn init() void {}

pub fn update(delta: f32) void {
    updatePlayer(delta);
}

fn updatePlayer(delta: f32) void {
    var velocity: math.Vector = .zero;

    if (window.isKeyDown(.UP) or window.isKeyDown(.W))
        velocity.selfAdd(.{ .y = -1 });
    if (window.isKeyDown(.DOWN) or window.isKeyDown(.S))
        velocity.selfAdd(.{ .y = 1 });
    if (window.isKeyDown(.LEFT) or window.isKeyDown(.A))
        velocity.selfAdd(.{ .x = -1 });
    if (window.isKeyDown(.RIGHT) or window.isKeyDown(.D))
        velocity.selfAdd(.{ .x = 1 });

    if (!velocity.approx(.zero)) {
        position.selfAdd(velocity.normalize().scale(delta * SPEED));
    }
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    gfx.draw(gfx.loadTexture("assets/role.png"), position);
}
```

## 效果

![控制主角][1]

[1]: images/教你制作RPG游戏02.webp

## 附录
