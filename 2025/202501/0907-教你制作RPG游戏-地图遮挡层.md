# 0907-教你制作RPG游戏-地图遮挡层

## 目标

有些地图元素应该可以遮挡角色，看起来角色好像是在物体的后面，这样更有层次感。

## 环境

- Time 2025-04-29

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

需要单独提供地图和地图的遮挡层图片，应该有其它更好的实现方式吧。

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const assets = @import("assets.zig");

const Player = @import("Player.zig");
const PLAYER_SPEED = 100;

var players: [3]Player = undefined;
var currentPlayer: *Player = &players[0];
var position: math.Vector = .zero;
var facing: math.FourDirection = .down;
var keyPressed: bool = false;
var velocity: math.Vector = .zero;

var map: gfx.Texture = undefined;
var mapShade: gfx.Texture = undefined;
const MAP_SIZE: math.Vector = .init(1000, 800);
const PLAYER_SIZE: math.Vector = .init(96, 96);

pub fn init() void {
    gfx.camera = .{ .rect = .init(.zero, window.size), .border = MAP_SIZE };
    gfx.camera.lookAt(position);

    players[0] = .init("assets/r1.png", 0);
    players[1] = .init("assets/r2.png", 1);
    players[2] = .init("assets/r3.png", 2);

    map = assets.loadTexture("assets/map1.png", MAP_SIZE);
    mapShade = assets.loadTexture("assets/map1_shade.png", MAP_SIZE);
}

pub fn event(ev: *const window.Event) void {
    if (ev.type == .KEY_UP and ev.key_code == .TAB) {
        currentPlayer = &players[(currentPlayer.index + 1) % players.len];
    }
}

pub fn update(delta: f32) void {
    velocity = .zero;
    keyPressed = false;

    if (window.isKeyDown(.UP) or window.isKeyDown(.W)) updatePlayer(.up);

    if (window.isKeyDown(.DOWN) or window.isKeyDown(.S)) updatePlayer(.down);

    if (window.isKeyDown(.LEFT) or window.isKeyDown(.A)) updatePlayer(.left);

    if (window.isKeyDown(.RIGHT) or window.isKeyDown(.D)) updatePlayer(.right);

    if (velocity.approx(.zero)) {
        currentPlayer.current(facing).reset();
    } else {
        velocity = velocity.normalize().scale(delta * PLAYER_SPEED);
        position = position.add(velocity);
        position = position.clamp(.zero, MAP_SIZE.sub(PLAYER_SIZE));
        gfx.camera.lookAt(position);
    }

    if (keyPressed) currentPlayer.current(facing).update(delta);
}

fn updatePlayer(direction: math.FourDirection) void {
    facing = direction;
    keyPressed = true;
    velocity = velocity.add(direction.toVector());
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    gfx.draw(map, .zero);

    gfx.drawOptions(currentPlayer.current(facing).current(), .{
        .targetRect = .init(position, PLAYER_SIZE),
    });

    gfx.draw(mapShade, .zero);
}
```

## 效果

![地图遮挡层][1]

[1]: images/教你制作RPG游戏12.webp

## 附录
