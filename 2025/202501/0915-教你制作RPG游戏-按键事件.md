# 0915-教你制作RPG游戏-按键事件

## 目标

靠近 NPC 时，按键可以和 NPC 互动。

## 环境

- Time 2025-04-30

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

对 main.zig 和 window.zig 进行了整理，将一些逻辑封装到 window.zig 中。

## main.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const audio = @import("audio.zig");
const scene = @import("scene.zig");

var soundBuffer: [20]audio.Sound = undefined;

fn init() void {
    audio.init(44100 / 4, &soundBuffer);
    scene.init();
}

fn event(ev: *const window.Event) void {
    scene.event(ev);
}

fn update(delta: f32) void {
    scene.update(delta);
}

fn render() void {
    scene.render();
}

fn deinit() void {
    audio.deinit();
}

pub fn main() void {
    var allocator: std.mem.Allocator = undefined;
    var debugAllocator: std.heap.DebugAllocator(.{}) = undefined;
    if (@import("builtin").mode == .Debug) {
        debugAllocator = std.heap.DebugAllocator(.{}).init;
        allocator = debugAllocator.allocator();
    } else {
        allocator = std.heap.c_allocator;
    }

    defer if (@import("builtin").mode == .Debug) {
        _ = debugAllocator.deinit();
    };

    window.run(.{
        .alloc = allocator,
        .title = "教你制作RPG游戏",
        .size = .{ .x = 800, .y = 600 },
        .init = init,
        .event = event,
        .update = update,
        .render = render,
        .deinit = deinit,
    });
}
```

## window.zig

```zig
const std = @import("std");
const sk = @import("sokol");
const math = @import("math.zig");
const assets = @import("assets.zig");
const gfx = @import("graphics.zig");

pub const Event = sk.app.Event;
pub const KeyCode = sk.app.Keycode;

pub const Timer = struct {
    duration: f32,
    elapsed: f32 = 0,

    pub fn init(duration: f32) Timer {
        return Timer{ .duration = duration };
    }

    pub fn update(self: *Timer, delta: f32) void {
        if (self.elapsed < self.duration) self.elapsed += delta;
    }

    pub fn isRunningAfterUpdate(self: *Timer, delta: f32) bool {
        self.update(delta);
        return self.isRunning();
    }

    pub fn isFinishedAfterUpdate(self: *Timer, delta: f32) bool {
        return !self.isRunningAfterUpdate(delta);
    }

    pub fn isRunning(self: *const Timer) bool {
        return self.elapsed < self.duration;
    }

    pub fn reset(self: *Timer) void {
        self.elapsed = 0;
    }
};

pub var lastKeyState: std.StaticBitSet(512) = .initEmpty();
pub var keyState: std.StaticBitSet(512) = .initEmpty();

pub fn isKeyDown(keyCode: KeyCode) bool {
    return keyState.isSet(@intCast(@intFromEnum(keyCode)));
}

pub fn isAnyKeyDown(keys: []const KeyCode) bool {
    for (keys) |key| if (isKeyDown(key)) return true;
    return false;
}

pub fn isAllKeyDown(keys: []const KeyCode) bool {
    for (keys) |key| if (!isKeyDown(key)) return false;
    return true;
}

pub fn isPressed(keyCode: KeyCode) bool {
    const key: usize = @intCast(@intFromEnum(keyCode));
    return !lastKeyState.isSet(key) and keyState.isSet(key);
}

pub fn isRelease(keyCode: KeyCode) bool {
    const key: usize = @intCast(@intFromEnum(keyCode));
    return lastKeyState.isSet(key) and !keyState.isSet(key);
}

pub fn showCursor(show: bool) void {
    sk.app.showMouse(show);
}

pub const WindowInfo = struct {
    title: [:0]const u8,
    size: math.Vector,
    alloc: std.mem.Allocator,
    init: ?*const fn () void = null,
    update: ?*const fn (delta: f32) void = null,
    render: ?*const fn () void = null,
    event: ?*const fn (*const Event) void = null,
    deinit: ?*const fn () void = null,
};

pub var size: math.Vector = .zero;
pub var allocator: std.mem.Allocator = undefined;
var timer: std.time.Timer = undefined;
var windowInfo: WindowInfo = undefined;

pub fn run(info: WindowInfo) void {
    timer = std.time.Timer.start() catch unreachable;
    size = info.size;
    allocator = info.alloc;
    windowInfo = info;
    sk.app.run(.{
        .window_title = info.title,
        .width = @as(i32, @intFromFloat(size.x)),
        .height = @as(i32, @intFromFloat(size.y)),
        .high_dpi = true,
        .init_cb = windowInit,
        .event_cb = windowEvent,
        .frame_cb = windowFrame,
        .cleanup_cb = windowDeinit,
    });
}

export fn windowInit() void {
    assets.init(allocator);
    gfx.init(size);

    if (windowInfo.init) |init| init();
    math.setRandomSeed(timer.lap());
}

export fn windowEvent(event: ?*const Event) void {
    if (event) |ev| {
        const code: usize = @intCast(@intFromEnum(ev.key_code));
        switch (ev.type) {
            .KEY_DOWN => keyState.set(code),
            .KEY_UP => keyState.unset(code),
            else => {},
        }
        if (windowInfo.event) |eventHandle| eventHandle(ev);
    }
}

export fn windowFrame() void {
    assets.loading();
    const delta: f32 = @floatFromInt(timer.lap());
    if (windowInfo.update) |update| update(delta / std.time.ns_per_s);
    if (windowInfo.render) |render| render();

    lastKeyState = keyState;
}

export fn windowDeinit() void {
    if (windowInfo.deinit) |deinit| deinit();
    gfx.deinit();
    assets.deinit();
}

pub fn displayText(x: f32, y: f32, text: [:0]const u8) void {
    sk.debugtext.canvas(sk.app.widthf() * 0.4, sk.app.heightf() * 0.4);
    sk.debugtext.origin(x, y);
    sk.debugtext.home();

    sk.debugtext.font(0);
    sk.debugtext.color3b(0xff, 0xff, 0xff);
    sk.debugtext.puts(text);
}

pub fn endDisplayText() void {
    sk.debugtext.draw();
}

pub fn exit() void {
    sk.app.requestQuit();
}
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const assets = @import("assets.zig");

const Player = @import("Player.zig");
const map = @import("map.zig");
const PLAYER_SPEED = 150;
const PLAYER_OFFSET: math.Vector = .init(120, 220);

var players: [3]Player = undefined;
var currentPlayer: *Player = &players[0];
var position: math.Vector = .init(800, 500);
var facing: math.FourDirection = .down;
var keyPressed: bool = false;
var velocity: math.Vector = .zero;

pub fn init() void {
    gfx.camera = .{ .rect = .init(.zero, window.size), .border = map.SIZE };
    gfx.camera.lookAt(position);

    players[0] = .init("assets/r1.png", 0);
    players[1] = .init("assets/r2.png", 1);
    players[2] = .init("assets/r3.png", 2);

    map.init();
}

pub fn event(ev: *const window.Event) void {
    if (ev.type == .KEY_UP and ev.key_code == .TAB) {
        currentPlayer = &players[(currentPlayer.index + 1) % players.len];
    }
}

pub fn update(delta: f32) void {
    velocity = .zero;
    keyPressed = false;

    if (window.isAnyKeyDown(&.{ .UP, .W })) updatePlayer(.up);
    if (window.isAnyKeyDown(&.{ .DOWN, .S })) updatePlayer(.down);
    if (window.isAnyKeyDown(&.{ .LEFT, .A })) updatePlayer(.left);
    if (window.isAnyKeyDown(&.{ .RIGHT, .D })) updatePlayer(.right);

    if (velocity.approx(.zero)) {
        currentPlayer.current(facing).reset();
    } else {
        velocity = velocity.normalize().scale(delta * PLAYER_SPEED);
        const tempPosition = position.add(velocity);
        if (map.canWalk(tempPosition)) position = tempPosition;
        gfx.camera.lookAt(position);
    }

    if (keyPressed) currentPlayer.current(facing).update(delta);

    if (window.isPressed(.SPACE)) {
        for (map.npcSlice()) |*npc| {
            if (npc.area.contains(position)) npc.action();
        }
    }
}

fn updatePlayer(direction: math.FourDirection) void {
    facing = direction;
    keyPressed = true;
    velocity = velocity.add(direction.toVector());
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    map.drawBackground();

    var playerNotDraw: bool = true;
    for (map.npcSlice()) |npc| {
        if (npc.position.y > position.y and playerNotDraw) {
            drawPlayer();
            playerNotDraw = false;
        }

        if (npc.texture) |texture| {
            gfx.draw(texture, npc.position.sub(PLAYER_OFFSET));
        }

        gfx.drawRectangle(npc.area);
    }

    if (playerNotDraw) drawPlayer();

    map.drawForeground();
}

fn drawPlayer() void {
    const playerTexture = currentPlayer.current(facing).currentTexture();
    gfx.draw(playerTexture, position.sub(PLAYER_OFFSET));
}
```

## 效果

![按键事件][1]

[1]: images/教你制作RPG游戏19.webp

## 附录
