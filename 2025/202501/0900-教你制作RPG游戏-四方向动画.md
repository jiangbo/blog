# 0900-教你制作RPG游戏-四方向动画

## 目标

前一节只显示每个动画的第一张图片，这里根据时间切换显示后面的图片，形成动画。

## 环境

- Time 2025-04-28

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

根据时间不停切换显示的图片，这个就是帧动画吧。

## animation.zig

之前有切片和图集动画，这里删除了之前的，定义了一个帧动画。

```zig
const std = @import("std");

const window = @import("window.zig");
const cache = @import("cache.zig");
const math = @import("math.zig");
const Texture = @import("gpu.zig").Texture;

pub const FrameAnimation = struct {
    timer: window.Timer = .init(0.1),
    index: usize = 0,
    loop: bool = true,
    texture: Texture,
    frames: []const math.Rectangle,
    offset: math.Vector = .zero,

    pub fn init(name: []const u8, texture: Texture, count: u8) FrameAnimation {
        const frames = cache.RectangleSlice.load(name, count);

        const width = @divExact(texture.width(), @as(f32, @floatFromInt(count)));
        const size: math.Vector = .{ .x = width, .y = texture.height() };

        for (0..frames.len) |index| {
            const x = @as(f32, @floatFromInt(index)) * width;
            frames[index] = .init(.init(x, texture.area.min.y), size);
        }

        return .{ .texture = texture, .frames = frames };
    }

    pub fn current(self: *const FrameAnimation) Texture {
        return self.texture.map(self.frames[self.index]);
    }

    pub fn update(self: *FrameAnimation, delta: f32) void {
        if (self.timer.isRunningAfterUpdate(delta)) return;

        if (self.index == self.frames.len - 1) {
            if (self.loop) self.reset();
        } else {
            self.timer.reset();
            self.index += 1;
        }
    }

    pub fn anchor(self: *FrameAnimation, direction: math.EightDirection) void {
        const tex = self.texture;
        self.offset = switch (direction) {
            .down => .{ .x = -tex.width() / 2, .y = -tex.height() },
            else => unreachable,
        };
    }

    pub fn anchorCenter(self: *FrameAnimation) void {
        self.offset.x = -self.texture.width() / 2;
        self.offset.y = -self.texture.height() / 2;
    }

    pub fn reset(self: *FrameAnimation) void {
        self.timer.reset();
        self.index = 0;
    }

    pub fn finished(self: *const FrameAnimation) bool {
        return self.timer.finished and !self.loop;
    }
};
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const cache = @import("cache.zig");

const SPEED = 100;
var position: math.Vector = .zero;
var facing: math.FourDirection = .up;

var upAnimation: gfx.FrameAnimation = undefined;
var downAnimation: gfx.FrameAnimation = undefined;
var leftAnimation: gfx.FrameAnimation = undefined;
var rightAnimation: gfx.FrameAnimation = undefined;

var roleTexture: gfx.Texture = undefined;

pub fn init() void {
    roleTexture = cache.loadTexture("assets/r1.png", .init(960, 960));

    const size: math.Vector = .init(960, 240);
    const upTexture = roleTexture.sub(.init(.{ .y = 720 }, size));
    upAnimation = .init("up", upTexture, 4);
    upAnimation.timer = .init(0.25);

    const downTexture = roleTexture.sub(.init(.{ .y = 0 }, size));
    downAnimation = .init("down", downTexture, 4);
    downAnimation.timer = .init(0.25);

    const leftTexture = roleTexture.sub(.init(.{ .y = 240 }, size));
    leftAnimation = .init("left", leftTexture, 4);
    leftAnimation.timer = .init(0.25);

    const rightTexture = roleTexture.sub(.init(.{ .y = 480 }, size));
    rightAnimation = .init("right", rightTexture, 4);
    rightAnimation.timer = .init(0.25);
}

pub fn update(delta: f32) void {
    updatePlayer(delta);

    switch (facing) {
        .up => upAnimation.update(delta),
        .down => downAnimation.update(delta),
        .left => leftAnimation.update(delta),
        .right => rightAnimation.update(delta),
    }
}

fn updatePlayer(delta: f32) void {
    var velocity: math.Vector = .zero;

    if (window.isKeyDown(.UP) or window.isKeyDown(.W)) {
        facing = .up;
        velocity = velocity.add(facing.toVector());
    }

    if (window.isKeyDown(.DOWN) or window.isKeyDown(.S)) {
        facing = .down;
        velocity = velocity.add(facing.toVector());
    }

    if (window.isKeyDown(.LEFT) or window.isKeyDown(.A)) {
        facing = .left;
        velocity = velocity.add(facing.toVector());
    }

    if (window.isKeyDown(.RIGHT) or window.isKeyDown(.D)) {
        facing = .right;
        velocity = velocity.add(facing.toVector());
    }

    if (!velocity.approx(.zero)) {
        velocity = velocity.normalize().scale(delta * SPEED);
        position = position.add(velocity);
    }
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    switch (facing) {
        .up => gfx.draw(upAnimation.current(), position),
        .down => gfx.draw(downAnimation.current(), position),
        .left => gfx.draw(leftAnimation.current(), position),
        .right => gfx.draw(rightAnimation.current(), position),
    }
}
```

## 效果

![四方向动画][1]

[1]: images/教你制作RPG游戏05.webp

## 附录
