# 0911-教你制作RPG游戏-播放背景音乐

## 目标

完善声音部分，播放背景音乐，之前是同步加载的，修改成异步加载音乐。

## 环境

- Time 2025-04-29

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

音效部分暂时还没有处理，将音乐加载部分修改成异步加载。

## assets.zig

```zig
const std = @import("std");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const c = @import("c.zig");
const sk = @import("sokol");

var allocator: std.mem.Allocator = undefined;

pub fn init(alloc: std.mem.Allocator) void {
    allocator = alloc;
    sk.fetch.setup(.{ .logger = .{ .func = sk.log.func } });
}

pub fn deinit() void {
    Texture.cache.deinit(allocator);
    Sound.deinit();
    String.deinit();
    sk.fetch.shutdown();
}

pub fn loading() void {
    sk.fetch.dowork();
}

var loadingBuffer: [1.5 * 1024 * 1024]u8 = undefined;

const SkCallback = *const fn ([*c]const sk.fetch.Response) callconv(.C) void;
const Response = struct { path: [:0]const u8, data: []const u8 };

fn send(path: [:0]const u8, cb: SkCallback) void {
    std.log.info("loading {s}", .{path});

    const buffer = sk.fetch.asRange(&loadingBuffer);
    _ = sk.fetch.send(.{ .path = path, .callback = cb, .buffer = buffer });
}

fn extractResponses(responses: [*c]const sk.fetch.Response) Response {
    const res = responses[0];
    if (res.failed) {
        std.debug.panic("assets load failed, path: {s}", .{res.path});
    }

    const data: [*]const u8 = @ptrCast(res.data.ptr);
    const path = std.mem.span(res.path);
    std.log.info("loaded from: {s}", .{path});
    return .{ .path = path, .data = data[0..res.data.size] };
}

pub fn loadTexture(path: [:0]const u8, size: math.Vector) gfx.Texture {
    return Texture.load(path, size);
}

pub const Texture = struct {
    var cache: std.StringHashMapUnmanaged(gfx.Texture) = .empty;

    pub fn load(path: [:0]const u8, size: math.Vector) gfx.Texture {
        const entry = cache.getOrPut(allocator, path) catch unreachable;
        if (entry.found_existing) return entry.value_ptr.*;

        send(path, callback);

        const image = sk.gfx.allocImage();
        entry.value_ptr.* = .{ .image = image, .area = .init(.zero, size) };
        return entry.value_ptr.*;
    }

    fn callback(responses: [*c]const sk.fetch.Response) callconv(.C) void {
        const response = extractResponses(responses);
        const data = response.data;

        const image = c.stbImage.loadFromMemory(data) catch unreachable;
        defer c.stbImage.unload(image);
        const texture = cache.getPtr(response.path).?;

        sk.gfx.initImage(texture.image, .{
            .width = image.width,
            .height = image.height,
            .data = init: {
                var imageData = sk.gfx.ImageData{};
                imageData.subimage[0][0] = sk.gfx.asRange(image.data);
                break :init imageData;
            },
        });
    }
};

const audio = @import("audio.zig");
pub const Sound = struct {
    var cache: std.StringHashMapUnmanaged(audio.Sound) = .empty;

    pub fn load(path: [:0]const u8) audio.Sound {
        const entry = cache.getOrPut(allocator, path) catch unreachable;
        if (entry.found_existing) return entry.value_ptr.*;

        send(path, callback);

        entry.value_ptr.* = .{ .source = undefined };
        entry.key_ptr.* = path;

        return entry.value_ptr.*;
    }

    fn callback(responses: [*c]const sk.fetch.Response) callconv(.C) void {
        const response = extractResponses(responses);
        const data = response.data;

        const stbAudio = c.stbAudio.loadFromMemory(data) catch unreachable;
        const info = c.stbAudio.getInfo(stbAudio);

        var sound = cache.getPtr(response.path).?;

        sound.channels = @intCast(info.channels);
        sound.sampleRate = @intCast(info.sample_rate);

        const size = c.stbAudio.getSampleCount(stbAudio) * sound.channels;
        sound.source = allocator.alloc(f32, size) catch unreachable;

        _ = c.stbAudio.fillSamples(stbAudio, sound.source, sound.channels);
        sound.valid = true;
    }

    pub fn deinit() void {
        var iterator = cache.valueIterator();
        while (iterator.next()) |value| allocator.free(value.source);
        cache.deinit(allocator);
    }
};

pub const String = struct {
    var cache: std.StringHashMapUnmanaged(StringCallback) = .empty;
    const Callback = *const fn ([]const u8) void;
    const StringCallback = struct { data: []const u8, callback: Callback };

    pub fn load(path: [:0]const u8, cb: Callback) void {
        const entry = cache.getOrPut(allocator, path) catch unreachable;
        if (entry.found_existing) return cb(entry.value_ptr.*.data);

        entry.value_ptr.* = .{ .data = &.{}, .callback = cb };
        send(path, callback);
    }

    fn callback(responses: [*c]const sk.fetch.Response) callconv(.C) void {
        const response = extractResponses(responses);
        const data = allocator.dupe(u8, response.data) catch unreachable;
        const value = cache.getPtr(response.path).?;
        value.data = data;
        value.callback(data);
    }

    pub fn deinit() void {
        var iterator = cache.valueIterator();
        while (iterator.next()) |value| allocator.free(value.data);
        cache.deinit(allocator);
    }
};
```

## map.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const assets = @import("assets.zig");
const c = @import("c.zig");

pub const SIZE: math.Vector = .init(1000, 800);

var map: gfx.Texture = undefined;
var mapShade: gfx.Texture = undefined;
var mapBack: gfx.Texture = undefined;
var mapBlock: ?std.StaticBitSet(SIZE.x * SIZE.y) = null;

pub fn init() void {
    map = assets.loadTexture("assets/map1.png", SIZE);
    mapShade = assets.loadTexture("assets/map1_shade.png", SIZE);
    mapBack = assets.loadTexture("assets/map1_back.png", SIZE);

    _ = assets.String.load("assets/map1_block.png", callback);

    audio.playMusic("assets/1.ogg");
}

pub fn canWalk(pos: math.Vector) bool {
    const x, const y = .{ @round(pos.x), @round(pos.y) };

    if (x < 0 or x >= SIZE.x or y < 0 or y >= SIZE.y) return false;
    if (mapBlock) |block| {
        return !block.isSet(@intFromFloat(x + y * SIZE.x));
    } else return false;
}

fn callback(buffer: []const u8) void {
    const image = c.stbImage.loadFromMemory(buffer) catch unreachable;
    defer c.stbImage.unload(image);

    const data: []const u32 = @ptrCast(@alignCast(image.data));
    std.debug.assert(data.len == SIZE.x * SIZE.y);

    var blocks: std.StaticBitSet(SIZE.x * SIZE.y) = .initEmpty();
    for (data, 0..) |color, index| {
        if (color == 0xFF000000) blocks.set(index);
    }
    mapBlock = blocks;
}

pub fn drawBackground() void {
    gfx.draw(mapBack, .zero);
    gfx.draw(map, .zero);
}

pub fn drawForeground() void {
    gfx.draw(mapShade, .zero);
}
```

## audio.zig

```zig
const std = @import("std");
const sk = @import("sokol");
const assets = @import("assets.zig");
const c = @import("c.zig");

pub fn init(soundBuffer: []Sound) void {
    sk.audio.setup(.{
        .num_channels = 2,
        .stream_cb = callback,
        .logger = .{ .func = sk.log.func },
    });
    sounds = soundBuffer;
}

pub fn deinit() void {
    stopMusic();
    for (sounds) |*sound| sound.valid = false;
    sk.audio.shutdown();
}

pub const Music = struct {
    path: [:0]const u8 = &.{},
    source: *c.stbAudio.Audio = undefined,
    paused: bool = false,
    loop: bool = true,
    valid: bool = false,
};

pub var music: ?Music = null;

pub fn playMusic(path: [:0]const u8) void {
    doPlayMusic(path, true);
}

pub fn playMusicOnce(path: [:0]const u8) void {
    doPlayMusic(path, false);
}

fn doPlayMusic(path: [:0]const u8, loop: bool) void {
    stopMusic();
    music = .{ .loop = loop };
    assets.String.load(path, musicCallback);
}

fn musicCallback(data: []const u8) void {
    const stbAudio = c.stbAudio.loadFromMemory(data);
    if (music) |*m| {
        m.source = stbAudio catch unreachable;
        m.valid = true;
    }
}

pub fn pauseMusic() void {
    if (music) |*value| value.paused = true;
}

pub fn resumeMusic() void {
    if (music) |*value| value.paused = false;
}

pub fn stopMusic() void {
    music = null;
}

var sounds: []Sound = &.{};

pub const Sound = struct {
    source: []f32,
    valid: bool = false,
    loop: bool = true,
    index: usize = 0,
    sampleRate: u16 = 0,
    channels: u8 = 0,
};
pub const SoundIndex = usize;

pub fn playSound(path: [:0]const u8) void {
    _ = doPlaySound(path, false);
}

pub fn playSoundLoop(path: [:0]const u8) SoundIndex {
    return doPlaySound(path, true);
}

pub fn stopSound(sound: SoundIndex) void {
    sounds[sound].valid = false;
}

fn doPlaySound(path: [:0]const u8, loop: bool) SoundIndex {
    var sound = assets.Sound.load(path);
    sound.loop = loop;

    return addItem(sounds, sound);
}

fn addItem(slice: anytype, item: anytype) usize {
    for (slice, 0..) |*value, index| {
        if (!value.valid) {
            value.* = item;
            return index;
        }
    }
    @panic("too many audio sound");
}

export fn callback(b: [*c]f32, frames: i32, channels: i32) void {
    const buffer = b[0..@as(usize, @intCast(frames * channels))];
    @memset(buffer, 0);
    {
        if (music) |m| blk: {
            if (m.paused or !m.valid) break :blk;
            const count = c.stbAudio.fillSamples(m.source, buffer, channels);
            if (count == 0) {
                if (m.loop) c.stbAudio.reset(m.source) else music = null;
            }
        }
    }

    for (sounds) |*sound| {
        if (!sound.valid) continue;
        var len = mixSamples(buffer, sound);
        while (len < buffer.len and sound.valid) {
            len += mixSamples(buffer[len..], sound);
        }
    }
}

fn mixSamples(buffer: []f32, sound: *Sound) usize {
    const len = if (sound.channels == 1)
        mixMonoSamples(buffer, sound)
    else if (sound.channels == 2)
        mixStereoSamples(buffer, sound)
    else
        std.debug.panic("unsupported channels: {d}", .{sound.channels});

    if (sound.index == sound.source.len) {
        if (sound.loop) sound.index = 0 else sound.valid = false;
    }

    return len;
}

fn mixStereoSamples(dstBuffer: []f32, sound: *Sound) usize {
    const srcBuffer = sound.source[sound.index..];
    const len = @min(dstBuffer.len, srcBuffer.len);

    for (0..len) |index| dstBuffer[index] += srcBuffer[index];
    sound.index += len;
    return len;
}

fn mixMonoSamples(dstBuffer: []f32, sound: *Sound) usize {
    const srcBuffer = sound.source[sound.index..];
    const len = @min(dstBuffer.len / 2, srcBuffer.len);

    for (0..len) |index| {
        dstBuffer[index * 2] += srcBuffer[index];
        dstBuffer[index * 2 + 1] += srcBuffer[index];
    }
    sound.index += len;
    return len * 2;
}
```

## 效果

声音播放就不展示效果了。

## 附录
