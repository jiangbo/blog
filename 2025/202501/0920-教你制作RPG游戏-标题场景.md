# 0920-教你制作RPG游戏-标题场景

## 目标

之前已经有一个游戏场景了，需要新增一个标题场景。

## 环境

- Time 2025-05-08

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

先把之前的场景单独提取到一个 world.zig 代表世界的场景中，然后新增一个 title.zig 场景。
提取后，先保证标题场景能显示，世界场景暂停还有点问题，可以先不管，后面来修改。

## 世界场景

先将之前 scene.zig 场景中的内容移动到新增 world.zig 文件中。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

const Player = @import("Player.zig");
const map = @import("map.zig");
const PLAYER_SPEED = 150;
const PLAYER_OFFSET: gfx.Vector = .init(120, 220);

var players: [3]Player = undefined;
var currentPlayer: *Player = &players[0];
pub var position: gfx.Vector = .init(100, 500);
var facing: gfx.FourDirection = .down;
var keyPressed: bool = false;
var velocity: gfx.Vector = .zero;

pub fn init() void {
    gfx.camera = .{ .rect = .init(.zero, window.size), .border = map.SIZE };
    gfx.camera.lookAt(position);

    players[0] = .init("assets/r1.png", 0);
    players[1] = .init("assets/r2.png", 1);
    players[2] = .init("assets/r3.png", 2);

    map.init();
}

pub fn update(delta: f32) void {
    velocity = .zero;
    keyPressed = false;

    if (window.isAnyKeyDown(&.{ .UP, .W })) updatePlayer(.up);
    if (window.isAnyKeyDown(&.{ .DOWN, .S })) updatePlayer(.down);
    if (window.isAnyKeyDown(&.{ .LEFT, .A })) updatePlayer(.left);
    if (window.isAnyKeyDown(&.{ .RIGHT, .D })) updatePlayer(.right);

    if (window.isRelease(.TAB)) {
        currentPlayer = &players[(currentPlayer.index + 1) % players.len];
    }

    if (velocity.approx(.zero)) {
        currentPlayer.current(facing).reset();
    } else {
        velocity = velocity.normalize().scale(delta * PLAYER_SPEED);
        const tempPosition = position.add(velocity);
        if (map.canWalk(tempPosition)) position = tempPosition;
        gfx.camera.lookAt(position);
    }

    if (keyPressed) currentPlayer.current(facing).update(delta);

    for (map.npcSlice()) |*npc| {
        if (npc.area.contains(position)) {
            if (npc.keyTrigger) {
                if (window.isRelease(.SPACE)) npc.action();
            } else npc.action();
        }

        map.updateNpc(npc, delta);
    }
}

fn updatePlayer(direction: gfx.FourDirection) void {
    facing = direction;
    keyPressed = true;
    velocity = velocity.add(direction.toVector());
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    map.drawBackground();

    var playerNotDraw: bool = true;
    for (map.npcSlice()) |npc| {
        if (npc.position.y > position.y and playerNotDraw) {
            drawPlayer();
            playerNotDraw = false;
        }

        const npcPosition = npc.position.sub(PLAYER_OFFSET);

        if (npc.animation != null and !npc.animation.?.finished()) {
            gfx.draw(npc.animation.?.currentTexture(), npcPosition);
        } else if (npc.texture) |texture| {
            gfx.draw(texture, npcPosition);
        }

        gfx.drawRectangle(npc.area);
    }

    if (playerNotDraw) drawPlayer();

    map.drawForeground();

    window.showFrameRate();
}

fn drawPlayer() void {
    const playerTexture = currentPlayer.current(facing).currentTexture();
    gfx.draw(playerTexture, position.sub(PLAYER_OFFSET));
}
```

## title.zig

新增一个标题场景，先画出一张背景图，其它的都可以先不做。

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

var background1: gfx.Texture = undefined;

pub fn init() void {
    background1 = gfx.loadTexture("assets/T_bg1.png", .init(800, 600));
}

pub fn update(delta: f32) void {
    std.log.info("update title", .{});
    _ = delta;
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    gfx.draw(background1, .zero);
}
```

## scene.zig

原先的场景就变成了一个场景的管理器，用来协调所有的场景。
先不要初始化 world 场景，会导致相机偏移，后面来解决。

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");

const titleScene = @import("scene/title.zig");
const worldScene = @import("scene/world.zig");

const SceneType = enum { title, world };

var currentSceneType: SceneType = .title;

pub fn init() void {
    titleScene.init();
    // worldScene.init();
}

pub fn update(delta: f32) void {
    switch (currentSceneType) {
        .title => titleScene.update(delta),
        .world => worldScene.update(delta),
    }
}

pub fn render() void {
    switch (currentSceneType) {
        .title => titleScene.render(),
        .world => worldScene.render(),
    }
}
```

## 效果

![标题场景][1]

[1]: images/教你制作RPG游戏24.webp

## 附录
