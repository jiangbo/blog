# 0921-教你制作RPG游戏-进入和退出场景

## 目标

场景除了初始化，应该还有一个进入场景和退出场景的方法。

## 环境

- Time 2025-05-09

## 参考

1. <https://www.bilibili.com/video/BV1eB4y197zi>

## 想法

进入和退出场景是参考的植物明星大乱斗的想法，不过去掉了使用联合的那种方式。

## 场景进入和退出

新增了两个方法，一个进入，一个退出。并且放开了 world 场景的初始化，需要解决一下相机的问题。

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");

const titleScene = @import("scene/title.zig");
const worldScene = @import("scene/world.zig");

const SceneType = enum { title, world };

var currentSceneType: SceneType = .title;

pub fn init() void {
    titleScene.init();
    worldScene.init();
    enter();
}

pub fn enter() void {
    sceneCall("enter", .{});
}

pub fn exit() void {
    sceneCall("exit", .{});
}

pub fn update(delta: f32) void {
    if (window.isKeyPress(.SPACE)) {
        exit();
        const next: usize = @intFromEnum(currentSceneType);
        const len = std.enums.values(SceneType).len;
        currentSceneType = @enumFromInt((next + 1) % len);
        enter();
    }
    sceneCall("update", .{delta});
}

pub fn render() void {
    sceneCall("render", .{});
}

fn sceneCall(comptime function: []const u8, args: anytype) void {
    switch (currentSceneType) {
        .title => @call(.auto, @field(titleScene, function), args),
        .world => @call(.auto, @field(worldScene, function), args),
    }
}
```

## 标题场景

增加了一个进入场景播放音乐，退出场景停止播放音乐的功能。2.ogg 我以为是地图的音乐，好像是标题的？

```zig
const std = @import("std");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

var background1: gfx.Texture = undefined;

pub fn init() void {
    background1 = gfx.loadTexture("assets/T_bg1.png", .init(800, 600));
}

pub fn enter() void {
    window.playMusic("assets/2.ogg");
}

pub fn exit() void {
    window.stopMusic();
}

pub fn update(delta: f32) void {
    std.log.info("update title", .{});
    _ = delta;
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    gfx.draw(background1, .zero);
}
```

## 世界场景

把相机的跟随逻辑从初始化移动到了进入，退出时重置相机，解决了标题画面偏移的问题。
进入世界场景开始播放音乐1，退出的时候停止播放。然后把地图中的播放音乐的逻辑删除了。

```zig
...
pub fn init() void {
    players[0] = .init("assets/r1.png", 0);
    players[1] = .init("assets/r2.png", 1);
    players[2] = .init("assets/r3.png", 2);

    map.init();
}

pub fn enter() void {
    gfx.camera = .{ .rect = .init(.zero, window.size), .border = map.SIZE };
    gfx.camera.lookAt(position);
    window.playMusic("assets/1.ogg");
}

pub fn exit() void {
    gfx.camera.lookAt(.zero);
    window.stopMusic();
}
...
```

## 效果

![进入和退出场景][1]

[1]: images/教你制作RPG游戏25.webp

## 附录
