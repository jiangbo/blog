# 1096-幽灵逃生-死亡画面

## 目标

角色死亡时，将右下角的菜单移动到屏幕中间来，并且去掉了一个菜单。

## 环境

- Time 2026-01-15
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1jf9XYQEhW>

## 想法

重构了菜单功能，敌人的动画也正常了，角色死亡后，敌人不再进行移动。

## menu.zig

将菜单功能单独放在了一个模块中。

```zig
const std = @import("std");
const zhu = @import("zhu");

const camera = zhu.camera;

const Button = struct {
    offset: zhu.Vector2,
    size: zhu.Vector2,
    normal: zhu.graphics.ImageId,
    hover: zhu.graphics.ImageId,
    pressed: zhu.graphics.ImageId,
    event: u8,
};

const Menu = struct {
    position: zhu.Vector2,
    offset: zhu.Vector2,
    buttonSize: zhu.Vector2,
    buttons: []const Button,
};

const ButtonState = enum { normal, hover, pressed };

const menus: []const Menu = @import("zon/menu.zon");
// pub var menus: []const Menu = &.{};
pub var menuIndex: u8 = 0;
var buttonIndex: ?usize = null;
var buttonState: ButtonState = .normal;

pub fn reload() void {
    const parseZon = std.zon.parse.fromSlice;
    const content = zhu.window.readAll("ghost/zon/menu.zon") catch unreachable;
    defer zhu.window.free(content);
    const c = zhu.window.allocator.dupeZ(u8, content) catch unreachable;
    defer zhu.window.free(c);
    const npc = parseZon([]Menu, zhu.window.allocator, c, null, .{});
    zhu.window.free(menus);
    menus = npc catch @panic("error parse zon");
}

pub fn update() ?u8 {
    const menu = menus[menuIndex];
    const mousePos = zhu.window.mousePosition;
    const position = menu.position;

    for (menu.buttons, 0..menu.buttons.len) |button, i| {
        const index: f32 = @floatFromInt(i);
        const pos = position.add(menu.offset.scale(index));
        const buttonPos = pos.add(button.offset);
        const buttonArea = zhu.Rect.init(buttonPos, button.size);

        const hover = buttonArea.contains(mousePos);
        const press = zhu.window.isMouseDown(.LEFT);
        if (hover) {
            if (buttonIndex == null) {
                // 刚刚进入悬停状态，播放音效
                zhu.audio.playSound("assets/sound/UI_button12.ogg");
            }
            if (zhu.window.isMouseRelease(.LEFT)) return button.event;
            buttonIndex = i;
            buttonState = if (press) .pressed else .hover;
            break;
        } else if (!press) {
            buttonState = .normal;
        }
    } else if (buttonState != .pressed) buttonIndex = null;

    return null;
}

pub fn draw() void {
    const menu = menus[menuIndex];
    const position = menu.position;
    for (menu.buttons, 0..menu.buttons.len) |button, i| {
        const index: f32 = @floatFromInt(i);
        const pos = position.add(menu.offset.scale(index));

        const buttonPos = pos.add(button.offset);
        const area = zhu.Rect.init(buttonPos, button.size);
        camera.drawRectBorder(area, 1, .green);

        if (i == buttonIndex and buttonState != .normal) {
            if (buttonState == .pressed) {
                camera.drawOption(button.pressed, pos, .{
                    .size = menu.buttonSize,
                });
            } else if (buttonState == .hover) {
                camera.drawOption(button.hover, pos, .{
                    .size = menu.buttonSize,
                });
            }
        } else {
            camera.drawOption(button.normal, pos, .{
                .size = menu.buttonSize,
            });
        }
    }
}
```

## 效果

![死亡画面][1]

[1]: images/幽灵逃生27.png

## 附录
