# 1093-幽灵逃生-授权信息页面

## 目标

显示游戏资产的授权信息，并且增加了按钮的相关音效。

## 环境

- Time 2026-01-14
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1jf9XYQEhW>

## 想法

现在还没有封装的思想，就是把所有的功能糊到一块。把按钮的尺寸显示出来了，感觉太大了。

## title.zig

实现授权显示页面。

```zig
const std = @import("std");
const zhu = @import("zhu");

const camera = zhu.camera;

const scene = @import("scene.zig");

const background = zhu.graphics.imageId("UI/Textfield_01.png");

const Button = struct {
    normal: zhu.graphics.ImageId,
    hover: zhu.graphics.ImageId,
    pressed: zhu.graphics.ImageId,
};

const ButtonState = enum { normal, hover, pressed };

const buttonSize = zhu.Vector2.xy(192, 64);
const buttonPosition = zhu.Vector2.xy(350, 500);
const buttons: [3]Button = .{
    Button{
        .normal = zhu.graphics.imageId("UI/A_Start1.png"),
        .hover = zhu.graphics.imageId("UI/A_Start2.png"),
        .pressed = zhu.graphics.imageId("UI/A_Start3.png"),
    },
    Button{
        .normal = zhu.graphics.imageId("UI/A_Credits1.png"),
        .hover = zhu.graphics.imageId("UI/A_Credits2.png"),
        .pressed = zhu.graphics.imageId("UI/A_Credits3.png"),
    },
    Button{
        .normal = zhu.graphics.imageId("UI/A_Quit1.png"),
        .hover = zhu.graphics.imageId("UI/A_Quit2.png"),
        .pressed = zhu.graphics.imageId("UI/A_Quit3.png"),
    },
};
var buttonIndex: ?usize = null;
var buttonState: ButtonState = .normal;
const creditsText = @embedFile("zon/credits.txt");
var showCredits: bool = false;

pub fn init() void {}

pub fn deinit() void {}

var time: f32 = 0;
pub fn update(delta: f32) void {
    time += delta;

    if (showCredits) {
        if (zhu.window.isAnyRelease()) showCredits = false;
        return;
    }

    const mousePos = zhu.window.mousePosition;

    for (0..buttons.len) |i| {
        const index: f32 = @floatFromInt(i);
        const pos = buttonPosition.addX(index * 200);
        const buttonArea = zhu.Rect.init(pos, buttonSize);

        const hover = buttonArea.contains(mousePos);
        const press = zhu.window.isMouseDown(.LEFT);
        if (hover) {
            if (buttonIndex == null) {
                // 刚刚进入悬停状态，播放音效
                zhu.audio.playSound("assets/sound/UI_button12.ogg");
            }
            if (zhu.window.isMouseRelease(.LEFT)) triggerButton(i);
            buttonIndex = i;
            buttonState = if (press) .pressed else .hover;
            break;
        } else if (!press) {
            buttonState = .normal;
        }
    } else if (buttonState != .pressed) buttonIndex = null;
}

fn triggerButton(index: usize) void {
    // 播放点击音效
    zhu.audio.playSound("assets/sound/UI_button08.ogg");
    switch (index) {
        0 => scene.isTitleScene = false, // 开始游戏
        1 => showCredits = !showCredits, // 显示版权信息
        2 => zhu.window.exit(), // 退出游戏
        else => unreachable,
    }
}

pub fn draw() void {

    // 边框
    var size = zhu.window.logicSize.sub(.xy(60, 60));
    camera.drawRectBorder(.init(.xy(30, 30), size), 10, .{
        .r = zhu.math.sinInt(u8, time * 0.9, 100, 255),
        .g = zhu.math.sinInt(u8, time * 0.8, 100, 255),
        .b = zhu.math.sinInt(u8, time * 0.7, 100, 255),
        .a = 255,
    });

    // 标题
    const basicPos = zhu.Vector2.xy(320, 100); // 定位位置
    size = zhu.window.logicSize.div(.xy(2, 3));

    // 先绘制图片，再绘制文字，减少批量绘制次数
    camera.drawOption(background, basicPos, .{ .size = size });
    if (showCredits) {
        const creditsSize = zhu.Vector2.xy(555, 600);
        const creditsPos = basicPos.addXY(45, -40);
        camera.drawOption(background, creditsPos, .{ .size = creditsSize });
        zhu.text.drawOption(creditsText, creditsPos.addXY(20, 40), .{
            .size = 16,
        });
        return;
    }
    drawButton();
    camera.drawOption(background, basicPos.addXY(220, 285), .{
        .size = .xy(200, 60),
    });

    var pos = basicPos.addXY(150, 80);
    zhu.text.drawOption("幽 灵 逃 生", pos, .{ .size = 64 });

    pos = basicPos.addXY(240, 300);
    zhu.text.drawText("最高分：", pos);
    zhu.text.drawNumber(77, pos.addX(125));
}

const imageId = zhu.graphics.imageId;
fn drawButton() void {
    for (&buttons, 0..buttons.len) |button, i| {
        const index: f32 = @floatFromInt(i);
        const pos = buttonPosition.addX(index * 200);

        const buttonArea = zhu.Rect.init(pos, buttonSize);
        camera.drawRectBorder(buttonArea, 1, .green);

        if (i == buttonIndex and buttonState != .normal) {
            if (buttonState == .pressed) {
                camera.drawOption(button.pressed, pos, .{
                    .size = buttonSize,
                });
            } else if (buttonState == .hover) {
                camera.drawOption(button.hover, pos, .{
                    .size = buttonSize,
                });
            }
        } else {
            camera.drawOption(button.normal, pos, .{
                .size = buttonSize,
            });
        }
    }
}
```

## 效果

![授权信息页面][1]

[1]: images/幽灵逃生25.gif

## 附录
