# 1084-幽灵逃生-角色释放法术

## 目标

当点击鼠标左键时，角色可以使用法术进行攻击。

## 环境

- Time 2026-01-11
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1jf9XYQEhW>

## 想法

没有单独的法术冷却时间，现在直接使用的动画来做计时器。

## battle.zig

其它地方的代码，就不粘贴了，比如角色模块使用法术的代码。

```zig
const std = @import("std");
const zhu = @import("zhu");

const camera = zhu.camera;

const player = @import("player.zig");
const enemy = @import("enemy.zig");

pub const Stats = struct {
    health: u32 = 100, // 生命值
    maxHealth: u32 = 100, // 最大生命值
    attack: u32 = 40, // 攻击力
};

const circle = zhu.graphics.imageId("circle.png"); // 显示碰撞范围
const spellFrames = zhu.graphics.framesX(13, .xy(64, 64), 0.1);
const spellDamageIndex = 6; // 动画第 6 帧造成伤害，视觉效果好一点
const spellSize = spellFrames[0].area.size.scale(3);
var spellAnimation: zhu.graphics.FrameAnimation = undefined;
var spellPosition: zhu.Vector2 = .zero;

pub fn init() void {
    const image = zhu.graphics.getImage("effect/Thunderstrike w blur.png");
    spellAnimation = .initFinished(image, &spellFrames);
}

pub fn update(delta: f32) void {
    const changed = spellAnimation.isNextOnceUpdate(delta);
    if (changed and spellAnimation.index == spellDamageIndex) {
        var iterator = std.mem.reverseIterator(enemy.enemies.items);
        while (iterator.nextPtr()) |e| {
            const len = (spellSize.x + enemy.size.x) * 0.5;
            const len2 = spellPosition.sub(e.position).length2();
            if (len2 < len * len) e.stats.health -|= player.stats.attack;
            if (e.stats.health == 0) {
                _ = enemy.enemies.swapRemove(iterator.index);
            }
        }
    }
}

pub fn playerCastSpell(position: zhu.Vector2) void {
    if (spellAnimation.isRunning()) return;

    spellAnimation.reset();
    spellPosition = position;
}

pub fn draw() void {
    if (spellAnimation.isRunning()) {
        const image = spellAnimation.currentImage();
        camera.drawImage(image, spellPosition, .{
            .anchor = .center,
            .size = spellSize,
        });

        camera.drawOption(circle, spellPosition, .{
            .anchor = .center,
            .size = spellSize,
            .color = .{ .y = 1, .w = 0.4 },
        });
    }
}
```

## 效果

![角色释放法术][1]

[1]: images/幽灵逃生17.gif

## 附录
