# 1081-幽灵逃生-玩家和敌人特效

## 目标

新增特效相关功能，玩家死亡时播放死亡特效，敌人出生时播放出生特效。

## 环境

- Time 2026-01-10
- Zig 0.15.1

## 参考

1. <https://www.bilibili.com/video/BV1jf9XYQEhW>

## 想法

教程中单独使用了一个特效类来实现，这里还是使用的动画系统，要不要给这种单独弄一个呢？

## player.zig

死亡特效相关功能。

```zig
const std = @import("std");
const zhu = @import("zhu");

const camera = zhu.camera;
const window = zhu.window;

const battle = @import("battle.zig");

const circle = zhu.graphics.imageId("circle.png"); // 显示碰撞范围
const maxSpeed = 500;
const frames = zhu.graphics.framesX(8, .init(48, 48), 0.1);
const deadFrames = zhu.graphics.framesX(17, .init(64, 64), 0.1);
pub const size = frames[0].area.size;
const Status = enum { idle, move };

var idleImage: zhu.graphics.Image = undefined;
var moveImage: zhu.graphics.Image = undefined;

pub var position: zhu.Vector2 = undefined;
pub var stats: battle.Stats = .{};

var hurtTimer: window.Timer = .init(1.5); // 无敌时间
var velocity: zhu.Vector2 = .zero;
var animation: zhu.graphics.FrameAnimation = undefined;
var deadAnimation: zhu.graphics.FrameAnimation = undefined;
var status: Status = .idle;

pub fn init(initPosition: zhu.Vector2) void {
    idleImage = zhu.graphics.getImage("sprite/ghost-idle.png");
    moveImage = zhu.graphics.getImage("sprite/ghost-move.png");

    const deadImage = zhu.graphics.getImage("effect/1764.png");
    deadAnimation = .init(deadImage, &deadFrames);
    deadAnimation.loop = false;

    animation = .init(idleImage, &frames);
    position = initPosition;
}

pub fn update(delta: f32, worldSize: zhu.Vector2) void {
    if (stats.health == 0) {
        // 角色已死亡
        deadAnimation.update(delta);
    }
    hurtTimer.update(delta);

    velocity = velocity.scale(0.9);
    if (window.isKeyPress(.A)) velocity = .init(-maxSpeed, 0);
    if (window.isKeyPress(.D)) velocity = .init(maxSpeed, 0);
    if (window.isKeyPress(.W)) velocity = .init(0, -maxSpeed);
    if (window.isKeyPress(.S)) velocity = .init(0, maxSpeed);

    move(delta);
    position.clamp(.zero, worldSize.sub(size));
    animation.update(delta);
}

pub fn hurt(damage: u32) void {
    if (hurtTimer.isRunning()) return; // 受伤后的无敌时间

    stats.health -|= damage; // 扣除生命值
    hurtTimer.elapsed = 0; // 重置计时器
}

fn move(delta: f32) void {
    position = position.add(velocity.scale(delta));

    const new: Status = if (velocity.length2() < 0.01) .idle else .move;
    if (new == status) return; // 状态未变化
    status = new;
    animation.image = if (new == .move) moveImage else idleImage;
}

pub fn draw() void {
    if (stats.health == 0) {
        if (deadAnimation.finished()) return; // 动画结束不需要显示

        const image = deadAnimation.currentImage();
        return camera.drawImage(image, position, .{
            .size = size.scale(2), // 和角色的显示区域一样大
            .anchor = .center,
        });
    }

    camera.drawImage(animation.currentImage(), position, .{
        .size = size.scale(2),
        .flipX = velocity.x < 0,
        .anchor = .center,
    });

    // debug 显示碰撞范围
    camera.drawOption(circle, position, .{
        .color = .{ .y = 1, .w = 0.4 },
        .size = size,
        .anchor = .center,
    });

    // 暂时显示生命值在头顶，方便查看
    zhu.text.drawNumber(stats.health, position.addXY(-24, -48));
}
```

## enemy.zig

出生特效相关功能。

```zig
const std = @import("std");
const zhu = @import("zhu");

const camera = zhu.camera;

const battle = @import("battle.zig");
const player = @import("player.zig");

const State = enum { normal, hurt, dead };
const Enemy = struct {
    position: zhu.Vector2,
    animation: zhu.graphics.FrameAnimation,
    stats: battle.Stats = .{},
};
const normalFrames = zhu.graphics.framesX(4, .init(32, 32), 0.2);
const deadFrames = zhu.graphics.framesX(8, .init(32, 32), 0.1);
const size = deadFrames[0].area.size.scale(2);
const maxSpeed = 100;
const circle = zhu.graphics.imageId("circle.png"); // 显示碰撞范围

var animations: zhu.graphics.EnumFrameAnimation(State) = undefined;
var enemy: Enemy = undefined;
const spawnFrames = zhu.graphics.framesX(11, .init(64, 64), 0.1);
var spawnAnimation: zhu.graphics.FrameAnimation = undefined;

pub fn init() void {
    var image = zhu.graphics.getImage("sprite/ghost-Sheet.png");
    animations.set(.normal, .init(image, &normalFrames));
    image = zhu.graphics.getImage("sprite/ghostHurt-Sheet.png");
    animations.set(.hurt, .init(image, &normalFrames)); // 受伤和普通动画一样
    image = zhu.graphics.getImage("sprite/ghostDead-Sheet.png");
    animations.set(.dead, .init(image, &deadFrames));

    // 暂时将动画设置为不重复播放，看看动画切换的效果
    for (&animations.values, 0..) |*animation, i| {
        animation.loop = false;
        animation.state = @intCast(i);
    }

    enemy = Enemy{
        .position = player.position.add(.init(200, 200)),
        .animation = animations.get(.normal),
    };
    const spawnImage = zhu.graphics.getImage("effect/184_3.png");
    spawnAnimation = .init(spawnImage, &spawnFrames);
    spawnAnimation.loop = false;
}

pub fn update(delta: f32) void {
    if (!spawnAnimation.isFinishedAfterUpdate(delta)) return;

    const dir = player.position.sub(enemy.position);
    const distance = dir.normalize().scale(maxSpeed * delta);
    enemy.position = enemy.position.add(distance);

    if (enemy.animation.isFinishedAfterUpdate(delta)) {
        const next = zhu.nextEnum(State, enemy.animation.state);
        enemy.animation = animations.get(next);
    }

    const len = (player.size.x + size.x) * 0.5;
    const len2 = player.position.sub(enemy.position).length2();
    collided = len2 < len * len;
    if (collided) player.hurt(enemy.stats.attack);
}
var collided: bool = false;

pub fn draw() void {
    if (!spawnAnimation.finished()) {
        const image = spawnAnimation.currentImage();
        return camera.drawImage(image, enemy.position, .{
            .size = size,
            .anchor = .center,
        });
    }

    const image = enemy.animation.currentImage();
    var option: camera.Option = .{ .size = size, .anchor = .center };
    camera.drawImage(image, enemy.position, option);

    option.color = .{ .y = 1, .w = 0.4 };
    if (collided) option.color = .{ .x = 1, .w = 0.4 };

    camera.drawOption(circle, enemy.position, option);
}
```

## 效果

![玩家和敌人特效][1]

[1]: images/幽灵逃生14.gif

## 附录
