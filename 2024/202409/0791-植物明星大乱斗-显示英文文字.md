# 0791-植物明星大乱斗-显示英文文字

## 目标

在窗口上显示英文文字，使用的 sokol 自带的 debug text 进行显示。

## 环境

- Time 2025-03-08
- Zig 0.14.0-dev.3298+6fe1993d8

## 参考

1. <https://www.bilibili.com/video/BV1jx4y1t7eP>

## 想法

先能显示文字，后面想办法支持中文，还有其它的字体。

## window.zig

```zig
const std = @import("std");
const sk = @import("sokol");

pub const Event = sk.app.Event;
pub const CallbackInfo = struct {
    title: [:0]const u8,
    init: ?*const fn () void = null,
    update: ?*const fn () void = null,
    render: ?*const fn () void = null,
    event: ?*const fn (*const Event) void = null,
    deinit: ?*const fn () void = null,
};

pub var width: f32 = 1280;
pub var height: f32 = 720;

var timer: std.time.Timer = undefined;
var deltaTime: f32 = 0;
var totalTime: f32 = 0;
pub fn deltaMillisecond() f32 {
    return deltaTime;
}

pub fn totalMillisecond() f32 {
    return totalTime;
}

pub fn displayText(x: f32, y: f32, text: [:0]const u8) void {
    sk.debugtext.canvas(sk.app.widthf() * 0.4, sk.app.heightf() * 0.4);
    sk.debugtext.origin(x, y);
    sk.debugtext.home();

    sk.debugtext.font(0);
    sk.debugtext.color3b(0xff, 0xff, 0xff);
    sk.debugtext.puts(text);
    sk.debugtext.draw();
}

pub fn exit() void {
    sk.app.quit();
}

var callback: CallbackInfo = undefined;
pub fn run(info: CallbackInfo) void {
    callback = info;
    sk.app.run(.{
        .width = @as(i32, @intFromFloat(width)),
        .height = @as(i32, @intFromFloat(height)),
        .window_title = info.title,
        .logger = .{ .func = sk.log.func },
        .win32_console_attach = true,
        .init_cb = if (info.init) |_| init else null,
        .event_cb = if (info.event) |_| event else null,
        .frame_cb = if (info.update != null or info.render != null) frame else null,
        .cleanup_cb = if (info.deinit) |_| cleanup else null,
    });
}

export fn init() void {
    sk.gfx.setup(.{
        .environment = sk.glue.environment(),
        .logger = .{ .func = sk.log.func },
    });

    sk.debugtext.setup(.{
        .fonts = init: {
            var f: [8]sk.debugtext.FontDesc = @splat(.{});
            f[0] = sk.debugtext.fontKc854();
            break :init f;
        },
        .logger = .{ .func = sk.log.func },
    });

    timer = std.time.Timer.start() catch unreachable;
    callback.init.?();
}

export fn event(evt: ?*const Event) void {
    if (evt) |e| callback.event.?(e);
}

export fn frame() void {
    const nano: f32 = @floatFromInt(timer.lap());
    deltaTime = nano / std.time.ns_per_ms;
    totalTime += deltaTime;
    callback.render.?();
    callback.update.?();
}

export fn cleanup() void {
    sk.gfx.shutdown();
    callback.deinit.?();
}
```

## scene.zig

```zig
const std = @import("std");

pub const MenuScene = struct {
    pub fn init(self: *MenuScene) void {
        std.log.info("menu scene init", .{});
        _ = self;
    }

    pub fn update(self: *MenuScene) void {
        std.log.info("menu scene update", .{});
        _ = self;
    }

    pub fn render(self: *MenuScene) void {
        std.log.info("menu scene render", .{});
        _ = self;
    }

    pub fn deinit(self: *MenuScene) void {
        std.log.info("menu scene deinit", .{});
        _ = self;
    }
};
```

## main.zig

```zig
const std = @import("std");

const gfx = @import("graphics.zig");
const window = @import("window.zig");
const scene = @import("scene.zig");

var menuScene: scene.MenuScene = undefined;

pub fn init() void {
    menuScene = scene.MenuScene{};
    menuScene.init();
}

pub fn update() void {
    menuScene.update();
}

pub fn render() void {
    var passEncoder = gfx.CommandEncoder.beginRenderPass(.{ .r = 1, .b = 1, .a = 1.0 });
    defer passEncoder.submit();
    menuScene.render();

    window.displayText(2, 2, "Hello World");
}

pub fn deinit() void {
    menuScene.deinit();
}

pub fn main() void {
    var gpa = std.heap.GeneralPurposeAllocator(.{}){};
    defer _ = gpa.deinit();

    window.width = 1280;
    window.height = 720;

    window.run(.{
        .title = "植物明星大乱斗",
        .init = init,
        .update = update,
        .render = render,
        .deinit = deinit,
    });
}
```

## graphics.zig

新增了 graphics.zig 文件，之前从上个项目拷贝过来的，去除掉了图形相关的代码。

```zig
const std = @import("std");
const sk = @import("sokol");

pub const Color = sk.gfx.Color;

pub const CommandEncoder = struct {
    pub fn beginRenderPass(color: Color) RenderPassEncoder {
        return RenderPassEncoder.begin(color);
    }
};

pub const RenderPassEncoder = struct {
    pub fn begin(color: Color) RenderPassEncoder {
        var action = sk.gfx.PassAction{};
        action.colors[0] = .{ .load_action = .CLEAR, .clear_value = color };
        sk.gfx.beginPass(.{ .action = action, .swapchain = sk.glue.swapchain() });
        return RenderPassEncoder{};
    }

    pub fn draw(self: *RenderPassEncoder, number: u32) void {
        _ = self;
        sk.gfx.draw(0, number, 1);
    }

    pub fn submit(self: *RenderPassEncoder) void {
        _ = self;
        sk.gfx.endPass();
        sk.gfx.commit();
    }
};
```

## 效果

![显示英文文字][1]

[1]: images/植物明星大乱斗03.png

## 附录
