# 0797-植物明星大乱斗-动画结束回调

## 目标

如果动画不是循环播放，则可以在结束动画时，调用传递的回调函数。

## 环境

- Time 2025-03-09
- Zig 0.14.0-dev.3298+6fe1993d8

## 参考

1. <https://www.bilibili.com/video/BV1jx4y1t7eP>

## 想法

把场景相关的内容分成了四个文件，但是文件间有循环依赖，不清楚 Zig 中怎么看待。不过现在支持这种，那么就先这样。

## window.zig

无变化。

## cache.zig

无变化。

## gpu.zig

无变化。

## graphics.zig

```zig
const std = @import("std");
const cache = @import("cache.zig");
const gpu = @import("gpu.zig");

pub const Texture = gpu.Texture;

pub const Camera = struct {
    const zm = @import("zmath");

    proj: zm.Mat,

    pub fn init(width: f32, height: f32) Camera {
        const proj = zm.orthographicOffCenterLh(0, width, 0, height, 0, 1);
        return .{ .proj = proj };
    }

    pub fn vp(self: Camera) zm.Mat {
        return self.proj;
    }
};

pub var camera: Camera = undefined;
pub var renderer: gpu.Renderer = undefined;
var passEncoder: gpu.RenderPassEncoder = undefined;

pub fn init(width: f32, height: f32) void {
    camera = Camera.init(width, height);
    renderer = gpu.Renderer.init();
}

pub fn loadTexture(path: [:0]const u8) ?Texture {
    return cache.TextureCache.load(path);
}

pub fn beginDraw() void {
    passEncoder = gpu.CommandEncoder.beginRenderPass(.{ .r = 1, .b = 1, .a = 1.0 });
    renderer.renderPass = passEncoder;
}

pub fn draw(x: f32, y: f32, tex: Texture) void {
    renderer.draw(.{
        .uniform = .{ .vp = camera.vp() },
        .x = x,
        .y = y,
        .texture = tex,
    });
}

pub fn drawFlipX(x: f32, y: f32, tex: Texture, flipX: bool) void {
    renderer.draw(.{
        .uniform = .{ .vp = camera.vp() },
        .x = x,
        .y = y,
        .texture = tex,
        .flipX = flipX,
    });
}

pub fn endDraw() void {
    passEncoder.submit();
}

pub fn BoundedTextureAtlas(max: u8) type {
    return struct {
        textures: [max]Texture,

        pub fn init(comptime pathFmt: []const u8) @This() {
            var self = @This(){ .textures = undefined };
            var buffer: [128]u8 = undefined;
            for (0..max) |index| {
                const path = std.fmt.bufPrintZ(&buffer, pathFmt, .{index + 1});

                const texture = cache.TextureCache.load(path catch unreachable);
                self.textures[index] = texture.?;
            }

            return self;
        }
    };
}

pub fn BoundedFrameAnimation(max: u8) type {
    return struct {
        interval: f32 = 100,
        timer: f32 = 0,
        index: usize = 0,
        loop: bool = true,
        flip: bool = false,
        atlas: BoundedTextureAtlas(max),
        callback: ?*const fn () void = null,

        pub fn init(comptime pathFmt: []const u8) @This() {
            return .{ .atlas = .init(pathFmt) };
        }

        pub fn update(self: *@This(), delta: f32) void {
            self.timer += delta;
            if (self.timer < self.interval) return;

            self.timer = 0;
            self.index += 1;

            if (self.index < self.atlas.textures.len) return;

            if (self.loop) self.index = 0 else {
                self.index = self.atlas.textures.len - 1;
                if (self.callback) |callback| callback();
            }
        }

        pub fn play(self: @This(), x: f32, y: f32) void {
            drawFlipX(x, y, self.atlas.textures[self.index], self.flip);
        }
    };
}
```

## scene.zig

```zig
const std = @import("std");
const window = @import("window.zig");
const gfx = @import("graphics.zig");

const MenuScene = @import("scene/MenuScene.zig");
const GameScene = @import("scene/GameScene.zig");
const SelectorScene = @import("scene/SelectorScene.zig");

pub var currentScene: Scene = undefined;

var menuScene: MenuScene = undefined;
var gameScene: GameScene = undefined;
var selectorScene: SelectorScene = undefined;

pub const SceneType = enum { menu, game, selector };
pub const Scene = union(SceneType) {
    menu: *MenuScene,
    game: *GameScene,
    selector: *SelectorScene,

    pub fn enter(self: Scene) void {
        switch (self) {
            inline else => |s| s.enter(),
        }
    }

    pub fn exit(self: Scene) void {
        switch (self) {
            inline else => |s| s.exit(),
        }
    }

    pub fn event(self: Scene, ev: *const window.Event) void {
        switch (self) {
            inline else => |s| s.event(ev),
        }
    }

    pub fn update(self: Scene) void {
        switch (self) {
            inline else => |s| s.update(),
        }
    }

    pub fn render(self: Scene) void {
        switch (self) {
            inline else => |s| s.render(),
        }
    }
};

pub fn init() void {
    std.log.info("scene init", .{});

    menuScene = MenuScene.init();
    gameScene = GameScene.init();
    selectorScene = SelectorScene.init();
    currentScene = Scene{ .menu = &menuScene };

    currentScene.enter();
}

pub fn changeCurrentScene(sceneType: SceneType) void {
    currentScene.exit();
    currentScene = switch (sceneType) {
        .menu => Scene{ .menu = &menuScene },
        .game => Scene{ .game = &gameScene },
        .selector => Scene{ .selector = &selectorScene },
    };
    currentScene.enter();
}

pub fn deinit() void {
    std.log.info("scene deinit", .{});
}
```

## scene/MenuScene.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

const changeCurrentScene = @import("../scene.zig").changeCurrentScene;
const MenuScene = @This();

background: gfx.Texture,

pub fn init() MenuScene {
    std.log.info("menu scene init", .{});

    return .{
        .background = gfx.loadTexture("assets/menu_background.png").?,
    };
}

pub fn enter(self: *MenuScene) void {
    std.log.info("menu scene enter", .{});
    _ = self;
}

pub fn exit(self: *MenuScene) void {
    std.log.info("menu scene exit", .{});
    _ = self;
}

pub fn event(self: *MenuScene, ev: *const window.Event) void {
    if (ev.type == .KEY_UP) changeCurrentScene(.game);

    _ = self;
}

pub fn update(self: *MenuScene) void {
    std.log.info("menu scene update", .{});
    _ = self;
}

pub fn render(self: *MenuScene) void {
    gfx.draw(0, 0, self.background);
    window.displayText(2, 2, "menu scene");
}
```

## scene/GameScene.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

const changeCurrentScene = @import("../scene.zig").changeCurrentScene;
const GameScene = @This();
animation: gfx.BoundedFrameAnimation(9),

pub fn init() GameScene {
    std.log.info("game scene init", .{});

    var self: GameScene = .{
        .animation = .init("assets/peashooter_idle_{}.png"),
    };
    self.animation.loop = false;
    self.animation.callback = struct {
        pub fn callback() void {
            changeCurrentScene(.menu);
        }
    }.callback;

    return self;
}

pub fn enter(self: *GameScene) void {
    std.log.info("game scene enter", .{});
    self.animation.index = 0;
}

pub fn exit(self: *GameScene) void {
    std.log.info("game scene exit", .{});
    _ = self;
}

pub fn event(self: *GameScene, ev: *const window.Event) void {
    if (ev.type == .KEY_UP) switch (ev.key_code) {
        .A => self.animation.flip = true,
        .D => self.animation.flip = false,
        .SPACE => changeCurrentScene(.menu),
        else => {},
    };
}

pub fn update(self: *GameScene) void {
    self.animation.update(window.deltaMillisecond());
}

pub fn render(self: *GameScene) void {
    self.animation.play(300, 300);
    window.displayText(2, 2, "game scene");
}
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

const changeCurrentScene = @import("../scene.zig").changeCurrentScene;
const GameScene = @This();
animation: gfx.BoundedFrameAnimation(9),

pub fn init() GameScene {
    std.log.info("game scene init", .{});

    var self: GameScene = .{
        .animation = .init("assets/peashooter_idle_{}.png"),
    };
    self.animation.loop = false;
    self.animation.callback = struct {
        pub fn callback() void {
            changeCurrentScene(.menu);
        }
    }.callback;

    return self;
}

pub fn enter(self: *GameScene) void {
    std.log.info("game scene enter", .{});
    self.animation.index = 0;
}

pub fn exit(self: *GameScene) void {
    std.log.info("game scene exit", .{});
    _ = self;
}

pub fn event(self: *GameScene, ev: *const window.Event) void {
    if (ev.type == .KEY_UP) switch (ev.key_code) {
        .A => self.animation.flip = true,
        .D => self.animation.flip = false,
        .SPACE => changeCurrentScene(.menu),
        else => {},
    };
}

pub fn update(self: *GameScene) void {
    self.animation.update(window.deltaMillisecond());
}

pub fn render(self: *GameScene) void {
    self.animation.play(300, 300);
    window.displayText(2, 2, "game scene");
}
```

## scene/SelectorScene.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

const changeCurrentScene = @import("../scene.zig").changeCurrentScene;

const SelectorScene = @This();
pub fn init() SelectorScene {
    std.log.info("selector scene init", .{});
    return .{};
}

pub fn enter(self: *SelectorScene) void {
    std.log.info("selector scene enter", .{});
    _ = self;
}

pub fn exit(self: *SelectorScene) void {
    std.log.info("selector scene exit", .{});
    _ = self;
}

pub fn event(self: *SelectorScene, ev: *const window.Event) void {
    std.log.info("selector scene event", .{});
    _ = self;
    _ = ev;
}

pub fn update(self: *SelectorScene) void {
    std.log.info("selector scene update", .{});
    _ = self;
}

pub fn render(self: *SelectorScene) void {
    std.log.info("selector scene render", .{});
    _ = self;
}
```

## 效果

![动画结束回调][1]

[1]: images/植物明星大乱斗09.webp

## 附录
