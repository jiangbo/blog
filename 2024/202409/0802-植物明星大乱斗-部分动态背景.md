# 0802-植物明星大乱斗-部分动态背景

## 目标

实现了部分动态背景，加入了渲染部分图片的功能。

## 环境

- Time 2025-03-09
- Zig 0.14.0-dev.3298+6fe1993d8

## 参考

1. <https://www.bilibili.com/video/BV1jx4y1t7eP>

## 想法

两边对抗的剪影在后面不断移动的功能，还不是那么好弄，需要在之前的基础上加入部分图片渲染的功能。

## gpu.zig

```zig
const std = @import("std");
const sk = @import("sokol");

const render = @import("shader/single.glsl.zig");

pub const Color = sk.gfx.Color;
pub const Buffer = sk.gfx.Buffer;

pub const Texture = struct {
    x: f32 = 0,
    y: f32 = 0,
    width: f32,
    height: f32,
    value: sk.gfx.Image,

    pub fn init(width: u32, height: u32, data: []u8) Texture {
        const image = sk.gfx.allocImage();

        sk.gfx.initImage(image, .{
            .width = @as(i32, @intCast(width)),
            .height = @as(i32, @intCast(height)),
            .pixel_format = .RGBA8,
            .data = init: {
                var imageData = sk.gfx.ImageData{};
                imageData.subimage[0][0] = sk.gfx.asRange(data);
                break :init imageData;
            },
        });

        return .{
            .width = @floatFromInt(width),
            .height = @floatFromInt(height),
            .value = image,
        };
    }
};

pub const BindGroup = struct {
    value: sk.gfx.Bindings = .{},
    uniform: render.VsParams = undefined,

    pub fn bindIndexBuffer(self: *BindGroup, buffer: Buffer) void {
        self.value.index_buffer = buffer;
    }

    pub fn bindVertexBuffer(self: *BindGroup, index: u32, buffer: Buffer) void {
        self.value.vertex_buffers[index] = buffer;
    }

    pub fn bindTexture(self: *BindGroup, index: u32, texture: Texture) void {
        self.value.images[index] = texture.value;
    }

    pub fn bindSampler(self: *BindGroup, index: u32, sampler: Sampler) void {
        self.value.samplers[index] = sampler.value;
    }

    pub fn bindUniformBuffer(self: *BindGroup, uniform: UniformParams) void {
        self.uniform = uniform;
    }
};

pub const CommandEncoder = struct {
    pub fn beginRenderPass(color: Color) RenderPassEncoder {
        return RenderPassEncoder.begin(color);
    }
};

pub const RenderPassEncoder = struct {
    pub fn begin(color: Color) RenderPassEncoder {
        var action = sk.gfx.PassAction{};
        action.colors[0] = .{ .load_action = .CLEAR, .clear_value = color };
        sk.gfx.beginPass(.{ .action = action, .swapchain = sk.glue.swapchain() });
        return RenderPassEncoder{};
    }

    pub fn setPipeline(self: *RenderPassEncoder, pipeline: RenderPipeline) void {
        _ = self;
        sk.gfx.applyPipeline(pipeline.value);
    }

    pub fn setBindGroup(self: *RenderPassEncoder, group: BindGroup) void {
        _ = self;
        sk.gfx.applyUniforms(render.UB_vs_params, sk.gfx.asRange(&group.uniform));
        sk.gfx.applyBindings(group.value);
    }

    pub fn draw(self: *RenderPassEncoder, number: u32) void {
        _ = self;
        sk.gfx.draw(0, number, 1);
    }

    pub fn submit(self: *RenderPassEncoder) void {
        _ = self;
        sk.gfx.endPass();
        sk.gfx.commit();
    }
};

const UniformParams = render.VsParams;
pub const Rectangle = struct {
    x: f32 = 0,
    y: f32 = 0,
    width: f32 = 0,
    height: f32 = 0,
};
pub const Renderer = struct {
    bind: BindGroup,
    renderPass: RenderPassEncoder,

    var indexBuffer: ?Buffer = null;
    var pipeline: ?RenderPipeline = null;
    var sampler: ?Sampler = null;

    pub fn init() Renderer {
        var self = Renderer{ .bind = .{}, .renderPass = undefined };

        indexBuffer = indexBuffer orelse sk.gfx.makeBuffer(.{
            .type = .INDEXBUFFER,
            .data = sk.gfx.asRange(&[_]u16{ 0, 1, 2, 0, 2, 3 }),
        });
        self.bind.bindIndexBuffer(indexBuffer.?);

        sampler = sampler orelse Sampler.liner();
        self.bind.bindSampler(render.SMP_smp, sampler.?);

        pipeline = pipeline orelse RenderPipeline{
            .value = sk.gfx.makePipeline(.{
                .shader = sk.gfx.makeShader(render.singleShaderDesc(sk.gfx.queryBackend())),
                .layout = init: {
                    var l = sk.gfx.VertexLayoutState{};
                    l.attrs[render.ATTR_single_position].format = .FLOAT3;
                    l.attrs[render.ATTR_single_color0].format = .FLOAT3;
                    l.attrs[render.ATTR_single_texcoord0].format = .FLOAT2;
                    break :init l;
                },
                .colors = init: {
                    var c: [4]sk.gfx.ColorTargetState = @splat(.{});
                    c[0] = .{
                        .blend = .{
                            .enabled = true,
                            .src_factor_rgb = .SRC_ALPHA,
                            .dst_factor_rgb = .ONE_MINUS_SRC_ALPHA,
                        },
                    };
                    break :init c;
                },
                .index_type = .UINT16,
                .depth = .{ .compare = .LESS_EQUAL, .write_enabled = true },
            }),
        };

        return self;
    }

    pub const DrawOptions = struct {
        uniform: UniformParams,
        x: f32 = 0,
        y: f32 = 0,
        texture: Texture,
        flipX: bool = false,
        sourceRect: ?Rectangle = null,
    };

    pub fn draw(self: *Renderer, options: DrawOptions) void {
        var w = options.texture.width;
        var h = options.texture.height;

        var texU0: f32, var texU1: f32 = .{ 0, 1 };
        var texV0: f32, var texV1: f32 = .{ 0, 1 };
        if (options.sourceRect) |rect| {
            texU0 = rect.x / options.texture.width;
            texU1 = (rect.x + rect.width) / options.texture.width;
            texV0 = rect.y / options.texture.height;
            texV1 = (rect.y + rect.height) / options.texture.height;
            w = rect.width;
            h = rect.height;
        }

        if (options.flipX) {
            texU0 = 1 - texU0;
            texU1 = 1 - texU1;
        }

        const vertexBuffer = sk.gfx.makeBuffer(.{
            .data = sk.gfx.asRange(&[_]f32{
                // 顶点和颜色
                options.x,     options.y + h, 0.5, 1.0, 1.0, 1.0, texU0, 1,
                options.x + w, options.y + h, 0.5, 1.0, 1.0, 1.0, texU1, 1,
                options.x + w, options.y,     0.5, 1.0, 1.0, 1.0, texU1, 0,
                options.x,     options.y,     0.5, 1.0, 1.0, 1.0, texU0, 0,
            }),
        });

        self.bind.bindVertexBuffer(0, vertexBuffer);
        self.bind.bindUniformBuffer(options.uniform);

        self.renderPass.setPipeline(pipeline.?);
        self.bind.bindTexture(render.IMG_tex, options.texture);
        self.renderPass.setBindGroup(self.bind);
        sk.gfx.draw(0, 6, 1);
        sk.gfx.destroyBuffer(vertexBuffer);
    }
};

pub const RenderPipeline = struct {
    value: sk.gfx.Pipeline,
};

pub const Sampler = struct {
    value: sk.gfx.Sampler,

    pub fn liner() Sampler {
        const sampler = sk.gfx.makeSampler(.{
            .min_filter = .LINEAR,
            .mag_filter = .LINEAR,
        });
        return .{ .value = sampler };
    }

    pub fn nearest() Sampler {
        const sampler = sk.gfx.makeSampler(.{
            .min_filter = .NEAREST,
            .mag_filter = .NEAREST,
        });
        return .{ .value = sampler };
    }
};
```

## graphics.zig

```zig
const std = @import("std");
const cache = @import("cache.zig");
const gpu = @import("gpu.zig");

pub const Texture = gpu.Texture;

pub const Camera = struct {
    const zm = @import("zmath");

    proj: zm.Mat,

    pub fn init(width: f32, height: f32) Camera {
        const proj = zm.orthographicOffCenterLh(0, width, 0, height, 0, 1);
        return .{ .proj = proj };
    }

    pub fn vp(self: Camera) zm.Mat {
        return self.proj;
    }
};

pub var camera: Camera = undefined;
pub var renderer: gpu.Renderer = undefined;
var passEncoder: gpu.RenderPassEncoder = undefined;

pub fn init(width: f32, height: f32) void {
    camera = Camera.init(width, height);
    renderer = gpu.Renderer.init();
}

pub fn loadTexture(path: [:0]const u8) ?Texture {
    return cache.TextureCache.load(path);
}

pub fn beginDraw() void {
    passEncoder = gpu.CommandEncoder.beginRenderPass(.{ .r = 1, .b = 1, .a = 1.0 });
    renderer.renderPass = passEncoder;
}

pub fn draw(x: f32, y: f32, tex: Texture) void {
    renderer.draw(.{
        .uniform = .{ .vp = camera.vp() },
        .x = x,
        .y = y,
        .texture = tex,
    });
}

pub fn drawFlipX(x: f32, y: f32, tex: Texture, flipX: bool) void {
    drawOptions(x, y, tex, .{ .flipX = flipX });
}

pub const DrawOptions = struct {
    flipX: bool = false,
    sourceRect: ?gpu.Rectangle = null,
};

pub fn drawOptions(x: f32, y: f32, texture: Texture, options: DrawOptions) void {
    renderer.draw(.{
        .uniform = .{ .vp = camera.vp() },
        .x = x,
        .y = y,
        .texture = texture,
        .flipX = options.flipX,
        .sourceRect = options.sourceRect,
    });
}

pub fn endDraw() void {
    passEncoder.submit();
}

pub fn BoundedTextureAtlas(max: u8) type {
    return struct {
        textures: [max]Texture,

        pub fn init(comptime pathFmt: []const u8) @This() {
            var self = @This(){ .textures = undefined };
            var buffer: [128]u8 = undefined;
            for (0..max) |index| {
                const path = std.fmt.bufPrintZ(&buffer, pathFmt, .{index + 1});

                const texture = cache.TextureCache.load(path catch unreachable);
                self.textures[index] = texture.?;
            }

            return self;
        }
    };
}

pub fn BoundedFrameAnimation(max: u8) type {
    return struct {
        interval: f32 = 100,
        timer: f32 = 0,
        index: usize = 0,
        loop: bool = true,
        atlas: BoundedTextureAtlas(max),
        callback: ?*const fn () void = null,

        pub fn init(comptime pathFmt: []const u8) @This() {
            return .{ .atlas = .init(pathFmt) };
        }

        pub fn update(self: *@This(), delta: f32) void {
            self.timer += delta;
            if (self.timer < self.interval) return;

            self.timer = 0;
            self.index += 1;

            if (self.index < self.atlas.textures.len) return;

            if (self.loop) self.index = 0 else {
                self.index = self.atlas.textures.len - 1;
                if (self.callback) |callback| callback();
            }
        }

        pub fn play(self: @This(), x: f32, y: f32) void {
            self.playFlipX(x, y, false);
        }

        pub fn playFlipX(self: @This(), x: f32, y: f32, flipX: bool) void {
            drawFlipX(x, y, self.atlas.textures[self.index], flipX);
        }
    };
}
```

## scene.zig

```zig
const std = @import("std");
const window = @import("window.zig");
const gfx = @import("graphics.zig");
const audio = @import("zaudio");

const MenuScene = @import("scene/MenuScene.zig");
const GameScene = @import("scene/GameScene.zig");
const SelectorScene = @import("scene/SelectorScene.zig");

pub var currentScene: Scene = undefined;
pub var camera: Camera = .{};
pub var audioEngine: *audio.Engine = undefined;

pub var player1: PlayerType = .peaShooter;
pub var player2: PlayerType = .sunFlower;

var menuScene: MenuScene = undefined;
var gameScene: GameScene = undefined;
var selectorScene: SelectorScene = undefined;

pub const PlayerType = enum { peaShooter, sunFlower };

pub const Camera = struct {
    x: f32 = 0,
    y: f32 = 0,
};

pub const SceneType = enum { menu, game, selector };
pub const Scene = union(SceneType) {
    menu: *MenuScene,
    game: *GameScene,
    selector: *SelectorScene,

    pub fn enter(self: Scene) void {
        switch (self) {
            inline else => |s| s.enter(),
        }
    }

    pub fn exit(self: Scene) void {
        switch (self) {
            inline else => |s| s.exit(),
        }
    }

    pub fn event(self: Scene, ev: *const window.Event) void {
        switch (self) {
            inline else => |s| s.event(ev),
        }
    }

    pub fn update(self: Scene) void {
        switch (self) {
            inline else => |s| s.update(),
        }
    }

    pub fn render(self: Scene) void {
        switch (self) {
            inline else => |s| s.render(),
        }
    }
};

pub fn init() void {
    std.log.info("scene init", .{});

    audioEngine = audio.Engine.create(null) catch unreachable;
    menuScene = MenuScene.init();
    gameScene = GameScene.init();
    selectorScene = SelectorScene.init();
    currentScene = Scene{ .selector = &selectorScene };
    // currentScene = Scene{ .menu = &menuScene };

    currentScene.enter();
}

pub fn changeCurrentScene(sceneType: SceneType) void {
    currentScene.exit();
    currentScene = switch (sceneType) {
        .menu => Scene{ .menu = &menuScene },
        .game => Scene{ .game = &gameScene },
        .selector => Scene{ .selector = &selectorScene },
    };
    currentScene.enter();
}

pub fn deinit() void {
    std.log.info("scene deinit", .{});
    currentScene.exit();
    menuScene.deinit();
    audioEngine.destroy();
}
```

## scene/SelectorScene.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

const scene = @import("../scene.zig");
const SelectorScene = @This();

const offsetX = 50;

background: gfx.Texture,

peaShooterBackground: gfx.Texture,
sunFlowerBackground: gfx.Texture,
imageVS: gfx.Texture,
imageTip: gfx.Texture,

image1P: gfx.Texture,
image2P: gfx.Texture,
image1PDesc: gfx.Texture,
image2PDesc: gfx.Texture,
imageGrave: gfx.Texture,

image1PButtonIdle: gfx.Texture,
image2PButtonIdle: gfx.Texture,

animationPeaShooterIdle: gfx.BoundedFrameAnimation(9),
animationSunFlowerIdle: gfx.BoundedFrameAnimation(8),

backgroundOffsetX: f32 = 0,

pub fn init() SelectorScene {
    std.log.info("selector scene init", .{});

    var self: SelectorScene = undefined;
    self.background = gfx.loadTexture("assets/selector_background.png").?;
    self.peaShooterBackground = gfx.loadTexture("assets/peashooter_selector_background.png").?;
    self.sunFlowerBackground = gfx.loadTexture("assets/sunflower_selector_background.png").?;

    self.imageVS = gfx.loadTexture("assets/VS.png").?;
    self.imageTip = gfx.loadTexture("assets/selector_tip.png").?;
    self.image1P = gfx.loadTexture("assets/1P.png").?;
    self.image2P = gfx.loadTexture("assets/2P.png").?;
    self.image1PDesc = gfx.loadTexture("assets/1P_desc.png").?;
    self.image2PDesc = gfx.loadTexture("assets/2P_desc.png").?;
    self.imageGrave = gfx.loadTexture("assets/gravestone.png").?;

    self.image1PButtonIdle = gfx.loadTexture("assets/1P_selector_btn_idle.png").?;
    self.image2PButtonIdle = gfx.loadTexture("assets/2P_selector_btn_idle.png").?;

    self.animationPeaShooterIdle = .init("assets/peashooter_idle_{}.png");
    self.animationSunFlowerIdle = .init("assets/sunflower_idle_{}.png");

    return self;
}

pub fn enter(self: *SelectorScene) void {
    std.log.info("selector scene enter", .{});
    _ = self;
}

pub fn exit(self: *SelectorScene) void {
    std.log.info("selector scene exit", .{});
    _ = self;
}

pub fn event(self: *SelectorScene, ev: *const window.Event) void {
    _ = self;
    _ = ev;
}

pub fn update(self: *SelectorScene) void {
    self.backgroundOffsetX += window.deltaMillisecond() * 0.2;
    if (self.backgroundOffsetX >= self.peaShooterBackground.width)
        self.backgroundOffsetX = 0;
    self.animationPeaShooterIdle.update(window.deltaMillisecond());
    self.animationSunFlowerIdle.update(window.deltaMillisecond());
}

pub fn render(self: *SelectorScene) void {
    self.renderBackground();

    self.renderStatic();

    var x = (window.width / 2 - self.imageGrave.width) / 2 - offsetX;
    const y = self.image1P.height + 70;

    var buttonX = x - self.image1PButtonIdle.width;
    const buttonY = y + (self.imageGrave.height - self.image1PButtonIdle.height) / 2;
    gfx.drawFlipX(buttonX, buttonY, self.image1PButtonIdle, true);

    buttonX = x + self.imageGrave.width;
    gfx.draw(buttonX, buttonY, self.image1PButtonIdle);

    x = window.width / 2 + (window.width / 2 - self.imageGrave.width) / 2 + offsetX;
    buttonX = x - self.image2PButtonIdle.width;
    gfx.drawFlipX(buttonX, buttonY, self.image2PButtonIdle, true);

    buttonX = x + self.imageGrave.width;
    gfx.draw(buttonX, buttonY, self.image2PButtonIdle);

    var w = window.width / 2 - self.animationPeaShooterIdle.atlas.textures[0].width;
    self.animationPlay(scene.player1, w / 2 - offsetX, y + 80, false);

    w = window.width / 2 - self.animationSunFlowerIdle.atlas.textures[0].width;
    self.animationPlay(scene.player2, window.width / 2 + w / 2 + offsetX, y + 80, true);
}

fn renderBackground(self: *SelectorScene) void {
    gfx.draw(0, 0, self.background);

    const width = self.peaShooterBackground.width;
    var texture = if (scene.player2 == .peaShooter)
        self.peaShooterBackground
    else
        self.sunFlowerBackground;
    gfx.draw(self.backgroundOffsetX - width, 0, texture);

    gfx.drawOptions(self.backgroundOffsetX, 0, texture, .{ .sourceRect = .{
        .width = width - self.backgroundOffsetX,
        .height = self.peaShooterBackground.height,
    } });

    texture = if (scene.player1 == .peaShooter)
        self.peaShooterBackground
    else
        self.sunFlowerBackground;

    gfx.drawOptions(window.width - width, 0, texture, .{
        .flipX = true,
        .sourceRect = .{
            .x = self.backgroundOffsetX,
            .width = width - self.backgroundOffsetX,
            .height = self.sunFlowerBackground.height,
        },
    });
    gfx.drawFlipX(window.width - self.backgroundOffsetX, 0, texture, true);
}

fn renderStatic(self: *SelectorScene) void {
    var w = window.width - self.imageVS.width;
    const h = window.height - self.imageVS.height;
    gfx.draw(w / 2, h / 2, self.imageVS);

    w = window.width - self.imageTip.width;
    gfx.draw(w / 2, window.height - 125, self.imageTip);

    w = window.width / 2 - self.image1P.width;
    const pos1PY = 35;
    gfx.draw(w / 2 - offsetX, pos1PY, self.image1P);
    w = window.width / 2 - self.image2P.width;
    gfx.draw(window.width / 2 + w / 2 + offsetX, 35, self.image2P);

    w = window.width / 2 - self.image1PDesc.width;
    gfx.draw(w / 2 - offsetX, window.height - 150, self.image1PDesc);
    w = window.width / 2 - self.image2PDesc.width;
    gfx.draw(window.width / 2 + w / 2 + offsetX, window.height - 150, self.image2PDesc);

    w = window.width / 2 - self.imageGrave.width;
    const posGraveY = pos1PY + self.image1P.height + 35;
    gfx.draw(w / 2 - offsetX, posGraveY, self.imageGrave);
    w = window.width / 2 - self.imageGrave.width;
    gfx.drawFlipX(window.width / 2 + w / 2 + offsetX, posGraveY, self.imageGrave, true);
}

fn animationPlay(self: *SelectorScene, player: scene.PlayerType, x: f32, y: f32, flip: bool) void {
    switch (player) {
        .sunFlower => self.animationSunFlowerIdle.playFlipX(x, y, flip),
        .peaShooter => self.animationPeaShooterIdle.playFlipX(x, y, flip),
    }
}
```

## 效果

![部分动态背景][1]

[1]: images/植物明星大乱斗14.webp

## 附录
