# 0800-植物明星大乱斗-主菜单界面

## 目标

实现主菜单界面，可以跳转到选择角色界面，同时实现了背景音乐。

## 环境

- Time 2025-03-09
- Zig 0.14.0-dev.3298+6fe1993d8

## 参考

1. <https://www.bilibili.com/video/BV1jx4y1t7eP>

## 想法

之前实现音乐用的 sokol 自带的 audio，不太会用。这次使用的是 miniAudio，zig 的封装是 zaudio。

## zig.build.zon

```zig
...
.zaudio = .{
    .url = "git+https://github.com/zig-gamedev/zaudio#a67604fdb79fb7e0b34fe074f8ffc22d268b3fb3",
    .hash = "122029d6de1bebccfc3ab2816d6f163bb394a0f03861a74857b336c58ce6c5308662",
},
...
```

## build.zig

```zig
...
const zaudio = b.dependency("zaudio", .{
    .target = target,
    .optimize = optimize,
});

exe.root_module.addImport("zaudio", zaudio.module("root"));
exe.linkLibrary(zaudio.artifact("miniaudio"));
...
```

## main.zig

```zig
const std = @import("std");

const gfx = @import("graphics.zig");
const window = @import("window.zig");
const scene = @import("scene.zig");
const cache = @import("cache.zig");
const audio = @import("zaudio");

pub fn init() void {
    cache.init(allocator);
    audio.init(allocator);
    gfx.init(window.width, window.height);
    scene.init();
}

pub fn event(ev: *const window.Event) void {
    scene.currentScene.event(ev);
}

pub fn update() void {
    scene.currentScene.update();
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    scene.currentScene.render();
}

pub fn deinit() void {
    scene.deinit();
    cache.deinit();
    audio.deinit();
}

var allocator: std.mem.Allocator = undefined;

pub fn main() void {
    var gpa = std.heap.GeneralPurposeAllocator(.{}){};
    defer _ = gpa.deinit();

    allocator = gpa.allocator();
    window.width = 1280;
    window.height = 720;

    window.run(.{
        .title = "植物明星大乱斗",
        .init = init,
        .event = event,
        .update = update,
        .render = render,
        .deinit = deinit,
    });
}
```

## scene.zig

```zig
const std = @import("std");
const window = @import("window.zig");
const gfx = @import("graphics.zig");
const audio = @import("zaudio");

const MenuScene = @import("scene/MenuScene.zig");
const GameScene = @import("scene/GameScene.zig");
const SelectorScene = @import("scene/SelectorScene.zig");

pub var currentScene: Scene = undefined;
pub var camera: Camera = .{};
pub var audioEngine: *audio.Engine = undefined;

var menuScene: MenuScene = undefined;
var gameScene: GameScene = undefined;
var selectorScene: SelectorScene = undefined;

pub const Camera = struct {
    x: f32 = 0,
    y: f32 = 0,
};

pub const SceneType = enum { menu, game, selector };
pub const Scene = union(SceneType) {
    menu: *MenuScene,
    game: *GameScene,
    selector: *SelectorScene,

    pub fn enter(self: Scene) void {
        switch (self) {
            inline else => |s| s.enter(),
        }
    }

    pub fn exit(self: Scene) void {
        switch (self) {
            inline else => |s| s.exit(),
        }
    }

    pub fn event(self: Scene, ev: *const window.Event) void {
        switch (self) {
            inline else => |s| s.event(ev),
        }
    }

    pub fn update(self: Scene) void {
        switch (self) {
            inline else => |s| s.update(),
        }
    }

    pub fn render(self: Scene) void {
        switch (self) {
            inline else => |s| s.render(),
        }
    }
};

pub fn init() void {
    std.log.info("scene init", .{});

    audioEngine = audio.Engine.create(null) catch unreachable;
    menuScene = MenuScene.init();
    gameScene = GameScene.init();
    selectorScene = SelectorScene.init();
    currentScene = Scene{ .menu = &menuScene };

    currentScene.enter();
}

pub fn changeCurrentScene(sceneType: SceneType) void {
    currentScene.exit();
    currentScene = switch (sceneType) {
        .menu => Scene{ .menu = &menuScene },
        .game => Scene{ .game = &gameScene },
        .selector => Scene{ .selector = &selectorScene },
    };
    currentScene.enter();
}

pub fn deinit() void {
    std.log.info("scene deinit", .{});
    currentScene.exit();
    menuScene.deinit();
    audioEngine.destroy();
}
```

## scene/MenuScene.zig

```zig
const std = @import("std");
const audio = @import("zaudio");

const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

const scene = @import("../scene.zig");
const MenuScene = @This();

background: gfx.Texture,
bgm: *audio.Sound,
confirm: *audio.Sound,

pub fn init() MenuScene {
    std.log.info("menu scene init", .{});

    var self: MenuScene = undefined;
    self.bgm = scene.audioEngine.createSoundFromFile(
        "assets/bgm_menu.mp3",
        .{ .flags = .{ .stream = true, .looping = true } },
    ) catch unreachable;

    self.confirm = scene.audioEngine.createSoundFromFile(
        "assets/ui_confirm.wav",
        .{},
    ) catch unreachable;

    self.background = gfx.loadTexture("assets/menu_background.png").?;
    return self;
}

pub fn enter(self: *MenuScene) void {
    std.log.info("menu scene enter", .{});
    self.bgm.start() catch unreachable;
}

pub fn exit(self: *MenuScene) void {
    std.log.info("menu scene exit", .{});
    self.bgm.stop() catch unreachable;
}

pub fn event(self: *MenuScene, ev: *const window.Event) void {
    if (ev.type == .KEY_UP) {
        self.confirm.start() catch unreachable;
        scene.changeCurrentScene(.selector);
    }
}

pub fn update(self: *MenuScene) void {
    std.log.info("menu scene update", .{});
    _ = self;
}

pub fn render(self: *MenuScene) void {
    gfx.draw(0, 0, self.background);
}

pub fn deinit(self: *MenuScene) void {
    std.log.info("menu scene deinit", .{});

    self.bgm.destroy();
    self.confirm.destroy();
}
```

## 效果

![主菜单界面][1]

[1]: images/植物明星大乱斗12.webp

## 附录
