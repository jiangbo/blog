# 0884-拼好饭传奇-完成游戏

## 目标

完成最终的游戏。

## 环境

- Time 2025-04-20
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1wmy6YuEzh>

## 想法

没有跟着教程使用继承的方式来实现，直接 if 和 switch 一把梭，感觉写起来还挺乱的。

## cursor.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");

pub const MealType = enum {
    cola,
    sprite,
    braisedChickenHot,
    braisedChickenCold,
    meatBallHot,
    meatBallCold,
    redCookedPorkHot,
    redCookedPorkCold,
    braisedChickenBox,
    meatBallBox,
    redCookedPorkBox,
    takeoutBox,
};

pub const Meal = struct {
    type: MealType,
    picked: gfx.Texture = undefined,
    place: gfx.Texture = undefined,
    icon: gfx.Texture = undefined,
    done: bool = false,

    pub fn init(mealType: MealType) Meal {
        var self: Meal = Meal{ .type = mealType };

        switch (mealType) {
            .cola => {
                self.picked = gfx.loadTexture("assets/cola.png");
                self.place = gfx.loadTexture("assets/cola.png");
                self.icon = gfx.loadTexture("assets/cola_icon.png");
            },
            .sprite => {
                self.picked = gfx.loadTexture("assets/sprite.png");
                self.place = gfx.loadTexture("assets/sprite.png");
                self.icon = gfx.loadTexture("assets/sprite_icon.png");
            },
            .braisedChickenHot => {
                self.picked = gfx.loadTexture("assets/bc_hot_picked.png");
                self.place = gfx.loadTexture("assets/bc_hot.png");
                self.icon = gfx.loadTexture("assets/bc_icon.png");
            },
            .braisedChickenCold => {
                self.picked = gfx.loadTexture("assets/bc_cold_picked.png");
                self.place = gfx.loadTexture("assets/bc_cold.png");
            },
            .meatBallHot => {
                self.picked = gfx.loadTexture("assets/mb_hot_picked.png");
                self.place = gfx.loadTexture("assets/mb_hot.png");
                self.icon = gfx.loadTexture("assets/mb_icon.png");
            },
            .meatBallCold => {
                self.picked = gfx.loadTexture("assets/mb_cold_picked.png");
                self.place = gfx.loadTexture("assets/mb_cold.png");
            },
            .redCookedPorkHot => {
                self.picked = gfx.loadTexture("assets/rcp_hot_picked.png");
                self.place = gfx.loadTexture("assets/rcp_hot.png");
                self.icon = gfx.loadTexture("assets/rcp_icon.png");
            },
            .redCookedPorkCold => {
                self.picked = gfx.loadTexture("assets/rcp_cold_picked.png");
                self.place = gfx.loadTexture("assets/rcp_cold.png");
            },

            .braisedChickenBox => {
                self.picked = gfx.loadTexture("assets/bc_box.png");
                self.place = gfx.loadTexture("assets/bc_box.png");
            },
            .meatBallBox => {
                self.picked = gfx.loadTexture("assets/mb_box.png");
                self.place = gfx.loadTexture("assets/mb_box.png");
            },
            .redCookedPorkBox => {
                self.picked = gfx.loadTexture("assets/rcp_box.png");
                self.place = gfx.loadTexture("assets/rcp_box.png");
            },
            .takeoutBox => {
                self.picked = gfx.loadTexture("assets/tb_picked.png");
                self.place = gfx.loadTexture("assets/tb.png");
            },
        }

        return self;
    }
};

pub var position: math.Vector = .zero;
pub var leftKeyDown: bool = false;
pub var picked: ?Meal = null;

pub fn event(ev: *const window.Event) void {
    if (ev.type == .MOUSE_MOVE) {
        position = .init(ev.mouse_x, ev.mouse_y);
    }

    if (ev.mouse_button == .LEFT) {
        if (ev.type == .MOUSE_DOWN) {
            leftKeyDown = true;
            switch (math.randU8(1, 3)) {
                1 => audio.playSound("assets/click_1.ogg"),
                2 => audio.playSound("assets/click_2.ogg"),
                3 => audio.playSound("assets/click_3.ogg"),
                else => unreachable,
            }
        }
        if (ev.type == .MOUSE_UP) leftKeyDown = false;
    }
}

pub fn render() void {
    if (picked) |meal| {
        gfx.draw(meal.picked, position.sub(meal.picked.size().scale(0.3)));
    }

    if (leftKeyDown) {
        gfx.draw(gfx.loadTexture("assets/cursor_down.png"), position);
    } else {
        gfx.draw(gfx.loadTexture("assets/cursor_idle.png"), position);
    }
}
```

## Region.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");

const cursor = @import("cursor.zig");
const scene = @import("scene.zig");

const Region = @This();

pub const pickType = gfx.Texture;

pub const RegionType = enum {
    deliver,
    cola,
    sprite,
    takeoutBoxBundle,
    meatBallBox,
    braisedChickenBox,
    redCookedPorkBox,
    microWave,
    takeoutBox,
};

type: RegionType,
area: math.Rectangle,
texture: ?gfx.Texture = null,
meal: ?cursor.Meal = null,
timer: ?window.Timer = null,

// 外卖员专有字段
wanted: ?std.BoundedArray(cursor.Meal, 10) = null,
waitedTime: f32 = math.epsilon, // 外卖员已经等待的时间

const DELIVER_TIMEOUT = 40; // 外卖员耐心超时秒数
const DRINKS_PER_LINE = 2; // 每行 2 个饮料
const DELIVER_TOTAL_LINES = 4; // 总共 4 行外卖

pub fn init(x: f32, y: f32, regionType: RegionType) Region {
    const position: math.Vector = .init(x, y);

    var self: Region = .{ .area = .{}, .type = regionType };
    switch (regionType) {
        .deliver => refreshDeliver(&self),

        .cola => {
            self.texture = gfx.loadTexture("assets/cola_bundle.png");
            self.meal = .init(.cola);
        },

        .sprite => {
            self.texture = gfx.loadTexture("assets/sprite_bundle.png");
            self.meal = .init(.sprite);
        },

        .takeoutBoxBundle => {
            self.texture = gfx.loadTexture("assets/tb_bundle.png");
            self.meal = .init(.takeoutBox);
        },

        .meatBallBox => {
            self.texture = gfx.loadTexture("assets/mb_box_bundle.png");
            self.meal = .init(.meatBallBox);
        },

        .braisedChickenBox => {
            self.texture = gfx.loadTexture("assets/bc_box_bundle.png");
            self.meal = .init(.braisedChickenBox);
        },

        .redCookedPorkBox => {
            self.texture = gfx.loadTexture("assets/rcp_box_bundle.png");
            self.meal = .init(.redCookedPorkBox);
        },

        .microWave => {
            self.texture = gfx.loadTexture("assets/mo_opening.png");
        },
        .takeoutBox => {
            self.area = .init(position, .{ .x = 92, .y = 100 });
        },
    }

    if (self.texture) |texture| {
        self.area = .init(position, texture.size());
    }

    return self;
}

fn refreshDeliver(self: *Region) void {
    // 随机外卖员形象
    const meituan = math.rand.boolean();
    if (meituan) {
        self.texture = gfx.loadTexture("assets/meituan.png");
    } else {
        self.texture = gfx.loadTexture("assets/eleme.png");
    }

    self.waitedTime = math.epsilon;

    // 随机要求餐品
    self.wanted = std.BoundedArray(cursor.Meal, 10).init(0) catch unreachable;
    const drinks = math.randU8(0, 7); // 随机 0 到 7 个饮料
    const lines = (drinks + DRINKS_PER_LINE - 1) / DRINKS_PER_LINE;

    // 先加菜品
    for (0..DELIVER_TOTAL_LINES - lines) |_| {
        // 随机要求菜品
        const meal: cursor.Meal = switch (math.randU8(0, 2)) {
            0 => .init(.braisedChickenHot),
            1 => .init(.meatBallHot),
            2 => .init(.redCookedPorkHot),
            else => unreachable,
        };
        self.wanted.?.appendAssumeCapacity(meal);
    }

    // 再加饮料
    for (0..drinks) |_| {
        if (math.rand.boolean())
            self.wanted.?.appendAssumeCapacity(.init(.cola))
        else
            self.wanted.?.appendAssumeCapacity(.init(.sprite));
    }
}

fn hiddenDeliver(self: *Region) void {
    self.wanted = null;
    self.texture = null;
    self.waitedTime = math.epsilon;
    self.timer = .init(math.randF32(10, 20));
}

pub fn updateDeliver(self: *Region, delta: f32) void {
    if (self.wanted == null) return;

    self.waitedTime += delta;
    if (self.waitedTime >= DELIVER_TIMEOUT)
        self.hiddenDeliver();
}

pub fn pick(self: *Region) void {
    cursor.picked = self.meal;
    scene.returnPosition = cursor.position;
    scene.pickedMeal = cursor.picked;
    scene.pickedRegion = self;

    if (self.type == .takeoutBox) {
        self.meal = null;
        scene.returnPosition = self.area.min;
    }
    if (self.type == .microWave) {
        self.meal = null;
        scene.returnPosition = self.area.min.add(.{ .x = 113, .y = 65 });
    }
}

pub fn place(self: *Region) void {
    if (self.type == .takeoutBox) return self.placeInTakeoutBox();
    if (self.type == .microWave) return self.placeInMicroWave();
    if (self.type == .deliver) return self.placeInDeliver();

    if (self.meal) |meal| {
        if (meal.type == cursor.picked.?.type) {
            cursor.picked = null;
        }
    }
}

pub fn placeInTakeoutBox(self: *Region) void {
    if (self.meal == null) {
        switch (cursor.picked.?.type) {
            .cola, .sprite, .meatBallBox => {},
            .braisedChickenBox, .redCookedPorkBox => {},
            else => {
                self.meal = cursor.picked;
                cursor.picked = null;
            },
        }
        return;
    }

    if (self.meal.?.type == .takeoutBox) {
        self.meal = switch (cursor.picked.?.type) {
            .braisedChickenBox => .init(.braisedChickenCold),
            .meatBallBox => .init(.meatBallCold),
            .redCookedPorkBox => .init(.redCookedPorkCold),
            else => return,
        };
        cursor.picked = null;
    }
}

pub fn placeInMicroWave(self: *Region) void {
    if (self.meal != null) return;

    self.meal = switch (cursor.picked.?.type) {
        .braisedChickenCold => .init(.braisedChickenHot),
        .meatBallCold => .init(.meatBallHot),
        .redCookedPorkCold => .init(.redCookedPorkHot),
        else => return,
    };
    cursor.picked = null;
    audio.playSound("assets/mo_working.ogg");
    self.texture = gfx.loadTexture("assets/mo_working.png");
    self.timer = .init(9);
}

pub fn placeInDeliver(self: *Region) void {
    if (self.wanted == null) return;

    const cursorType = cursor.picked.?.type;
    for (self.wanted.?.slice()) |*meal| {
        if (meal.type == cursorType and !meal.done) {
            cursor.picked = null;
            meal.done = true;
            audio.playSound("assets/complete.ogg");
            break;
        }
    }

    // 检查是否全部完成
    for (self.wanted.?.slice()) |meal| if (!meal.done) return;

    self.hiddenDeliver();
}

pub fn timerFinished(self: *Region) void {
    if (self.type == .microWave) {
        self.texture = gfx.loadTexture("assets/mo_opening.png");
        audio.playSound("assets/mo_complete.ogg");
    }

    if (self.type == .deliver) refreshDeliver(self);

    self.timer = null;
}

pub fn renderDeliver(self: *const Region) void {
    if (self.wanted == null) return;

    var pos = self.area.min.add(.{ .x = -35, .y = 15 });

    // 耐心条的边框
    gfx.draw(gfx.loadTexture("assets/patience_border.png"), pos);

    const percent: f32 = self.waitedTime / DELIVER_TIMEOUT;

    // 耐心条的长度
    const content = gfx.loadTexture("assets/patience_content.png");
    var dst: math.Rectangle = .init(pos, content.size());
    dst.min.y = dst.max.y - content.height() * percent;
    var src: math.Rectangle = .init(.zero, content.size());
    src.min.y = src.max.y - content.height() * percent;
    gfx.drawOptions(content, .{ .sourceRect = src, .targetRect = dst });

    // 对话框
    pos = self.area.min.add(.{ .x = 175, .y = 55 });
    gfx.draw(gfx.loadTexture("assets/bubble.png"), pos);

    var drinks: u8 = 0;
    for (self.wanted.?.slice(), 0..) |meal, i| {
        const index: f32 = @floatFromInt(i);
        if (meal.type == .cola or meal.type == .sprite) {
            // 所有食物放置后的偏移
            const mealOffsetY = 32 * (i - drinks) + 10;
            const drinkOffsetY = 28 * (drinks / DRINKS_PER_LINE); // 饮料本身的偏移
            const offsetY: f32 = @floatFromInt(mealOffsetY + drinkOffsetY);
            var offset: math.Vector = .init(18, offsetY);

            if (drinks % DRINKS_PER_LINE != 0) offset.x += 32; // 饮料并排放置

            gfx.drawOptions(meal.icon, .{
                .targetRect = .init(pos.add(offset), .init(20, 26)),
                .alpha = if (meal.done) 0.35 else 1,
            });

            drinks += 1;
            continue;
        }

        const offset: math.Vector = .init(18, 32 * index + 5);
        gfx.drawOptions(meal.icon, .{
            .targetRect = .init(pos.add(offset), .init(45, 25)),
            .alpha = if (meal.done) 0.35 else 1,
        });
    }
}
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");

const cursor = @import("cursor.zig");
const Region = @import("Region.zig");

var regions: [15]Region = undefined;
var returnTimer: ?window.Timer = null;
pub var returnPosition: math.Vector = .zero;
pub var pickedRegion: *Region = undefined;
pub var pickedMeal: ?cursor.Meal = undefined;
var position: math.Vector = .zero;
var velocity: math.Vector = .zero;

pub fn init() void {
    window.showCursor(false);

    regions[0] = .init(385, 142, .deliver);
    regions[1] = .init(690, 142, .deliver);
    regions[2] = .init(995, 142, .deliver);

    regions[3] = .init(300, 390, .cola);
    regions[4] = .init(425, 390, .sprite);
    regions[5] = .init(550, 418, .takeoutBoxBundle);

    regions[6] = .init(225, 520, .meatBallBox);
    regions[7] = .init(395, 520, .braisedChickenBox);
    regions[8] = .init(565, 520, .redCookedPorkBox);

    regions[9] = .init(740, 400, .microWave);
    regions[10] = .init(975, 400, .microWave);

    regions[11] = .init(830, 560, .takeoutBox);
    regions[12] = .init(935, 560, .takeoutBox);
    regions[13] = .init(1040, 560, .takeoutBox);
    regions[14] = .init(1145, 560, .takeoutBox);

    audio.playMusic("assets/bgm.ogg");
}

pub fn event(ev: *const window.Event) void {
    cursor.event(ev);
}

pub fn update(delta: f32) void {
    if (returnTimer) |*timer| {
        if (timer.isRunningAfterUpdate(delta)) {
            position = position.add(velocity.scale(delta));
        } else {
            returnTimer = null;
            pickedRegion.meal = pickedMeal;
            pickedMeal = null;
        }
        return;
    }

    for (&regions) |*region| {
        if (region.timer) |*timer| if (timer.isFinishedAfterUpdate(delta)) {
            region.timerFinished();
        };

        if (region.type == .deliver) region.updateDeliver(delta);

        if (cursor.picked == null and cursor.leftKeyDown) {
            if (region.area.contains(cursor.position)) region.pick();
        }

        if (cursor.picked != null and !cursor.leftKeyDown) {
            if (region.area.contains(cursor.position)) region.place();
        }
    }

    if (cursor.picked != null and !cursor.leftKeyDown) returnMeal();
}

fn returnMeal() void {
    cursor.picked = null;
    returnTimer = .init(0.5);
    position = cursor.position;
    velocity = returnPosition.sub(position).scale(1 / returnTimer.?.duration);
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    gfx.draw(gfx.loadTexture("assets/background.png"), .zero);

    for (&regions) |*value| {
        if (value.texture) |texture| gfx.draw(texture, value.area.min);

        if (value.type == .takeoutBox) {
            if (value.meal) |meal|
                gfx.draw(meal.place, value.area.min.add(.{ .y = 20 }));
        }

        if (value.type == .microWave and value.timer == null) {
            if (value.meal) |meal|
                gfx.draw(meal.place, value.area.min.add(.{ .x = 113, .y = 65 }));
        }

        if (value.type == .deliver) value.renderDeliver();
    }

    if (returnTimer != null) {
        if (pickedMeal) |meal| gfx.draw(meal.picked, position);
    }

    cursor.render();
}

pub fn deinit() void {
    window.showCursor(true);
    audio.stopMusic();
}
```

## 效果

![完成游戏][1]

[1]: images/拼好饭传奇13.webp

## 附录
