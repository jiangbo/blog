# 0881-拼好饭传奇-支持透明度显示

## 目标

实现渲染纹理的时候，可以自定义 alpha 通道。

## 环境

- Time 2025-04-20
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1wmy6YuEzh>

## 想法

了解了图片透明度怎么显示。

## gpu.zig

```zig
const std = @import("std");
const sk = @import("sokol");

const render = @import("shader/single.glsl.zig");
const math = @import("math.zig");

pub const Color = sk.gfx.Color;
pub const Buffer = sk.gfx.Buffer;

pub const Texture = struct {
    value: sk.gfx.Image,

    pub fn init(w: u32, h: u32, data: []const u8) Texture {
        const image = sk.gfx.allocImage();

        sk.gfx.initImage(image, .{
            .width = @as(i32, @intCast(w)),
            .height = @as(i32, @intCast(h)),
            .pixel_format = .RGBA8,
            .data = init: {
                var imageData = sk.gfx.ImageData{};
                imageData.subimage[0][0] = sk.gfx.asRange(data);
                break :init imageData;
            },
        });

        return .{ .value = image };
    }

    pub fn width(self: Texture) f32 {
        return @floatFromInt(sk.gfx.queryImageWidth(self.value));
    }

    pub fn height(self: Texture) f32 {
        return @floatFromInt(sk.gfx.queryImageHeight(self.value));
    }

    pub fn size(self: Texture) math.Vector {
        return .{ .x = self.width(), .y = self.height(), .z = 1 };
    }

    pub fn deinit(self: *Texture) void {
        sk.gfx.destroyImage(self.value);
    }
};

pub const BindGroup = struct {
    value: sk.gfx.Bindings = .{},
    uniform: render.VsParams = undefined,

    pub fn bindIndexBuffer(self: *BindGroup, buffer: Buffer) void {
        self.value.index_buffer = buffer;
    }

    pub fn bindVertexBuffer(self: *BindGroup, index: u32, buffer: Buffer) void {
        self.value.vertex_buffers[index] = buffer;
    }

    pub fn bindTexture(self: *BindGroup, index: u32, texture: Texture) void {
        self.value.images[index] = texture.value;
    }

    pub fn bindSampler(self: *BindGroup, index: u32, sampler: Sampler) void {
        self.value.samplers[index] = sampler.value;
    }

    pub fn bindUniformBuffer(self: *BindGroup, uniform: UniformParams) void {
        self.uniform = uniform;
    }
};

pub const CommandEncoder = struct {
    pub fn beginRenderPass(color: Color, matrix: []const f32) RenderPassEncoder {
        sk.gl.defaults();
        sk.gl.matrixModeModelview();
        sk.gl.loadMatrix(@ptrCast(matrix));
        sk.gl.pushMatrix();
        return RenderPassEncoder.begin(color);
    }
};

pub const Rectangle = math.Rectangle;

pub const RenderPassEncoder = struct {
    pub fn begin(color: Color) RenderPassEncoder {
        var action = sk.gfx.PassAction{};
        action.colors[0] = .{ .load_action = .CLEAR, .clear_value = color };
        sk.gfx.beginPass(.{ .action = action, .swapchain = sk.glue.swapchain() });
        return RenderPassEncoder{};
    }

    pub fn setPipeline(self: *RenderPassEncoder, pipeline: RenderPipeline) void {
        _ = self;
        sk.gfx.applyPipeline(pipeline.value);
    }

    pub fn setBindGroup(self: *RenderPassEncoder, group: BindGroup) void {
        _ = self;
        sk.gfx.applyUniforms(render.UB_vs_params, sk.gfx.asRange(&group.uniform));
        sk.gfx.applyBindings(group.value);
    }

    pub fn draw(self: *RenderPassEncoder, number: u32) void {
        _ = self;
        sk.gfx.draw(0, number, 1);
    }

    pub fn submit(self: *RenderPassEncoder) void {
        _ = self;
        sk.gl.popMatrix();
        sk.gl.draw();
        sk.gfx.endPass();
        sk.gfx.commit();
    }
};

const UniformParams = render.VsParams;

pub const Renderer = struct {
    bind: BindGroup,
    renderPass: RenderPassEncoder,

    var indexBuffer: ?Buffer = null;
    var pipeline: ?RenderPipeline = null;
    var sampler: ?Sampler = null;

    pub fn init() Renderer {
        sk.gfx.setup(.{
            .environment = sk.glue.environment(),
            .logger = .{ .func = sk.log.func },
            .image_pool_size = 150,
        });

        sk.gl.setup(.{
            .logger = .{ .func = sk.log.func },
        });

        sk.debugtext.setup(.{
            .fonts = init: {
                var f: [8]sk.debugtext.FontDesc = @splat(.{});
                f[0] = sk.debugtext.fontKc854();
                break :init f;
            },
            .logger = .{ .func = sk.log.func },
        });

        var self = Renderer{ .bind = .{}, .renderPass = undefined };

        indexBuffer = indexBuffer orelse sk.gfx.makeBuffer(.{
            .type = .INDEXBUFFER,
            .data = sk.gfx.asRange(&[_]u16{ 0, 1, 2, 0, 2, 3 }),
        });
        self.bind.bindIndexBuffer(indexBuffer.?);

        sampler = sampler orelse Sampler.nearest();
        self.bind.bindSampler(render.SMP_smp, sampler.?);

        pipeline = pipeline orelse RenderPipeline{
            .value = sk.gfx.makePipeline(.{
                .shader = sk.gfx.makeShader(render.singleShaderDesc(sk.gfx.queryBackend())),
                .layout = init: {
                    var l = sk.gfx.VertexLayoutState{};
                    l.attrs[render.ATTR_single_position].format = .FLOAT3;
                    l.attrs[render.ATTR_single_color0].format = .FLOAT3;
                    l.attrs[render.ATTR_single_texcoord0].format = .FLOAT2;
                    break :init l;
                },
                .colors = init: {
                    var c: [4]sk.gfx.ColorTargetState = @splat(.{});
                    c[0] = .{
                        .blend = .{
                            .enabled = true,
                            .src_factor_rgb = .SRC_ALPHA,
                            .dst_factor_rgb = .ONE_MINUS_SRC_ALPHA,
                        },
                    };
                    break :init c;
                },
                .index_type = .UINT16,
                .depth = .{ .compare = .LESS_EQUAL, .write_enabled = true },
            }),
        };

        return self;
    }

    pub const DrawOptions = struct {
        uniform: UniformParams,
        texture: Texture,
        sourceRect: Rectangle,
        targetRect: Rectangle,
        radians: f32 = 0,
        pivot: math.Vector = .zero,
    };

    pub fn draw(self: *Renderer, options: DrawOptions) void {
        const dst = options.targetRect;

        const min = options.sourceRect.min.div(options.texture.size());
        const max = options.sourceRect.max.div(options.texture.size());

        var vertex = [_]math.Vector3{
            .{ .x = dst.min.x, .y = dst.max.y },
            .{ .x = dst.max.x, .y = dst.max.y },
            .{ .x = dst.max.x, .y = dst.min.y },
            .{ .x = dst.min.x, .y = dst.min.y },
        };

        if (options.radians != 0) {
            const percent = options.pivot.div(options.texture.size());
            const pivot = dst.min.add(percent.mul(dst.size()));

            for (&vertex) |*point| {
                point.* = pivot.add(point.sub(pivot).rotate(options.radians));
            }
        }

        const myTest = [_]f32{
            // 顶点和颜色
            vertex[0].x, vertex[0].y, 0.5, 1.0, 1.0, 1.0, min.x, max.y, // 左上
            vertex[1].x, vertex[1].y, 0.5, 1.0, 1.0, 1.0, max.x, max.y, // 右上
            vertex[2].x, vertex[2].y, 0.5, 1.0, 1.0, 1.0, max.x, min.y, // 右下
            vertex[3].x, vertex[3].y, 0.5, 1.0, 1.0, 1.0, min.x, min.y, // 左下
        };

        const vertexBuffer = sk.gfx.makeBuffer(.{
            .data = sk.gfx.asRange(&myTest),
        });

        self.bind.bindVertexBuffer(0, vertexBuffer);
        self.bind.bindUniformBuffer(options.uniform);

        self.renderPass.setPipeline(pipeline.?);
        self.bind.bindTexture(render.IMG_tex, options.texture);
        self.renderPass.setBindGroup(self.bind);
        sk.gfx.draw(0, 6, 1);
        sk.gfx.destroyBuffer(vertexBuffer);
    }
};

pub fn deinit() void {
    sk.gfx.shutdown();
}

pub const RenderPipeline = struct {
    value: sk.gfx.Pipeline,
};

pub const Sampler = struct {
    value: sk.gfx.Sampler,

    pub fn liner() Sampler {
        const sampler = sk.gfx.makeSampler(.{
            .min_filter = .LINEAR,
            .mag_filter = .LINEAR,
        });
        return .{ .value = sampler };
    }

    pub fn nearest() Sampler {
        const sampler = sk.gfx.makeSampler(.{
            .min_filter = .NEAREST,
            .mag_filter = .NEAREST,
        });
        return .{ .value = sampler };
    }
};

pub fn drawRectangleLine(rect: Rectangle) void {
    sk.gl.beginLineStrip();

    sk.gl.v2fC3b(rect.min.x, rect.min.y, 0, 255, 0);
    sk.gl.v2fC3b(rect.max.x, rect.min.y, 0, 255, 0);
    sk.gl.v2fC3b(rect.max.x, rect.max.y, 0, 255, 0);
    sk.gl.v2fC3b(rect.min.x, rect.max.y, 0, 255, 0);
    sk.gl.v2fC3b(rect.min.x, rect.min.y, 0, 255, 0);

    sk.gl.end();
}
```

## graphics.zig

```zig
const std = @import("std");

const cache = @import("cache.zig");
const gpu = @import("gpu.zig");
const math = @import("math.zig");
const animation = @import("animation.zig");

pub const Texture = gpu.Texture;

pub const Camera = struct {
    rect: math.Rectangle = .{},

    pub fn lookAt(self: *Camera, pos: math.Vector) void {
        self.rect.x = pos.x - self.rect.w / 2;
        self.rect.y = pos.y - self.rect.h / 2;
    }
};

pub var renderer: gpu.Renderer = undefined;
var matrix: [16]f32 = undefined;
var passEncoder: gpu.RenderPassEncoder = undefined;
pub var camera: Camera = undefined;

pub fn init(size: math.Vector) void {
    matrix = .{
        2 / size.x, 0.0,         0.0, 0.0,
        0.0,        2 / -size.y, 0.0, 0.0,
        0.0,        0.0,         1,   0.0,
        -1,         1,           0,   1.0,
    };
    renderer = gpu.Renderer.init();
}

pub const deinit = gpu.deinit;

pub fn loadTexture(path: [:0]const u8) Texture {
    return cache.Texture.load(path);
}

pub fn beginDraw() void {
    passEncoder = gpu.CommandEncoder.beginRenderPass(
        .{ .r = 1, .b = 1, .a = 1.0 },
        &matrix,
    );

    renderer.renderPass = passEncoder;
}

pub fn drawRectangle(rect: math.Rectangle) void {
    gpu.drawRectangleLine(rect);
}

pub fn draw(tex: Texture, position: math.Vector) void {
    drawFlipX(tex, position, false);
}

pub fn drawFlipX(tex: Texture, pos: math.Vector, flipX: bool) void {
    const target: math.Rectangle = .init(pos, .zero);
    const src = math.Rectangle{ .max = .{
        .x = if (flipX) -tex.width() else tex.width(),
    } };

    drawOptions(tex, .{ .sourceRect = src, .targetRect = target });
}

pub const DrawOptions = struct {
    sourceRect: math.Rectangle = .{},
    targetRect: math.Rectangle = .{},
    angle: f32 = 0,
    pivot: math.Vector = .zero,
};

pub fn drawOptions(texture: Texture, options: DrawOptions) void {
    matrix[12] = -1 - camera.rect.min.x * matrix[0];
    matrix[13] = 1 - camera.rect.min.y * matrix[5];

    var src, var dst = .{ options.sourceRect, options.targetRect };
    if (src.min.x == src.max.x) src.max.x = src.min.x + texture.width();
    if (src.min.y == src.max.y) src.max.y = src.min.y + texture.height();
    if (dst.min.x == dst.max.x) dst.max.x = dst.min.x + texture.width();
    if (dst.min.y == dst.max.y) dst.max.y = dst.min.y + texture.height();

    renderer.draw(.{
        .uniform = .{ .vp = matrix },
        .texture = texture,
        .sourceRect = src,
        .targetRect = dst,
        .radians = std.math.degreesToRadians(options.angle),
        .pivot = options.pivot,
    });
}

pub fn endDraw() void {
    passEncoder.submit();
}

pub const FrameAnimation = animation.FrameAnimation;
pub const SliceFrameAnimation = animation.SliceFrameAnimation;
pub const AtlasFrameAnimation = animation.AtlasFrameAnimation;

pub fn playSlice(frameAnimation: *const FrameAnimation, pos: math.Vector) void {
    playSliceFlipX(frameAnimation, pos, false);
}

pub fn playSliceFlipX(frame: *const FrameAnimation, pos: math.Vector, flipX: bool) void {
    const offset = pos.add(frame.offset);
    drawFlipX(frame.textures[frame.index], offset, flipX);
}

pub fn playAtlas(frameAnimation: *const AtlasFrameAnimation, pos: math.Vector) void {
    playAtlasFlipX(frameAnimation, pos, false);
}

pub fn playAtlasFlipX(frame: *const AtlasFrameAnimation, pos: math.Vector, flipX: bool) void {
    var src = frame.frames[frame.index];
    const offset = pos.add(frame.offset);

    const dst: gpu.Rectangle = .{ .x = offset.x, .y = offset.y, .w = src.w };
    if (flipX) src.w = -src.w;
    drawOptions(frame.texture, .{ .sourceRect = src, .targetRect = dst });
}
```

## Region.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");

const cursor = @import("cursor.zig");
const scene = @import("scene.zig");

const Region = @This();

pub const pickType = gfx.Texture;

pub const RegionType = enum {
    deliver,
    cola,
    sprite,
    takeoutBoxBundle,
    meatBallBox,
    braisedChickenBox,
    redCookedPorkBox,
    microWave,
    takeoutBox,
};

type: RegionType,
area: math.Rectangle,
texture: ?gfx.Texture = null,
meal: ?cursor.Meal = null,
timer: ?window.Timer = null,

wanted: ?std.BoundedArray(cursor.Meal, 10) = null,

const DELIVER_TIMEOUT = 40; // 外卖员耐心超时秒数
const DRINKS_PER_LINE = 2; // 每行 2 个饮料
const DELIVER_TOTAL_LINES = 4; // 总共 4 行外卖

pub fn init(x: f32, y: f32, regionType: RegionType) Region {
    const position: math.Vector = .init(x, y);

    var self: Region = .{ .area = .{}, .type = regionType };
    switch (regionType) {
        .deliver => {

            // 随机外卖员形象
            const meituan = math.rand.boolean();
            if (meituan) {
                self.texture = gfx.loadTexture("assets/meituan.png");
            } else {
                self.texture = gfx.loadTexture("assets/eleme.png");
            }

            // 随机要求餐品
            self.wanted = std.BoundedArray(cursor.Meal, 10).init(0) catch unreachable;
            const drinks = math.randU8(0, 7); // 随机 0 到 7 个饮料
            const lines = (drinks + DRINKS_PER_LINE - 1) / DRINKS_PER_LINE;

            // 先加菜品
            for (0..DELIVER_TOTAL_LINES - lines) |_| {
                // 随机要求菜品
                const meal: cursor.Meal = switch (math.randU8(0, 2)) {
                    0 => .init(.braisedChickenHot),
                    1 => .init(.meatBallHot),
                    2 => .init(.redCookedPorkHot),
                    else => unreachable,
                };
                self.wanted.?.appendAssumeCapacity(meal);
            }

            // 再加饮料
            for (0..drinks) |_| {
                if (math.rand.boolean())
                    self.wanted.?.appendAssumeCapacity(.init(.cola))
                else
                    self.wanted.?.appendAssumeCapacity(.init(.sprite));
            }
        },

        .cola => {
            self.texture = gfx.loadTexture("assets/cola_bundle.png");
            self.meal = .init(.cola);
        },

        .sprite => {
            self.texture = gfx.loadTexture("assets/sprite_bundle.png");
            self.meal = .init(.sprite);
        },

        .takeoutBoxBundle => {
            self.texture = gfx.loadTexture("assets/tb_bundle.png");
            self.meal = .init(.takeoutBox);
        },

        .meatBallBox => {
            self.texture = gfx.loadTexture("assets/mb_box_bundle.png");
            self.meal = .init(.meatBallBox);
        },

        .braisedChickenBox => {
            self.texture = gfx.loadTexture("assets/bc_box_bundle.png");
            self.meal = .init(.braisedChickenBox);
        },

        .redCookedPorkBox => {
            self.texture = gfx.loadTexture("assets/rcp_box_bundle.png");
            self.meal = .init(.redCookedPorkBox);
        },

        .microWave => {
            self.texture = gfx.loadTexture("assets/mo_opening.png");
        },
        .takeoutBox => {
            self.area = .init(position, .{ .x = 92, .y = 100 });
        },
    }

    if (self.texture) |texture| {
        self.area = .init(position, texture.size());
    }

    return self;
}

pub fn pick(self: *Region) void {
    cursor.picked = self.meal;
    scene.returnPosition = cursor.position;
    scene.pickedMeal = cursor.picked;
    scene.pickedRegion = self;

    if (self.type == .takeoutBox) {
        self.meal = null;
        scene.returnPosition = self.area.min;
    }
    if (self.type == .microWave) {
        self.meal = null;
        scene.returnPosition = self.area.min.add(.{ .x = 113, .y = 65 });
    }
}

pub fn place(self: *Region) void {
    if (self.type == .takeoutBox) return self.placeInTakeoutBox();
    if (self.type == .microWave) return self.placeInMicroWave();

    if (self.meal) |meal| {
        if (meal.type == cursor.picked.?.type) {
            cursor.picked = null;
        }
    }
}

pub fn placeInTakeoutBox(self: *Region) void {
    if (self.meal == null) {
        switch (cursor.picked.?.type) {
            .cola, .sprite, .meatBallBox => {},
            .braisedChickenBox, .redCookedPorkBox => {},
            else => {
                self.meal = cursor.picked;
                cursor.picked = null;
            },
        }
        return;
    }

    if (self.meal.?.type == .takeoutBox) {
        self.meal = switch (cursor.picked.?.type) {
            .braisedChickenBox => .init(.braisedChickenCold),
            .meatBallBox => .init(.meatBallCold),
            .redCookedPorkBox => .init(.redCookedPorkCold),
            else => return,
        };
        cursor.picked = null;
    }
}

pub fn placeInMicroWave(self: *Region) void {
    if (self.meal != null) return;

    self.meal = switch (cursor.picked.?.type) {
        .braisedChickenCold => .init(.braisedChickenHot),
        .meatBallCold => .init(.meatBallHot),
        .redCookedPorkCold => .init(.redCookedPorkHot),
        else => return,
    };
    cursor.picked = null;
    audio.playSound("assets/mo_working.ogg");
    self.texture = gfx.loadTexture("assets/mo_working.png");
    self.timer = .init(9);
}

pub fn timerFinished(self: *Region) void {
    if (self.type == .microWave) {
        self.texture = gfx.loadTexture("assets/mo_opening.png");
        audio.playSound("assets/mo_complete.ogg");
    }
    self.timer = null;
}

pub fn renderDeliver(self: *const Region) void {
    var pos = self.area.min.add(.{ .x = -35, .y = 15 });

    // 耐心条的边框
    gfx.draw(gfx.loadTexture("assets/patience_border.png"), pos);

    const percent: f32 = 0.4;

    // 耐心条的长度
    const content = gfx.loadTexture("assets/patience_content.png");
    var dst: math.Rectangle = .init(pos, content.size());
    dst.min.y = dst.max.y - content.height() * percent;
    var src: math.Rectangle = .init(.zero, content.size());
    src.min.y = src.max.y - content.height() * percent;
    gfx.drawOptions(content, .{ .sourceRect = src, .targetRect = dst });

    // 对话框
    pos = self.area.min.add(.{ .x = 175, .y = 55 });
    gfx.draw(gfx.loadTexture("assets/bubble.png"), pos);

    var drinks: u8 = 0;
    for (self.wanted.?.slice(), 0..) |meal, i| {
        const index: f32 = @floatFromInt(i);
        if (meal.type == .cola or meal.type == .sprite) {
            // 所有食物放置后的偏移
            const mealOffsetY = 32 * (i - drinks) + 10;
            const drinkOffsetY = 28 * (drinks / DRINKS_PER_LINE); // 饮料本身的偏移
            const offsetY: f32 = @floatFromInt(mealOffsetY + drinkOffsetY);
            var offset: math.Vector = .init(18, offsetY);

            if (drinks % DRINKS_PER_LINE != 0) offset.x += 32; // 饮料并排放置

            gfx.drawOptions(meal.icon, .{
                .targetRect = .init(pos.add(offset), .init(20, 26)),
                .alpha = 0.35,
            });

            drinks += 1;
            continue;
        }

        const offset: math.Vector = .init(18, 32 * index + 5);
        gfx.drawOptions(meal.icon, .{
            .targetRect = .init(pos.add(offset), .init(45, 25)),
            .alpha = 0.35,
        });
    }
}
```

## 效果

![支持透明度显示][1]

[1]: images/拼好饭传奇11.webp

## 附录
