# 0874-拼好饭传奇-鼠标点击拾取

## 目标

当鼠标点击到可以拾取的食物上时，将其绑定到鼠标上，跟随鼠标移动。

## 环境

- Time 2025-04-19
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1wmy6YuEzh>

## 想法

左键按下鼠标，如果可以拾取食物，则拾取到鼠标上，松开鼠标后食物消失。

## cursor.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");

pub const MealType = enum {
    cola,
    sprite,
    braisedChickenHot,
    braisedChickenCold,
    meatBallHot,
    meatBallCold,
    redCookedPorkHot,
    redCookedPorkCold,
    braisedChickenBox,
    meatBallBox,
    redCookedPorkBox,
    takeoutBox,
};

pub const Meal = struct {
    type: MealType,
    picked: gfx.Texture,

    pub fn init(mealType: MealType) Meal {
        const path = switch (mealType) {
            .cola => "assets/cola.png",
            .sprite => "assets/sprite.png",
            .braisedChickenHot => "assets/bc_hot_picked.png",
            .braisedChickenCold => "assets/bc_cold_picked.png",
            .meatBallHot => "assets/mb_hot_picked.png",
            .meatBallCold => "assets/mb_cold_picked.png",
            .redCookedPorkHot => "assets/rcp_hot_picked.png",
            .redCookedPorkCold => "assets/rcp_cold_picked.png",
            .braisedChickenBox => "assets/bc_box.png",
            .meatBallBox => "assets/mb_box.png",
            .redCookedPorkBox => "assets/rcp_box.png",
            .takeoutBox => "assets/tb_picked.png",
        };

        return Meal{ .type = mealType, .picked = gfx.loadTexture(path) };
    }
};

pub var position: math.Vector = .zero;
pub var leftKeyDown: bool = false;
pub var picked: ?Meal = null;

pub fn event(ev: *const window.Event) void {
    if (ev.type == .MOUSE_MOVE) {
        position = .init(ev.mouse_x, ev.mouse_y);
    }

    if (ev.mouse_button == .LEFT) {
        if (ev.type == .MOUSE_DOWN) {
            leftKeyDown = true;
            switch (math.randU8(1, 3)) {
                1 => audio.playSound("assets/click_1.ogg"),
                2 => audio.playSound("assets/click_2.ogg"),
                3 => audio.playSound("assets/click_3.ogg"),
                else => unreachable,
            }
        }
        if (ev.type == .MOUSE_UP) leftKeyDown = false;
    }
}

pub fn render() void {
    if (picked) |meal| gfx.draw(meal.picked, position);

    if (leftKeyDown) {
        gfx.draw(gfx.loadTexture("assets/cursor_down.png"), position);
    } else {
        gfx.draw(gfx.loadTexture("assets/cursor_idle.png"), position);
    }
}
```

## Region.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");

const cursor = @import("cursor.zig");

const Region = @This();

pub const pickType = gfx.Texture;

pub const RegionType = enum {
    deliver,
    cola,
    sprite,
    takeoutBoxBundle,
    meatBallBox,
    braisedChickenBox,
    redCookedPorkBox,
    microWave,
    takeoutBox,
};

area: math.Rectangle,
texture: ?gfx.Texture,
pick: ?cursor.Meal = null,

pub fn init(x: f32, y: f32, regionType: RegionType) Region {
    const position: math.Vector = .init(x, y);

    var self: Region = .{ .area = .{}, .texture = undefined };
    switch (regionType) {
        .deliver => {
            self.texture = gfx.loadTexture("assets/eleme.png");
        },

        .cola => {
            self.texture = gfx.loadTexture("assets/cola_bundle.png");
            self.pick = .init(.cola);
        },

        .sprite => {
            self.texture = gfx.loadTexture("assets/sprite_bundle.png");
            self.pick = .init(.sprite);
        },

        .takeoutBoxBundle => {
            self.texture = gfx.loadTexture("assets/tb_bundle.png");
            self.pick = .init(.takeoutBox);
        },

        .meatBallBox => {
            self.texture = gfx.loadTexture("assets/mb_box_bundle.png");
            self.pick = .init(.meatBallBox);
        },

        .braisedChickenBox => {
            self.texture = gfx.loadTexture("assets/bc_box_bundle.png");
            self.pick = .init(.braisedChickenBox);
        },

        .redCookedPorkBox => {
            self.texture = gfx.loadTexture("assets/rcp_box_bundle.png");
            self.pick = .init(.redCookedPorkBox);
        },

        .microWave => {
            self.texture = gfx.loadTexture("assets/mo_opening.png");
        },
        .takeoutBox => {
            const texture = gfx.loadTexture("assets/tb.png");
            self.area = .init(position, texture.size());
        },
    }

    if (self.texture) |texture| {
        self.area = .init(position, texture.size());
    }

    return self;
}
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");

const cursor = @import("cursor.zig");
const Region = @import("Region.zig");

// var deliver1: Bundle = undefined;
// var deliver2: Bundle = undefined;
// var deliver3: Bundle = undefined;

// var colaBundle: Bundle = undefined;
// var spriteBundle: Bundle = undefined;
// var tbBundle: Bundle = undefined;
// var mbBoxBundle: Bundle = undefined;
// var bcBoxBundle: Bundle = undefined;
// var rcpBoxBundle: Bundle = undefined;

// var microWave1: Bundle = undefined;
// var microWave2: Bundle = undefined;

// var takeoutBox1: Bundle = undefined;
// var takeoutBox2: Bundle = undefined;
// var takeoutBox3: Bundle = undefined;
// var takeoutBox4: Bundle = undefined;

var regions: [15]Region = undefined;

pub fn init() void {
    window.showCursor(false);

    regions[0] = .init(385, 142, .deliver);
    regions[1] = .init(690, 142, .deliver);
    regions[2] = .init(995, 142, .deliver);

    regions[3] = .init(300, 390, .cola);
    regions[4] = .init(425, 390, .sprite);
    regions[5] = .init(550, 418, .takeoutBoxBundle);

    regions[6] = .init(225, 520, .meatBallBox);
    regions[7] = .init(395, 520, .braisedChickenBox);
    regions[8] = .init(565, 520, .redCookedPorkBox);

    regions[9] = .init(740, 400, .microWave);
    regions[10] = .init(975, 400, .microWave);

    regions[11] = .init(830, 580, .takeoutBox);
    regions[12] = .init(935, 580, .takeoutBox);
    regions[13] = .init(1040, 580, .takeoutBox);
    regions[14] = .init(1145, 580, .takeoutBox);

    audio.playMusic("assets/bgm.ogg");
}

pub fn event(ev: *const window.Event) void {
    cursor.event(ev);
}

pub fn update(delta: f32) void {
    _ = delta;

    if (!cursor.leftKeyDown) cursor.picked = null;

    for (&regions) |value| {
        if (cursor.picked == null and cursor.leftKeyDown) {
            if (value.area.contains(cursor.position)) {
                cursor.picked = value.pick;
            }
        }
    }
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    gfx.draw(gfx.loadTexture("assets/background.png"), .zero);

    for (&regions) |value| {
        if (value.texture) |texture| gfx.draw(texture, value.area.min);
    }

    cursor.render();
}

pub fn deinit() void {
    window.showCursor(true);
    audio.stopMusic();
}
```

## 效果

![鼠标点击拾取][1]

[1]: images/拼好饭传奇04.webp

## 附录
