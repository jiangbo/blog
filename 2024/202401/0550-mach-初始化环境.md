# 0550-mach-初始化环境

## 环境

- Time 2024-06-13
- Zig 0.13.0-dev.351+64ef45eb0
- mach b72f0e11b6d292c2b60789359a61f7ee6d7dc371

## 前言

### 说明

参考资料：

1. <https://github.com/hexops/mach/tree/main/examples>

### 目标

建立 mach 的基础环境，打开一个窗口。

## build.zig.zon

```zig
.{
    .name = "demo",
    .version = "0.0.0",
    .dependencies = .{
        .mach = .{
            .url = "https://pkg.machengine.org/mach/b72f0e11b6d292c2b60789359a61f7ee6d7dc371.tar.gz",
            .hash = "122015e1dac4afaf275f7f2adde3814e6a27f5799cbef96bb487ee305f7e33f4dca3",
        },
    },
    .paths = .{""},
}
```

## build.zig

```zig
const std = @import("std");
const mach = @import("mach");

pub fn build(b: *std.Build) !void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});

    const exe = b.addExecutable(.{
        .name = "demo",
        .root_source_file = b.path("src/main.zig"),
        .target = target,
        .optimize = optimize,
    });
    b.installArtifact(exe);

    const mach_dep = b.dependency("mach", .{
        .target = target,
        .optimize = optimize,
    });

    exe.root_module.addImport("mach", mach_dep.module("mach"));
    @import("mach").link(mach_dep.builder, exe);

    const run_cmd = b.addRunArtifact(exe);
    run_cmd.step.dependOn(b.getInstallStep());
    if (b.args) |args| {
        run_cmd.addArgs(args);
    }
    const run_step = b.step("run", "Run the app");
    run_step.dependOn(&run_cmd.step);

    const exe_unit_tests = b.addTest(.{
        .root_source_file = b.path("src/main.zig"),
        .target = target,
        .optimize = optimize,
    });

    const run_exe_unit_tests = b.addRunArtifact(exe_unit_tests);

    const test_step = b.step("test", "Run unit tests");
    test_step.dependOn(&run_exe_unit_tests.step);
}
```

## main.zig

```zig
const mach = @import("mach");

// 向 mach 注册模块
pub const modules = .{
    mach.Core,
    @import("App.zig"),
};

pub fn main() !void {
    // 初始化注册的模块
    try mach.core.initModule();

    // 主循环
    while (try mach.core.tick()) {}
}
```

## App.zig

```zig
const std = @import("std");
const mach = @import("mach");

pub const name = .app;
pub const Mod = mach.Mod(@This());

pub const systems = .{
    .init = .{ .handler = init },
    .after_init = .{ .handler = afterInit },
    .deinit = .{ .handler = deinit },
    .tick = .{ .handler = tick },
};

pub fn deinit(core: *mach.Core.Mod) void {
    core.schedule(.deinit);
}

fn init(game: *Mod, core: *mach.Core.Mod) !void {
    core.schedule(.init);
    game.schedule(.after_init);
}

fn afterInit(core: *mach.Core.Mod) !void {
    core.schedule(.start);
}

fn tick(core: *mach.Core.Mod) !void {
    var iter = mach.core.pollEvents();
    while (iter.next()) |e| if (e == .close) core.schedule(.exit);

    const view = mach.core.swap_chain.getCurrentTextureView().?;
    defer view.release();

    core.schedule(.present_frame);
}
```

## 效果

![打开窗口][1]

## 总结

引入 mach 引擎，打开窗口。

[1]: images/mach01.png

## 附录
