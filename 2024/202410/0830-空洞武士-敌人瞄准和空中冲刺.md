# 0830-空洞武士-敌人瞄准和空中冲刺

## 目标

在之前一节的基础上，新增瞄准和空中冲刺状态。

## 环境

- Time 2025-04-06
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

空中冲刺时，方向是瞄准玩家脚下的地面，不是直接获取的玩家位置。

## Enemy.zig

新增了瞄准和空中冲刺状态。

```zig
const std = @import("std");

const gfx = @import("../graphics.zig");
const math = @import("../math.zig");
const window = @import("../window.zig");
const scene = @import("../scene.zig");
const SharedActor = @import("actor.zig").SharedActor;

const Enemy = @This();

shared: SharedActor,
state: State = .idle,

idleTimer: window.Timer = .init(0.5),
idleAnimation: gfx.SliceFrameAnimation,

jumpAnimation: gfx.SliceFrameAnimation,

fallAnimation: gfx.SliceFrameAnimation,

aimTimer: window.Timer = .init(0.5),
aimAnimation: gfx.SliceFrameAnimation,

dashInAirAnimation: gfx.SliceFrameAnimation,

dashTimer: window.Timer = .init(0.5),
dashOnFloorAnimation: gfx.SliceFrameAnimation,

pub fn init() Enemy {
    timer = std.time.Timer.start() catch unreachable;
    var enemy: Enemy = .{
        .shared = .{
            .position = .{ .x = 1050, .y = 200 },
            .faceLeft = true,
            .health = 10,
        },
        .idleAnimation = .load("assets/enemy/idle/{}.png", 5),
        .jumpAnimation = .load("assets/enemy/jump/{}.png", 8),
        .fallAnimation = .load("assets/enemy/fall/{}.png", 4),
        .aimAnimation = .load("assets/enemy/aim/{}.png", 9),
        .dashInAirAnimation = .load("assets/enemy/dash_in_air/{}.png", 2),
        .dashOnFloorAnimation = .load("assets/enemy/dash_on_floor/{}.png", 2),
    };

    enemy.state.enter(&enemy);
    enemy.jumpAnimation.loop = false;
    return enemy;
}

var timer: std.time.Timer = undefined;

pub fn update(self: *Enemy, delta: f32) void {
    self.shared.update(delta);
    self.state.update(self, delta);
}

pub fn render(self: *const Enemy) void {
    self.shared.render();
    self.state.render(self);
}

fn changeState(self: *Enemy, new: State) void {
    self.state.exit(self);
    new.enter(self);
}

fn play(self: *const Enemy, animation: *const gfx.SliceFrameAnimation) void {
    gfx.playSliceFlipX(animation, self.shared.position, !self.shared.faceLeft);
}

fn isEnraged(self: *const Enemy) bool {
    return self.shared.health <= 5;
}

const State = union(enum) {
    idle: IdleState,
    jump: JumpState,
    fall: FallState,
    aim: AimState,
    dashInAir: DashInAirState,
    // dashOnFloor: DashOnFloorState,

    fn enter(self: State, enemy: *Enemy) void {
        switch (self) {
            inline else => |case| @TypeOf(case).enter(enemy),
        }
    }

    fn update(self: State, enemy: *Enemy, delta: f32) void {
        switch (self) {
            inline else => |case| @TypeOf(case).update(enemy, delta),
        }
    }

    fn render(self: State, enemy: *const Enemy) void {
        switch (self) {
            inline else => |case| @TypeOf(case).render(enemy),
        }
    }

    fn exit(self: State, enemy: *Enemy) void {
        switch (self) {
            inline else => |case| @TypeOf(case).exit(enemy),
        }
    }
};

const IdleState = struct {
    fn enter(enemy: *Enemy) void {
        enemy.state = .idle;
        enemy.shared.velocity.x = 0;

        const max: f32 = if (enemy.isEnraged()) 0.5 else 0.25;
        enemy.idleTimer.duration = window.randomFloat(0, max);
        enemy.idleTimer.reset();
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.idleAnimation.update(delta);
        if (enemy.idleTimer.isRunningAfterUpdate(delta)) return;

        if (enemy.isEnraged()) return updateEnraged(enemy);

        const rand = window.rand.intRangeLessThanBiased(u8, 0, 100);
        switch (rand) {
            0...50 => {
                if (enemy.shared.isOnFloor()) {
                    enemy.changeState(.jump);
                } else {
                    enemy.changeState(.idle);
                }
            },
            else => enemy.changeState(.idle),
        }
    }

    fn updateEnraged(enemy: *Enemy) void {
        _ = enemy;
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.idleAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.idleAnimation.reset();
        const playerPosition = scene.player.shared.position;
        enemy.shared.faceLeft = playerPosition.x < enemy.shared.position.x;
    }
};

const JumpState = struct {
    const SPEED_JUMP = 1000;

    fn enter(enemy: *Enemy) void {
        enemy.state = .jump;
        enemy.shared.velocity.y = -SPEED_JUMP;
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.jumpAnimation.update(delta);
        if (enemy.shared.velocity.y < 0) return;

        const rand = window.rand.intRangeLessThanBiased(u8, 0, 100);
        if (enemy.isEnraged()) {
            switch (rand) {
                0...49 => enemy.changeState(.aim),
                50...79 => enemy.changeState(.fall),
                else => enemy.changeState(.fall),
            }
        } else {
            switch (rand) {
                0...49 => enemy.changeState(.aim),
                50...79 => enemy.changeState(.fall),
                else => enemy.changeState(.aim),
            }
        }
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.jumpAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.jumpAnimation.reset();
    }
};

const FallState = struct {
    fn enter(enemy: *Enemy) void {
        enemy.state = .fall;
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.fallAnimation.update(delta);
        if (enemy.shared.isOnFloor()) enemy.changeState(.idle);
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.fallAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.fallAnimation.reset();
    }
};

const AimState = struct {
    fn enter(enemy: *Enemy) void {
        enemy.state = .aim;
        enemy.shared.velocity = .{};
        enemy.shared.enableGravity = false;
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.aimAnimation.update(delta);
        if (enemy.aimTimer.isRunningAfterUpdate(delta)) return;

        enemy.changeState(.dashInAir);
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.aimAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.aimTimer.reset();
        enemy.shared.enableGravity = true;
        enemy.aimAnimation.reset();
    }
};

const DashInAirState = struct {
    const SPEED_DASH = 1500;

    fn enter(enemy: *Enemy) void {
        enemy.state = .dashInAir;
        enemy.shared.enableGravity = false;

        const playerPosition = scene.player.shared.position;
        const target: math.Vector = .{ .x = playerPosition.x, .y = SharedActor.FLOOR_Y };
        const direction = target.sub(enemy.shared.position).normalize();
        enemy.shared.velocity = direction.scale(SPEED_DASH);
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.dashInAirAnimation.update(delta);
        if (enemy.shared.isOnFloor()) enemy.changeState(.idle);
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.dashInAirAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.dashInAirAnimation.reset();
        enemy.shared.enableGravity = true;
    }
};
```

## 效果

![敌人瞄准和空中冲刺][1]

[1]: images/空洞武士12.webp

## 附录
