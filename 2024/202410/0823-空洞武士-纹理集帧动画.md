# 0823-空洞武士-纹理集帧动画

## 目标

上一节实现了多张图片实现帧动画，这一节使用纹理图集，也就是同一张图片实现帧动画。

## 环境

- Time 2025-03-31
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

感觉上纹理图集实现帧动画应该是比较科学的，后面考虑删除单张图片生成帧动画。

## cache.zig

```zig
const std = @import("std");
const gfx = @import("graphics.zig");
const math = @import("math.zig");

var allocator: std.mem.Allocator = undefined;

pub fn init(alloc: std.mem.Allocator) void {
    allocator = alloc;
}

pub fn deinit() void {
    Texture.deinit();
    TextureSlice.deinit();
    RectangleSlice.deinit();
}

pub const Texture = struct {
    const stbImage = @import("c.zig").stbImage;

    var cache: std.StringHashMapUnmanaged(gfx.Texture) = undefined;

    pub fn load(path: [:0]const u8) gfx.Texture {
        const entry = cache.getOrPut(allocator, path) catch unreachable;
        if (entry.found_existing) return entry.value_ptr.*;

        std.log.info("loading texture from: {s}", .{path});
        const image = stbImage.load(path) catch unreachable;
        defer stbImage.unload(image);

        const texture = gfx.Texture.init(image.width, image.height, image.data);
        entry.value_ptr.* = texture;
        entry.key_ptr.* = allocator.dupe(u8, path) catch unreachable;
        return texture;
    }

    pub fn loadSlice(textures: []gfx.Texture, comptime pathFmt: []const u8, from: u8) void {
        std.log.info("loading texture slice : {s}", .{pathFmt});

        var buffer: [128]u8 = undefined;
        for (from..from + textures.len) |index| {
            const path = std.fmt.bufPrintZ(&buffer, pathFmt, .{index});

            const texture = Texture.load(path catch unreachable);
            textures[index - from] = texture;
        }
    }

    pub fn deinit() void {
        var keyIter = cache.keyIterator();
        while (keyIter.next()) |key| allocator.free(key.*);
        cache.deinit(allocator);
    }
};

pub const TextureSlice = struct {
    var cache: std.StringHashMapUnmanaged([]gfx.Texture) = undefined;

    pub fn load(comptime pathFmt: []const u8, from: u8, len: u8) []const gfx.Texture {
        const entry = cache.getOrPut(allocator, pathFmt) catch unreachable;
        if (entry.found_existing) return entry.value_ptr.*;

        const textures = allocator.alloc(gfx.Texture, len) catch unreachable;

        Texture.loadSlice(textures, pathFmt, from);
        entry.value_ptr.* = textures;
        entry.key_ptr.* = allocator.dupe(u8, pathFmt) catch unreachable;
        return textures;
    }

    pub fn deinit() void {
        var iterator = cache.iterator();
        while (iterator.next()) |entry| {
            allocator.free(entry.key_ptr.*);
            allocator.free(entry.value_ptr.*);
        }
        cache.deinit(allocator);
    }
};

pub const RectangleSlice = struct {
    var cache: std.StringHashMapUnmanaged([]math.Rectangle) = undefined;

    pub fn load(path: []const u8, count: u8) []math.Rectangle {
        const entry = cache.getOrPut(allocator, path) catch unreachable;
        if (entry.found_existing) return entry.value_ptr.*;

        const slice = allocator.alloc(math.Rectangle, count) catch unreachable;
        entry.value_ptr.* = slice;
        return slice;
    }

    pub fn deinit() void {
        var iterator = cache.valueIterator();
        while (iterator.next()) |value| allocator.free(value.*);
        cache.deinit(allocator);
    }
};
```

## animation.zig

将动画相关的单独提取到了一个文件中。

```zig
const std = @import("std");

const window = @import("window.zig");
const cache = @import("cache.zig");
const math = @import("math.zig");
const Texture = @import("gpu.zig").Texture;

const Anchor = enum {
    topLeft,
    topCenter,
    topRight,
    centerLeft,
    centerCenter,
    centerRight,
    bottomLeft,
    bottomCenter,
    bottomRight,
};

pub const FrameAnimation = SliceFrameAnimation;

pub const SliceFrameAnimation = struct {
    timer: window.Timer,
    index: usize = 0,
    loop: bool = true,

    textures: []const Texture,

    pub fn init(textures: []const Texture) SliceFrameAnimation {
        return .{ .textures = textures, .timer = .init(100) };
    }

    pub fn load(comptime pathFmt: []const u8, max: u8) SliceFrameAnimation {
        const textures = cache.TextureSlice.load(pathFmt, 1, max);
        return .init(textures);
    }

    pub fn update(self: *@This(), delta: f32) void {
        if (self.timer.isRunningAfterUpdate(delta)) return;

        if (self.index == self.textures.len - 1) {
            if (self.loop) {
                self.index = 0;
                self.timer.reset();
            }
        } else {
            self.timer.reset();
            self.index += 1;
        }
    }

    pub fn reset(self: *@This()) void {
        self.timer.reset();
        self.index = 0;
    }

    pub fn finished(self: *const @This()) bool {
        return self.timer.finished and !self.loop;
    }
};

pub const AtlasFrameAnimation = struct {
    timer: window.Timer,
    index: usize = 0,
    loop: bool = true,
    texture: Texture,
    frames: []const math.Rectangle,

    pub fn init(texture: Texture, frames: []const math.Rectangle) AtlasFrameAnimation {
        return .{ .texture = texture, .frames = frames, .timer = .init(100) };
    }

    pub fn load(path: [:0]const u8, count: u8) AtlasFrameAnimation {
        const texture = cache.Texture.load(path);

        const frames = cache.RectangleSlice.load(path, count);

        const width = @divExact(texture.width(), @as(f32, @floatFromInt(frames.len)));
        var rect: math.Rectangle = .{ .w = width, .h = texture.height() };

        for (0..frames.len) |index| {
            rect.x = @as(f32, @floatFromInt(index)) * width;
            frames[index] = rect;
        }

        return .init(texture, frames);
    }

    pub fn update(self: *@This(), delta: f32) void {
        if (self.timer.isRunningAfterUpdate(delta)) return;

        if (self.index == self.frames.len - 1) {
            if (self.loop) {
                self.index = 0;
                self.timer.reset();
            }
        } else {
            self.timer.reset();
            self.index += 1;
        }
    }

    pub fn reset(self: *@This()) void {
        self.timer.reset();
        self.index = 0;
    }

    pub fn finished(self: *const @This()) bool {
        return self.timer.finished and !self.loop;
    }
};
```

## graphics.zig

```zig
const std = @import("std");

const cache = @import("cache.zig");
const gpu = @import("gpu.zig");
const animation = @import("animation.zig");

pub const Texture = gpu.Texture;

pub var renderer: gpu.Renderer = undefined;
var matrix: [16]f32 = undefined;
var passEncoder: gpu.RenderPassEncoder = undefined;

pub fn init(width: f32, height: f32) void {
    matrix = .{
        2 / width, 0.0,         0.0, 0.0,
        0.0,       2 / -height, 0.0, 0.0,
        0.0,       0.0,         1,   0.0,
        -1,        1,           0,   1.0,
    };
    renderer = gpu.Renderer.init();
}

pub fn loadTexture(path: [:0]const u8) Texture {
    return cache.Texture.load(path);
}

pub fn beginDraw() void {
    passEncoder = gpu.CommandEncoder.beginRenderPass(.{ .r = 1, .b = 1, .a = 1.0 });
    renderer.renderPass = passEncoder;
}

pub fn draw(tex: Texture, x: f32, y: f32) void {
    drawFlipX(tex, x, y, false);
}

pub fn drawFlipX(tex: Texture, x: f32, y: f32, flipX: bool) void {
    const target: gpu.Rectangle = .{ .x = x, .y = y };
    const src = gpu.Rectangle{
        .w = if (flipX) -tex.width() else tex.width(),
    };

    drawOptions(tex, .{ .sourceRect = src, .targetRect = target });
}

pub const DrawOptions = struct {
    sourceRect: ?gpu.Rectangle = null,
    targetRect: gpu.Rectangle,
};

pub fn drawOptions(texture: Texture, options: DrawOptions) void {
    renderer.draw(.{
        .uniform = .{ .vp = matrix },
        .texture = texture,
        .sourceRect = options.sourceRect,
        .targetRect = options.targetRect,
    });
}

pub fn endDraw() void {
    passEncoder.submit();
}

pub const FrameAnimation = animation.FrameAnimation;
pub const SliceFrameAnimation = animation.SliceFrameAnimation;
pub const AtlasFrameAnimation = animation.AtlasFrameAnimation;

pub fn playSlice(frameAnimation: *const FrameAnimation, x: f32, y: f32) void {
    playSliceFlipX(frameAnimation, x, y, false);
}

pub fn playSliceFlipX(frame: *const FrameAnimation, x: f32, y: f32, flipX: bool) void {
    drawFlipX(frame.textures[frame.index], x, y, flipX);
}

pub fn playAtlas(frameAnimation: *const AtlasFrameAnimation, x: f32, y: f32) void {
    playAtlasFlipX(frameAnimation, x, y, false);
}

pub fn playAtlasFlipX(frame: *const AtlasFrameAnimation, x: f32, y: f32, flipX: bool) void {
    var src = frame.frames[frame.index];
    const dst: gpu.Rectangle = .{ .x = x, .y = y, .w = src.w };
    if (flipX) src.w = -src.w;
    drawOptions(frame.texture, .{ .sourceRect = src, .targetRect = dst });
}
```

## main.zig

```zig
const std = @import("std");

const cache = @import("cache.zig");
const window = @import("window.zig");
const gfx = @import("graphics.zig");

var enemyRunAnimation: gfx.SliceFrameAnimation = undefined;
var playerRunAnimation: gfx.AtlasFrameAnimation = undefined;

pub fn init() void {
    cache.init(allocator);
    gfx.init(window.width, window.height);

    enemyRunAnimation = .load("assets/enemy/run/{}.png", 8);
    playerRunAnimation = .load("assets/player/run.png", 10);
}

pub fn event(ev: *const window.Event) void {
    _ = ev;
}

pub fn update() void {
    const delta = window.deltaMillisecond();
    enemyRunAnimation.update(delta);
    playerRunAnimation.update(delta);
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    gfx.draw(gfx.loadTexture("assets/background.png"), 0, 0);
    gfx.playSliceFlipX(&enemyRunAnimation, 0, 0, true);

    var x = window.width - enemyRunAnimation.textures[0].width();
    gfx.playSlice(&enemyRunAnimation, x, 0);

    const y = window.height - playerRunAnimation.texture.height();
    gfx.playAtlas(&playerRunAnimation, 0, y);

    x = window.width - playerRunAnimation.frames[0].w;
    gfx.playAtlasFlipX(&playerRunAnimation, x, y, true);
}

pub fn deinit() void {
    cache.deinit();
}

var allocator: std.mem.Allocator = undefined;

pub fn main() void {
    var debugAllocator = std.heap.DebugAllocator(.{}).init;
    defer _ = debugAllocator.deinit();

    allocator = debugAllocator.allocator();
    window.width = 1280;
    window.height = 720;

    var prng = std.Random.DefaultPrng.init(@intCast(std.time.timestamp()));
    window.rand = prng.random();

    window.run(.{
        .title = "空洞武士",
        .init = init,
        .event = event,
        .update = update,
        .render = render,
        .deinit = deinit,
    });
}
```

## 效果

![纹理集帧动画][1]

[1]: images/空洞武士05.webp

## 附录
