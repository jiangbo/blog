# 0814-植物明星大乱斗-纹理源和目的位置

## 目标

修改渲染纹理的方法，支持指定纹理的源位置和目的位置。

## 环境

- Time 2025-03-20
- Zig 0.14.0-dev.3298+6fe1993d8

## 参考

1. <https://www.bilibili.com/video/BV1jx4y1t7eP>

## 想法

支持指定源纹理和目的纹理的位置，使用 AI 帮助写的，目前对其理解上还有点问题，不过可以先使用着。

## gpu.zig

只列出了修改的地方。

```zig
    pub const DrawOptions = struct {
        uniform: UniformParams,
        texture: Texture,
        sourceRect: ?Rectangle = null,
        targetRect: Rectangle,
    };

    pub fn draw(self: *Renderer, options: DrawOptions) void {
        const src: Rectangle = options.sourceRect orelse .{
            .width = options.texture.width,
            .height = options.texture.height,
        };

        const texU0 = src.x / options.texture.width;
        const texU1 = (src.x + src.width) / options.texture.width;
        const texV0 = src.y / options.texture.height;
        const texV1 = (src.y + src.height) / options.texture.height;

        const target = options.targetRect;
        const w, const h = .{ target.width, target.height };
        const vertexBuffer = sk.gfx.makeBuffer(.{
            .data = sk.gfx.asRange(&[_]f32{
                // 顶点和颜色
                target.x,     target.y + h, 0.5, 1.0, 1.0, 1.0, texU0, texV1,
                target.x + w, target.y + h, 0.5, 1.0, 1.0, 1.0, texU1, texV1,
                target.x + w, target.y,     0.5, 1.0, 1.0, 1.0, texU1, texV0,
                target.x,     target.y,     0.5, 1.0, 1.0, 1.0, texU0, texV0,
            }),
        });

        self.bind.bindVertexBuffer(0, vertexBuffer);
        self.bind.bindUniformBuffer(options.uniform);

        self.renderPass.setPipeline(pipeline.?);
        self.bind.bindTexture(render.IMG_tex, options.texture);
        self.renderPass.setBindGroup(self.bind);
        sk.gfx.draw(0, 6, 1);
        sk.gfx.destroyBuffer(vertexBuffer);
    }
```

## graphics.zig

```zig
const std = @import("std");
const cache = @import("cache.zig");
const gpu = @import("gpu.zig");
const window = @import("window.zig");

pub const Texture = gpu.Texture;

pub const Camera = struct {
    const zm = @import("zmath");

    proj: zm.Mat,

    pub fn init(width: f32, height: f32) Camera {
        const proj = zm.orthographicOffCenterLh(0, width, 0, height, 0, 1);
        return .{ .proj = proj };
    }

    pub fn vp(self: Camera) zm.Mat {
        return self.proj;
    }
};

pub var camera: Camera = undefined;
pub var renderer: gpu.Renderer = undefined;
var passEncoder: gpu.RenderPassEncoder = undefined;

pub fn init(width: f32, height: f32) void {
    camera = Camera.init(width, height);
    renderer = gpu.Renderer.init();
}

pub fn loadTexture(path: [:0]const u8) ?Texture {
    return cache.TextureCache.load(path);
}

pub fn loadTextures(textures: []Texture, comptime pathFmt: []const u8, from: u8) void {
    std.log.info("loading texture slice : {s}", .{pathFmt});

    var buffer: [128]u8 = undefined;
    for (from..from + textures.len) |index| {
        const path = std.fmt.bufPrintZ(&buffer, pathFmt, .{index});

        const texture = loadTexture(path catch unreachable);
        textures[index - from] = texture.?;
    }
}

pub fn beginDraw() void {
    passEncoder = gpu.CommandEncoder.beginRenderPass(.{ .r = 1, .b = 1, .a = 1.0 });
    renderer.renderPass = passEncoder;
}

pub fn draw(x: f32, y: f32, tex: Texture) void {
    drawFlipX(x, y, tex, false);
}

pub fn drawFlipX(x: f32, y: f32, tex: Texture, flipX: bool) void {
    const target: gpu.Rectangle = .{
        .x = x - window.shakeCamera.shakingX,
        .y = y - window.shakeCamera.shakingY,
        .width = tex.width,
        .height = tex.height,
    };

    const src = gpu.Rectangle{
        .width = if (flipX) -tex.width else tex.width,
        .height = tex.height,
    };

    drawOptions(tex, .{ .sourceRect = src, .targetRect = target });
}

pub const DrawOptions = struct {
    sourceRect: ?gpu.Rectangle = null,
    targetRect: gpu.Rectangle,
};

pub fn drawOptions(texture: Texture, options: DrawOptions) void {
    renderer.draw(.{
        .uniform = .{ .vp = camera.vp() },
        .texture = texture,
        .sourceRect = options.sourceRect,
        .targetRect = options.targetRect,
    });
}

pub fn endDraw() void {
    passEncoder.submit();
}

pub fn TextureArray(max: u8) type {
    return struct {
        textures: [max]Texture,

        pub fn init(comptime pathFmt: []const u8) @This() {
            var self = @This(){ .textures = undefined };
            cache.TextureSliceCache.loadToSlice(&self.textures, pathFmt, 1);
            return self;
        }

        pub fn asSlice(self: @This()) []const Texture {
            return self.textures[0..];
        }
    };
}

pub const FrameAnimation = SliceFrameAnimation;

pub const SliceFrameAnimation = struct {
    timer: window.Timer,
    index: usize = 0,
    loop: bool = true,

    textures: []const Texture,

    pub fn init(textures: []const Texture) SliceFrameAnimation {
        return .{ .textures = textures, .timer = .init(100) };
    }

    pub fn load(comptime pathFmt: []const u8, max: u8) SliceFrameAnimation {
        const textures = cache.TextureSliceCache.load(pathFmt, 1, max);
        return .init(textures.?);
    }

    pub fn update(self: *@This(), delta: f32) void {
        if (self.timer.isRunningAfterUpdate(delta)) return;

        if (self.index == self.textures.len - 1) {
            if (self.loop) {
                self.index = 0;
                self.timer.reset();
            }
        } else {
            self.timer.reset();
            self.index += 1;
        }
    }

    pub fn finished(self: *@This()) bool {
        return self.timer.finished and !self.loop;
    }

    pub fn play(self: @This(), x: f32, y: f32) void {
        self.playFlipX(x, y, false);
    }

    pub fn playFlipX(self: @This(), x: f32, y: f32, flipX: bool) void {
        drawFlipX(x, y, self.textures[self.index], flipX);
    }
};
```

## scene/SelectorScene.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const audio = @import("zaudio");

const scene = @import("../scene.zig");
const SelectorScene = @This();

const offsetX = 50;

background: gfx.Texture,

peaShooterBackground: gfx.Texture,
sunFlowerBackground: gfx.Texture,
imageVS: gfx.Texture,
imageTip: gfx.Texture,

image1P: gfx.Texture,
image2P: gfx.Texture,
image1PDesc: gfx.Texture,
image2PDesc: gfx.Texture,
imageGrave: gfx.Texture,

image1PButtonIdle: gfx.Texture,
image2PButtonIdle: gfx.Texture,
image1PButtonDown: gfx.Texture,
image2PButtonDown: gfx.Texture,

animationPeaShooterIdle: gfx.FrameAnimation,
animationSunFlowerIdle: gfx.FrameAnimation,

soundUISwitch: *audio.Sound = undefined,
soundUIConfirm: *audio.Sound = undefined,

backgroundOffsetX: f32 = 0,

button1PLeftDown: bool = false,
button1PRightDown: bool = false,
button2PLeftDown: bool = false,
button2PRightDown: bool = false,

pub fn init() SelectorScene {
    std.log.info("selector scene init", .{});

    var self: SelectorScene = undefined;
    self.background = gfx.loadTexture("assets/selector_background.png").?;
    self.peaShooterBackground = gfx.loadTexture("assets/peashooter_selector_background.png").?;
    self.sunFlowerBackground = gfx.loadTexture("assets/sunflower_selector_background.png").?;

    self.imageVS = gfx.loadTexture("assets/VS.png").?;
    self.imageTip = gfx.loadTexture("assets/selector_tip.png").?;
    self.image1P = gfx.loadTexture("assets/1P.png").?;
    self.image2P = gfx.loadTexture("assets/2P.png").?;
    self.image1PDesc = gfx.loadTexture("assets/1P_desc.png").?;
    self.image2PDesc = gfx.loadTexture("assets/2P_desc.png").?;
    self.imageGrave = gfx.loadTexture("assets/gravestone.png").?;

    self.image1PButtonIdle = gfx.loadTexture("assets/1P_selector_btn_idle.png").?;
    self.image2PButtonIdle = gfx.loadTexture("assets/2P_selector_btn_idle.png").?;
    self.image1PButtonDown = gfx.loadTexture("assets/1P_selector_btn_down.png").?;
    self.image2PButtonDown = gfx.loadTexture("assets/2P_selector_btn_down.png").?;

    self.animationPeaShooterIdle = .load("assets/peashooter_idle_{}.png", 9);
    self.animationSunFlowerIdle = .load("assets/sunflower_idle_{}.png", 8);

    self.soundUISwitch = scene.audioEngine.createSoundFromFile(
        "assets/ui_switch.wav",
        .{},
    ) catch unreachable;
    self.soundUIConfirm = scene.audioEngine.createSoundFromFile(
        "assets/ui_confirm.wav",
        .{},
    ) catch unreachable;

    return self;
}

pub fn enter(self: *SelectorScene) void {
    std.log.info("selector scene enter", .{});
    _ = self;
}

pub fn exit(self: *SelectorScene) void {
    std.log.info("selector scene exit", .{});
    _ = self;
}

pub fn event(self: *SelectorScene, ev: *const window.Event) void {
    if (ev.type == .KEY_DOWN) switch (ev.key_code) {
        .A => self.button1PLeftDown = true,
        .D => self.button1PRightDown = true,
        .LEFT => self.button2PLeftDown = true,
        .RIGHT => self.button2PRightDown = true,
        else => {},
    } else if (ev.type == .KEY_UP) switch (ev.key_code) {
        .A => {
            self.button1PLeftDown = false;
            self.changePlayerType(&scene.playerType1);
        },
        .D => {
            self.button1PRightDown = false;
            self.changePlayerType(&scene.playerType1);
        },
        .LEFT => {
            self.button2PLeftDown = false;
            self.changePlayerType(&scene.playerType2);
        },
        .RIGHT => {
            self.button2PRightDown = false;
            self.changePlayerType(&scene.playerType2);
        },
        .ENTER => {
            scene.changeCurrentScene(.game);
            self.soundUIConfirm.start() catch unreachable;
        },
        else => {},
    };
}

fn changePlayerType(self: *SelectorScene, player: *scene.PlayerType) void {
    player.* = if (player.* == .peaShooter) .sunFlower else .peaShooter;
    self.soundUISwitch.start() catch unreachable;
}

pub fn update(self: *SelectorScene) void {
    self.backgroundOffsetX += window.deltaMillisecond() * 0.2;
    if (self.backgroundOffsetX >= self.peaShooterBackground.width)
        self.backgroundOffsetX = 0;
    self.animationPeaShooterIdle.update(window.deltaMillisecond());
    self.animationSunFlowerIdle.update(window.deltaMillisecond());
}

pub fn render(self: *SelectorScene) void {
    self.renderBackground();

    self.renderStatic();

    self.renderButton();

    self.renderPlayerAnimation();

    self.renderPlayerName();
}

fn renderBackground(self: *SelectorScene) void {
    gfx.draw(0, 0, self.background);

    const width = self.peaShooterBackground.width;
    var texture = if (scene.playerType2 == .peaShooter)
        self.peaShooterBackground
    else
        self.sunFlowerBackground;
    gfx.draw(self.backgroundOffsetX - width, 0, texture);

    gfx.drawOptions(texture, .{
        .sourceRect = .{
            .width = width - self.backgroundOffsetX,
            .height = self.peaShooterBackground.height,
        },
        .targetRect = .{
            .x = self.backgroundOffsetX,
            .width = width - self.backgroundOffsetX,
            .height = self.peaShooterBackground.height,
        },
    });

    texture = if (scene.playerType1 == .peaShooter)
        self.peaShooterBackground
    else
        self.sunFlowerBackground;

    gfx.drawOptions(texture, .{
        .sourceRect = .{
            .x = -self.backgroundOffsetX,
            .width = self.backgroundOffsetX - width,
            .height = self.sunFlowerBackground.height,
        },
        .targetRect = .{
            .x = window.width - width,
            .width = width - self.backgroundOffsetX,
            .height = self.sunFlowerBackground.height,
        },
    });
    gfx.drawFlipX(window.width - self.backgroundOffsetX, 0, texture, true);
}

fn renderStatic(self: *SelectorScene) void {
    var w = window.width - self.imageVS.width;
    const h = window.height - self.imageVS.height;
    gfx.draw(w / 2, h / 2, self.imageVS);

    w = window.width - self.imageTip.width;
    gfx.draw(w / 2, window.height - 125, self.imageTip);

    w = window.width / 2 - self.image1P.width;
    const pos1PY = 35;
    gfx.draw(w / 2 - offsetX, pos1PY, self.image1P);
    w = window.width / 2 - self.image2P.width;
    gfx.draw(window.width / 2 + w / 2 + offsetX, 35, self.image2P);

    w = window.width / 2 - self.image1PDesc.width;
    gfx.draw(w / 2 - offsetX, window.height - 150, self.image1PDesc);
    w = window.width / 2 - self.image2PDesc.width;
    gfx.draw(window.width / 2 + w / 2 + offsetX, window.height - 150, self.image2PDesc);

    w = window.width / 2 - self.imageGrave.width;
    const posGraveY = pos1PY + self.image1P.height + 35;
    gfx.draw(w / 2 - offsetX, posGraveY, self.imageGrave);
    w = window.width / 2 - self.imageGrave.width;
    gfx.drawFlipX(window.width / 2 + w / 2 + offsetX, posGraveY, self.imageGrave, true);
}

fn renderButton(self: *SelectorScene) void {
    var x = (window.width / 2 - self.imageGrave.width) / 2 - offsetX;
    const y = self.image1P.height + 70;

    var buttonX = x - self.image1PButtonIdle.width;
    const buttonY = y + (self.imageGrave.height - self.image1PButtonIdle.height) / 2;

    var down, var idle = .{ self.image1PButtonDown, self.image1PButtonIdle };
    var texture = if (self.button1PLeftDown) down else idle;
    gfx.drawFlipX(buttonX, buttonY, texture, true);

    texture = if (self.button1PRightDown) down else idle;
    buttonX = x + self.imageGrave.width;
    gfx.draw(buttonX, buttonY, texture);

    x = window.width / 2 + (window.width / 2 - self.imageGrave.width) / 2 + offsetX;
    buttonX = x - self.image2PButtonIdle.width;
    down, idle = .{ self.image2PButtonDown, self.image2PButtonIdle };
    texture = if (self.button2PLeftDown) down else idle;
    gfx.drawFlipX(buttonX, buttonY, texture, true);

    buttonX = x + self.imageGrave.width;
    texture = if (self.button2PRightDown) down else idle;
    gfx.draw(buttonX, buttonY, texture);
}

fn renderPlayerAnimation(self: *SelectorScene) void {
    const y = self.image1P.height + 70;
    var w = window.width / 2 - self.animationPeaShooterIdle.textures[0].width;
    self.animationPlay(scene.playerType1, w / 2 - offsetX, y + 80, false);

    w = window.width / 2 - self.animationSunFlowerIdle.textures[0].width;
    self.animationPlay(scene.playerType2, window.width / 2 + w / 2 + offsetX, y + 80, true);
}

fn renderPlayerName(_: *SelectorScene) void {
    var name = if (scene.playerType1 == .peaShooter) "Pea" else "Sun";
    window.displayText(11, 19, name);

    name = if (scene.playerType2 == .peaShooter) "Pea" else "Sun";
    window.displayText(49, 19, name);
    window.endDisplayText();
}

fn animationPlay(self: *SelectorScene, player: scene.PlayerType, x: f32, y: f32, flip: bool) void {
    switch (player) {
        .sunFlower => self.animationSunFlowerIdle.playFlipX(x, y, flip),
        .peaShooter => self.animationPeaShooterIdle.playFlipX(x, y, flip),
    }
}

pub fn deinit(self: *SelectorScene) void {
    std.log.info("selector scene deinit", .{});
    self.soundUISwitch.destroy();
    self.soundUIConfirm.destroy();
}
```

## scene/GameScene.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const audio = @import("zaudio");

const scene = @import("../scene.zig");
const Bullet = @import("bullet.zig").Bullet;
const player = @import("player.zig");
const GameScene = @This();

player1: player.Player,
player2: player.Player,

bullets: std.BoundedArray(Bullet, 64),

imageSky: gfx.Texture,
imageHill: gfx.Texture,

platforms: [4]Platform,

backgroundSound: *audio.Sound,

pub fn init() GameScene {
    std.log.info("game scene init", .{});

    window.shakeCamera = window.ShakeCamera.init(0, 0);
    var self: GameScene = undefined;

    self.imageSky = gfx.loadTexture("assets/sky.png").?;
    self.imageHill = gfx.loadTexture("assets/hills.png").?;
    self.bullets = std.BoundedArray(Bullet, 64).init(0) catch unreachable;
    self.backgroundSound = scene.audioEngine.createSoundFromFile(
        "assets/bgm_game.mp3",
        .{ .flags = .{ .stream = true, .looping = true } },
    ) catch unreachable;

    self.initPlatforms();
    @import("bullet.zig").init();

    return self;
}

fn initPlatforms(self: *GameScene) void {
    var texture = gfx.loadTexture("assets/platform_large.png").?;
    var platform: Platform = .{ .x = 122, .y = 455, .texture = texture };
    platform.shape.left = platform.x + 30;
    platform.shape.right = platform.x + texture.width - 30;
    platform.shape.y = platform.y + 60;
    self.platforms[0] = platform;

    texture = gfx.loadTexture("assets/platform_small.png").?;
    platform = .{ .x = 175, .y = 360, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[1] = platform;

    platform = .{ .x = 855, .y = 360, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[2] = platform;

    platform = .{ .x = 515, .y = 225, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[3] = platform;
}

pub fn enter(self: *GameScene) void {
    std.log.info("game scene enter", .{});
    self.backgroundSound.start() catch unreachable;

    self.player1 = .init(scene.playerType1, 200, 50, false);
    self.player2 = .init(scene.playerType2, 975, 50, true);
    self.player2.p1 = false;
}

pub fn exit(self: *GameScene) void {
    std.log.info("game scene exit", .{});
    self.backgroundSound.stop() catch unreachable;
}

pub fn event(self: *GameScene, ev: *const window.Event) void {
    switch (ev.key_code) {
        .A, .D, .W, .F, .G => self.player1.event(ev),
        .LEFT, .RIGHT, .UP, .PERIOD, .SLASH => self.player2.event(ev),
        else => {},
    }
}

pub fn update(self: *GameScene) void {
    const deltaTime = window.deltaMillisecond();

    self.player1.update(deltaTime);
    self.player2.update(deltaTime);

    self.updateBullets(deltaTime);
    window.shakeCamera.update(deltaTime);
}

fn updateBullets(self: *GameScene, delta: f32) void {
    for (self.bullets.slice(), 0..) |*bullet, index| {
        bullet.update(delta);

        if (bullet.p1 and !bullet.collide and !self.player2.invulnerable) {
            if (self.player2.isCollide(bullet)) {
                bullet.collidePlayer();
                self.player2.collideBullet(bullet);
            }
        }

        if (!bullet.p1 and !bullet.collide and !self.player1.invulnerable) {
            if (self.player1.isCollide(bullet)) {
                bullet.collidePlayer();
                self.player1.collideBullet(bullet);
            }
        }
        if (bullet.dead) _ = self.bullets.swapRemove(index);
    }
}

pub fn render(self: *GameScene) void {
    var x = window.width - self.imageSky.width;
    var y = window.height - self.imageSky.height;
    gfx.draw(x / 2, y / 2, self.imageSky);

    x = window.width - self.imageHill.width;
    y = window.height - self.imageHill.height;
    gfx.draw(x / 2, y / 2, self.imageHill);

    for (&self.platforms) |platform| {
        gfx.draw(platform.x, platform.y, platform.texture);
    }

    self.player1.render();
    self.player2.render();

    for (self.bullets.slice()) |*bullet| bullet.render();
}

pub fn deinit(self: *GameScene) void {
    std.log.info("game scene deinit", .{});
    @import("bullet.zig").deinit();
    self.backgroundSound.destroy();
}

const Platform = struct {
    x: f32,
    y: f32,
    texture: gfx.Texture,
    shape: Collision = .{ .left = 0, .right = 0, .y = 0 },

    const Collision = struct { left: f32, right: f32, y: f32 };
};

const StatusBar = struct {
    x: f32,
    y: f32,
    texture: gfx.Texture,
};
```

## 效果

![纹理源和目的位置][1]

[1]: images/植物明星大乱斗25.webp

## 附录
