# 0807-植物明星大乱斗-发射水平子弹

## 目标

实现发送水平子弹的功能，目前两个角色都只能发射水平方向上的子弹，并且没有动画。

## 环境

- Time 2025-03-15
- Zig 0.14.0-dev.3298+6fe1993d8

## 参考

1. <https://www.bilibili.com/video/BV1jx4y1t7eP>

## 想法

先做一个发送水平豌豆子弹的功能，后续复杂的功能，再慢慢实现。

## scene/player.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

const scene = @import("../scene.zig");

const Vector = struct {
    x: f32 = 0,
    y: f32 = 0,
    z: f32 = 0,

    pub fn add(a: Vector, b: Vector) Vector {
        return .{ .x = a.x + b.x, .y = a.y + b.y, .z = a.z + b.z };
    }

    pub fn scale(a: Vector, b: f32) Vector {
        return .{ .x = a.x * b, .y = a.y * b, .z = a.z * b };
    }
};

pub const Bullet = struct {
    size: Vector,
    position: Vector,
    velocity: Vector,
    damage: f32,
    dead: bool = false,
    p1: bool = true,

    texture: gfx.Texture = undefined,
};

pub const Player = union(scene.PlayerType) {
    peaShooter: PeaShooterPlayer,
    sunFlower: SunFlowerPlayer,

    pub fn init(playerType: scene.PlayerType, x: f32, y: f32, faceLeft: bool) Player {
        return switch (playerType) {
            .peaShooter => .{ .peaShooter = .init(x, y, faceLeft) },
            .sunFlower => .{ .sunFlower = .init(x, y, faceLeft) },
        };
    }

    pub fn event(self: *Player, ev: *const window.Event) void {
        switch (self.*) {
            inline else => |*s| switch (ev.type) {
                .KEY_DOWN => switch (ev.key_code) {
                    .A, .LEFT => {
                        s.shared.leftKeyDown = true;
                        s.shared.facingLeft = true;
                    },
                    .D, .RIGHT => {
                        s.shared.rightKeyDown = true;
                        s.shared.facingLeft = false;
                    },
                    .W, .UP => {
                        if (s.shared.velocity != 0) return;
                        s.shared.velocity += SharedPlayer.jumpVelocity;
                    },
                    .F => s.attack(true),
                    .PERIOD => s.attack(false),
                    else => {},
                },
                .KEY_UP => switch (ev.key_code) {
                    .A, .LEFT => s.shared.leftKeyDown = false,
                    .D, .RIGHT => s.shared.rightKeyDown = false,
                    else => {},
                },
                else => {},
            },
        }
    }

    pub fn update(self: *Player, delta: f32) void {
        switch (self.*) {
            inline else => |*player| {
                var direction: f32 = 0;
                if (player.shared.leftKeyDown) direction -= 1;
                if (player.shared.rightKeyDown) direction += 1;
                player.shared.x += direction * SharedPlayer.runVelocity * delta;

                if (player.shared.leftKeyDown or player.shared.rightKeyDown) {
                    player.animationRun.update(delta);
                } else {
                    player.animationIdle.update(delta);
                }

                moveAndCollide(&player.shared, delta);

                player.shared.attackTimer -= delta;
            },
        }
    }

    fn moveAndCollide(player: anytype, delta: f32) void {
        const velocity = player.velocity + SharedPlayer.gravity * delta;
        const y = player.y + velocity * delta;

        const platforms = &scene.gameScene.platforms;
        for (platforms) |*platform| {
            if (player.x + player.width < platform.shape.left) continue;
            if (player.x > platform.shape.right) continue;
            if (y + player.height < platform.shape.y) continue;

            const deltaPosY = player.velocity * delta;
            const lastFootPosY = player.y + player.height - deltaPosY;

            if (lastFootPosY <= platform.shape.y) {
                player.y = platform.shape.y - player.height;
                player.velocity = 0;
                break;
            }
        } else {
            player.y = y;
            player.velocity = velocity;
        }
    }

    pub fn draw(self: Player) void {
        switch (self) {
            inline else => |*s| {
                if (s.shared.leftKeyDown) {
                    s.animationRun.playFlipX(s.shared.x, s.shared.y, true);
                } else if (s.shared.rightKeyDown) {
                    s.animationRun.playFlipX(s.shared.x, s.shared.y, false);
                } else {
                    s.animationIdle.playFlipX(s.shared.x, s.shared.y, s.shared.facingLeft);
                }
            },
        }
    }
};

const SharedPlayer = struct {
    x: f32,
    y: f32,
    facingLeft: bool,
    leftKeyDown: bool = false,
    rightKeyDown: bool = false,
    velocity: f32 = 0,
    width: f32 = 96,
    height: f32 = 96,

    attackInterval: f32 = 200,
    attackTimer: f32 = 0,

    hp: u32 = 100,
    mp: u32 = 100,

    const runVelocity: f32 = 0.55;
    const gravity: f32 = 1.6e-3;
    const jumpVelocity: f32 = -0.85;

    pub fn attack(self: *SharedPlayer, p1: bool, texturePath: [:0]const u8) void {
        if (self.attackTimer > 0) return;
        self.attackTimer = self.attackInterval;

        const x: f32 = if (self.facingLeft) self.x else self.x + self.width;
        const texture = gfx.loadTexture(texturePath).?;
        const bullet = Bullet{
            .size = .{ .x = texture.width, .y = texture.height },
            .position = .{ .x = x - texture.width / 2, .y = self.y },
            .velocity = .{ .x = if (self.facingLeft) -0.5 else 0.5 },
            .damage = 10,
            .p1 = p1,
            .texture = texture,
        };

        scene.gameScene.bullets.append(bullet) catch unreachable;
    }
};

const PeaShooterPlayer = struct {
    shared: SharedPlayer,

    animationIdle: gfx.BoundedFrameAnimation(9),
    animationRun: gfx.BoundedFrameAnimation(5),

    pub fn init(x: f32, y: f32, faceLeft: bool) PeaShooterPlayer {
        return .{
            .shared = .{ .x = x, .y = y, .facingLeft = faceLeft },
            .animationIdle = .init("assets/peashooter_idle_{}.png"),
            .animationRun = .init("assets/peashooter_run_{}.png"),
        };
    }

    pub fn attack(self: *PeaShooterPlayer, p1: bool) void {
        self.shared.attack(p1, "assets/pea.png");
    }
};

const SunFlowerPlayer = struct {
    shared: SharedPlayer,

    animationIdle: gfx.BoundedFrameAnimation(8),
    animationRun: gfx.BoundedFrameAnimation(5),

    pub fn init(x: f32, y: f32, faceLeft: bool) SunFlowerPlayer {
        return .{
            .shared = .{ .x = x, .y = y, .facingLeft = faceLeft },
            .animationIdle = .init("assets/sunflower_idle_{}.png"),
            .animationRun = .init("assets/sunflower_run_{}.png"),
        };
    }

    pub fn attack(self: *SunFlowerPlayer, p1: bool) void {
        self.shared.attack(p1, "assets/sun_1.png");
    }
};
```

## scene/GameScene.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

const scene = @import("../scene.zig");
const player = @import("player.zig");
const GameScene = @This();

player1: player.Player,
player2: player.Player,

bullets: std.BoundedArray(player.Bullet, 64),

imageSky: gfx.Texture,
imageHill: gfx.Texture,

platforms: [4]Platform,

pub fn init() GameScene {
    std.log.info("game scene init", .{});

    var self: GameScene = undefined;

    self.imageSky = gfx.loadTexture("assets/sky.png").?;
    self.imageHill = gfx.loadTexture("assets/hills.png").?;
    self.bullets = std.BoundedArray(player.Bullet, 64).init(0) catch unreachable;

    self.initPlatforms();

    return self;
}

fn initPlatforms(self: *GameScene) void {
    var texture = gfx.loadTexture("assets/platform_large.png").?;
    var platform: Platform = .{ .x = 122, .y = 455, .texture = texture };
    platform.shape.left = platform.x + 30;
    platform.shape.right = platform.x + texture.width - 30;
    platform.shape.y = platform.y + 60;
    self.platforms[0] = platform;

    texture = gfx.loadTexture("assets/platform_small.png").?;
    platform = .{ .x = 175, .y = 360, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[1] = platform;

    platform = .{ .x = 855, .y = 360, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[2] = platform;

    platform = .{ .x = 515, .y = 225, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[3] = platform;
}

pub fn enter(self: *GameScene) void {
    std.log.info("game scene enter", .{});

    self.player1 = .init(scene.playerType1, 200, 50, false);
    self.player2 = .init(scene.playerType2, 975, 50, true);
}

pub fn exit(self: *GameScene) void {
    std.log.info("game scene exit", .{});
    _ = self;
}

pub fn event(self: *GameScene, ev: *const window.Event) void {
    switch (ev.key_code) {
        .A, .D, .W, .F, .G => self.player1.event(ev),
        .LEFT, .RIGHT, .UP, .PERIOD, .SLASH => self.player2.event(ev),
        else => {},
    }
}

pub fn update(self: *GameScene) void {
    const deltaTime = window.deltaMillisecond();

    self.player1.update(deltaTime);
    self.player2.update(deltaTime);

    self.updateBullets(deltaTime);
}

fn updateBullets(self: *GameScene, delta: f32) void {
    for (self.bullets.slice(), 0..) |*bullet, index| {
        bullet.position = bullet.position.add(bullet.velocity.scale(delta));

        if (bullet.position.x + bullet.size.x < 0 or bullet.position.x > window.width)
            bullet.dead = true;

        if (bullet.position.y + bullet.size.y < 0 or bullet.position.y > window.height)
            bullet.dead = true;

        if (bullet.dead) {
            _ = self.bullets.swapRemove(index);
        }
    }
}

pub fn render(self: *GameScene) void {
    var x = window.width - self.imageSky.width;
    var y = window.height - self.imageSky.height;
    gfx.draw(x / 2, y / 2, self.imageSky);

    x = window.width - self.imageHill.width;
    y = window.height - self.imageHill.height;
    gfx.draw(x / 2, y / 2, self.imageHill);

    for (&self.platforms) |platform| {
        gfx.draw(platform.x, platform.y, platform.texture);
    }

    self.player1.draw();
    self.player2.draw();

    for (self.bullets.slice()) |bullet| {
        gfx.draw(bullet.position.x, bullet.position.y, bullet.texture);
    }
}

const Platform = struct {
    x: f32,
    y: f32,
    texture: gfx.Texture,
    shape: Collision = .{ .left = 0, .right = 0, .y = 0 },

    const Collision = struct { left: f32, right: f32, y: f32 };
};
```

## 效果

![发射水平子弹][1]

[1]: images/植物明星大乱斗19.webp

## 附录
