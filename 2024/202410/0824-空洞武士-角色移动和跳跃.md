# 0824-空洞武士-角色移动和跳跃

## 目标

实现角色的移动和跳跃功能。

## 环境

- Time 2025-04-05
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

跳跃时，Y 方向上速度为0。把上个项目中移动相关的代码借鉴过来了。教程中后续要使用状态机，不清楚 Zig 支持怎么样。
把 delta time 修改成秒了，好像业界都是使用秒而不是毫秒。

## window.zig

将 delta time 修改成秒，并且直接使用 sokol 中自带的方法。

```zig
...
pub fn deltaSecond() f32 {
    return @floatCast(sk.app.frameDuration());
}
...
```

## graphics.zig

渲染时，根据锚点，计算出偏移，然后再进行渲染。

```zig
...
pub fn playAtlasFlipX(frame: *const AtlasFrameAnimation, pos: math.Vector, flipX: bool) void {
    var src = frame.frames[frame.index];

    const offset: math.Vector = switch (frame.anchor) {
        .bottomCenter => .{ .x = pos.x - src.w / 2, .y = pos.y - src.h },
        else => unreachable,
    };

    const dst: gpu.Rectangle = .{ .x = offset.x, .y = offset.y, .w = src.w };
    if (flipX) src.w = -src.w;
    drawOptions(frame.texture, .{ .sourceRect = src, .targetRect = dst });
}
...
```

## animation.zig

增加了一个对齐的属性。

```zig
...
pub const AtlasFrameAnimation = struct {
    timer: window.Timer,
    index: usize = 0,
    loop: bool = true,
    texture: Texture,
    frames: []const math.Rectangle,
    anchor: Anchor = .bottomCenter,
}
...
```

## actor.zig

```zig
const std = @import("std");

const gfx = @import("graphics.zig");
const math = @import("math.zig");
const window = @import("window.zig");

const sharedPlayer = struct {
    const floorY = 620;
    const gravity = 980;

    enableGravity: bool = true,
    position: math.Vector = .{ .x = 100, .y = floorY },
    velocity: math.Vector = .{},
    idleAnimation: gfx.AtlasFrameAnimation = undefined,
    runAnimation: gfx.AtlasFrameAnimation = undefined,
    faceLeft: bool = false,
    running: bool = false,

    pub fn update(self: *sharedPlayer, delta: f32) void {
        if (self.enableGravity) {
            self.velocity.y += gravity * delta;
        }

        self.position = self.position.add(self.velocity.scale(delta));
        if (self.position.y >= floorY) {
            self.position.y = floorY;
            self.velocity.y = 0;
        }

        self.position.x = std.math.clamp(self.position.x, 0, window.width);
        if (self.running) {
            self.runAnimation.update(delta);
        } else {
            self.idleAnimation.update(delta);
        }
    }

    pub fn render(self: *const sharedPlayer) void {
        if (self.running) {
            gfx.playAtlasFlipX(&self.runAnimation, self.position, self.faceLeft);
        } else {
            gfx.playAtlasFlipX(&self.idleAnimation, self.position, self.faceLeft);
        }
    }
};

pub const Player = struct {
    shared: sharedPlayer,

    pub fn init() Player {
        return .{
            .shared = .{
                .idleAnimation = .load("assets/player/idle.png", 5),
                .runAnimation = .load("assets/player/run.png", 10),
            },
        };
    }

    pub fn deinit() void {}

    pub fn event(self: *Player, ev: *const window.Event) void {
        if (ev.type == .KEY_DOWN) {
            switch (ev.key_code) {
                .A => {
                    self.shared.velocity.x = -300;
                    self.shared.faceLeft = true;
                    self.shared.running = true;
                },
                .D => {
                    self.shared.velocity.x = 300;
                    self.shared.faceLeft = false;
                    self.shared.running = true;
                },
                .W => {
                    if (self.shared.velocity.y != 0) return;
                    self.shared.velocity.y -= 780;
                },
                else => {},
            }
        } else if (ev.type == .KEY_UP) {
            switch (ev.key_code) {
                .A, .D => {
                    self.shared.velocity.x = 0;
                    self.shared.running = false;
                },
                else => {},
            }
        }
    }

    pub fn update(self: *Player, delta: f32) void {
        self.shared.update(delta);
    }

    pub fn render(self: *const Player) void {
        self.shared.render();
    }
};
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const actor = @import("actor.zig");

var player: actor.Player = undefined;

pub fn init() void {
    player = actor.Player.init();
}

pub fn deinit() void {}

pub fn event(ev: *const window.Event) void {
    player.event(ev);
}

pub fn update() void {
    const delta = window.deltaSecond();
    player.update(delta);
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    renderBackground();
    player.render();
}

pub fn renderBackground() void {
    const background = gfx.loadTexture("assets/background.png");
    const width = window.width - background.width();
    const height = window.height - background.height();
    gfx.draw(background, width / 2, height / 2);
}
```

## main.zig

将主文件中的内容提取到 scene.zig 中。

```zig
const std = @import("std");

const cache = @import("cache.zig");
const window = @import("window.zig");
const gfx = @import("graphics.zig");
const scene = @import("scene.zig");

pub fn init() void {
    cache.init(allocator);
    gfx.init(window.width, window.height);
    scene.init();
}

pub fn event(ev: *const window.Event) void {
    scene.event(ev);
}

pub fn update() void {
    scene.update();
}

pub fn render() void {
    scene.render();
}

pub fn deinit() void {
    scene.deinit();
    cache.deinit();
}

var allocator: std.mem.Allocator = undefined;

pub fn main() void {
    var debugAllocator = std.heap.DebugAllocator(.{}).init;
    defer _ = debugAllocator.deinit();

    allocator = debugAllocator.allocator();
    window.width = 1280;
    window.height = 720;

    var prng = std.Random.DefaultPrng.init(@intCast(std.time.timestamp()));
    window.rand = prng.random();

    window.run(.{
        .title = "空洞武士",
        .init = init,
        .event = event,
        .update = update,
        .render = render,
        .deinit = deinit,
    });
}
```

## 效果

![角色移动和跳跃][1]

[1]: images/空洞武士06.webp

## 附录
