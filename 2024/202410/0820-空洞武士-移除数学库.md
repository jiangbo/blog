# 0820-空洞武士-移除数学库

## 目标

清除屏幕颜色，并且移除数学库。

## 环境

- Time 2025-03-30
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

之前的项目中引入了 zmath，但是好像没有遇到数学计算，所以就先移除了。

## single.glsl

```glsl
@vs vs
layout(binding=0) uniform vs_params {
    mat4 vp;
};

in vec4 position;
in vec4 color0;
in vec2 texcoord0;

out vec4 color;
out vec2 uv;

void main() {
    gl_Position = vp * position;
    color = color0;
    uv = texcoord0;
}
@end

@fs fs

layout(binding=0) uniform texture2D tex;
layout(binding=0) uniform sampler smp;

in vec4 color;
in vec2 uv;
out vec4 frag_color;

void main() {
     frag_color = texture(sampler2D(tex, smp), uv) * color;
}
@end

@program single vs fs
```

## math.zig

单独建立了一个数学模块。

```zig
const std = @import("std");

pub const Vector = Vector3;
pub const Vector3 = struct {
    x: f32 = 0,
    y: f32 = 0,
    z: f32 = 0,

    pub fn init(x: f32, y: f32) Vector3 {
        return .{ .x = x, .y = y, .z = 0 };
    }

    pub fn add(self: Vector3, other: Vector3) Vector3 {
        return .{ .x = self.x + other.x, .y = self.y + other.y, .z = self.z + other.z };
    }

    pub fn sub(self: Vector3, other: Vector3) Vector3 {
        return .{ .x = self.x - other.x, .y = self.y - other.y, .z = self.z - other.z };
    }

    pub fn length(self: Vector3) f32 {
        return std.math.sqrt(self.x * self.x + self.y * self.y + self.z * self.z);
    }

    pub fn normalize(self: Vector3) Vector3 {
        const len = self.length();
        return Vector3.init(self.x / len, self.y / len, self.z / len);
    }
};

pub const Rectangle = struct {
    x: f32 = 0,
    y: f32 = 0,
    w: f32 = 0,
    h: f32 = 0,

    pub fn init(x1: f32, y1: f32, x2: f32, y2: f32) Rectangle {
        return .{ .x = x1, .y = y1, .w = x2 - x1, .h = y2 - y1 };
    }

    pub fn right(self: Rectangle) f32 {
        return self.x + self.w;
    }

    pub fn bottom(self: Rectangle) f32 {
        return self.y + self.h;
    }

    pub fn intersects(self: Rectangle, other: Rectangle) bool {
        return self.left < other.right and self.right > other.left and
            self.top < other.bottom and self.bottom > other.top;
    }

    pub fn contains(self: Rectangle, x: f32, y: f32) bool {
        return x >= self.left and x < self.right and
            y >= self.top and y < self.bottom;
    }
};
```

## gpu.zig

把之前的代码拷贝过来，修改了一下 math 相关的代码。

```zig
const std = @import("std");
const sk = @import("sokol");

const render = @import("shader/single.glsl.zig");
const math = @import("math.zig");

pub const Color = sk.gfx.Color;
pub const Buffer = sk.gfx.Buffer;

pub const Texture = struct {
    width: f32,
    height: f32,
    value: sk.gfx.Image,

    pub fn init(width: u32, height: u32, data: []u8) Texture {
        const image = sk.gfx.allocImage();

        sk.gfx.initImage(image, .{
            .width = @as(i32, @intCast(width)),
            .height = @as(i32, @intCast(height)),
            .pixel_format = .RGBA8,
            .data = init: {
                var imageData = sk.gfx.ImageData{};
                imageData.subimage[0][0] = sk.gfx.asRange(data);
                break :init imageData;
            },
        });

        return .{
            .width = @floatFromInt(width),
            .height = @floatFromInt(height),
            .value = image,
        };
    }
};

pub const BindGroup = struct {
    value: sk.gfx.Bindings = .{},
    uniform: render.VsParams = undefined,

    pub fn bindIndexBuffer(self: *BindGroup, buffer: Buffer) void {
        self.value.index_buffer = buffer;
    }

    pub fn bindVertexBuffer(self: *BindGroup, index: u32, buffer: Buffer) void {
        self.value.vertex_buffers[index] = buffer;
    }

    pub fn bindTexture(self: *BindGroup, index: u32, texture: Texture) void {
        self.value.images[index] = texture.value;
    }

    pub fn bindSampler(self: *BindGroup, index: u32, sampler: Sampler) void {
        self.value.samplers[index] = sampler.value;
    }

    pub fn bindUniformBuffer(self: *BindGroup, uniform: UniformParams) void {
        self.uniform = uniform;
    }
};

pub const CommandEncoder = struct {
    pub fn beginRenderPass(color: Color) RenderPassEncoder {
        return RenderPassEncoder.begin(color);
    }
};

pub const Rectangle = math.Rectangle;

pub const RenderPassEncoder = struct {
    pub fn begin(color: Color) RenderPassEncoder {
        var action = sk.gfx.PassAction{};
        action.colors[0] = .{ .load_action = .CLEAR, .clear_value = color };
        sk.gfx.beginPass(.{ .action = action, .swapchain = sk.glue.swapchain() });
        return RenderPassEncoder{};
    }

    pub fn setPipeline(self: *RenderPassEncoder, pipeline: RenderPipeline) void {
        _ = self;
        sk.gfx.applyPipeline(pipeline.value);
    }

    pub fn setBindGroup(self: *RenderPassEncoder, group: BindGroup) void {
        _ = self;
        sk.gfx.applyUniforms(render.UB_vs_params, sk.gfx.asRange(&group.uniform));
        sk.gfx.applyBindings(group.value);
    }

    pub fn draw(self: *RenderPassEncoder, number: u32) void {
        _ = self;
        sk.gfx.draw(0, number, 1);
    }

    pub fn submit(self: *RenderPassEncoder) void {
        _ = self;
        sk.gfx.endPass();
        sk.gfx.commit();
    }
};

const UniformParams = render.VsParams;

pub const Renderer = struct {
    bind: BindGroup,
    renderPass: RenderPassEncoder,

    var indexBuffer: ?Buffer = null;
    var pipeline: ?RenderPipeline = null;
    var sampler: ?Sampler = null;

    pub fn init() Renderer {
        var self = Renderer{ .bind = .{}, .renderPass = undefined };

        indexBuffer = indexBuffer orelse sk.gfx.makeBuffer(.{
            .type = .INDEXBUFFER,
            .data = sk.gfx.asRange(&[_]u16{ 0, 1, 2, 0, 2, 3 }),
        });
        self.bind.bindIndexBuffer(indexBuffer.?);

        sampler = sampler orelse Sampler.nearest();
        self.bind.bindSampler(render.SMP_smp, sampler.?);

        pipeline = pipeline orelse RenderPipeline{
            .value = sk.gfx.makePipeline(.{
                .shader = sk.gfx.makeShader(render.singleShaderDesc(sk.gfx.queryBackend())),
                .layout = init: {
                    var l = sk.gfx.VertexLayoutState{};
                    l.attrs[render.ATTR_single_position].format = .FLOAT3;
                    l.attrs[render.ATTR_single_color0].format = .FLOAT3;
                    l.attrs[render.ATTR_single_texcoord0].format = .FLOAT2;
                    break :init l;
                },
                .colors = init: {
                    var c: [4]sk.gfx.ColorTargetState = @splat(.{});
                    c[0] = .{
                        .blend = .{
                            .enabled = true,
                            .src_factor_rgb = .SRC_ALPHA,
                            .dst_factor_rgb = .ONE_MINUS_SRC_ALPHA,
                        },
                    };
                    break :init c;
                },
                .index_type = .UINT16,
                .depth = .{ .compare = .LESS_EQUAL, .write_enabled = true },
            }),
        };

        return self;
    }

    pub const DrawOptions = struct {
        uniform: UniformParams,
        texture: Texture,
        sourceRect: ?Rectangle = null,
        targetRect: Rectangle,
    };

    pub fn draw(self: *Renderer, options: DrawOptions) void {
        var src: Rectangle = options.sourceRect orelse .{
            .w = options.texture.width,
            .h = options.texture.height,
        };

        if (src.w == 0) src.w = options.texture.width;
        if (src.h == 0) src.h = options.texture.height;
        const U0 = src.x / options.texture.width;
        const U1 = src.right() / options.texture.width;
        const V0 = src.y / options.texture.height;
        const V1 = src.bottom() / options.texture.height;

        var dst = options.targetRect;
        if (dst.w == 0) dst.w = options.texture.width;
        if (dst.h == 0) dst.h = options.texture.height;
        const vertexBuffer = sk.gfx.makeBuffer(.{
            .data = sk.gfx.asRange(&[_]f32{
                // 顶点和颜色
                dst.x,       dst.bottom(), 0.5, 1.0, 1.0, 1.0, U0, V1,
                dst.right(), dst.bottom(), 0.5, 1.0, 1.0, 1.0, U1, V1,
                dst.right(), dst.y,        0.5, 1.0, 1.0, 1.0, U1, V0,
                dst.x,       dst.y,        0.5, 1.0, 1.0, 1.0, U0, V0,
            }),
        });

        self.bind.bindVertexBuffer(0, vertexBuffer);
        self.bind.bindUniformBuffer(options.uniform);

        self.renderPass.setPipeline(pipeline.?);
        self.bind.bindTexture(render.IMG_tex, options.texture);
        self.renderPass.setBindGroup(self.bind);
        sk.gfx.draw(0, 6, 1);
        sk.gfx.destroyBuffer(vertexBuffer);
    }
};

pub const RenderPipeline = struct {
    value: sk.gfx.Pipeline,
};

pub const Sampler = struct {
    value: sk.gfx.Sampler,

    pub fn liner() Sampler {
        const sampler = sk.gfx.makeSampler(.{
            .min_filter = .LINEAR,
            .mag_filter = .LINEAR,
        });
        return .{ .value = sampler };
    }

    pub fn nearest() Sampler {
        const sampler = sk.gfx.makeSampler(.{
            .min_filter = .NEAREST,
            .mag_filter = .NEAREST,
        });
        return .{ .value = sampler };
    }
};
```

## graphics.zig

先移除了动画相关的代码。

```zig
const std = @import("std");
// const cache = @import("cache.zig");
const gpu = @import("gpu.zig");
const window = @import("window.zig");

pub const Texture = gpu.Texture;

pub var renderer: gpu.Renderer = undefined;
var matrix: [16]f32 = undefined;
var passEncoder: gpu.RenderPassEncoder = undefined;

pub fn init(width: f32, height: f32) void {
    matrix = .{
        2 / width, 0.0,         0.0, 0.0,
        0.0,       2 / -height, 0.0, 0.0,
        0.0,       0.0,         1,   0.0,
        -1,        1,           0,   1.0,
    };
    renderer = gpu.Renderer.init();
}

// pub fn loadTexture(path: [:0]const u8) ?Texture {
//     return cache.TextureCache.load(path);
// }

// pub fn loadTextures(textures: []Texture, comptime pathFmt: []const u8, from: u8) void {
//     std.log.info("loading texture slice : {s}", .{pathFmt});

//     var buffer: [128]u8 = undefined;
//     for (from..from + textures.len) |index| {
//         const path = std.fmt.bufPrintZ(&buffer, pathFmt, .{index});

//         const texture = loadTexture(path catch unreachable);
//         textures[index - from] = texture.?;
//     }
// }

pub fn beginDraw() void {
    passEncoder = gpu.CommandEncoder.beginRenderPass(.{ .r = 1, .b = 1, .a = 1.0 });
    renderer.renderPass = passEncoder;
}

pub fn draw(x: f32, y: f32, tex: Texture) void {
    drawFlipX(x, y, tex, false);
}

pub fn drawFlipX(x: f32, y: f32, tex: Texture, flipX: bool) void {
    const target: gpu.Rectangle = .{ .x = x, .y = y };
    const src = gpu.Rectangle{
        .w = if (flipX) -tex.width else tex.width,
    };

    drawOptions(tex, .{ .sourceRect = src, .targetRect = target });
}

pub const DrawOptions = struct {
    sourceRect: ?gpu.Rectangle = null,
    targetRect: gpu.Rectangle,
};

pub fn drawOptions(texture: Texture, options: DrawOptions) void {
    renderer.draw(.{
        .uniform = .{ .vp = matrix },
        .texture = texture,
        .sourceRect = options.sourceRect,
        .targetRect = options.targetRect,
    });
}

pub fn endDraw() void {
    passEncoder.submit();
}
```

## main.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");

pub fn init() void {
    gfx.init(window.width, window.height);
}

pub fn event(ev: *const window.Event) void {
    _ = ev;
}

pub fn update() void {}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();
}

pub fn deinit() void {}

var allocator: std.mem.Allocator = undefined;

pub fn main() void {
    var debugAllocator = std.heap.DebugAllocator(.{}).init;
    defer _ = debugAllocator.deinit();

    allocator = debugAllocator.allocator();
    window.width = 1280;
    window.height = 720;

    var prng = std.Random.DefaultPrng.init(@intCast(std.time.timestamp()));
    window.rand = prng.random();

    window.run(.{
        .title = "空洞武士",
        .init = init,
        .event = event,
        .update = update,
        .render = render,
        .deinit = deinit,
    });
}
```

## 效果

![清除屏幕][1]

[1]: images/空洞武士02.png

## 附录
