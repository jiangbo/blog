# 0828-空洞武士-敌人空闲状态机

## 目标

将前一节直接实现的动画，修改为状态机实现。

## 环境

- Time 2025-04-06
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

敌人可以感知到玩家的位置，自动根据玩家的位置来调整自身的朝向。

## window.zig

新增了一个生成随机 f32 数字的方法，感觉这个方法放到 math.zig 更合理。

```zig
...
pub fn randomFloat(min: f32, max: f32) f32 {
    return rand.float(f32) * (max - min) + min;
}
...
```

## scene.zig

将场景中的角色和敌人修改为公共字段，可以外部访问。

```zig
...
pub var player: actor.Player = undefined;
pub var enemy: actor.Enemy = undefined;
...
```

## actor.zig

新增了生命值属性。

```zig
const std = @import("std");

const gfx = @import("../graphics.zig");
const math = @import("../math.zig");
const window = @import("../window.zig");

pub const Player = @import("Player.zig");
pub const Enemy = @import("Enemy.zig");

pub const SharedActor = struct {
    pub const FLOOR_Y = 620;
    const GRAVITY = 980 * 2;

    enableGravity: bool = true,
    position: math.Vector,
    velocity: math.Vector = .{},
    faceLeft: bool = false,
    health: u8 = 5,

    pub fn update(self: *SharedActor, delta: f32) void {
        if (self.enableGravity) {
            self.velocity.y += GRAVITY * delta;
        }

        self.position = self.position.add(self.velocity.scale(delta));
        if (self.position.y >= FLOOR_Y) {
            self.position.y = FLOOR_Y;
            self.velocity.y = 0;
        }

        self.position.x = std.math.clamp(self.position.x, 0, window.width);
    }

    pub fn render(self: *const SharedActor) void {
        _ = self;
    }
};
```

## Enemy.zig

新增了空闲状态。

```zig
const std = @import("std");

const gfx = @import("../graphics.zig");
const math = @import("../math.zig");
const window = @import("../window.zig");
const scene = @import("../scene.zig");
const SharedActor = @import("actor.zig").SharedActor;

const Enemy = @This();

shared: SharedActor,
state: State = .idle,

idleTimer: window.Timer = .init(0.5),
idleAnimation: gfx.SliceFrameAnimation,

pub fn init() Enemy {
    var enemy: Enemy = .{
        .shared = .{
            .position = .{ .x = 700, .y = SharedActor.FLOOR_Y },
            .faceLeft = true,
            .health = 10,
        },
        .idleAnimation = .load("assets/enemy/idle/{}.png", 5),
    };

    enemy.state.enter(&enemy);
    return enemy;
}

pub fn update(self: *Enemy, delta: f32) void {
    self.shared.update(delta);
    self.state.update(self, delta);
}

pub fn render(self: *const Enemy) void {
    self.shared.render();
    self.state.render(self);
}

fn changeState(self: *Enemy, new: State) void {
    self.state.exit(self);
    new.enter(self);
}

fn play(self: *const Enemy, animation: *const gfx.SliceFrameAnimation) void {
    gfx.playSliceFlipX(animation, self.shared.position, !self.shared.faceLeft);
}

const State = union(enum) {
    idle: IdleState,

    fn enter(self: State, enemy: *Enemy) void {
        switch (self) {
            inline else => |case| @TypeOf(case).enter(enemy),
        }
    }

    fn update(self: State, enemy: *Enemy, delta: f32) void {
        switch (self) {
            inline else => |case| @TypeOf(case).update(enemy, delta),
        }
    }

    fn render(self: State, enemy: *const Enemy) void {
        switch (self) {
            inline else => |case| @TypeOf(case).render(enemy),
        }
    }

    fn exit(self: State, enemy: *Enemy) void {
        switch (self) {
            inline else => |case| @TypeOf(case).exit(enemy),
        }
    }
};

const IdleState = struct {
    fn enter(enemy: *Enemy) void {
        enemy.state = .idle;
        enemy.shared.velocity.x = 0;

        const max: f32 = if (enemy.shared.health > 5) 0.5 else 0.25;
        enemy.idleTimer.duration = window.randomFloat(0, max);
        enemy.idleTimer.reset();
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.idleAnimation.update(delta);
        if (enemy.idleTimer.isRunningAfterUpdate(delta)) return;

        enemy.changeState(.idle);
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.idleAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.idleAnimation.reset();
        const playerPosition = scene.player.shared.position;
        enemy.shared.faceLeft = playerPosition.x < enemy.shared.position.x;
    }
};
```

## 效果

![敌人空闲状态机][1]

[1]: images/空洞武士10.webp

## 附录
