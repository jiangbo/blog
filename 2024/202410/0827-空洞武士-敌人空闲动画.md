# 0827-空洞武士-敌人空闲动画

## 目标

敌人目前使用的还是切片动画，需要将之前切片动画相关的地方进行修改。

## 环境

- Time 2025-04-05
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

将角色和敌人分开到了两个文件，感觉上 `SharedActor` 目前好像没有多少作用。

## animation.zig

主要修改了切片动画中的对齐属性。

```zig
const std = @import("std");

const window = @import("window.zig");
const cache = @import("cache.zig");
const math = @import("math.zig");
const Texture = @import("gpu.zig").Texture;

const Anchor = enum {
    topLeft,
    topCenter,
    topRight,
    centerLeft,
    centerCenter,
    centerRight,
    bottomLeft,
    bottomCenter,
    bottomRight,
};

pub const FrameAnimation = SliceFrameAnimation;

pub const SliceFrameAnimation = struct {
    timer: window.Timer,
    index: usize = 0,
    loop: bool = true,
    anchor: Anchor = .bottomCenter,

    textures: []const Texture,

    pub fn init(textures: []const Texture) SliceFrameAnimation {
        return .{ .textures = textures, .timer = .init(0.1) };
    }

    pub fn load(comptime pathFmt: []const u8, max: u8) SliceFrameAnimation {
        const textures = cache.TextureSlice.load(pathFmt, 1, max);
        return .init(textures);
    }

    pub fn update(self: *@This(), delta: f32) void {
        if (self.timer.isRunningAfterUpdate(delta)) return;

        if (self.index == self.textures.len - 1) {
            if (self.loop) self.reset();
        } else {
            self.timer.reset();
            self.index += 1;
        }
    }

    pub fn reset(self: *@This()) void {
        self.timer.reset();
        self.index = 0;
    }

    pub fn finished(self: *const @This()) bool {
        return self.timer.finished and !self.loop;
    }
};

pub const AtlasFrameAnimation = struct {
    timer: window.Timer,
    index: usize = 0,
    loop: bool = true,
    texture: Texture,
    frames: []const math.Rectangle,
    anchor: Anchor = .bottomCenter,

    pub fn init(texture: Texture, frames: []const math.Rectangle) AtlasFrameAnimation {
        return .{ .texture = texture, .frames = frames, .timer = .init(0.1) };
    }

    pub fn load(path: [:0]const u8, count: u8) AtlasFrameAnimation {
        const texture = cache.Texture.load(path);

        const frames = cache.RectangleSlice.load(path, count);

        const width = @divExact(texture.width(), @as(f32, @floatFromInt(frames.len)));
        var rect: math.Rectangle = .{ .w = width, .h = texture.height() };

        for (0..frames.len) |index| {
            rect.x = @as(f32, @floatFromInt(index)) * width;
            frames[index] = rect;
        }

        return .init(texture, frames);
    }

    pub fn update(self: *@This(), delta: f32) void {
        if (self.timer.isRunningAfterUpdate(delta)) return;

        if (self.index == self.frames.len - 1) {
            if (self.loop) self.reset();
        } else {
            self.timer.reset();
            self.index += 1;
        }
    }

    pub fn reset(self: *@This()) void {
        self.timer.reset();
        self.index = 0;
    }

    pub fn finished(self: *const @This()) bool {
        return self.timer.finished and !self.loop;
    }
};
```

## graphics.zig

切片动画渲染时的对齐。

```zig
...
pub fn playSliceFlipX(frame: *const FrameAnimation, pos: math.Vector, flipX: bool) void {
    const offset: math.Vector = switch (frame.anchor) {
        .bottomCenter => .{
            .x = pos.x - frame.textures[0].width() / 2,
            .y = pos.y - frame.textures[0].height(),
        },
        else => unreachable,
    };
    drawFlipX(frame.textures[frame.index], offset, flipX);
}
...
```

## scene.zig

场景中新增了敌人字段，新增了更新和渲染方法。

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const actor = @import("actor/actor.zig");

var player: actor.Player = undefined;
var enemy: actor.Enemy = undefined;

pub fn init() void {
    player = actor.Player.init();
    enemy = actor.Enemy.init();
}

pub fn deinit() void {}

pub fn event(ev: *const window.Event) void {
    player.event(ev);
}

pub fn update() void {
    const delta = window.deltaSecond();
    player.update(delta);
    enemy.update(delta);
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    renderBackground();
    enemy.render();
    player.render();
}

pub fn renderBackground() void {
    const background = gfx.loadTexture("assets/background.png");
    const width = window.width - background.width();
    const height = window.height - background.height();
    gfx.draw(background, width / 2, height / 2);
}
```

## actor.zig

actor 文件中分开了角色和敌人两个文件。

```zig
const std = @import("std");

const gfx = @import("../graphics.zig");
const math = @import("../math.zig");
const window = @import("../window.zig");

pub const Player = @import("Player.zig");
pub const Enemy = @import("Enemy.zig");

pub const SharedActor = struct {
    pub const FLOOR_Y = 620;
    const GRAVITY = 980 * 2;

    enableGravity: bool = true,
    position: math.Vector,
    velocity: math.Vector = .{},
    faceLeft: bool = false,

    pub fn update(self: *SharedActor, delta: f32) void {
        if (self.enableGravity) {
            self.velocity.y += GRAVITY * delta;
        }

        self.position = self.position.add(self.velocity.scale(delta));
        if (self.position.y >= FLOOR_Y) {
            self.position.y = FLOOR_Y;
            self.velocity.y = 0;
        }

        self.position.x = std.math.clamp(self.position.x, 0, window.width);
    }

    pub fn render(self: *const SharedActor) void {
        _ = self;
    }
};
```

## Player.zig

将一些共享的属性从 `SharedActor` 中放到了 `Player` 中，因为两个的动画类型不一样。

```zig
const std = @import("std");

const gfx = @import("../graphics.zig");
const math = @import("../math.zig");
const window = @import("../window.zig");
const SharedActor = @import("actor.zig").SharedActor;

const Player = @This();

const State = union(enum) {
    idle: IdleState,
    run: RunState,
    jump: JumpState,
    fall: FallState,
    roll: RollState,
    attack: AttackState,
    // dead: DeadState,

    fn enter(self: State, player: *Player) void {
        switch (self) {
            inline else => |case| @TypeOf(case).enter(player),
        }
    }

    fn update(self: State, player: *Player, delta: f32) void {
        switch (self) {
            inline else => |case| @TypeOf(case).update(player, delta),
        }
    }

    fn render(self: State, player: *const Player) void {
        switch (self) {
            inline else => |case| @TypeOf(case).render(player),
        }
    }

    fn exit(self: State, player: *Player) void {
        switch (self) {
            inline else => |case| @TypeOf(case).exit(player),
        }
    }
};

shared: SharedActor,

state: State,
leftKeyDown: bool = false,
rightKeyDown: bool = false,
jumpKeyDown: bool = false,

idleAnimation: gfx.AtlasFrameAnimation,
runAnimation: gfx.AtlasFrameAnimation,
jumpAnimation: gfx.AtlasFrameAnimation,
fallAnimation: gfx.AtlasFrameAnimation,

rollKeyDown: bool = false,
rollAnimation: gfx.AtlasFrameAnimation,
rollTimer: window.Timer = .init(0.35),
rollCoolDown: window.Timer = .init(0.75),

attackKeyDown: bool = false,
attackAnimation: gfx.AtlasFrameAnimation,
attackLeft: gfx.AtlasFrameAnimation,
attackRight: gfx.AtlasFrameAnimation,
attackTimer: window.Timer = .init(0.3),
attackCoolDown: window.Timer = .init(0.5),

deadTimer: window.Timer = .init(0.5),

pub fn init() Player {
    var player: Player = .{
        .shared = .{
            .position = .{ .x = 100, .y = SharedActor.FLOOR_Y },
        },
        .idleAnimation = .load("assets/player/idle.png", 5),
        .runAnimation = .load("assets/player/run.png", 10),
        .jumpAnimation = .load("assets/player/jump.png", 5),
        .fallAnimation = .load("assets/player/fall.png", 5),

        .state = .idle,
        .rollAnimation = .load("assets/player/roll.png", 7),
        .attackAnimation = .load("assets/player/attack.png", 5),
        .attackLeft = .load("assets/player/vfx_attack_left.png", 5),
        .attackRight = .load("assets/player/vfx_attack_right.png", 5),
    };

    player.rollAnimation.loop = false;
    player.rollAnimation.timer = .init(0.005);
    player.attackAnimation.loop = false;
    player.attackAnimation.timer = .init(0.05);
    player.attackLeft.loop = false;
    player.attackLeft.timer = .init(0.05);
    player.attackRight.loop = false;
    player.attackRight.timer = .init(0.05);
    return player;
}

pub fn deinit() void {}

pub fn event(self: *Player, ev: *const window.Event) void {
    if (ev.type == .KEY_DOWN) {
        switch (ev.key_code) {
            .A => self.leftKeyDown = true,
            .D => self.rightKeyDown = true,
            .W => self.jumpKeyDown = true,
            .F => self.attackKeyDown = true,
            .SPACE => self.rollKeyDown = true,
            else => {},
        }
    } else if (ev.type == .KEY_UP) {
        switch (ev.key_code) {
            .A => self.leftKeyDown = false,
            .D => self.rightKeyDown = false,
            .W => self.jumpKeyDown = false,
            .F => self.attackKeyDown = false,
            .SPACE => self.rollKeyDown = false,
            else => {},
        }
    }
}

pub fn update(self: *Player, delta: f32) void {
    self.rollCoolDown.update(delta);
    self.attackCoolDown.update(delta);
    self.shared.update(delta);
    self.state.update(self, delta);
}

pub fn render(self: *const Player) void {
    self.shared.render();
    self.state.render(self);
}

fn changeState(self: *Player, new: State) void {
    self.state.exit(self);
    new.enter(self);
}

fn play(self: *const Player, animation: *const gfx.AtlasFrameAnimation) void {
    gfx.playAtlasFlipX(animation, self.shared.position, self.shared.faceLeft);
}

const IdleState = struct {
    fn enter(player: *Player) void {
        player.state = .idle;
        player.shared.velocity.x = 0;
    }

    fn update(player: *Player, delta: f32) void {
        if (player.attackKeyDown and player.attackCoolDown.finished) {
            player.changeState(.attack);
        }

        if (player.leftKeyDown or player.rightKeyDown) {
            player.changeState(.run);
        }

        if (player.rollKeyDown and player.rollCoolDown.finished) {
            player.changeState(.roll);
        }

        if (player.jumpKeyDown and player.shared.velocity.y == 0) {
            player.changeState(.jump);
        }

        player.idleAnimation.update(delta);
    }

    fn render(player: *const Player) void {
        player.play(&player.idleAnimation);
    }

    fn exit(player: *Player) void {
        player.idleAnimation.reset();
    }
};

const JumpState = struct {
    const SPEED_JUMP = 780;

    fn enter(player: *Player) void {
        player.state = .jump;
        player.shared.velocity.y = -SPEED_JUMP;
    }

    fn update(player: *Player, delta: f32) void {
        if (player.shared.velocity.y > 0) {
            player.changeState(.fall);
        }

        player.jumpAnimation.update(delta);
    }

    fn render(player: *const Player) void {
        player.play(&player.jumpAnimation);
    }

    fn exit(player: *Player) void {
        player.jumpAnimation.reset();
    }
};

const FallState = struct {
    fn enter(player: *Player) void {
        player.state = .fall;
    }

    fn update(player: *Player, delta: f32) void {
        if (player.shared.velocity.y == 0) {
            player.changeState(.idle);
        }

        player.fallAnimation.update(delta);
    }

    fn render(player: *const Player) void {
        player.play(&player.fallAnimation);
    }

    fn exit(player: *Player) void {
        player.fallAnimation.reset();
    }
};

const RunState = struct {
    const SPEED_RUN = 300;

    fn enter(player: *Player) void {
        player.state = .run;

        if (player.leftKeyDown) {
            player.shared.velocity.x = -SPEED_RUN;
            player.shared.faceLeft = true;
        } else if (player.rightKeyDown) {
            player.shared.velocity.x = SPEED_RUN;
            player.shared.faceLeft = false;
        }
    }

    fn update(player: *Player, delta: f32) void {
        if (player.attackKeyDown and player.attackCoolDown.finished) {
            player.changeState(.attack);
        }

        if (!player.leftKeyDown and !player.rightKeyDown) {
            player.changeState(.idle);
        }

        if (player.rollKeyDown and player.rollCoolDown.finished) {
            player.changeState(.roll);
        }

        if (player.jumpKeyDown and player.shared.velocity.y == 0) {
            player.changeState(.jump);
        }

        player.runAnimation.update(delta);
    }

    fn render(player: *const Player) void {
        player.play(&player.runAnimation);
    }

    fn exit(player: *Player) void {
        player.runAnimation.reset();
    }
};

const RollState = struct {
    const SPEED_ROLL = 800;
    fn enter(player: *Player) void {
        player.state = .roll;
        player.rollTimer.reset();
        player.rollCoolDown.reset();

        if (player.shared.faceLeft) {
            player.shared.velocity.x = -SPEED_ROLL;
        } else {
            player.shared.velocity.x = SPEED_ROLL;
        }
    }

    fn update(player: *Player, delta: f32) void {
        if (player.rollTimer.isFinishedAfterUpdate(delta)) {
            player.changeState(.idle);
        }

        player.rollAnimation.update(delta);
    }

    fn render(player: *const Player) void {
        player.play(&player.rollAnimation);
    }

    fn exit(player: *Player) void {
        player.rollAnimation.reset();
    }
};

const AttackState = struct {
    fn enter(player: *Player) void {
        player.state = .attack;
        player.attackTimer.reset();
        player.attackCoolDown.reset();
    }

    fn update(player: *Player, delta: f32) void {
        if (player.attackTimer.isFinishedAfterUpdate(delta)) {
            player.changeState(.idle);
        }

        player.attackAnimation.update(delta);
        player.attackLeft.update(delta);
        player.attackRight.update(delta);
    }

    fn render(player: *const Player) void {
        player.play(&player.attackAnimation);
        const pos = player.shared.position.add(.{ .y = 100 });
        if (player.shared.faceLeft) {
            gfx.playAtlas(&player.attackLeft, pos);
        } else {
            gfx.playAtlas(&player.attackRight, pos);
        }
    }

    fn exit(player: *Player) void {
        player.attackAnimation.reset();
        player.attackLeft.reset();
        player.attackRight.reset();
    }
};
```

## Enemy.zig

初始化了敌人，生成了一个最简单的空闲动画。

```zig
const std = @import("std");

const gfx = @import("../graphics.zig");
const math = @import("../math.zig");
const window = @import("../window.zig");
const SharedActor = @import("actor.zig").SharedActor;

const Enemy = @This();

shared: SharedActor,

idleAnimation: gfx.SliceFrameAnimation,

pub fn init() Enemy {
    var enemy: Enemy = .{
        .shared = .{
            .position = .{ .x = 1000, .y = SharedActor.FLOOR_Y },
            .faceLeft = true,
        },
        .idleAnimation = .load("assets/enemy/idle/{}.png", 5),
    };

    enemy.idleAnimation.loop = true;
    return enemy;
}

pub fn update(self: *Enemy, delta: f32) void {
    self.shared.update(delta);
    self.idleAnimation.update(delta);
}

pub fn render(self: *const Enemy) void {
    self.shared.render();
    self.play(&self.idleAnimation);
}

fn play(self: *const Enemy, animation: *const gfx.SliceFrameAnimation) void {
    gfx.playSliceFlipX(animation, self.shared.position, !self.shared.faceLeft);
}
```

## 效果

![敌人空闲动画][1]

[1]: images/空洞武士09.webp

## 附录
