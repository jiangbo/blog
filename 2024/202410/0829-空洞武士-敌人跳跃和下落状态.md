# 0829-空洞武士-敌人跳跃和下落状态

## 目标

在之前一节的基础上，新增跳跃和下落的状态。

## 环境

- Time 2025-04-06
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

目前还不能移动，只能在原地空闲或者跳跃。

## Enemy.zig

新增了跳跃和下落状态。

```zig
const std = @import("std");

const gfx = @import("../graphics.zig");
const math = @import("../math.zig");
const window = @import("../window.zig");
const scene = @import("../scene.zig");
const SharedActor = @import("actor.zig").SharedActor;

const Enemy = @This();

shared: SharedActor,
state: State = .idle,

idleTimer: window.Timer = .init(0.5),
idleAnimation: gfx.SliceFrameAnimation,

jumpAnimation: gfx.SliceFrameAnimation,

fallAnimation: gfx.SliceFrameAnimation,

pub fn init() Enemy {
    timer = std.time.Timer.start() catch unreachable;
    var enemy: Enemy = .{
        .shared = .{
            .position = .{ .x = 1050, .y = 200 },
            .faceLeft = true,
            .health = 10,
        },
        .idleAnimation = .load("assets/enemy/idle/{}.png", 5),
        .jumpAnimation = .load("assets/enemy/jump/{}.png", 8),
        .fallAnimation = .load("assets/enemy/fall/{}.png", 4),
    };

    enemy.state.enter(&enemy);
    enemy.jumpAnimation.loop = false;
    return enemy;
}

var timer: std.time.Timer = undefined;

pub fn update(self: *Enemy, delta: f32) void {
    self.shared.update(delta);
    self.state.update(self, delta);
}

pub fn render(self: *const Enemy) void {
    self.shared.render();
    self.state.render(self);
}

fn changeState(self: *Enemy, new: State) void {
    self.state.exit(self);
    new.enter(self);
}

fn play(self: *const Enemy, animation: *const gfx.SliceFrameAnimation) void {
    gfx.playSliceFlipX(animation, self.shared.position, !self.shared.faceLeft);
}

const State = union(enum) {
    idle: IdleState,
    jump: JumpState,
    fall: FallState,

    fn enter(self: State, enemy: *Enemy) void {
        switch (self) {
            inline else => |case| @TypeOf(case).enter(enemy),
        }
    }

    fn update(self: State, enemy: *Enemy, delta: f32) void {
        switch (self) {
            inline else => |case| @TypeOf(case).update(enemy, delta),
        }
    }

    fn render(self: State, enemy: *const Enemy) void {
        switch (self) {
            inline else => |case| @TypeOf(case).render(enemy),
        }
    }

    fn exit(self: State, enemy: *Enemy) void {
        switch (self) {
            inline else => |case| @TypeOf(case).exit(enemy),
        }
    }
};

const IdleState = struct {
    fn enter(enemy: *Enemy) void {
        enemy.state = .idle;
        enemy.shared.velocity.x = 0;

        const max: f32 = if (enemy.shared.health > 5) 0.5 else 0.25;
        enemy.idleTimer.duration = window.randomFloat(0, max);
        enemy.idleTimer.reset();
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.idleAnimation.update(delta);
        if (enemy.idleTimer.isRunningAfterUpdate(delta)) return;

        if (enemy.shared.health <= 5) {
            return updateEnraged(enemy);
        }

        const rand = window.rand.intRangeLessThanBiased(u8, 0, 100);
        switch (rand) {
            0...50 => enemy.changeState(.jump),
            else => enemy.changeState(.idle),
        }
    }

    fn updateEnraged(enemy: *Enemy) void {
        _ = enemy;
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.idleAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.idleAnimation.reset();
        const playerPosition = scene.player.shared.position;
        enemy.shared.faceLeft = playerPosition.x < enemy.shared.position.x;
    }
};

const JumpState = struct {
    const SPEED_JUMP = 1000;

    fn enter(enemy: *Enemy) void {
        enemy.state = .jump;
        enemy.shared.velocity.y = -SPEED_JUMP;
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.jumpAnimation.update(delta);
        if (enemy.shared.velocity.y < 0) return;

        enemy.changeState(.fall);
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.jumpAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.jumpAnimation.reset();
    }
};

const FallState = struct {
    fn enter(enemy: *Enemy) void {
        enemy.state = .fall;
    }
    fn update(enemy: *Enemy, delta: f32) void {
        enemy.fallAnimation.update(delta);
        if (enemy.shared.isOnFloor()) enemy.changeState(.idle);
    }
    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.fallAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.fallAnimation.reset();
    }
};
```

## 效果

![敌人跳跃和下落状态][1]

[1]: images/空洞武士11.webp

## 附录
