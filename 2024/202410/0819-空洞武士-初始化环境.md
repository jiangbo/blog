# 0819-空洞武士-初始化环境

## 目标

继续新的项目，接下来是跟着教程，继续学习空洞武士项目。

## 环境

- Time 2025-03-30
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

把 Zig 的版本，从小版本固定到发布版本了，这样后续找对应的版本比较好找。

## build.zig.zon

打算直接使用 stb 的库了，不清楚能不能弄好绑定。不过想学着一点 C/CPP 的编译知识。

```zig
.{
    .name = .demo,
    .version = "0.0.0",
    .fingerprint = 0xd642dfa0e7fb6c02,
    .minimum_zig_version = "0.14.0",
    .dependencies = .{
        .sokol = .{
            .url = "git+https://github.com/floooh/sokol-zig#38c5d4c35b375c8aa4e104c1db8d218b588cf293",
            .hash = "sokol-0.1.0-pb1HK46pLACblkKEuBJAHV6dapSeaWooJO87AN5tickD",
        },
        .stb = .{
            .url = "git+https://github.com/nothings/stb#f0569113c93ad095470c54bf34a17b36646bbbb5",
            .hash = "12206e61b3cf9e8e8612da8501e221287ca14c9709f95a761691e3f1a503d7a6671b",
        },
    },

    .paths = .{""},
}
```

## build.zig

```zig
const std = @import("std");

pub fn build(b: *std.Build) !void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});

    const exe = b.addExecutable(.{
        .name = "demo",
        .root_source_file = b.path("src/main.zig"),
        .target = target,
        .optimize = optimize,
    });

    if (optimize != .Debug) exe.subsystem = .Windows;

    b.installArtifact(exe);

    const sokol = b.dependency("sokol", .{ .target = target, .optimize = optimize });
    exe.root_module.addImport("sokol", sokol.module("sokol"));

    const run_cmd = b.addRunArtifact(exe);
    run_cmd.step.dependOn(b.getInstallStep());
    if (b.args) |args| {
        run_cmd.addArgs(args);
    }
    const run_step = b.step("run", "Run the app");
    run_step.dependOn(&run_cmd.step);
}
```

## window.zig

把之前的 window.zig 代码直接拷贝过来。

```zig
const std = @import("std");
const sk = @import("sokol");

pub const Event = sk.app.Event;
pub const CallbackInfo = struct {
    title: [:0]const u8,
    init: ?*const fn () void = null,
    update: ?*const fn () void = null,
    render: ?*const fn () void = null,
    event: ?*const fn (*const Event) void = null,
    deinit: ?*const fn () void = null,
};

pub const Timer = struct {
    finished: bool = false,
    duration: f32,
    elapsed: f32 = 0,

    pub fn init(duration: f32) Timer {
        return Timer{ .duration = duration };
    }

    pub fn update(self: *Timer, delta: f32) void {
        if (self.finished) return;
        self.elapsed += delta;
        if (self.elapsed >= self.duration) self.finished = true;
    }

    pub fn isRunningAfterUpdate(self: *Timer, delta: f32) bool {
        return !self.isFinishedAfterUpdate(delta);
    }

    pub fn isFinishedAfterUpdate(self: *Timer, delta: f32) bool {
        self.update(delta);
        return self.finished;
    }

    pub fn reset(self: *Timer) void {
        self.finished = false;
        self.elapsed = 0;
    }

    pub fn isRunning(self: *const Timer) bool {
        return !self.finished;
    }
};

pub var width: f32 = 0;
pub var height: f32 = 0;
pub var rand: std.Random = undefined;

var timer: std.time.Timer = undefined;
var deltaTime: f32 = 0;
var totalTime: f32 = 0;
pub fn deltaMillisecond() f32 {
    return deltaTime;
}

pub fn totalMillisecond() f32 {
    return totalTime;
}

pub fn displayText(x: f32, y: f32, text: [:0]const u8) void {
    sk.debugtext.canvas(sk.app.widthf() * 0.4, sk.app.heightf() * 0.4);
    sk.debugtext.origin(x, y);
    sk.debugtext.home();

    sk.debugtext.font(0);
    sk.debugtext.color3b(0xff, 0xff, 0xff);
    sk.debugtext.puts(text);
}

pub fn endDisplayText() void {
    sk.debugtext.draw();
}

pub fn exit() void {
    sk.app.quit();
}

var callback: CallbackInfo = undefined;
pub fn run(info: CallbackInfo) void {
    callback = info;
    sk.app.run(.{
        .width = @as(i32, @intFromFloat(width)),
        .height = @as(i32, @intFromFloat(height)),
        .window_title = info.title,
        .logger = .{ .func = sk.log.func },
        .init_cb = if (info.init) |_| init else null,
        .event_cb = if (info.event) |_| event else null,
        .frame_cb = if (info.update != null or info.render != null) frame else null,
        .cleanup_cb = if (info.deinit) |_| cleanup else null,
    });
}

fn init() callconv(.C) void {
    sk.gfx.setup(.{
        .environment = sk.glue.environment(),
        .logger = .{ .func = sk.log.func },
    });

    sk.debugtext.setup(.{
        .fonts = init: {
            var f: [8]sk.debugtext.FontDesc = @splat(.{});
            f[0] = sk.debugtext.fontKc854();
            break :init f;
        },
        .logger = .{ .func = sk.log.func },
    });

    timer = std.time.Timer.start() catch unreachable;
    callback.init.?();
}

fn event(evt: ?*const Event) callconv(.C) void {
    if (evt) |e| callback.event.?(e);
}

fn frame() callconv(.C) void {
    const nano: f32 = @floatFromInt(timer.lap());
    deltaTime = nano / std.time.ns_per_ms;
    totalTime += deltaTime;
    callback.update.?();
    callback.render.?();
}

fn cleanup() callconv(.C) void {
    sk.gfx.shutdown();
    callback.deinit.?();
}
```

## main.zig

```zig
const std = @import("std");

const window = @import("window.zig");

pub fn init() void {}

pub fn event(ev: *const window.Event) void {
    _ = ev;
}

pub fn update() void {}

pub fn render() void {}

pub fn deinit() void {}

var allocator: std.mem.Allocator = undefined;

pub fn main() void {
    var debugAllocator = std.heap.DebugAllocator(.{}).init;
    defer _ = debugAllocator.deinit();

    allocator = debugAllocator.allocator();
    window.width = 1280;
    window.height = 720;

    var prng = std.Random.DefaultPrng.init(@intCast(std.time.timestamp()));
    window.rand = prng.random();

    window.run(.{
        .title = "空洞武士",
        .init = init,
        .event = event,
        .update = update,
        .render = render,
        .deinit = deinit,
    });
}
```

## 效果

![初始化环境][1]

[1]: images/空洞武士01.png

## 附录
