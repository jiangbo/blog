# 0808-植物明星大乱斗-增加帧动画缓存

## 目标

之前的帧动画是直接分配到栈上，使用的数组，增加一个帧动画的缓存，可以分配到堆上。

## 环境

- Time 2025-03-16
- Zig 0.14.0-dev.3298+6fe1993d8

## 参考

1. <https://www.bilibili.com/video/BV1jx4y1t7eP>

## 想法

将帧动画分配到堆上，子弹对象中帧动画字段类型就一致了，而且所以子弹共享动画。

## cache.zig

```zig
const std = @import("std");
const gfx = @import("graphics.zig");

var allocator: std.mem.Allocator = undefined;

pub fn init(alloc: std.mem.Allocator) void {
    allocator = alloc;
    TextureCache.init();
    TextureSliceCache.init();
}

pub fn deinit() void {
    TextureSliceCache.deinit();
    TextureCache.deinit();
}

pub const TextureCache = struct {
    const stbi = @import("stbi");
    const Cache = std.StringHashMap(gfx.Texture);

    var cache: Cache = undefined;

    pub fn init() void {
        cache = Cache.init(allocator);
        stbi.init(allocator);
    }

    pub fn load(path: [:0]const u8) ?gfx.Texture {
        const entry = cache.getOrPut(path) catch |e| {
            std.log.err("texture cache allocate error: {}", .{e});
            return null;
        };
        if (entry.found_existing) return entry.value_ptr.*;

        std.log.info("loading texture from: {s}", .{path});
        var image = stbi.Image.loadFromFile(path, 4) catch |e| {
            std.log.err("loading image error: {}", .{e});
            return null;
        };

        defer image.deinit();

        const texture = gfx.Texture.init(image.width, image.height, image.data);
        entry.value_ptr.* = texture;
        entry.key_ptr.* = allocator.dupe(u8, path) catch unreachable;
        return texture;
    }

    pub fn deinit() void {
        stbi.deinit();
        var keyIter = cache.keyIterator();
        while (keyIter.next()) |key| allocator.free(key.*);
        cache.deinit();
    }
};

pub const TextureSliceCache = struct {
    const Cache = std.StringHashMap([]gfx.Texture);

    var cache: Cache = undefined;

    pub fn init() void {
        cache = Cache.init(allocator);
    }

    pub fn load(comptime pathFmt: []const u8, from: u8, len: u8) ?[]const gfx.Texture {
        const entry = cache.getOrPut(pathFmt) catch |e| {
            std.log.err("texture slices cache allocate error: {}", .{e});
            return null;
        };
        if (entry.found_existing) return entry.value_ptr.*;

        const textures = allocator.alloc(gfx.Texture, len) catch |e| {
            std.log.err("texture slices allocate error: {}", .{e});
            return null;
        };

        gfx.loadTextures(textures, pathFmt, from);
        entry.value_ptr.* = textures;
        entry.key_ptr.* = allocator.dupe(u8, pathFmt) catch unreachable;
        return textures;
    }

    pub fn deinit() void {
        var iterator = cache.iterator();
        while (iterator.next()) |entry| {
            allocator.free(entry.key_ptr.*);
            allocator.free(entry.value_ptr.*);
        }
        cache.deinit();
    }
};
```

## graphics.zig

```zig
const std = @import("std");
const cache = @import("cache.zig");
const gpu = @import("gpu.zig");

pub const Texture = gpu.Texture;

pub const Camera = struct {
    const zm = @import("zmath");

    proj: zm.Mat,

    pub fn init(width: f32, height: f32) Camera {
        const proj = zm.orthographicOffCenterLh(0, width, 0, height, 0, 1);
        return .{ .proj = proj };
    }

    pub fn vp(self: Camera) zm.Mat {
        return self.proj;
    }
};

pub var camera: Camera = undefined;
pub var renderer: gpu.Renderer = undefined;
var passEncoder: gpu.RenderPassEncoder = undefined;

pub fn init(width: f32, height: f32) void {
    camera = Camera.init(width, height);
    renderer = gpu.Renderer.init();
}

pub fn loadTexture(path: [:0]const u8) ?Texture {
    return cache.TextureCache.load(path);
}

pub fn loadTextures(textures: []Texture, comptime pathFmt: []const u8, from: u8) void {
    std.log.info("loading texture slice : {s}", .{pathFmt});

    var buffer: [128]u8 = undefined;
    for (from..from + textures.len) |index| {
        const path = std.fmt.bufPrintZ(&buffer, pathFmt, .{index});

        const texture = loadTexture(path catch unreachable);
        textures[index - from] = texture.?;
    }
}

pub fn beginDraw() void {
    passEncoder = gpu.CommandEncoder.beginRenderPass(.{ .r = 1, .b = 1, .a = 1.0 });
    renderer.renderPass = passEncoder;
}

pub fn draw(x: f32, y: f32, tex: Texture) void {
    renderer.draw(.{
        .uniform = .{ .vp = camera.vp() },
        .x = x,
        .y = y,
        .texture = tex,
    });
}

pub fn drawFlipX(x: f32, y: f32, tex: Texture, flipX: bool) void {
    drawOptions(x, y, tex, .{ .flipX = flipX });
}

pub const DrawOptions = struct {
    flipX: bool = false,
    sourceRect: ?gpu.Rectangle = null,
};

pub fn drawOptions(x: f32, y: f32, texture: Texture, options: DrawOptions) void {
    renderer.draw(.{
        .uniform = .{ .vp = camera.vp() },
        .x = x,
        .y = y,
        .texture = texture,
        .flipX = options.flipX,
        .sourceRect = options.sourceRect,
    });
}

pub fn endDraw() void {
    passEncoder.submit();
}

pub fn TextureArray(max: u8) type {
    return struct {
        textures: [max]Texture,

        pub fn init(comptime pathFmt: []const u8) @This() {
            var self = @This(){ .textures = undefined };
            cache.TextureSliceCache.loadToSlice(&self.textures, pathFmt, 1);
            return self;
        }

        pub fn asSlice(self: @This()) []const Texture {
            return self.textures[0..];
        }
    };
}

pub const FrameAnimation = SliceFrameAnimation;

pub const SliceFrameAnimation = struct {
    interval: f32 = 100,
    timer: f32 = 0,
    index: usize = 0,
    loop: bool = true,
    done: bool = false,

    textures: []const Texture,

    pub fn init(textures: []const Texture) SliceFrameAnimation {
        return .{ .textures = textures };
    }

    pub fn load(comptime pathFmt: []const u8, max: u8) SliceFrameAnimation {
        const textures = cache.TextureSliceCache.load(pathFmt, 1, max);
        return .init(textures.?);
    }

    pub fn update(self: *@This(), delta: f32) void {
        if (self.done) return;

        self.timer += delta;
        if (self.timer < self.interval) return;

        self.timer = 0;
        self.index += 1;

        if (self.index < self.textures.len) return;

        if (self.loop) self.index = 0 else {
            self.index = self.textures.len - 1;
            self.done = true;
        }
    }

    pub fn play(self: @This(), x: f32, y: f32) void {
        self.playFlipX(x, y, false);
    }

    pub fn playFlipX(self: @This(), x: f32, y: f32, flipX: bool) void {
        drawFlipX(x, y, self.textures[self.index], flipX);
    }
};
const std = @import("std");
const cache = @import("cache.zig");
const gpu = @import("gpu.zig");

pub const Texture = gpu.Texture;

pub const Camera = struct {
    const zm = @import("zmath");

    proj: zm.Mat,

    pub fn init(width: f32, height: f32) Camera {
        const proj = zm.orthographicOffCenterLh(0, width, 0, height, 0, 1);
        return .{ .proj = proj };
    }

    pub fn vp(self: Camera) zm.Mat {
        return self.proj;
    }
};

pub var camera: Camera = undefined;
pub var renderer: gpu.Renderer = undefined;
var passEncoder: gpu.RenderPassEncoder = undefined;

pub fn init(width: f32, height: f32) void {
    camera = Camera.init(width, height);
    renderer = gpu.Renderer.init();
}

pub fn loadTexture(path: [:0]const u8) ?Texture {
    return cache.TextureCache.load(path);
}

pub fn loadTextures(textures: []Texture, comptime pathFmt: []const u8, from: u8) void {
    std.log.info("loading texture slice : {s}", .{pathFmt});

    var buffer: [128]u8 = undefined;
    for (from..from + textures.len) |index| {
        const path = std.fmt.bufPrintZ(&buffer, pathFmt, .{index});

        const texture = loadTexture(path catch unreachable);
        textures[index - from] = texture.?;
    }
}

pub fn beginDraw() void {
    passEncoder = gpu.CommandEncoder.beginRenderPass(.{ .r = 1, .b = 1, .a = 1.0 });
    renderer.renderPass = passEncoder;
}

pub fn draw(x: f32, y: f32, tex: Texture) void {
    renderer.draw(.{
        .uniform = .{ .vp = camera.vp() },
        .x = x,
        .y = y,
        .texture = tex,
    });
}

pub fn drawFlipX(x: f32, y: f32, tex: Texture, flipX: bool) void {
    drawOptions(x, y, tex, .{ .flipX = flipX });
}

pub const DrawOptions = struct {
    flipX: bool = false,
    sourceRect: ?gpu.Rectangle = null,
};

pub fn drawOptions(x: f32, y: f32, texture: Texture, options: DrawOptions) void {
    renderer.draw(.{
        .uniform = .{ .vp = camera.vp() },
        .x = x,
        .y = y,
        .texture = texture,
        .flipX = options.flipX,
        .sourceRect = options.sourceRect,
    });
}

pub fn endDraw() void {
    passEncoder.submit();
}

pub fn TextureArray(max: u8) type {
    return struct {
        textures: [max]Texture,

        pub fn init(comptime pathFmt: []const u8) @This() {
            var self = @This(){ .textures = undefined };
            cache.TextureSliceCache.loadToSlice(&self.textures, pathFmt, 1);
            return self;
        }

        pub fn asSlice(self: @This()) []const Texture {
            return self.textures[0..];
        }
    };
}

pub const FrameAnimation = SliceFrameAnimation;

pub const SliceFrameAnimation = struct {
    interval: f32 = 100,
    timer: f32 = 0,
    index: usize = 0,
    loop: bool = true,
    done: bool = false,

    textures: []const Texture,

    pub fn init(textures: []const Texture) SliceFrameAnimation {
        return .{ .textures = textures };
    }

    pub fn load(comptime pathFmt: []const u8, max: u8) SliceFrameAnimation {
        const textures = cache.TextureSliceCache.load(pathFmt, 1, max);
        return .init(textures.?);
    }

    pub fn update(self: *@This(), delta: f32) void {
        if (self.done) return;

        self.timer += delta;
        if (self.timer < self.interval) return;

        self.timer = 0;
        self.index += 1;

        if (self.index < self.textures.len) return;

        if (self.loop) self.index = 0 else {
            self.index = self.textures.len - 1;
            self.done = true;
        }
    }

    pub fn play(self: @This(), x: f32, y: f32) void {
        self.playFlipX(x, y, false);
    }

    pub fn playFlipX(self: @This(), x: f32, y: f32, flipX: bool) void {
        drawFlipX(x, y, self.textures[self.index], flipX);
    }
};
```

## 效果

和之前一致，没有改变游戏的逻辑。

![增加帧动画缓存][1]

[1]: images/植物明星大乱斗19.webp

## 附录
