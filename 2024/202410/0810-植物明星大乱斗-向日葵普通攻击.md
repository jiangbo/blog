# 0810-植物明星大乱斗-向日葵普通攻击

## 目标

实现向日葵的普通攻击。

## 环境

- Time 2025-03-16
- Zig 0.14.0-dev.3298+6fe1993d8

## 参考

1. <https://www.bilibili.com/video/BV1jx4y1t7eP>

## 想法

越写越乱了，先不管，继续写下去，看看能不能写完。

## scene/GameScene.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const audio = @import("zaudio");

const scene = @import("../scene.zig");
const Bullet = @import("bullet.zig").Bullet;
const player = @import("player.zig");
const GameScene = @This();

player1: player.Player,
player2: player.Player,

bullets: std.BoundedArray(Bullet, 64),

imageSky: gfx.Texture,
imageHill: gfx.Texture,

platforms: [4]Platform,

pub fn init() GameScene {
    std.log.info("game scene init", .{});

    var self: GameScene = undefined;

    self.imageSky = gfx.loadTexture("assets/sky.png").?;
    self.imageHill = gfx.loadTexture("assets/hills.png").?;
    self.bullets = std.BoundedArray(Bullet, 64).init(0) catch unreachable;

    self.initPlatforms();
    @import("bullet.zig").init();

    return self;
}

fn initPlatforms(self: *GameScene) void {
    var texture = gfx.loadTexture("assets/platform_large.png").?;
    var platform: Platform = .{ .x = 122, .y = 455, .texture = texture };
    platform.shape.left = platform.x + 30;
    platform.shape.right = platform.x + texture.width - 30;
    platform.shape.y = platform.y + 60;
    self.platforms[0] = platform;

    texture = gfx.loadTexture("assets/platform_small.png").?;
    platform = .{ .x = 175, .y = 360, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[1] = platform;

    platform = .{ .x = 855, .y = 360, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[2] = platform;

    platform = .{ .x = 515, .y = 225, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[3] = platform;
}

pub fn enter(self: *GameScene) void {
    std.log.info("game scene enter", .{});

    self.player1 = .init(scene.playerType1, 200, 50, false);
    self.player2 = .init(scene.playerType2, 975, 50, true);
    self.player2.p1 = false;
}

pub fn exit(self: *GameScene) void {
    std.log.info("game scene exit", .{});
    _ = self;
}

pub fn event(self: *GameScene, ev: *const window.Event) void {
    switch (ev.key_code) {
        .A, .D, .W, .F, .G => self.player1.event(ev),
        .LEFT, .RIGHT, .UP, .PERIOD, .SLASH => self.player2.event(ev),
        else => {},
    }
}

pub fn update(self: *GameScene) void {
    const deltaTime = window.deltaMillisecond();

    self.player1.update(deltaTime);
    self.player2.update(deltaTime);

    self.updateBullets(deltaTime);
}

fn updateBullets(self: *GameScene, delta: f32) void {
    for (self.bullets.slice(), 0..) |*bullet, index| {
        bullet.update(delta);

        if (bullet.p1 and !bullet.collide) {
            if (self.player2.vectorIn(bullet.center())) {
                bullet.collidePlayer();
            }
        }

        if (!bullet.p1 and !bullet.collide) {
            if (self.player1.vectorIn(bullet.center())) {
                bullet.collidePlayer();
            }
        }

        if (bullet.dead) _ = self.bullets.swapRemove(index);
    }
}

pub fn render(self: *GameScene) void {
    var x = window.width - self.imageSky.width;
    var y = window.height - self.imageSky.height;
    gfx.draw(x / 2, y / 2, self.imageSky);

    x = window.width - self.imageHill.width;
    y = window.height - self.imageHill.height;
    gfx.draw(x / 2, y / 2, self.imageHill);

    for (&self.platforms) |platform| {
        gfx.draw(platform.x, platform.y, platform.texture);
    }

    self.player1.render();
    self.player2.render();

    for (self.bullets.slice()) |*bullet| bullet.render();
}

pub fn deinit(self: *GameScene) void {
    std.log.info("game scene deinit", .{});
    @import("bullet.zig").deinit();
    _ = self;
}

const Platform = struct {
    x: f32,
    y: f32,
    texture: gfx.Texture,
    shape: Collision = .{ .left = 0, .right = 0, .y = 0 },

    const Collision = struct { left: f32, right: f32, y: f32 };
};
```

## scene/player.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

const scene = @import("../scene.zig");
const Bullet = @import("bullet.zig").Bullet;
const Vector = @import("bullet.zig").Vector;

pub const Player = struct {
    x: f32,
    y: f32,
    facingLeft: bool,
    leftKeyDown: bool = false,
    rightKeyDown: bool = false,
    velocity: f32 = 0,
    width: f32 = 96,
    height: f32 = 96,
    p1: bool = true,

    attackTimer: f32 = 0,

    isAttackEx: bool = false,
    attackExTimer: f32 = 0,

    hp: u32 = 100,
    mp: u32 = 100,

    animationIdle: gfx.FrameAnimation = undefined,
    animationRun: gfx.FrameAnimation = undefined,
    animationAttack: gfx.FrameAnimation = undefined,
    animationSunText: gfx.FrameAnimation = undefined,

    const runVelocity: f32 = 0.55;
    const gravity: f32 = 1.6e-3;
    const jumpVelocity: f32 = -0.85;
    const attackInterval: f32 = 500;
    const attackIntervalEx: f32 = 100;

    const attackExDuration: f32 = 2500;

    pub fn init(playerType: scene.PlayerType, x: f32, y: f32, faceLeft: bool) Player {
        var self: Player = .{ .x = x, .y = y, .facingLeft = faceLeft };

        if (playerType == .peaShooter) {
            self.animationIdle = .load("assets/peashooter_idle_{}.png", 9);
            self.animationRun = .load("assets/peashooter_run_{}.png", 5);
            self.animationAttack = .load("assets/peashooter_attack_ex_{}.png", 3);
        } else {
            self.animationIdle = .load("assets/sunflower_idle_{}.png", 8);
            self.animationRun = .load("assets/sunflower_run_{}.png", 5);
            self.animationAttack = .load("assets/sunflower_attack_ex_{}.png", 9);
            self.animationSunText = .load("assets/sun_text_{}.png", 5);
        }

        return self;
    }

    pub fn event(self: *Player, ev: *const window.Event) void {
        if (self.isAttackEx and ev.type == .KEY_DOWN) return;

        switch (ev.type) {
            .KEY_DOWN => switch (ev.key_code) {
                .A, .LEFT => {
                    self.leftKeyDown = true;
                    self.facingLeft = true;
                },
                .D, .RIGHT => {
                    self.rightKeyDown = true;
                    self.facingLeft = false;
                },
                .W, .UP => {
                    if (self.velocity != 0) return;
                    self.velocity += Player.jumpVelocity;
                },
                .F, .PERIOD => self.attack(),
                .G, .SLASH => self.attackEx(),
                else => {},
            },
            .KEY_UP => switch (ev.key_code) {
                .A, .LEFT => self.leftKeyDown = false,
                .D, .RIGHT => self.rightKeyDown = false,
                else => {},
            },
            else => {},
        }
    }

    pub fn update(self: *Player, delta: f32) void {
        self.attackTimer -= delta;

        if (self.isAttackEx) {
            self.animationAttack.update(delta);
            self.attackExTimer -= delta;
            if (self.attackExTimer <= 0) {
                self.isAttackEx = false;
                self.attackTimer = attackInterval;
            } else if (self.attackTimer < 0) {
                const bullet = self.spawnBullet();
                scene.gameScene.bullets.append(bullet) catch unreachable;
            }
            return;
        }

        var direction: f32 = 0;
        if (self.leftKeyDown) direction -= 1;
        if (self.rightKeyDown) direction += 1;
        self.x += direction * Player.runVelocity * delta;

        if (self.leftKeyDown or self.rightKeyDown) {
            self.animationRun.update(delta);
        } else {
            self.animationIdle.update(delta);
        }

        moveAndCollide(self, delta);
    }

    fn moveAndCollide(self: *Player, delta: f32) void {
        const velocity = self.velocity + Player.gravity * delta;
        const y = self.y + velocity * delta;

        const platforms = &scene.gameScene.platforms;
        for (platforms) |*platform| {
            if (self.x + self.width < platform.shape.left) continue;
            if (self.x > platform.shape.right) continue;
            if (y + self.height < platform.shape.y) continue;

            const deltaPosY = self.velocity * delta;
            const lastFootPosY = self.y + self.height - deltaPosY;

            if (lastFootPosY <= platform.shape.y) {
                self.y = platform.shape.y - self.height;
                self.velocity = 0;
                break;
            }
        } else {
            self.y = y;
            self.velocity = velocity;
        }
    }

    pub fn render(self: *Player) void {
        if (self.isAttackEx) {
            self.animationAttack.playFlipX(self.x, self.y, self.facingLeft);
        } else if (self.leftKeyDown) {
            self.animationRun.playFlipX(self.x, self.y, true);
        } else if (self.rightKeyDown) {
            self.animationRun.playFlipX(self.x, self.y, false);
        } else {
            self.animationIdle.playFlipX(self.x, self.y, self.facingLeft);
        }
    }

    pub fn attack(self: *Player) void {
        if (self.attackTimer > 0) return;

        var bullet = self.spawnBullet();
        bullet.playShootSound();

        scene.gameScene.bullets.append(bullet) catch unreachable;
    }

    fn spawnBullet(self: *Player) Bullet {
        self.attackTimer = if (self.isAttackEx) attackIntervalEx else attackInterval;

        const playerType = if (self.p1) scene.playerType1 else scene.playerType2;
        const bulletType: Bullet.BulletType = if (playerType == .peaShooter) .pea else .sun;
        var bullet: Bullet = .init(bulletType);

        const x: f32 = if (self.facingLeft) self.x else self.x + self.width;
        bullet.position = .{ .x = x - bullet.texture.width / 2, .y = self.y };
        if (self.facingLeft) bullet.velocity.x = -bullet.velocity.x;
        bullet.p1 = self.p1;

        return bullet;
    }

    pub fn attackEx(self: *Player) void {
        if (self.mp < 100) return;
        self.isAttackEx = true;
        self.attackExTimer = attackExDuration;
        self.attackTimer = attackIntervalEx;
        Bullet.playShootExSound();
        self.mp = 0;
    }

    pub fn vectorIn(self: Player, vector: Vector) bool {
        if (vector.x < self.x or vector.x > self.x + self.width) return false;
        if (vector.y < self.y or vector.y > self.y + self.height) return false;
        return true;
    }
};
```

## scene/bullet.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const audio = @import("zaudio");

const scene = @import("../scene.zig");

var peaBreakSound: [3]*audio.Sound = undefined;
var peaShootSound: [2]*audio.Sound = undefined;
var peaShootExSound: *audio.Sound = undefined;

var sunExplodeSound: *audio.Sound = undefined;

pub fn init() void {
    peaBreakSound[0] = scene.audioEngine.createSoundFromFile( //
        "assets/pea_break_1.mp3", .{}) catch unreachable;
    peaBreakSound[1] = scene.audioEngine.createSoundFromFile( //
        "assets/pea_break_2.mp3", .{}) catch unreachable;
    peaBreakSound[2] = scene.audioEngine.createSoundFromFile( //
        "assets/pea_break_3.mp3", .{}) catch unreachable;

    peaShootSound[0] = scene.audioEngine.createSoundFromFile( //
        "assets/pea_shoot_1.mp3", .{}) catch unreachable;

    peaShootSound[1] = scene.audioEngine.createSoundFromFile( //
        "assets/pea_shoot_2.mp3", .{}) catch unreachable;

    peaShootExSound = scene.audioEngine.createSoundFromFile( //
        "assets/pea_shoot_ex.mp3", .{}) catch unreachable;

    sunExplodeSound = scene.audioEngine.createSoundFromFile( //
        "assets/sun_explode.mp3", .{}) catch unreachable;
}

pub fn deinit() void {
    for (peaBreakSound) |sound| sound.destroy();
    for (peaShootSound) |sound| sound.destroy();
    peaShootExSound.destroy();
    sunExplodeSound.destroy();
}

pub const Vector = struct {
    x: f32 = 0,
    y: f32 = 0,
    z: f32 = 0,

    pub fn add(a: Vector, b: Vector) Vector {
        return .{ .x = a.x + b.x, .y = a.y + b.y, .z = a.z + b.z };
    }

    pub fn scale(a: Vector, b: f32) Vector {
        return .{ .x = a.x * b, .y = a.y * b, .z = a.z * b };
    }
};

pub const Bullet = struct {
    size: Vector,
    position: Vector,
    velocity: Vector,
    damage: f32,
    dead: bool = false,
    collide: bool = false,
    p1: bool = true,
    explodeOffset: Vector = .{},

    type: BulletType = .pea,
    animationIdle: gfx.FrameAnimation,
    animationBreak: gfx.FrameAnimation,

    texture: gfx.Texture = undefined,

    const peaSpeed: f32 = 0.75;
    const peaSpeedEx: f32 = 1.5;
    const gravity: f32 = 1.6e-3;

    pub const BulletType = enum { pea, sun, sunEx };

    pub fn init(bulletType: BulletType) Bullet {
        var self = switch (bulletType) {
            .pea => initPeaBullet(),
            .sun => initSunBullet(),
            else => unreachable,
        };

        self.size = .{ .x = self.texture.width, .y = self.texture.height };

        return self;
    }

    fn initPeaBullet() Bullet {
        var self: Bullet = undefined;
        self.texture = gfx.loadTexture("assets/pea.png").?;
        self.type = .pea;
        self.animationBreak = .load("assets/pea_break_{}.png", 3);
        self.animationBreak.loop = false;
        self.damage = 10;
        self.velocity = .{ .x = peaSpeed };

        return self;
    }

    fn initSunBullet() Bullet {
        var self: Bullet = undefined;
        self.texture = gfx.loadTexture("assets/sun_1.png").?;
        self.type = .sun;
        self.animationIdle = .load("assets/sun_{}.png", 5);
        self.animationBreak = .load("assets/sun_explode_{}.png", 5);
        self.animationBreak.interval = 75;
        self.animationBreak.loop = false;
        self.damage = 20;
        self.velocity = .{ .x = 0.25, .y = -0.65 };

        self.explodeOffset = .{
            .x = (self.texture.width - self.animationBreak.textures[0].width) / 2,
            .y = (self.texture.height - self.animationBreak.textures[0].height) / 2,
        };

        return self;
    }

    pub fn playShootSound(self: *Bullet) void {
        if (self.type == .pea) {
            const i = window.rand.uintLessThanBiased(u32, peaShootSound.len);
            peaShootSound[i].start() catch unreachable;
        }
    }

    pub fn playShootExSound() void {
        peaShootExSound.start() catch unreachable;
    }

    pub fn center(self: Bullet) Vector {
        return .{
            .x = self.position.x + self.size.x / 2,
            .y = self.position.y + self.size.y / 2,
        };
    }

    pub fn update(self: *Bullet, delta: f32) void {
        if (self.type != .pea) {
            self.velocity = self.velocity.add(.{ .y = gravity * delta });
        }
        const position = self.position.add(self.velocity.scale(delta));

        if (self.collide) {
            self.animationBreak.update(delta);
            if (self.type == .pea) self.position = position;
            if (self.animationBreak.done) self.dead = true;
            return;
        }

        if (outWindow(position, self.size)) self.dead = true;

        self.position = position;
    }

    pub fn collidePlayer(self: *Bullet) void {
        if (self.type == .pea) {
            const i = window.rand.uintLessThanBiased(u32, peaBreakSound.len);
            peaBreakSound[i].start() catch unreachable;
            self.collide = true;
            self.velocity = .{ .x = 0.2 };
        } else {
            self.collide = true;
            sunExplodeSound.start() catch unreachable;
        }
    }

    fn outWindow(position: Vector, size: Vector) bool {
        if (position.x + size.x < 0 or position.x > window.width) return true;
        if (position.y + size.y < 0 or position.y > window.height) return true;
        return false;
    }

    pub fn render(self: *Bullet) void {
        if (self.collide) {
            const pos = self.position.add(self.explodeOffset);
            self.animationBreak.play(pos.x, pos.y);
        } else {
            gfx.draw(self.position.x, self.position.y, self.texture);
        }
    }
};
```

## 效果

![向日葵普通攻击][1]

[1]: images/植物明星大乱斗21.webp

## 附录
