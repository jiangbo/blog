# 0821-空洞武士-显示背景图片

## 目标

加载背景图片，并且将其显示到窗口中。

## 环境

- Time 2025-03-30
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

将 zstbi 也移除了，所以直接使用 stb 仓库的代码，自己来进行编译。

## build.zig

增加了 stb_image 的编译相关的配置和代码，目前只有 png 图片，所以只配置了 png 图片解析。

```zig
const std = @import("std");

pub fn build(b: *std.Build) !void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});

    const exe = b.addExecutable(.{
        .name = "demo",
        .root_source_file = b.path("src/main.zig"),
        .target = target,
        .optimize = optimize,
    });

    if (optimize != .Debug) exe.subsystem = .Windows;

    b.installArtifact(exe);

    const sokol = b.dependency("sokol", .{ .target = target, .optimize = optimize });
    exe.root_module.addImport("sokol", sokol.module("sokol"));

    const writeFiles = b.addWriteFiles();
    exe.step.dependOn(&writeFiles.step);

    const stb = b.dependency("stb", .{ .target = target, .optimize = optimize });
    exe.root_module.addIncludePath(stb.path("."));
    const stbImagePath = writeFiles.add("stb_image.c", stbImageSource);
    exe.root_module.addCSourceFile(.{ .file = stbImagePath, .flags = &.{"-O2"} });

    const run_cmd = b.addRunArtifact(exe);
    run_cmd.step.dependOn(b.getInstallStep());
    if (b.args) |args| {
        run_cmd.addArgs(args);
    }
    const run_step = b.step("run", "Run the app");
    run_step.dependOn(&run_cmd.step);
}

const stbImageSource =
    \\
    \\#define STB_IMAGE_IMPLEMENTATION
    \\#define STBI_ONLY_PNG
    \\#include "stb_image.h"
    \\
;
```

## c.zig

```zig
const std = @import("std");

pub const stbImage = struct {
    const stb = @cImport(@cInclude("stb_image.h"));

    const Image = struct { data: []u8 = &[_]u8{}, width: u32, height: u32 };

    pub fn load(path: [:0]const u8) !Image {
        var width: c_int, var height: c_int = .{ 0, 0 };
        const result = stb.stbi_load(path, &width, &height, 0, 4);
        if (result == null) return error.LoadImageFailed;

        var image: Image = .{ .width = @intCast(width), .height = @intCast(height) };
        image.data = @as([*]u8, @ptrCast(result))[0 .. image.width * image.height * 4];
        return image;
    }

    pub fn unload(self: Image) void {
        stb.stbi_image_free(self.data.ptr);
    }
};
```

## cache.zig

简化了代码，去掉了内存分配失败相关的错误。

```zig
const std = @import("std");
const gfx = @import("graphics.zig");

var allocator: std.mem.Allocator = undefined;

pub fn init(alloc: std.mem.Allocator) void {
    allocator = alloc;
}

pub fn deinit() void {
    TextureCache.deinit();
}

pub const TextureCache = struct {
    const stbImage = @import("c.zig").stbImage;
    const Cache = std.StringHashMapUnmanaged(gfx.Texture);

    var cache: Cache = undefined;

    pub fn load(path: [:0]const u8) gfx.Texture {
        const entry = cache.getOrPut(allocator, path) catch unreachable;
        if (entry.found_existing) return entry.value_ptr.*;

        std.log.info("loading texture from: {s}", .{path});
        const image = stbImage.load(path) catch unreachable;
        defer stbImage.unload(image);

        const texture = gfx.Texture.init(image.width, image.height, image.data);
        entry.value_ptr.* = texture;
        entry.key_ptr.* = allocator.dupe(u8, path) catch unreachable;
        return texture;
    }

    pub fn deinit() void {
        var keyIter = cache.keyIterator();
        while (keyIter.next()) |key| allocator.free(key.*);
        cache.deinit(allocator);
    }
};
```

## main.zig

```zig
const std = @import("std");

const cache = @import("cache.zig");
const window = @import("window.zig");
const gfx = @import("graphics.zig");

pub fn init() void {
    cache.init(allocator);
    gfx.init(window.width, window.height);
}

pub fn event(ev: *const window.Event) void {
    _ = ev;
}

pub fn update() void {}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    gfx.draw(0, 0, cache.TextureCache.load("assets/background.png"));
}

pub fn deinit() void {
    cache.deinit();
}

var allocator: std.mem.Allocator = undefined;

pub fn main() void {
    var debugAllocator = std.heap.DebugAllocator(.{}).init;
    defer _ = debugAllocator.deinit();

    allocator = debugAllocator.allocator();
    window.width = 1280;
    window.height = 720;

    var prng = std.Random.DefaultPrng.init(@intCast(std.time.timestamp()));
    window.rand = prng.random();

    window.run(.{
        .title = "空洞武士",
        .init = init,
        .event = event,
        .update = update,
        .render = render,
        .deinit = deinit,
    });
}
```

## 效果

![显示背景图片][1]

[1]: images/空洞武士03.png

## 附录
