# 0818-植物明星大乱斗-死亡动画和完结

## 目标

死亡时，角色被弹飞，然后播放结束动画，最后跳转到菜单页面。

## 环境

- Time 2025-03-22
- Zig 0.14.0-dev.3298+6fe1993d8

## 参考

1. <https://www.bilibili.com/video/BV1jx4y1t7eP>

## 想法

呼，写完了。教程里大量使用了继承，Zig 语言没有继承，写得不顺畅，加了很多 if 判断。
完整的代码见附录。

## scene/GameScene.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const audio = @import("zaudio");

const scene = @import("../scene.zig");
const bulletModule = @import("bullet.zig");
const Bullet = bulletModule.Bullet;
const player = @import("player.zig");
const GameScene = @This();

player1: player.Player,
player2: player.Player,

bullets: std.BoundedArray(Bullet, 64),

imageSky: gfx.Texture,
imageHill: gfx.Texture,

platforms: [4]Platform,
player1StatusBars: StatusBar,
player2StatusBars: StatusBar,

backgroundSound: *audio.Sound,
winSound: *audio.Sound,

gameOver: bool = false,

player1Win: gfx.Texture = undefined,
player2Win: gfx.Texture = undefined,
winnerBar: gfx.Texture = undefined,

positionWinBar: bulletModule.Vector = .{},
positionWinText: bulletModule.Vector = .{},
WinBarDst: f32 = 0,
WinTextDst: f32 = 0,

timerIn: window.Timer = undefined,
timerOut: window.Timer = undefined,
startSlideOut: bool = false,

const speedWinnerBar: f32 = 3.0;
const speedWinnerText: f32 = 1.5;

pub fn init() GameScene {
    std.log.info("game scene init", .{});

    window.shakeCamera = window.ShakeCamera.init(0, 0);
    var self: GameScene = undefined;

    self.imageSky = gfx.loadTexture("assets/sky.png").?;
    self.imageHill = gfx.loadTexture("assets/hills.png").?;

    self.player1Win = gfx.loadTexture("assets/1P_winner.png").?;
    self.player2Win = gfx.loadTexture("assets/2P_winner.png").?;
    self.winnerBar = gfx.loadTexture("assets/winner_bar.png").?;

    self.bullets = std.BoundedArray(Bullet, 64).init(0) catch unreachable;
    self.backgroundSound = scene.audioEngine.createSoundFromFile(
        "assets/bgm_game.mp3",
        .{ .flags = .{ .stream = true, .looping = true } },
    ) catch unreachable;

    self.winSound = scene.audioEngine.createSoundFromFile( //
        "assets/ui_win.wav", .{}) catch unreachable;

    self.initPlatforms();

    self.player1StatusBars = .init(235, 625, scene.playerType1);
    self.player2StatusBars = .init(675, 625, scene.playerType2);
    self.player2StatusBars.faceLeft = true;
    @import("bullet.zig").init();

    return self;
}

fn initPlatforms(self: *GameScene) void {
    var texture = gfx.loadTexture("assets/platform_large.png").?;
    var platform: Platform = .{ .x = 122, .y = 455, .texture = texture };
    platform.shape.left = platform.x + 30;
    platform.shape.right = platform.x + texture.width - 30;
    platform.shape.y = platform.y + 60;
    self.platforms[0] = platform;

    texture = gfx.loadTexture("assets/platform_small.png").?;
    platform = .{ .x = 175, .y = 360, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[1] = platform;

    platform = .{ .x = 855, .y = 360, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[2] = platform;

    platform = .{ .x = 515, .y = 225, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[3] = platform;
}

pub fn enter(self: *GameScene) void {
    std.log.info("game scene enter", .{});
    self.backgroundSound.start() catch unreachable;

    self.player1 = .init(scene.playerType1, 200, 50, false);
    self.player2 = .init(scene.playerType2, 975, 50, true);
    self.player2.p1 = false;

    self.gameOver = false;
    self.startSlideOut = false;

    self.positionWinBar.x = -self.winnerBar.width;
    self.positionWinBar.y = (window.height - self.winnerBar.height) / 2;
    self.WinBarDst = (window.width - self.winnerBar.width) / 2;

    self.positionWinText.x = -self.player1Win.width;
    self.positionWinText.y = (window.height - self.player1Win.height) / 2;
    self.WinTextDst = (window.width - self.player1Win.width) / 2;

    self.timerIn = window.Timer.init(2500);
    self.timerOut = window.Timer.init(1000);
}

pub fn exit(self: *GameScene) void {
    std.log.info("game scene exit", .{});
    self.backgroundSound.stop() catch unreachable;
    self.winSound.stop() catch unreachable;
}

pub fn event(self: *GameScene, ev: *const window.Event) void {
    if (self.gameOver) return;
    switch (ev.key_code) {
        .A, .D, .W, .F, .G => self.player1.event(ev),
        .LEFT, .RIGHT, .UP, .PERIOD, .SLASH => self.player2.event(ev),
        else => {},
    }
}

pub fn update(self: *GameScene) void {
    const deltaTime = window.deltaMillisecond();

    if (self.player1.y > window.height) self.player1.hp = 0;
    if (self.player2.y > window.height) self.player2.hp = 0;

    if (self.player1.hp == 0 or self.player2.hp == 0) {
        self.gameOver = true;
        self.backgroundSound.stop() catch unreachable;
        self.winSound.start() catch unreachable;
    }

    if (self.gameOver) {
        self.positionWinBar.x += speedWinnerBar * deltaTime;
        self.positionWinText.x += speedWinnerText * deltaTime;

        if (!self.startSlideOut) {
            self.timerIn.update(deltaTime);
            if (self.positionWinBar.x > self.WinBarDst)
                self.positionWinBar.x = self.WinBarDst;

            if (self.positionWinText.x > self.WinTextDst)
                self.positionWinText.x = self.WinTextDst;

            if (self.timerIn.finished) self.startSlideOut = true;
        } else {
            if (self.timerOut.isFinishedAfterUpdate(deltaTime)) {
                scene.changeCurrentScene(.menu);
            }
        }
    }
    self.player1.update(deltaTime);
    self.player2.update(deltaTime);

    self.updateBullets(deltaTime);
    window.shakeCamera.update(deltaTime);
}

fn updateBullets(self: *GameScene, delta: f32) void {
    for (self.bullets.slice(), 0..) |*bullet, index| {
        bullet.update(delta);

        if (bullet.p1 and !bullet.collide and !self.player2.invulnerable) {
            if (self.player2.isCollide(bullet)) {
                bullet.collidePlayer();
                self.player2.collideBullet(bullet);
                self.player1.mp += 30;
                if (self.player1.mp > 100) self.player1.mp = 100;
            }
        }

        if (!bullet.p1 and !bullet.collide and !self.player1.invulnerable) {
            if (self.player1.isCollide(bullet)) {
                bullet.collidePlayer();
                self.player1.collideBullet(bullet);
                self.player2.mp += 30;
                if (self.player2.mp > 100) self.player2.mp = 100;
            }
        }
        if (bullet.dead) _ = self.bullets.swapRemove(index);
    }
}

pub fn render(self: *GameScene) void {
    var x = window.width - self.imageSky.width;
    var y = window.height - self.imageSky.height;
    gfx.draw(x / 2, y / 2, self.imageSky);

    x = window.width - self.imageHill.width;
    y = window.height - self.imageHill.height;
    gfx.draw(x / 2, y / 2, self.imageHill);

    for (&self.platforms) |platform| {
        gfx.draw(platform.x, platform.y, platform.texture);
    }

    self.player1.render();
    self.player2.render();

    for (self.bullets.slice()) |*bullet| bullet.render();

    self.player1StatusBars.render(self.player1.hp, self.player1.mp);
    self.player2StatusBars.render(self.player2.hp, self.player2.mp);

    if (self.gameOver) {
        gfx.draw(self.positionWinBar.x, self.positionWinBar.y, self.winnerBar);
        const win = if (self.player1.hp == 0) self.player2Win else self.player1Win;
        gfx.draw(self.positionWinText.x, self.positionWinText.y, win);
    }
}

pub fn deinit(self: *GameScene) void {
    std.log.info("game scene deinit", .{});
    bulletModule.deinit();
    self.backgroundSound.destroy();
    self.winSound.destroy();
}

const Platform = struct {
    x: f32,
    y: f32,
    texture: gfx.Texture,
    shape: Collision = .{ .left = 0, .right = 0, .y = 0 },

    const Collision = struct { left: f32, right: f32, y: f32 };
};

const StatusBar = struct {
    x: f32,
    y: f32,
    faceLeft: bool = false,
    back: gfx.Texture,
    hp: gfx.Texture,
    mp: gfx.Texture,
    avatar: gfx.Texture,

    pub fn init(x: f32, y: f32, playerType: scene.PlayerType) StatusBar {
        const avatar = if (playerType == .peaShooter)
            gfx.loadTexture("assets/avatar_peashooter.png").?
        else
            gfx.loadTexture("assets/avatar_sunflower.png").?;

        return .{
            .x = x,
            .y = y,
            .back = gfx.loadTexture("assets/empty.png").?,
            .hp = gfx.loadTexture("assets/hp.png").?,
            .mp = gfx.loadTexture("assets/mp.png").?,
            .avatar = avatar,
        };
    }

    pub fn render(self: *const StatusBar, hp: u32, mp: u32) void {
        gfx.drawOptions(self.avatar, .{
            .sourceRect = .{
                .width = if (self.faceLeft) -self.avatar.width else self.avatar.width,
                .height = self.avatar.height,
            },
            .targetRect = .{
                .x = self.x,
                .y = self.y,
                .width = self.avatar.width,
                .height = self.avatar.height,
            },
        });
        renderBar(self.x + 100, self.y + 10, self.back, 100);
        renderBar(self.x + 100, self.y + 10, self.hp, hp);

        renderBar(self.x + 100, self.y + 45, self.back, 100);
        renderBar(self.x + 100, self.y + 45, self.mp, mp);
    }

    fn renderBar(x: f32, y: f32, texture: gfx.Texture, value: u32) void {
        if (value == 0) return;
        gfx.drawOptions(texture, .{ .targetRect = .{
            .x = x,
            .y = y,
            .width = texture.width * @as(f32, @floatFromInt(value)) / 5.5,
            .height = texture.height * 1.5,
        } });
    }
};
```

## scene/player.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

const scene = @import("../scene.zig");
const Bullet = @import("bullet.zig").Bullet;
const Vector = @import("bullet.zig").Vector;

pub const Player = struct {
    x: f32,
    y: f32,
    facingLeft: bool,
    leftKeyDown: bool = false,
    rightKeyDown: bool = false,
    velocity: Vector = .{},
    width: f32 = 96,
    height: f32 = 96,
    p1: bool = true,

    attackTimer: window.Timer = .init(attackInterval),
    attackExTimer: window.Timer = .init(2500),
    sunTextTimer: window.Timer = .init(2500),

    invulnerableTimer: window.Timer = .init(750),
    invulnerableToggleTimer: window.Timer = .init(75),
    invulnerable: bool = false,
    invulnerableToggle: bool = false,

    hp: u32 = 100,
    mp: u32 = 0,

    textureIdle: gfx.Texture = undefined,
    animationIdle: gfx.FrameAnimation = undefined,
    animationRun: gfx.FrameAnimation = undefined,
    animationAttack: gfx.FrameAnimation = undefined,
    animationSunText: gfx.FrameAnimation = undefined,

    animationJump: gfx.FrameAnimation = undefined,
    positionJump: Vector = .{},
    jumpVisible: bool = false,

    animationLand: gfx.FrameAnimation = undefined,
    positionLand: Vector = .{},
    landVisible: bool = false,

    particles: std.BoundedArray(Particle, 32) = undefined,
    particleTimer: window.Timer = .init(75),

    cursorVisible: bool = true,
    cursorTimer: window.Timer = undefined,
    cursorP1: gfx.Texture = undefined,
    cursorP2: gfx.Texture = undefined,

    hurtDirection: Vector = .{},

    const runVelocity: f32 = 0.55;
    const gravity: f32 = 1.6e-3;
    const jumpVelocity: f32 = -0.85;
    const attackInterval: f32 = 500;
    const attackIntervalEx: f32 = 200;

    pub fn init(playerType: scene.PlayerType, x: f32, y: f32, faceLeft: bool) Player {
        var self: Player = .{ .x = x, .y = y, .facingLeft = faceLeft };
        self.attackExTimer.finished = true;
        self.sunTextTimer.finished = true;
        self.invulnerableTimer.finished = true;
        self.invulnerableToggleTimer.finished = true;

        self.cursorVisible = true;
        self.cursorTimer = window.Timer.init(2500);
        self.cursorP1 = gfx.loadTexture("assets/1P_cursor.png").?;
        self.cursorP2 = gfx.loadTexture("assets/2P_cursor.png").?;

        if (playerType == .peaShooter) {
            self.animationIdle = .load("assets/peashooter_idle_{}.png", 9);
            self.animationRun = .load("assets/peashooter_run_{}.png", 5);
            self.animationAttack = .load("assets/peashooter_attack_ex_{}.png", 3);

            self.textureIdle = whiteTexture("assets/peashooter_idle_1.png");
        } else {
            self.animationIdle = .load("assets/sunflower_idle_{}.png", 8);
            self.animationRun = .load("assets/sunflower_run_{}.png", 5);
            self.animationAttack = .load("assets/sunflower_attack_ex_{}.png", 9);
            self.animationSunText = .load("assets/sun_text_{}.png", 5);

            self.textureIdle = whiteTexture("assets/sunflower_idle_1.png");
        }

        self.animationJump = .load("assets/jump_effect_{}.png", 5);
        self.animationJump.loop = false;
        self.animationLand = .load("assets/land_effect_{}.png", 2);
        self.animationLand.loop = false;
        self.particles = std.BoundedArray(Particle, 32).init(0) catch unreachable;

        return self;
    }

    pub fn event(self: *Player, ev: *const window.Event) void {
        if (self.attackExTimer.isRunning() and ev.type == .KEY_DOWN) return;

        switch (ev.type) {
            .KEY_DOWN => switch (ev.key_code) {
                .A, .LEFT => {
                    self.leftKeyDown = true;
                    self.facingLeft = true;
                },
                .D, .RIGHT => {
                    self.rightKeyDown = true;
                    self.facingLeft = false;
                },
                .W, .UP => {
                    if (self.velocity.y != 0) return;
                    self.velocity.y += Player.jumpVelocity;
                    self.jumpVisible = true;
                    const x = self.x + self.width / 2 - self.animationJump.textures[0].width / 2;
                    const y = self.y + self.height - self.animationJump.textures[0].height;
                    self.positionJump = .{ .x = x, .y = y };
                    self.animationJump.reset();
                },
                .F, .PERIOD => self.attack(),
                .G, .SLASH => self.attackEx(),
                else => {},
            },
            .KEY_UP => switch (ev.key_code) {
                .A, .LEFT => self.leftKeyDown = false,
                .D, .RIGHT => self.rightKeyDown = false,
                else => {},
            },
            else => {},
        }
    }

    pub fn update(self: *Player, delta: f32) void {
        self.attackTimer.update(delta);
        self.invulnerableUpdate(delta);

        if (self.cursorTimer.isFinishedAfterUpdate(delta)) self.cursorVisible = false;

        {
            var index: usize = self.particles.len;
            while (index > 0) {
                index -= 1;
                var particle = &self.particles.buffer[index];
                particle.update(delta);
                if (!particle.valid) _ = self.particles.swapRemove(index);
            }
        }
        {
            if (self.jumpVisible) {
                self.animationJump.update(delta);
                if (self.animationJump.finished()) self.jumpVisible = false;
            }
            if (self.landVisible) {
                self.animationLand.update(delta);
                if (self.animationLand.finished()) self.landVisible = false;
            }
        }

        if (self.attackExTimer.isRunningAfterUpdate(delta))
            self.animationAttack.update(delta);

        if (self.sunTextTimer.isRunningAfterUpdate(delta))
            self.animationSunText.update(delta);

        if (self.attackExTimer.isRunning()) {
            self.animationAttack.update(delta);
            self.attackExTimer.update(delta);
            if (self.attackExTimer.finished) {
                self.attackTimer.duration = attackInterval;
            } else if (self.attackTimer.finished) {
                window.shakeCamera.restart(5, 100);
                const bullet = self.spawnBullet();
                scene.gameScene.bullets.append(bullet) catch unreachable;
            }
            return;
        }

        var direction: f32 = 0;
        if (self.leftKeyDown) direction -= 1;
        if (self.rightKeyDown) direction += 1;
        self.x += direction * Player.runVelocity * delta;

        if (self.leftKeyDown or self.rightKeyDown) {
            self.animationRun.update(delta);
            if (self.particleTimer.isFinishedAfterUpdate(delta)) {
                self.particleTimer.reset();
                var effect: Particle = .load("assets/run_effect_{}.png", 4, 45);
                effect.x = self.x + self.width / 2 - effect.width / 2;
                effect.y = self.y + self.height - effect.height;
                self.particles.appendAssumeCapacity(effect);
            }
        } else {
            self.animationIdle.update(delta);
        }

        moveAndCollide(self, delta);
        self.x += self.velocity.x * delta;
    }

    fn invulnerableUpdate(self: *Player, delta: f32) void {
        if (self.invulnerableTimer.isFinishedAfterUpdate(delta)) {
            self.invulnerable = false;
            self.invulnerableToggleTimer.reset();
            return;
        }

        if (self.invulnerableToggleTimer.isFinishedAfterUpdate(delta)) {
            self.invulnerableToggle = !self.invulnerableToggle;
            self.invulnerableToggleTimer.reset();
        }
    }

    fn moveAndCollide(self: *Player, delta: f32) void {
        const velocity = self.velocity.y + Player.gravity * delta;
        const y = self.y + velocity * delta;

        const platforms = &scene.gameScene.platforms;
        for (platforms) |*platform| {
            if (self.x + self.width < platform.shape.left) continue;
            if (self.x > platform.shape.right or self.hp == 0) continue;
            if (y + self.height < platform.shape.y) continue;

            const deltaPosY = self.velocity.y * delta;
            const lastFootPosY = self.y + self.height - deltaPosY;

            if (lastFootPosY <= platform.shape.y) {
                self.y = platform.shape.y - self.height;
                defer self.velocity.y = 0;
                if (self.velocity.y == 0) break;

                self.landVisible = true;
                const x = self.x + self.width / 2 - self.animationLand.textures[0].width / 2;
                const height = self.y + self.height - self.animationLand.textures[0].height;
                self.positionLand = .{ .x = x, .y = height };
                self.animationLand.reset();
                break;
            }
        } else {
            self.y = y;
            self.velocity.y = velocity;
        }
    }

    pub fn render(self: *const Player) void {
        if (self.sunTextTimer.isRunning()) {
            const text = self.animationSunText;
            const x = self.x - self.width / 2 + text.textures[0].width / 2;
            const y = self.y - text.textures[0].height;
            text.playFlipX(x, y, self.facingLeft);
        }

        if (self.cursorVisible) {
            const x = self.x + (self.width - self.cursorP1.width) / 2;
            const y = self.y - self.cursorP1.height;
            const cursor = if (self.p1) self.cursorP1 else self.cursorP2;
            gfx.draw(x, y, cursor);
        }

        for (self.particles.slice()) |*particle| particle.render();
        if (self.jumpVisible) {
            self.animationJump.play(self.positionJump.x, self.positionJump.y);
        }
        if (self.landVisible) {
            self.animationLand.play(self.positionLand.x, self.positionLand.y);
        }

        if (self.invulnerable and self.invulnerableToggle) {
            gfx.draw(self.x, self.y, self.textureIdle);
            return;
        }

        if (self.attackExTimer.isRunning()) {
            self.animationAttack.playFlipX(self.x, self.y, self.facingLeft);
        } else if (self.leftKeyDown) {
            self.animationRun.playFlipX(self.x, self.y, true);
        } else if (self.rightKeyDown) {
            self.animationRun.playFlipX(self.x, self.y, false);
        } else {
            self.animationIdle.playFlipX(self.x, self.y, self.facingLeft);
        }
    }

    pub fn attack(self: *Player) void {
        if (self.attackTimer.isRunning()) return;

        var bullet = self.spawnBullet();
        bullet.playShootSound();

        scene.gameScene.bullets.append(bullet) catch unreachable;
    }

    fn spawnBullet(self: *Player) Bullet {
        self.attackTimer.reset();

        var bullet = Bullet.init(self.p1);

        const x: f32 = if (self.facingLeft) self.x else self.x + self.width;
        bullet.position = .{ .x = x - bullet.texture.width / 2, .y = self.y };
        if (self.facingLeft) bullet.velocity.x = -bullet.velocity.x;

        return bullet;
    }

    pub fn attackEx(self: *Player) void {
        if (self.mp < 100) return;

        const playerType = if (self.p1) scene.playerType1 else scene.playerType2;

        if (playerType == .peaShooter) {
            self.attackExTimer.reset();
            self.attackTimer.duration = attackIntervalEx;
        } else {
            var bullet = Bullet.initSunBulletEx();
            const player = if (self.p1)
                scene.gameScene.player2
            else
                scene.gameScene.player1;
            bullet.p1 = self.p1;
            bullet.position.x = player.x + player.width / 2 - bullet.texture.width / 2;
            scene.gameScene.bullets.append(bullet) catch unreachable;
            self.sunTextTimer.reset();
        }

        Bullet.playShootExSound(playerType);
        self.mp = 0;
    }

    pub fn isCollide(self: *Player, bullet: *Bullet) bool {
        if (bullet.type != .sunEx) {
            const pos = bullet.center();
            if (pos.x < self.x or pos.x > self.x + self.width) return false;
            if (pos.y < self.y or pos.y > self.y + self.height) return false;
            return true;
        }

        if (self.x < bullet.position.x) return false;
        if (self.x + self.width > bullet.position.x + bullet.texture.width) return false;
        if (self.y < bullet.position.y) return false;
        if (self.y + self.height > bullet.position.y + bullet.texture.height) return false;
        return true;
    }

    pub fn collideBullet(self: *Player, bullet: *Bullet) void {
        self.invulnerable = true;
        self.invulnerableTimer.reset();

        self.hp -|= bullet.damage;
        const position: Vector = .{ .x = self.x, .y = self.y };
        self.hurtDirection = bullet.position.sub(position);

        if (self.hp == 0) {
            self.velocity.x = if (self.hurtDirection.x < 0) 0.35 else -0.35;
            self.velocity.y = -1;
        }
    }
};

const stbi = @import("stbi");
fn whiteTexture(path: [:0]const u8) gfx.Texture {
    var image = stbi.Image.loadFromFile(path, 4) catch unreachable;
    defer image.deinit();

    for (0..image.data.len / 4) |index| {
        const i = index * 4;
        if (image.data[i + 3] == 0) continue;
        image.data[i + 0] = 255;
        image.data[i + 1] = 255;
        image.data[i + 2] = 255;
    }

    return gfx.Texture.init(image.width, image.height, image.data);
}

const Particle = struct {
    x: f32 = 0,
    y: f32 = 0,
    width: f32,
    height: f32,
    index: usize = 0,
    timer: f32 = 0,
    lifespan: f32,
    valid: bool = true,

    textures: []const gfx.Texture,

    pub fn load(comptime pathFmt: []const u8, max: u8, lifespan: f32) Particle {
        const frame = gfx.FrameAnimation.load(pathFmt, max);
        return .{
            .textures = frame.textures,
            .width = frame.textures[0].width,
            .height = frame.textures[0].height,
            .lifespan = lifespan,
        };
    }

    pub fn update(self: *Particle, delta: f32) void {
        self.timer += delta;

        if (self.timer < self.lifespan) return;
        self.timer = 0;
        self.index += 1;
        if (self.index >= self.textures.len) {
            self.index = self.textures.len - 1;
            self.valid = false;
        }
    }

    pub fn render(self: *const Particle) void {
        gfx.draw(self.x, self.y, self.textures[self.index]);
    }
};
```

## scene/bullet.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");
const audio = @import("zaudio");

const scene = @import("../scene.zig");

var peaBreakSound: [3]*audio.Sound = undefined;
var peaShootSound: [2]*audio.Sound = undefined;
var peaShootExSound: *audio.Sound = undefined;

var sunExplodeSound: *audio.Sound = undefined;
var sunExplodeExSound: *audio.Sound = undefined;
var sunTextSound: *audio.Sound = undefined;

pub fn init() void {
    peaBreakSound[0] = scene.audioEngine.createSoundFromFile( //
        "assets/pea_break_1.mp3", .{}) catch unreachable;
    peaBreakSound[1] = scene.audioEngine.createSoundFromFile( //
        "assets/pea_break_2.mp3", .{}) catch unreachable;
    peaBreakSound[2] = scene.audioEngine.createSoundFromFile( //
        "assets/pea_break_3.mp3", .{}) catch unreachable;

    peaShootSound[0] = scene.audioEngine.createSoundFromFile( //
        "assets/pea_shoot_1.mp3", .{}) catch unreachable;

    peaShootSound[1] = scene.audioEngine.createSoundFromFile( //
        "assets/pea_shoot_2.mp3", .{}) catch unreachable;

    peaShootExSound = scene.audioEngine.createSoundFromFile( //
        "assets/pea_shoot_ex.mp3", .{}) catch unreachable;

    sunExplodeSound = scene.audioEngine.createSoundFromFile( //
        "assets/sun_explode.mp3", .{}) catch unreachable;

    sunExplodeExSound = scene.audioEngine.createSoundFromFile( //
        "assets/sun_explode_ex.mp3", .{}) catch unreachable;

    sunTextSound = scene.audioEngine.createSoundFromFile( //
        "assets/sun_text.mp3", .{}) catch unreachable;
}

pub fn deinit() void {
    for (peaBreakSound) |sound| sound.destroy();
    for (peaShootSound) |sound| sound.destroy();
    peaShootExSound.destroy();
    sunExplodeSound.destroy();
    sunExplodeExSound.destroy();
    sunTextSound.destroy();
}

pub const Vector = struct {
    x: f32 = 0,
    y: f32 = 0,
    z: f32 = 0,

    pub fn add(a: Vector, b: Vector) Vector {
        return .{ .x = a.x + b.x, .y = a.y + b.y, .z = a.z + b.z };
    }

    pub fn scale(a: Vector, b: f32) Vector {
        return .{ .x = a.x * b, .y = a.y * b, .z = a.z * b };
    }

    pub fn sub(a: Vector, b: Vector) Vector {
        return .{ .x = a.x - b.x, .y = a.y - b.y, .z = a.z - b.z };
    }
};

pub const Bullet = struct {
    size: Vector,
    position: Vector,
    velocity: Vector,
    damage: u32,
    dead: bool = false,
    collide: bool = false,
    p1: bool = true,
    explodeOffset: Vector = .{},

    type: Type = .pea,
    animationIdle: gfx.FrameAnimation,
    animationBreak: gfx.FrameAnimation,

    texture: gfx.Texture = undefined,

    const peaSpeed: f32 = 0.75;
    const peaSpeedEx: f32 = 1.5;
    const gravity: f32 = 1.6e-3;
    const Type = enum { pea, sun, sunEx };

    pub fn init(p1: bool) Bullet {
        const playerType = if (p1) scene.playerType1 else scene.playerType2;
        var self = switch (playerType) {
            .peaShooter => initPeaBullet(),
            .sunFlower => initSunBullet(),
        };

        self.size = .{ .x = self.texture.width, .y = self.texture.height };
        self.p1 = p1;
        return self;
    }

    fn initPeaBullet() Bullet {
        var self: Bullet = undefined;
        self.texture = gfx.loadTexture("assets/pea.png").?;
        self.type = .pea;
        self.animationBreak = .load("assets/pea_break_{}.png", 3);
        self.animationBreak.loop = false;
        self.damage = 5;
        self.velocity = .{ .x = peaSpeed };

        return self;
    }

    fn initSunBullet() Bullet {
        var self: Bullet = undefined;
        self.texture = gfx.loadTexture("assets/sun_1.png").?;
        self.type = .sun;
        self.animationIdle = .load("assets/sun_{}.png", 5);
        self.animationBreak = .load("assets/sun_explode_{}.png", 5);
        self.animationBreak.timer.duration = 75;
        self.animationBreak.loop = false;
        self.damage = 10;
        self.velocity = .{ .x = 0.25, .y = -0.65 };

        self.explodeOffset = .{
            .x = (self.texture.width - self.animationBreak.textures[0].width) / 2,
            .y = (self.texture.height - self.animationBreak.textures[0].height) / 2,
        };

        return self;
    }

    pub fn initSunBulletEx() Bullet {
        var self: Bullet = undefined;
        self.texture = gfx.loadTexture("assets/sun_ex_1.png").?;
        self.type = .sunEx;
        self.animationIdle = .load("assets/sun_ex_{}.png", 5);
        self.animationBreak = .load("assets/sun_ex_explode_{}.png", 5);
        self.animationBreak.timer.duration = 75;
        self.animationBreak.loop = false;
        self.damage = 20;
        self.velocity = .{ .y = 0.15 };
        self.position.y = -self.texture.height;

        self.explodeOffset = .{
            .x = (self.texture.width - self.animationBreak.textures[0].width) / 2,
            .y = (self.texture.height - self.animationBreak.textures[0].height) / 2,
        };

        self.size = .{ .x = self.texture.width, .y = self.texture.height };

        return self;
    }

    pub fn playShootSound(self: *Bullet) void {
        if (self.type == .pea) {
            const i = window.rand.uintLessThanBiased(u32, peaShootSound.len);
            peaShootSound[i].start() catch unreachable;
        }
    }

    pub fn playShootExSound(playerType: scene.PlayerType) void {
        if (playerType == .peaShooter)
            peaShootExSound.start() catch unreachable
        else
            sunTextSound.start() catch unreachable;
    }

    pub fn center(self: Bullet) Vector {
        return .{
            .x = self.position.x + self.size.x / 2,
            .y = self.position.y + self.size.y / 2,
        };
    }

    pub fn update(self: *Bullet, delta: f32) void {
        if (self.type == .sun) {
            self.velocity = self.velocity.add(.{ .y = gravity * delta });
        }
        const position = self.position.add(self.velocity.scale(delta));

        if (self.collide) {
            self.animationBreak.update(delta);
            if (self.type == .pea) self.position = position;
            if (self.animationBreak.finished()) self.dead = true;
            return;
        }

        if (outWindow(position, self.size)) self.dead = true;

        if (self.type != .pea) self.animationIdle.update(delta);
        self.position = position;
    }

    pub fn collidePlayer(self: *Bullet) void {
        self.collide = true;

        switch (self.type) {
            .pea => {
                const i = window.rand.uintLessThanBiased(u32, peaBreakSound.len);
                peaBreakSound[i].start() catch unreachable;
            },
            .sun => {
                sunExplodeSound.start() catch unreachable;
                window.shakeCamera.restart(5, 250);
            },
            .sunEx => {
                sunExplodeExSound.start() catch unreachable;
                window.shakeCamera.restart(20, 350);
            },
        }
    }

    fn outWindow(position: Vector, size: Vector) bool {
        if (position.x + size.x < 0 or position.x > window.width) return true;
        if (position.y + size.y < 0 or position.y > window.height) return true;
        return false;
    }

    pub fn render(self: *Bullet) void {
        if (self.collide) {
            const pos = self.position.add(self.explodeOffset);
            self.animationBreak.play(pos.x, pos.y);
        } else switch (self.type) {
            .pea => gfx.draw(self.position.x, self.position.y, self.texture),
            .sun => self.animationIdle.play(self.position.x, self.position.y),
            .sunEx => self.animationIdle.play(self.position.x, self.position.y),
        }
    }
};
```

## 效果

![死亡动画和完结][1]

[1]: images/植物明星大乱斗29.webp

## 附录
