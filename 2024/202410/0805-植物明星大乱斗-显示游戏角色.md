# 0805-植物明星大乱斗-显示游戏角色

## 目标

显示了背景后，需要显示玩家选定的游戏角色，游戏角色可以响应玩家的按键，改变朝向。

## 环境

- Time 2025-03-11
- Zig 0.14.0-dev.3298+6fe1993d8

## 参考

1. <https://www.bilibili.com/video/BV1jx4y1t7eP>

## 想法

有两种可以选择的角色，有两个玩家，对应两套按键，还不太好组合。

## scene/player.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

const scene = @import("../scene.zig");

pub const Player = union(scene.PlayerType) {
    peaShooter: PeaShooterPlayer,
    sunFlower: SunFlowerPlayer,

    pub fn init(playerType: scene.PlayerType, x: f32, y: f32, faceLeft: bool) Player {
        return switch (playerType) {
            .peaShooter => .{ .peaShooter = .init(x, y, faceLeft) },
            .sunFlower => .{ .sunFlower = .init(x, y, faceLeft) },
        };
    }

    pub fn event(self: *Player, ev: *const window.Event) void {
        switch (self.*) {
            inline else => |*s| switch (ev.type) {
                .KEY_DOWN => switch (ev.key_code) {
                    .A, .LEFT => {
                        s.leftKeyDown = true;
                        s.facingLeft = true;
                    },
                    .D, .RIGHT => {
                        s.rightKeyDown = true;
                        s.facingLeft = false;
                    },
                    else => {},
                },
                .KEY_UP => switch (ev.key_code) {
                    .A, .LEFT => s.leftKeyDown = false,
                    .D, .RIGHT => s.rightKeyDown = false,
                    else => {},
                },
                else => {},
            },
        }
    }

    pub fn update(self: *Player, delta: f32) void {
        switch (self.*) {
            inline else => |*s| s.animationIdle.update(delta),
        }
    }

    pub fn draw(self: Player) void {
        switch (self) {
            inline else => |*s| s.animationIdle.playFlipX(s.x, s.y, s.facingLeft),
        }
    }
};

const PeaShooterPlayer = struct {
    x: f32,
    y: f32,
    facingLeft: bool = false,
    leftKeyDown: bool = false,
    rightKeyDown: bool = false,

    animationIdle: gfx.BoundedFrameAnimation(9),

    pub fn init(x: f32, y: f32, faceLeft: bool) PeaShooterPlayer {
        return .{
            .x = x,
            .y = y,
            .facingLeft = faceLeft,
            .animationIdle = .init("assets/peashooter_idle_{}.png"),
        };
    }
};

const SunFlowerPlayer = struct {
    x: f32,
    y: f32,
    facingLeft: bool,
    leftKeyDown: bool = false,
    rightKeyDown: bool = false,

    animationIdle: gfx.BoundedFrameAnimation(8),

    pub fn init(x: f32, y: f32, faceLeft: bool) SunFlowerPlayer {
        return .{
            .x = x,
            .y = y,
            .facingLeft = faceLeft,
            .animationIdle = .init("assets/sunflower_idle_{}.png"),
        };
    }
};
```

## scene/GameScene.zig

```zig
const std = @import("std");
const window = @import("../window.zig");
const gfx = @import("../graphics.zig");

const scene = @import("../scene.zig");
const Player = @import("player.zig").Player;
const GameScene = @This();

player1: Player = undefined,
player2: Player = undefined,

imageSky: gfx.Texture,
imageHill: gfx.Texture,

platforms: [4]Platform = undefined,

pub fn init() GameScene {
    std.log.info("game scene init", .{});

    var self: GameScene = undefined;

    self.imageSky = gfx.loadTexture("assets/sky.png").?;
    self.imageHill = gfx.loadTexture("assets/hills.png").?;

    self.initPlatforms();

    return self;
}

fn initPlatforms(self: *GameScene) void {
    var texture = gfx.loadTexture("assets/platform_large.png").?;
    var platform: Platform = .{ .x = 122, .y = 455, .texture = texture };
    platform.shape.left = platform.x + 30;
    platform.shape.right = platform.x + texture.width - 30;
    platform.shape.y = platform.y + 60;
    self.platforms[0] = platform;

    texture = gfx.loadTexture("assets/platform_small.png").?;
    platform = .{ .x = 175, .y = 360, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[1] = platform;

    platform = .{ .x = 855, .y = 360, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[2] = platform;

    platform = .{ .x = 515, .y = 225, .texture = texture };
    platform.shape.left = platform.x + 40;
    platform.shape.right = platform.x + texture.width - 40;
    platform.shape.y = platform.y + texture.height / 2;
    self.platforms[3] = platform;
}

pub fn enter(self: *GameScene) void {
    std.log.info("game scene enter", .{});

    self.player1 = .init(scene.playerType1, 200, 50, false);
    self.player2 = .init(scene.playerType2, 975, 50, true);
}

pub fn exit(self: *GameScene) void {
    std.log.info("game scene exit", .{});
    _ = self;
}

pub fn event(self: *GameScene, ev: *const window.Event) void {
    switch (ev.key_code) {
        .A, .D => self.player1.event(ev),
        .LEFT, .RIGHT => self.player2.event(ev),
        else => {},
    }
}

pub fn update(self: *GameScene) void {
    const deltaTime = window.deltaMillisecond();

    self.player1.update(deltaTime);
    self.player2.update(deltaTime);
}

pub fn render(self: *GameScene) void {
    var x = window.width - self.imageSky.width;
    var y = window.height - self.imageSky.height;
    gfx.draw(x / 2, y / 2, self.imageSky);

    x = window.width - self.imageHill.width;
    y = window.height - self.imageHill.height;
    gfx.draw(x / 2, y / 2, self.imageHill);

    for (&self.platforms) |platform| {
        gfx.draw(platform.x, platform.y, platform.texture);
    }

    self.player1.draw();
    self.player2.draw();
}

const Platform = struct {
    x: f32,
    y: f32,
    texture: gfx.Texture,
    shape: Collision = .{ .left = 0, .right = 0, .y = 0 },

    const Collision = struct { left: f32, right: f32, y: f32 };
};
```

## 效果

![显示游戏角色][1]

[1]: images/植物明星大乱斗17.webp

## 附录
