# 0839-空洞武士-混合音乐和音效

## 目标

之前实现的播放音乐的功能，只能播放一个音乐，后面的会替换前面的，解决同时播放多个的问题。

## 环境

- Time 2025-04-09
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

传递给 stb_vorbis 的缓冲，直接赋值而不是相加，导致只能播放一个，增加一个相加的方法。

## c.zig

stb_vorbis_add_samples_float_interleaved 是一个自定义的函数。

```zig
const std = @import("std");

pub const stbImage = struct {
    const stb = @cImport(@cInclude("stb_image.h"));

    const Image = struct { data: []u8 = &[_]u8{}, width: u32, height: u32 };

    pub fn load(path: [:0]const u8) !Image {
        var width: c_int, var height: c_int = .{ 0, 0 };
        const result = stb.stbi_load(path, &width, &height, 0, 4);
        if (result == null) return error.LoadImageFailed;

        var image: Image = .{ .width = @intCast(width), .height = @intCast(height) };
        image.data = @as([*]u8, @ptrCast(result))[0 .. image.width * image.height * 4];
        return image;
    }

    pub fn unload(self: Image) void {
        stb.stbi_image_free(self.data.ptr);
    }
};

pub const stbAudio = stbVorbis;
pub const stbVorbis = struct {
    const stb = @cImport({
        @cDefine("STB_VORBIS_NO_PUSHDATA_API", {});
        @cDefine("STB_VORBIS_HEADER_ONLY", {});
        @cDefine("STB_VORBIS_NO_INTEGER_CONVERSION", {});
        @cInclude("stb_Vorbis.c");
    });

    pub const Audio = stb.stb_vorbis;
    pub const AudioInfo = stb.stb_vorbis_info;

    pub fn load(path: [:0]const u8) !*Audio {
        var errorCode: c_int = 0;

        const vorbis = stb.stb_vorbis_open_filename(path, &errorCode, null);
        if (errorCode != 0 or vorbis == null) return error.loadAudioFailed;
        return vorbis.?;
    }

    pub fn getInfo(audio: *Audio) AudioInfo {
        return stb.stb_vorbis_get_info(audio);
    }

    pub fn getSampleCount(audio: *Audio) usize {
        return stb.stb_vorbis_stream_length_in_samples(audio);
    }

    pub fn addSamples(audio: *Audio, buffer: []f32, channels: i32) c_int {
        return stb_vorbis_add_samples_float_interleaved(
            audio,
            channels,
            @ptrCast(buffer),
            @intCast(buffer.len),
        );
    }

    extern fn stb_vorbis_add_samples_float_interleaved(
        f: *Audio,
        channels: i32,
        buffer: [*]f32,
        num_floats: i32,
    ) c_int;

    pub fn reset(audio: *Audio) void {
        _ = stb.stb_vorbis_seek_start(audio);
    }

    pub fn unload(audio: *Audio) void {
        stb.stb_vorbis_close(audio);
    }
};
```

## build.zig

支持同时播放多个音频，将单通道转为了多通道。

```zig
const std = @import("std");

pub fn build(b: *std.Build) !void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});

    const exe = b.addExecutable(.{
        .name = "demo",
        .root_source_file = b.path("src/main.zig"),
        .target = target,
        .optimize = optimize,
    });

    if (optimize != .Debug) exe.subsystem = .Windows;

    b.installArtifact(exe);

    const sokol = b.dependency("sokol", .{ .target = target, .optimize = optimize });
    exe.root_module.addImport("sokol", sokol.module("sokol"));

    const writeFiles = b.addWriteFiles();
    exe.step.dependOn(&writeFiles.step);

    const stb = b.dependency("stb", .{ .target = target, .optimize = optimize });
    exe.root_module.addIncludePath(stb.path("."));
    const stbImagePath = writeFiles.add("stb_image.c", stbImageSource);
    exe.root_module.addCSourceFile(.{ .file = stbImagePath, .flags = &.{"-O2"} });

    const stbAudioPath = writeFiles.add("stb_audio.c", stbAudioSource);
    exe.root_module.addCSourceFile(.{ .file = stbAudioPath, .flags = &.{"-O2"} });

    const run_cmd = b.addRunArtifact(exe);
    run_cmd.step.dependOn(b.getInstallStep());
    if (b.args) |args| {
        run_cmd.addArgs(args);
    }
    const run_step = b.step("run", "Run the app");
    run_step.dependOn(&run_cmd.step);
}

const stbImageSource =
    \\
    \\#define STB_IMAGE_IMPLEMENTATION
    \\#define STBI_ONLY_PNG
    \\#include "stb_image.h"
    \\
;

const stbAudioSource =
    \\
    \\#define STB_VORBIS_NO_PUSHDATA_API
    \\#define STB_VORBIS_NO_INTEGER_CONVERSION
    \\
    \\#include "stb_vorbis.c"
    \\
    \\int stb_vorbis_add_samples_float_interleaved(stb_vorbis *f,
    \\int channels, float *buffer, int num_floats) {
    \\   float **outputs;
    \\   int len = num_floats / channels;
    \\   int n=0;
    \\   int z = f->channels;
    \\   if (z > channels) z = channels;
    \\   while (n < len) {
    \\      int i,j;
    \\      int k = f->channel_buffer_end - f->channel_buffer_start;
    \\      if (n+k >= len) k = len - n;
    \\      for (j=0; j < k; ++j) {
    \\         for (i=0; i < z; ++i)
    \\            *buffer++ += f->channel_buffers[i][f->channel_buffer_start+j];
    \\         for (   ; i < channels; ++i)
    \\            *buffer++ += f->channel_buffers[i-1][f->channel_buffer_start+j];;
    \\      }
    \\      n += k;
    \\      f->channel_buffer_start += k;
    \\      if (n == len)
    \\         break;
    \\      if (!stb_vorbis_get_frame_float(f, NULL, &outputs))
    \\         break;
    \\   }
    \\   return n;
    \\}
    \\
;
```

## main.zig

同时播放两个音频。

```zig
const std = @import("std");

const cache = @import("cache.zig");
const window = @import("window.zig");
const gfx = @import("graphics.zig");
const audio = @import("audio.zig");
const scene = @import("scene.zig");

pub fn init() void {
    cache.init(allocator);
    gfx.init(window.width, window.height);
    audio.init();

    scene.init();
    const bgm = audio.AudioNode.loadLoop("assets/audio/bgm.ogg");
    bgm.play();

    const sound = audio.AudioNode.loadLoop("assets/audio/barb_break.ogg");
    sound.play();
}

pub fn event(ev: *const window.Event) void {
    scene.event(ev);
}

pub fn update() void {
    scene.update();
}

pub fn render() void {
    scene.render();
}

pub fn deinit() void {
    scene.deinit();
    audio.deinit();
    cache.deinit();
}

var allocator: std.mem.Allocator = undefined;

pub fn main() void {
    var debugAllocator = std.heap.DebugAllocator(.{}).init;
    defer _ = debugAllocator.deinit();

    allocator = debugAllocator.allocator();
    window.width = 1280;
    window.height = 720;

    var prng = std.Random.DefaultPrng.init(@intCast(std.time.timestamp()));
    window.rand = prng.random();

    window.run(.{
        .title = "空洞武士",
        .init = init,
        .event = event,
        .update = update,
        .render = render,
        .deinit = deinit,
    });
}
```

## 效果

由于是音频，所以就不上效果了。

## 附录
