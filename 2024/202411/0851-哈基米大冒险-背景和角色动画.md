# 0851-哈基米大冒险-背景和角色动画

## 目标

将背景图片渲染到窗口中，新增了一个角色的动画。

## 环境

- Time 2025-04-13
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1wmy6YuEzh>

## 想法

基于之前所做的基础之上，基本上没有怎么变化，将其中的一些封装拆掉了。

## math.zig

```zig
const std = @import("std");

pub const FourDirection = enum { up, down, left, right };
pub const EightDirection = enum { up, down, left, right, leftUp, leftDown, rightUp, rightDown };

pub const Vector2 = struct {
    x: f32 = 0,
    y: f32 = 0,

    pub const zero = Vector2{ .x = 0, .y = 0 };

    pub fn init(x: f32, y: f32) Vector2 {
        return .{ .x = x, .y = y };
    }

    pub fn toVector3(self: Vector2, z: f32) Vector3 {
        return .{ .x = self.x, .y = self.y, .z = z };
    }

    pub fn add(self: Vector2, other: Vector2) Vector2 {
        return .{ .x = self.x + other.x, .y = self.y + other.y };
    }

    pub fn sub(self: Vector2, other: Vector2) Vector2 {
        return .{ .x = self.x - other.x, .y = self.y - other.y };
    }

    pub fn scale(self: Vector2, scalar: f32) Vector2 {
        return .{ .x = self.x * scalar, .y = self.y * scalar };
    }

    pub fn length(self: Vector2) f32 {
        return std.math.sqrt(self.x * self.x + self.y * self.y);
    }

    pub fn normalize(self: Vector2) Vector2 {
        return .{ .x = self.x / self.length(), .y = self.y / self.length() };
    }
};

pub const Vector = Vector3;
pub const Vector3 = struct {
    x: f32 = 0,
    y: f32 = 0,
    z: f32 = 0,

    pub const zero = Vector3{ .x = 0, .y = 0, .z = 0 };

    pub fn init(x: f32, y: f32) Vector3 {
        return .{ .x = x, .y = y, .z = 0 };
    }

    pub fn toVector2(self: Vector3) Vector2 {
        return .{ .x = self.x, .y = self.y };
    }

    pub fn add(self: Vector3, other: Vector3) Vector3 {
        return .{ .x = self.x + other.x, .y = self.y + other.y, .z = self.z + other.z };
    }

    pub fn sub(self: Vector3, other: Vector3) Vector3 {
        return .{ .x = self.x - other.x, .y = self.y - other.y, .z = self.z - other.z };
    }

    pub fn scale(self: Vector3, scalar: f32) Vector3 {
        return .{ .x = self.x * scalar, .y = self.y * scalar, .z = self.z * scalar };
    }

    pub fn length(self: Vector3) f32 {
        return std.math.sqrt(self.x * self.x + self.y * self.y + self.z * self.z);
    }

    pub fn normalize(self: Vector3) Vector3 {
        const len = self.length();
        return .{ .x = self.x / len, .y = self.y / len, .z = self.z / len };
    }

    pub fn angle(self: Vector3) f32 {
        return std.math.atan2(self.y, self.x);
    }
};

pub const Rectangle = struct {
    x: f32 = 0,
    y: f32 = 0,
    w: f32 = 0,
    h: f32 = 0,

    pub fn init(x1: f32, y1: f32, x2: f32, y2: f32) Rectangle {
        return .{ .x = x1, .y = y1, .w = x2 - x1, .h = y2 - y1 };
    }

    pub fn right(self: Rectangle) f32 {
        return self.x + self.w;
    }

    pub fn bottom(self: Rectangle) f32 {
        return self.y + self.h;
    }

    pub fn intersects(self: Rectangle, other: Rectangle) bool {
        return self.x < other.right() and self.right() > other.x and
            self.y < other.bottom() and self.bottom() > other.y;
    }

    pub fn contains(self: Rectangle, x: f32, y: f32) bool {
        return x >= self.left and x < self.right and
            y >= self.top and y < self.bottom;
    }
};

pub var rand: std.Random = undefined;

pub fn randomF32(min: f32, max: f32) f32 {
    return rand.float(f32) * (max - min) + min;
}

pub fn randomU8(min: u8, max: u8) u8 {
    return rand.intRangeLessThanBiased(u8, min, max);
}
```

## window.zig

```zig
const std = @import("std");
const sk = @import("sokol");

pub const Event = sk.app.Event;

pub const Timer = struct {
    finished: bool = false,
    duration: f32,
    elapsed: f32 = 0,

    pub fn init(duration: f32) Timer {
        return Timer{ .duration = duration };
    }

    pub fn update(self: *Timer, delta: f32) void {
        if (self.finished) return;
        self.elapsed += delta;
        if (self.elapsed >= self.duration) self.finished = true;
    }

    pub fn isRunningAfterUpdate(self: *Timer, delta: f32) bool {
        return !self.isFinishedAfterUpdate(delta);
    }

    pub fn isFinishedAfterUpdate(self: *Timer, delta: f32) bool {
        self.update(delta);
        return self.finished;
    }

    pub fn reset(self: *Timer) void {
        self.finished = false;
        self.elapsed = 0;
    }

    pub fn isRunning(self: *const Timer) bool {
        return !self.finished;
    }
};

pub var width: f32 = 0;
pub var height: f32 = 0;

pub fn displayText(x: f32, y: f32, text: [:0]const u8) void {
    sk.debugtext.canvas(sk.app.widthf() * 0.4, sk.app.heightf() * 0.4);
    sk.debugtext.origin(x, y);
    sk.debugtext.home();

    sk.debugtext.font(0);
    sk.debugtext.color3b(0xff, 0xff, 0xff);
    sk.debugtext.puts(text);
}

pub fn endDisplayText() void {
    sk.debugtext.draw();
}

pub fn exit() void {
    sk.app.quit();
}
pub const run = sk.app.run;
```

## main.zig

```zig
const std = @import("std");

const cache = @import("cache.zig");
const window = @import("window.zig");
const math = @import("math.zig");
const gfx = @import("graphics.zig");
const audio = @import("audio.zig");
const scene = @import("scene.zig");

var soundBuffer: [10]audio.Sound = undefined;

fn init() callconv(.C) void {
    cache.init(allocator);
    gfx.init(window.width, window.height);
    audio.init(&soundBuffer);

    scene.init();
    timer = std.time.Timer.start() catch unreachable;
}

fn event(ev: ?*const window.Event) callconv(.C) void {
    if (ev) |e| scene.event(e);
}

fn frame() callconv(.C) void {
    const delta: f32 = @floatFromInt(timer.lap());
    scene.update(delta / std.time.ns_per_s);
    scene.render();
}

fn deinit() callconv(.C) void {
    scene.deinit();
    audio.deinit();
    gfx.deinit();
    cache.deinit();
}

var allocator: std.mem.Allocator = undefined;
var timer: std.time.Timer = undefined;

pub fn main() void {
    var debugAllocator = std.heap.DebugAllocator(.{}).init;
    defer _ = debugAllocator.deinit();

    allocator = debugAllocator.allocator();
    window.width = 1280;
    window.height = 720;

    var prng = std.Random.DefaultPrng.init(@intCast(std.time.timestamp()));
    math.rand = prng.random();

    window.run(.{
        .window_title = "哈基米大冒险",
        .width = @as(i32, @intFromFloat(window.width)),
        .height = @as(i32, @intFromFloat(window.height)),
        .init_cb = init,
        .swap_interval = 1,
        .event_cb = event,
        .frame_cb = frame,
        .cleanup_cb = deinit,
    });
}
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");

var animationDown1: gfx.SliceFrameAnimation = undefined;

pub fn init() void {
    animationDown1 = .load("assets/hajimi_idle_front_{}.png", 4);
}

pub fn deinit() void {}

pub fn event(ev: *const window.Event) void {
    _ = ev;
}

pub fn update(delta: f32) void {
    animationDown1.update(delta);
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    const background = gfx.loadTexture("assets/background.png");
    gfx.draw(background, 0, 0);

    gfx.playSlice(&animationDown1, .{ .x = 100, .y = 100 });
}
```

## 效果

![背景和角色动画][1]

[1]: images/哈基米大冒险01.png

## 附录
