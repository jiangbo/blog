# 0843-空洞武士-角色和攻击碰撞箱

## 目标

新增了角色的受击碰撞箱和攻击碰撞箱。

## 环境

- Time 2025-04-10
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

新增了碰撞箱，完善了一部分动画和逻辑。

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const actor = @import("actor/actor.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");

var debug: bool = false;
pub var player: actor.Player = undefined;
pub var enemy: actor.Enemy = undefined;
pub var boxes: std.BoundedArray(actor.CollisionBox, 30) = undefined;

pub fn init() void {
    boxes = std.BoundedArray(actor.CollisionBox, 30).init(0) catch unreachable;
    player = actor.Player.init();
    enemy = actor.Enemy.init();

    audio.playMusic("assets/audio/bgm.ogg");
}

pub fn addCollisionBox(box: actor.CollisionBox) *actor.CollisionBox {
    for (boxes.slice()) |*value| {
        if (!value.valid) value.* = box;
    } else {
        boxes.appendAssumeCapacity(box);
        return &boxes.slice()[boxes.len - 1];
    }
}

pub fn deinit() void {
    audio.stopMusic();
}

pub fn event(ev: *const window.Event) void {
    if (ev.type == .KEY_UP and ev.key_code == .X) {
        debug = !debug;
        return;
    }

    player.event(ev);
}

pub fn update() void {
    const delta = window.deltaSecond();
    player.update(delta);
    enemy.update(delta);

    for (boxes.slice()) |*srcBox| {
        if (!srcBox.enable or srcBox.dst == .none or !srcBox.valid) continue;
        for (boxes.slice()) |*dstBox| {
            if (!dstBox.enable or srcBox == dstBox or //
                srcBox.dst != dstBox.src or !dstBox.valid) continue;

            if (srcBox.rect.intersects(dstBox.rect)) {
                dstBox.valid = false;
                std.log.info("src box: {any}", .{srcBox});
                std.log.info("dst box: {any}", .{dstBox});
                if (dstBox.callback) |callback| callback();
            }
        }
    }
}
pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    renderBackground();
    enemy.render();
    player.render();

    if (debug) {
        for (boxes.slice()) |box| {
            if (box.enable and box.valid) gfx.drawRectangle(box.rect);
        }
    }
}

pub fn renderBackground() void {
    const background = gfx.loadTexture("assets/background.png");
    const width = window.width - background.width();
    const height = window.height - background.height();
    gfx.draw(background, width / 2, height / 2);
}
```

## actor/actor.zig

```zig
const std = @import("std");

const gfx = @import("../graphics.zig");
const math = @import("../math.zig");
const window = @import("../window.zig");
const scene = @import("../scene.zig");

pub const CollisionLayer = enum { none, player, enemy };

pub const CollisionBox = struct {
    rect: math.Rectangle,
    enable: bool = true,
    src: CollisionLayer = .none,
    dst: CollisionLayer = .none,
    callback: ?*const fn () void = null,
    valid: bool = true,

    pub fn setCenter(self: *CollisionBox, center: math.Vector) void {
        self.rect.x = center.x - self.rect.w / 2;
        self.rect.y = center.y - self.rect.h / 2;
    }
};

pub const Player = @import("Player.zig");
pub const Enemy = @import("Enemy.zig");

pub const SharedActor = struct {
    pub const FLOOR_Y = 620;
    const GRAVITY = 980 * 2;

    enableGravity: bool = true,
    position: math.Vector,
    velocity: math.Vector = .{},
    faceLeft: bool = false,
    logicHeight: f32 = 150,
    health: u8 = 10,

    hitBox: *CollisionBox = undefined,
    hurtBox: *CollisionBox = undefined,

    isInvulnerable: bool = false,
    invulnerableStatusTimer: window.Timer = .init(1),
    invulnerableBlinkTimer: window.Timer = .init(0.075),
    isBlink: bool = false,

    pub fn init(x: f32) SharedActor {
        var self: SharedActor = .{
            .position = .{ .x = x, .y = 200 },
            .hitBox = scene.addCollisionBox(.{ .rect = .{} }),
            .hurtBox = scene.addCollisionBox(.{ .rect = .{} }),
        };

        self.health = 10;
        return self;
    }

    pub fn update(self: *SharedActor, delta: f32) void {
        if (self.health <= 0) self.velocity.x = 0;

        if (self.enableGravity) {
            self.velocity.y += GRAVITY * delta;
        }

        self.position = self.position.add(self.velocity.scale(delta));
        if (self.position.y >= FLOOR_Y) {
            self.position.y = FLOOR_Y;
            self.velocity.y = 0;
        }

        if (self.invulnerableStatusTimer.isFinishedAfterUpdate(delta)) {
            self.isInvulnerable = false;
        }

        if (self.isInvulnerable) {
            if (self.invulnerableBlinkTimer.isFinishedAfterUpdate(delta)) {
                self.isBlink = !self.isBlink;
                self.invulnerableBlinkTimer.reset();
            }
        }

        self.position.x = std.math.clamp(self.position.x, 0, window.width);
        self.hurtBox.setCenter(self.logicCenter());
    }

    pub fn isOnFloor(self: *const SharedActor) bool {
        return self.position.y >= FLOOR_Y;
    }

    pub fn logicCenter(self: *const SharedActor) math.Vector {
        return .{
            .x = self.position.x,
            .y = self.position.y - self.logicHeight / 2,
        };
    }

    pub fn hurtIf(self: *SharedActor) bool {
        if (self.isInvulnerable) return false;

        self.health -= 1;
        if (self.health > 0) {
            self.enterInvulnerable();
            return true;
        }
        return false;
    }

    pub fn enterInvulnerable(self: *SharedActor) void {
        self.isInvulnerable = true;
        self.invulnerableStatusTimer.reset();
    }
};
```

## actor/Player.zig

```zig
const std = @import("std");

const gfx = @import("../graphics.zig");
const math = @import("../math.zig");
const window = @import("../window.zig");
const audio = @import("../audio.zig");
const SharedActor = @import("actor.zig").SharedActor;
const scene = @import("../scene.zig");

const Player = @This();
const AttackDirection = enum { left, right, up, down };

shared: SharedActor,

state: State,
leftKeyDown: bool = false,
rightKeyDown: bool = false,
jumpKeyDown: bool = false,

idleAnimation: gfx.AtlasFrameAnimation,
runAnimation: gfx.AtlasFrameAnimation,
jumpAnimation: gfx.AtlasFrameAnimation,
fallAnimation: gfx.AtlasFrameAnimation,

rollKeyDown: bool = false,
rollAnimation: gfx.AtlasFrameAnimation,
rollTimer: window.Timer = .init(0.35),
rollCoolDown: window.Timer = .init(0.75),

attackKeyDown: bool = false,
attackAnimation: gfx.AtlasFrameAnimation,
attackLeft: gfx.AtlasFrameAnimation,
attackRight: gfx.AtlasFrameAnimation,
attackUp: gfx.AtlasFrameAnimation,
attackDown: gfx.AtlasFrameAnimation,
attackTimer: window.Timer = .init(0.3),
attackCoolDown: window.Timer = .init(0.5),
attackDirection: AttackDirection = .left,

deadTimer: window.Timer = .init(0.5),
deadAnimation: gfx.AtlasFrameAnimation,

jumpVfxAnimation: gfx.AtlasFrameAnimation,
jumpPosition: math.Vector = .{},
landVfxAnimation: gfx.AtlasFrameAnimation,
landPosition: math.Vector = .{},

pub fn init() Player {
    var shared: SharedActor = .init(200);
    shared.logicHeight = 120;
    shared.hitBox.* = .{
        .rect = .{ .w = 150, .h = 150 },
        .dst = .enemy,
        .enable = false,
    };

    shared.hurtBox.* = .{
        .rect = .{ .w = 40, .h = 80 },
        .src = .player,
        .callback = struct {
            fn callback() void {
                std.log.info("player hurt", .{});
                audio.playSound("assets/audio/player_hurt.ogg");
                _ = scene.player.shared.hurtIf();
            }
        }.callback,
    };

    var player: Player = .{
        .shared = shared,

        .idleAnimation = .load("assets/player/idle.png", 5),
        .runAnimation = .load("assets/player/run.png", 10),
        .jumpAnimation = .load("assets/player/jump.png", 5),
        .fallAnimation = .load("assets/player/fall.png", 5),

        .state = .idle,
        .rollAnimation = .load("assets/player/roll.png", 7),
        .attackAnimation = .load("assets/player/attack.png", 5),
        .deadAnimation = .load("assets/player/dead.png", 6),

        .attackLeft = .load("assets/player/vfx_attack_left.png", 5),
        .attackRight = .load("assets/player/vfx_attack_right.png", 5),
        .attackUp = .load("assets/player/vfx_attack_up.png", 5),
        .attackDown = .load("assets/player/vfx_attack_down.png", 5),

        .jumpVfxAnimation = .load("assets/player/vfx_jump.png", 5),
        .landVfxAnimation = .load("assets/player/vfx_land.png", 2),
    };

    player.idleAnimation.timer = .init(0.15);

    player.runAnimation.loop = true;
    player.runAnimation.timer = .init(0.075);

    player.jumpAnimation.loop = false;
    player.jumpAnimation.timer = .init(0.15);

    player.fallAnimation.loop = false;
    player.fallAnimation.timer = .init(0.15);

    player.rollAnimation.loop = false;
    player.rollAnimation.timer = .init(0.05);

    player.attackAnimation.loop = false;
    player.attackAnimation.timer = .init(0.05);

    player.deadAnimation.loop = false;
    player.deadAnimation.timer = .init(0.1);

    player.attackLeft.loop = false;
    player.attackLeft.timer = .init(0.07);
    player.attackLeft.anchor = .centerCenter;
    player.attackRight.loop = false;
    player.attackRight.timer = .init(0.07);
    player.attackRight.anchor = .centerCenter;
    player.attackUp.loop = false;
    player.attackUp.timer = .init(0.07);
    player.attackUp.anchor = .centerCenter;
    player.attackDown.loop = false;
    player.attackDown.timer = .init(0.07);
    player.attackDown.anchor = .centerCenter;

    player.jumpVfxAnimation.loop = false;
    player.jumpVfxAnimation.timer = .init(0.05);
    player.jumpVfxAnimation.timer.finished = true;
    player.landVfxAnimation.loop = false;
    player.landVfxAnimation.timer = .init(0.1);
    player.landVfxAnimation.timer.finished = true;

    return player;
}

pub fn deinit() void {}

pub fn event(self: *Player, ev: *const window.Event) void {
    if (self.shared.health <= 0) return;

    if (ev.type == .KEY_DOWN) {
        switch (ev.key_code) {
            .A => self.leftKeyDown = true,
            .D => self.rightKeyDown = true,
            .W => self.jumpKeyDown = true,
            .F => self.attackKeyDown = true,
            .SPACE => self.rollKeyDown = true,
            else => {},
        }
    } else if (ev.type == .KEY_UP) {
        switch (ev.key_code) {
            .A => self.leftKeyDown = false,
            .D => self.rightKeyDown = false,
            .W => self.jumpKeyDown = false,
            .F => self.attackKeyDown = false,
            .SPACE => self.rollKeyDown = false,
            else => {},
        }
    }
}

pub fn update(self: *Player, delta: f32) void {
    self.rollCoolDown.update(delta);
    self.attackCoolDown.update(delta);

    self.jumpVfxAnimation.update(delta);
    self.landVfxAnimation.update(delta);

    self.shared.update(delta);
    self.state.update(self, delta);
}

pub fn render(self: *const Player) void {
    if (self.jumpVfxAnimation.timer.isRunning()) {
        gfx.playAtlas(&self.jumpVfxAnimation, self.jumpPosition);
    }

    if (self.landVfxAnimation.timer.isRunning()) {
        gfx.playAtlas(&self.landVfxAnimation, self.landPosition);
    }

    if (self.shared.isInvulnerable) {
        if (self.shared.isBlink) self.state.render(self);
    } else {
        self.state.render(self);
    }
}

fn changeState(self: *Player, new: State) void {
    self.state.exit(self);
    new.enter(self);
}

fn play(self: *const Player, animation: *const gfx.AtlasFrameAnimation) void {
    gfx.playAtlasFlipX(animation, self.shared.position, self.shared.faceLeft);
}

const State = union(enum) {
    idle: IdleState,
    run: RunState,
    jump: JumpState,
    fall: FallState,
    roll: RollState,
    attack: AttackState,
    // dead: DeadState,

    fn enter(self: State, player: *Player) void {
        switch (self) {
            inline else => |case| @TypeOf(case).enter(player),
        }
    }

    fn update(self: State, player: *Player, delta: f32) void {
        switch (self) {
            inline else => |case| @TypeOf(case).update(player, delta),
        }
    }

    fn render(self: State, player: *const Player) void {
        switch (self) {
            inline else => |case| @TypeOf(case).render(player),
        }
    }

    fn exit(self: State, player: *Player) void {
        switch (self) {
            inline else => |case| @TypeOf(case).exit(player),
        }
    }
};

const IdleState = struct {
    fn enter(player: *Player) void {
        player.state = .idle;
        player.shared.velocity.x = 0;
    }

    fn update(player: *Player, delta: f32) void {
        if (player.attackKeyDown and player.attackCoolDown.finished) {
            player.changeState(.attack);
        }

        if (player.leftKeyDown or player.rightKeyDown) {
            player.changeState(.run);
        }

        if (player.rollKeyDown and player.rollCoolDown.finished) {
            player.changeState(.roll);
        }

        if (player.jumpKeyDown and player.shared.velocity.y == 0) {
            player.changeState(.jump);
        }

        player.idleAnimation.update(delta);
    }

    fn render(player: *const Player) void {
        player.play(&player.idleAnimation);
    }

    fn exit(player: *Player) void {
        player.idleAnimation.reset();
    }
};

const JumpState = struct {
    const SPEED_JUMP = 780;

    fn enter(player: *Player) void {
        player.state = .jump;
        player.shared.velocity.y = -SPEED_JUMP;
        player.jumpPosition = player.shared.position;
        player.jumpVfxAnimation.reset();
    }

    fn update(player: *Player, delta: f32) void {
        if (player.shared.velocity.y > 0) {
            player.changeState(.fall);
        }

        player.jumpAnimation.update(delta);
    }

    fn render(player: *const Player) void {
        player.play(&player.jumpAnimation);
    }

    fn exit(player: *Player) void {
        player.jumpAnimation.reset();
    }
};

const FallState = struct {
    fn enter(player: *Player) void {
        player.state = .fall;
    }

    fn update(player: *Player, delta: f32) void {
        if (player.shared.isOnFloor()) {
            player.changeState(.idle);
            player.landPosition = player.shared.position;
            player.landVfxAnimation.reset();
        }

        player.fallAnimation.update(delta);
    }

    fn render(player: *const Player) void {
        player.play(&player.fallAnimation);
    }

    fn exit(player: *Player) void {
        player.fallAnimation.reset();
    }
};

const RunState = struct {
    const SPEED_RUN = 300;

    fn enter(player: *Player) void {
        player.state = .run;

        if (player.leftKeyDown) {
            player.shared.velocity.x = -SPEED_RUN;
            player.shared.faceLeft = true;
        } else if (player.rightKeyDown) {
            player.shared.velocity.x = SPEED_RUN;
            player.shared.faceLeft = false;
        }
    }

    fn update(player: *Player, delta: f32) void {
        if (player.attackKeyDown and player.attackCoolDown.finished) {
            player.changeState(.attack);
        }

        if (!player.leftKeyDown and !player.rightKeyDown) {
            player.changeState(.idle);
        }

        if (player.rollKeyDown and player.rollCoolDown.finished) {
            player.changeState(.roll);
        }

        if (player.jumpKeyDown and player.shared.velocity.y == 0) {
            player.changeState(.jump);
        }

        player.runAnimation.update(delta);
    }

    fn render(player: *const Player) void {
        player.play(&player.runAnimation);
    }

    fn exit(player: *Player) void {
        player.runAnimation.reset();
    }
};

const RollState = struct {
    const SPEED_ROLL = 800;
    fn enter(player: *Player) void {
        player.state = .roll;
        player.rollTimer.reset();
        player.rollCoolDown.reset();

        if (player.shared.faceLeft) {
            player.shared.velocity.x = -SPEED_ROLL;
        } else {
            player.shared.velocity.x = SPEED_ROLL;
        }
    }

    fn update(player: *Player, delta: f32) void {
        if (player.rollTimer.isFinishedAfterUpdate(delta)) {
            player.changeState(.idle);
        }

        player.rollAnimation.update(delta);
    }

    fn render(player: *const Player) void {
        player.play(&player.rollAnimation);
    }

    fn exit(player: *Player) void {
        player.rollAnimation.reset();
    }
};

const AttackState = struct {
    fn enter(player: *Player) void {
        player.state = .attack;
        player.attackTimer.reset();
        player.attackCoolDown.reset();

        player.attackDirection = if (player.shared.faceLeft) .left else .right;

        switch (window.rand.intRangeAtMostBiased(u8, 1, 3)) {
            1 => audio.playSound("assets/audio/player_attack_1.ogg"),
            2 => audio.playSound("assets/audio/player_attack_2.ogg"),
            3 => audio.playSound("assets/audio/player_attack_3.ogg"),
            else => unreachable,
        }
    }

    fn update(player: *Player, delta: f32) void {
        if (player.attackTimer.isFinishedAfterUpdate(delta)) {
            player.changeState(.idle);
        }

        player.attackAnimation.update(delta);
        updateHitBox(player, delta);
    }

    fn updateHitBox(player: *Player, delta: f32) void {
        const playerCenter = player.shared.logicCenter();
        var hitBox = player.shared.hitBox;
        hitBox.enable = true;

        std.log.info("dir: {}", .{player.attackDirection});
        switch (player.attackDirection) {
            .left => {
                hitBox.setCenter(.{
                    .x = playerCenter.x - hitBox.rect.w / 2,
                    .y = playerCenter.y,
                });
                player.attackLeft.update(delta);
            },
            .right => {
                hitBox.setCenter(.{
                    .x = playerCenter.x + hitBox.rect.w / 2,
                    .y = playerCenter.y,
                });
                player.attackRight.update(delta);
            },
            .up => {
                hitBox.rect.x = playerCenter.x;
                hitBox.rect.y = playerCenter.y - hitBox.rect.h / 2;
                player.attackUp.update(delta);
            },
            .down => {
                hitBox.rect.x = playerCenter.x;
                hitBox.rect.y = playerCenter.y + hitBox.rect.h / 2;
                player.attackDown.update(delta);
            },
        }
    }

    fn render(player: *const Player) void {
        player.play(&player.attackAnimation);
        if (player.shared.faceLeft) {
            gfx.playAtlas(&player.attackLeft, player.shared.logicCenter());
        } else {
            gfx.playAtlas(&player.attackRight, player.shared.logicCenter());
        }
    }

    fn exit(player: *Player) void {
        player.attackAnimation.reset();
        player.attackLeft.reset();
        player.attackRight.reset();
        player.shared.hitBox.enable = false;
    }
};
```

## 效果

![角色和攻击碰撞箱][1]

[1]: images/空洞武士22.webp

## 附录
