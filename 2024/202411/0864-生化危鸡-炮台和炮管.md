# 0864-生化危鸡-炮台和炮管

## 目标

将炮台和炮管显示到屏幕的下面中间，并且旋转了炮管的方向。

## 环境

- Time 2025-04-17
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1wmy6YuEzh>

## 想法

图片旋转的功能在之前已经实现了。

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");

pub const Chicken = struct {
    position: math.Vector = .{ .x = -50, .y = -50 },
    speed: f32,
    animationRun: gfx.SliceFrameAnimation,
    animationExplosion: gfx.SliceFrameAnimation = undefined,

    alive: bool = true,
    valid: bool = true,
};

const ChickenArray = std.BoundedArray(Chicken, 1000);
var chickens: ChickenArray = undefined;

var background: gfx.Texture = undefined;
var battery: gfx.Texture = undefined;
var batteryPosition: math.Vector = .{ .x = 640, .y = 600 };
var barrelIdle: gfx.Texture = undefined;
var barrelPosition: math.Vector = .{ .x = 592, .y = 585 };
var barrelCenter: math.Vector = .{ .x = 48, .y = 25 };

var spawnIntervalTimer: window.Timer = .init(1.5);
var spawnNumberTimer: window.Timer = .init(8);
var spawnNumber: usize = 0;

pub fn init() void {
    background = gfx.loadTexture("assets/background.png");
    battery = gfx.loadTexture("assets/battery.png");
    barrelIdle = gfx.loadTexture("assets/barrel_idle.png");
    chickens = ChickenArray.init(0) catch unreachable;
}

pub fn event(ev: *const window.Event) void {
    _ = ev;
}
pub fn update(delta: f32) void {
    if (spawnIntervalTimer.isFinishedAfterUpdate(delta)) {
        spawnIntervalTimer.reset();
        spawnChicken();
    }

    if (spawnNumberTimer.isFinishedAfterUpdate(delta)) {
        spawnNumberTimer.reset();
        spawnNumber += 1;
    }

    for (chickens.slice()) |*chicken| {
        chicken.position.y += chicken.speed * delta;
        chicken.animationRun.update(delta);
    }
}

fn spawnChicken() void {
    for (0..spawnNumber) |_| {
        var chicken: Chicken = switch (math.randomU8(0, 100)) {
            0...49 => .{
                .speed = 80,
                .animationRun = .load("assets/chicken_fast_{}.png", 4),
            },
            50...79 => .{
                .speed = 50,
                .animationRun = .load("assets/chicken_medium_{}.png", 6),
            },
            80...99 => .{
                .speed = 30,
                .animationRun = .load("assets/chicken_slow_{}.png", 8),
            },
            else => unreachable,
        };

        chicken.position = .{ .x = math.randomF32(40, window.size.x - 40) };
        chicken.animationExplosion = .load("assets/explosion_{}.png", 5);
        chicken.animationExplosion.loop = false;
        chicken.animationExplosion.timer.duration = 0.08;

        chickens.append(chicken) catch {
            std.log.info("chicken array is full", .{});
        };
    }
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    gfx.draw(background, window.size.sub(background.size()).scale(0.5));

    for (chickens.slice()) |*chicken| {
        gfx.playSlice(&chicken.animationRun, chicken.position);
    }

    gfx.draw(battery, batteryPosition.sub(battery.size().scale(0.5)));
    gfx.drawOptions(barrelIdle, .{
        .targetRect = .{ .position = barrelPosition },
        .angle = -90,
        .pivot = barrelCenter,
    });
}

pub fn deinit() void {}
```

## 效果

![炮台和炮管][1]

[1]: images/生化危鸡04.webp

## 附录
