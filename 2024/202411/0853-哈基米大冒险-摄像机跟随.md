# 0853-哈基米大冒险-摄像机跟随

## 目标

角色移动时，摄像机跟随角色，镜头始终位于屏幕中间。

## 环境

- Time 2025-04-13
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1wmy6YuEzh>

## 想法

没有做边缘的检测，不过这个游戏自己控制移动，所以可以先不实现这个功能。

## graphics.zig

```zig
const std = @import("std");

const cache = @import("cache.zig");
const gpu = @import("gpu.zig");
const math = @import("math.zig");
const animation = @import("animation.zig");

pub const Texture = gpu.Texture;

pub const Camera = struct {
    rect: math.Rectangle,

    pub fn setPosition(self: *Camera, pos: math.Vector) void {
        self.rect.x = pos.x;
        self.rect.y = pos.y;
    }

    pub fn setSize(self: *Camera, size: math.Vector) void {
        self.rect.w = size.x;
        self.rect.h = size.y;
    }

    pub fn lookAt(self: *Camera, pos: math.Vector) void {
        self.rect.x = pos.x - self.rect.w / 2;
        self.rect.y = pos.y - self.rect.h / 2;
    }
};

pub var renderer: gpu.Renderer = undefined;
var matrix: [16]f32 = undefined;
var passEncoder: gpu.RenderPassEncoder = undefined;
pub var camera: Camera = undefined;

pub fn init(width: f32, height: f32) void {
    matrix = .{
        2 / width, 0.0,         0.0, 0.0,
        0.0,       2 / -height, 0.0, 0.0,
        0.0,       0.0,         1,   0.0,
        -1,        1,           0,   1.0,
    };
    renderer = gpu.Renderer.init();
}

pub const deinit = gpu.deinit;

pub fn loadTexture(path: [:0]const u8) Texture {
    return cache.Texture.load(path);
}

pub fn beginDraw() void {
    passEncoder = gpu.CommandEncoder.beginRenderPass(
        .{ .r = 1, .b = 1, .a = 1.0 },
        &matrix,
    );

    renderer.renderPass = passEncoder;
}

pub fn drawRectangle(rect: math.Rectangle) void {
    gpu.drawRectangleLine(rect);
}

pub fn draw(tex: Texture, x: f32, y: f32) void {
    drawFlipX(tex, .{ .x = x, .y = y }, false);
}

pub fn drawFlipX(tex: Texture, pos: math.Vector, flipX: bool) void {
    const target: math.Rectangle = .{ .x = pos.x, .y = pos.y };
    const src = math.Rectangle{
        .w = if (flipX) -tex.width() else tex.width(),
    };

    drawOptions(tex, .{ .sourceRect = src, .targetRect = target });
}

pub const DrawOptions = struct {
    sourceRect: ?math.Rectangle = null,
    targetRect: math.Rectangle,
};

pub fn drawOptions(texture: Texture, options: DrawOptions) void {
    var target = options.targetRect;
    target.x = target.x - camera.rect.x;
    target.y = target.y - camera.rect.y;

    renderer.draw(.{
        .uniform = .{ .vp = matrix },
        .texture = texture,
        .sourceRect = options.sourceRect,
        .targetRect = target,
    });
}

pub fn endDraw() void {
    passEncoder.submit();
}

pub const FrameAnimation = animation.FrameAnimation;
pub const SliceFrameAnimation = animation.SliceFrameAnimation;
pub const AtlasFrameAnimation = animation.AtlasFrameAnimation;

pub fn playSlice(frameAnimation: *const FrameAnimation, pos: math.Vector) void {
    playSliceFlipX(frameAnimation, pos, false);
}

pub fn playSliceFlipX(frame: *const FrameAnimation, pos: math.Vector, flipX: bool) void {
    const offset: math.Vector = switch (frame.anchor) {
        .bottomCenter => .{
            .x = pos.x - frame.textures[0].width() / 2,
            .y = pos.y - frame.textures[0].height(),
        },
        .centerCenter => .{
            .x = pos.x - frame.textures[0].width() / 2,
            .y = pos.y - frame.textures[0].height() / 2,
        },
        else => unreachable,
    };
    drawFlipX(frame.textures[frame.index], offset, flipX);
}

pub fn playAtlas(frameAnimation: *const AtlasFrameAnimation, pos: math.Vector) void {
    playAtlasFlipX(frameAnimation, pos, false);
}

pub fn playAtlasFlipX(frame: *const AtlasFrameAnimation, pos: math.Vector, flipX: bool) void {
    var src = frame.frames[frame.index];

    const offset: math.Vector = switch (frame.anchor) {
        .bottomCenter => .{ .x = pos.x - src.w / 2, .y = pos.y - src.h },
        .centerCenter => .{ .x = pos.x - src.w / 2, .y = pos.y - src.h / 2 },
        else => unreachable,
    };

    const dst: gpu.Rectangle = .{ .x = offset.x, .y = offset.y, .w = src.w };
    if (flipX) src.w = -src.w;
    drawOptions(frame.texture, .{ .sourceRect = src, .targetRect = dst });
}
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const http = @import("http.zig");

const FourAnimation = struct {
    up: gfx.SliceFrameAnimation,
    down: gfx.SliceFrameAnimation,
    left: gfx.SliceFrameAnimation,
    right: gfx.SliceFrameAnimation,
};

const BASE_URL = "http://127.0.0.1:4444/api";
const SPEED = 1;

const Player = struct {
    index: i32,
    position: math.Vector = .{ .x = 400, .y = 400 },
    idle: FourAnimation,
    run: FourAnimation,
    keydown: ?math.FourDirection = null,
    current: math.FourDirection = .down,

    fn init(index: i32) Player {
        if (index == 1) return .{
            .index = index,
            .idle = .{
                .up = .load("assets/hajimi_idle_back_{}.png", 4),
                .down = .load("assets/hajimi_idle_front_{}.png", 4),
                .left = .load("assets/hajimi_idle_left_{}.png", 4),
                .right = .load("assets/hajimi_idle_right_{}.png", 4),
            },

            .run = .{
                .up = .load("assets/hajimi_run_back_{}.png", 4),
                .down = .load("assets/hajimi_run_front_{}.png", 4),
                .left = .load("assets/hajimi_run_left_{}.png", 4),
                .right = .load("assets/hajimi_run_right_{}.png", 4),
            },
        };

        return .{
            .index = index,
            .idle = .{
                .up = .load("assets/manbo_idle_back_{}.png", 4),
                .down = .load("assets/manbo_idle_front_{}.png", 4),
                .left = .load("assets/manbo_idle_left_{}.png", 4),
                .right = .load("assets/manbo_idle_right_{}.png", 4),
            },

            .run = .{
                .up = .load("assets/manbo_run_back_{}.png", 4),
                .down = .load("assets/manbo_run_front_{}.png", 4),
                .left = .load("assets/manbo_run_left_{}.png", 4),
                .right = .load("assets/manbo_run_right_{}.png", 4),
            },
        };
    }

    pub fn currentAnimation(player: *Player) *gfx.SliceFrameAnimation {
        var animation = if (player.keydown == null) &player.idle else &player.run;

        return switch (player.current) {
            .up => &animation.up,
            .down => &animation.down,
            .left => &animation.left,
            .right => &animation.right,
        };
    }
};

var text: std.ArrayList(u8) = undefined;
var player1: Player = undefined;
var player2: Player = undefined;

var self: *Player = undefined;
var other: *Player = undefined;

pub fn init(allocator: std.mem.Allocator) void {
    player1 = Player.init(1);
    player2 = Player.init(2);
    gfx.camera.setSize(.{ .x = window.width, .y = window.height });

    text = http.sendAlloc(allocator, BASE_URL ++ "/text");
    const playerIndex = http.sendValue(BASE_URL ++ "/login", null);
    self = if (playerIndex == 1) &player1 else &player2;
    other = if (playerIndex == 1) &player2 else &player1;

    audio.playMusic("assets/bgm.ogg");
}

pub fn deinit() void {
    _ = http.sendValue(BASE_URL ++ "/logout", self.index);
    text.deinit();
    audio.stopMusic();
}

pub fn event(ev: *const window.Event) void {
    if (ev.type == .KEY_DOWN) {
        switch (ev.key_code) {
            .A, .LEFT => self.keydown = .left,
            .D, .RIGHT => self.keydown = .right,
            .W, .UP => self.keydown = .up,
            .S, .DOWN => self.keydown = .down,
            else => {},
        }
    } else if (ev.type == .KEY_UP) {
        switch (ev.key_code) {
            .A, .LEFT, .D, .RIGHT => self.keydown = null,
            .W, .UP, .S, .DOWN => self.keydown = null,
            else => {},
        }
    }
}

pub fn update(delta: f32) void {
    if (self.keydown) |key| {
        const direction: math.Vector = switch (key) {
            .up => .{ .y = -1 },
            .down => .{ .y = 1 },
            .left => .{ .x = -1 },
            .right => .{ .x = 1 },
        };
        self.current = key;
        self.position = self.position.add(direction.scale(SPEED));
    }

    gfx.camera.lookAt(self.position);

    self.currentAnimation().update(delta);
    other.currentAnimation().update(delta);
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    const background = gfx.loadTexture("assets/background.png");
    gfx.draw(background, 0, 0);

    gfx.playSlice(other.currentAnimation(), other.position);
    gfx.playSlice(self.currentAnimation(), self.position);
}
```

## 效果

![摄像机跟随][1]

[1]: images/哈基米大冒险03.webp

## 附录
