# 0849-哈基米大冒险-服务端

## 目标

实现哈基米大冒险中的服务端接口。

## 环境

- Time 2025-04-13
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1wmy6YuEzh>

## 想法

使用了 http 库来实现服务端的开发。

## build.zig.zon

```zig
.{
    .name = .server,
    .version = "0.0.1",
    .fingerprint = 0x5a6dd5f6226fc004,
    .minimum_zig_version = "0.14.0",
    .dependencies = .{
        .httpz = .{
            .url = "git+https://github.com/karlseguin/http.zig?ref=master#8ecf3a330ab1bed8495604d444e549b94f08bc0f",
            .hash = "httpz-0.0.0-PNVzrOy2BgBA2lU1zixuVrv0UUkSnVrBUIlIHl1XV0XV",
        },
    },
    .paths = .{
        "build.zig",
        "build.zig.zon",
        "src",
    },
}
```

## build.zig

```zig
const std = @import("std");

pub fn build(b: *std.Build) void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});

    const exe = b.addExecutable(.{
        .name = "server",
        .root_module = b.createModule(.{
            .root_source_file = b.path("src/main.zig"),
            .target = target,
            .optimize = optimize,
        }),
    });

    const http = b.dependency("httpz", .{ .target = target, .optimize = optimize });
    exe.root_module.addImport("http", http.module("httpz"));

    b.installArtifact(exe);
    const run_cmd = b.addRunArtifact(exe);

    run_cmd.step.dependOn(b.getInstallStep());
    if (b.args) |args| {
        run_cmd.addArgs(args);
    }

    const run_step = b.step("run", "Run the app");
    run_step.dependOn(&run_cmd.step);
}
```

## main.zig

```zig
const std = @import("std");
const http = @import("http");

pub fn main() !void {
    var gpa: std.heap.DebugAllocator(.{}) = .init;
    const allocator = gpa.allocator();

    var server = try http.Server(void).init(allocator, .{ .port = 4444 }, {});
    defer {
        server.stop();
        server.deinit();
    }

    var router = try server.router(.{});
    router.post("/api/login", login, .{});
    router.post("/api/logout", logout, .{});
    router.post("/api/text", text, .{});
    router.post("/api/update1", update1, .{});
    router.post("/api/update2", update2, .{});

    std.log.info("start http server", .{});

    try server.listen();
}

const fileText: []const u8 = @embedFile("text.txt");
var mutex: std.Thread.Mutex = .{};

var progress1: i32 = -1;
var progress2: i32 = -1;

fn login(_: *http.Request, res: *http.Response) !void {
    mutex.lock();
    defer mutex.unlock();

    if (progress1 >= 0 and progress2 >= 0) {
        res.status = 403;
        return;
    }

    var result: i32 = 0;
    if (progress1 < 0) {
        progress1 = 0;
        std.log.info("player1 online", .{});
        result = 1;
    } else if (progress2 < 0) {
        progress2 = 0;
        std.log.info("player2 online", .{});
        result = 2;
    }

    res.status = 200;
    try res.writer().writeAll(&std.mem.toBytes(result));
}

fn logout(req: *http.Request, res: *http.Response) !void {
    mutex.lock();
    defer mutex.unlock();

    if (req.body()) |body| {
        const value = std.mem.bytesToValue(i32, body);
        if (value == 1) {
            progress1 = -1;
            std.log.info("player1 offline", .{});
        } else if (value == 2) {
            progress2 = -1;
            std.log.info("player2 offline", .{});
        }
    }
    try res.writer().writeAll(req.body().?);
}

fn text(_: *http.Request, res: *http.Response) !void {
    try res.writer().writeAll(fileText);
}

fn update1(req: *http.Request, res: *http.Response) !void {
    mutex.lock();
    defer mutex.unlock();

    progress1 = std.mem.bytesToValue(i32, req.body().?);
    std.log.info("player1 progress: {d}", .{progress1});
    try res.writer().writeAll(&std.mem.toBytes(progress2));
}

fn update2(req: *http.Request, res: *http.Response) !void {
    mutex.lock();
    defer mutex.unlock();

    progress2 = std.mem.bytesToValue(i32, req.body().?);
    std.log.info("player2 progress: {d}", .{progress2});
    try res.writer().writeAll(&std.mem.toBytes(progress1));
}
```

## 效果

```text
PS C:\workspace\server> zig build run
info: start http server
```

## 附录

### 文本

```text
It was terribly cold and nearly dark on the last evening
of the old year, and the snow was falling fast.
In the cold and the darkness, a poor little girl,
with bare head and naked feet, roamed through the
streets. It is true she had on a pair of slippers when
she left home, but they were not of much use.
They were very large, so large, indeed, that they had
belonged to her mother, and the poor little creature
had lost them in running across the street to avoid
two carriages that were rolling along at a terrible rate.
One of the slippers she could not find, and a boy
seized upon the other and ran away with it, saying that
he could use it as a cradle, when he had children of his
own. So the little girl went on with her little naked feet,
which were quite red and blue with the cold. In an old
apron she carried a number of matches, and had a
bundle of them in her hands. No one had bought anything
of her the whole day, nor had any one given here even
a penny. Shivering with cold and hunger, she crept along;
poor little child, she looked the picture of misery.
The snowflakes fell on her long, fair hair, which hung
in curls on her shoulders, but she regarded them not.
```
