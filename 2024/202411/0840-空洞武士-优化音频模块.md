# 0840-空洞武士-优化音频模块

## 目标

对于音效部分，直接全部解码到内存，对于音乐部分，使用流式播放。

## 环境

- Time 2025-04-09
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

将音频分为了 music 和 sound，然后使用不同的策略进行播放。music 流式播放，而 sound 解析后缓存直接播放。

## c.zig

去掉了之前自定义的方法，使用自带的流式解析，单声道的话，右声道就没有声音。

```zig
pub const stbAudio = stbVorbis;
pub const stbVorbis = struct {
    const stb = @cImport({
        @cDefine("STB_VORBIS_NO_PUSHDATA_API", {});
        @cDefine("STB_VORBIS_HEADER_ONLY", {});
        @cDefine("STB_VORBIS_NO_INTEGER_CONVERSION", {});
        @cInclude("stb_Vorbis.c");
    });

    pub const Audio = stb.stb_vorbis;
    pub const AudioInfo = stb.stb_vorbis_info;

    pub fn load(path: [:0]const u8) !*Audio {
        var errorCode: c_int = 0;

        const vorbis = stb.stb_vorbis_open_filename(path, &errorCode, null);
        if (errorCode != 0 or vorbis == null) return error.loadAudioFailed;
        return vorbis.?;
    }

    pub fn getInfo(audio: *Audio) AudioInfo {
        return stb.stb_vorbis_get_info(audio);
    }

    pub fn getSampleCount(audio: *Audio) usize {
        return stb.stb_vorbis_stream_length_in_samples(audio);
    }

    pub fn fillSamples(audio: *Audio, buffer: []f32, channels: i32) c_int {
        return stb.stb_vorbis_get_samples_float_interleaved(
            audio,
            channels,
            @ptrCast(buffer),
            @intCast(buffer.len),
        );
    }

    pub fn reset(audio: *Audio) void {
        _ = stb.stb_vorbis_seek_start(audio);
    }

    pub fn unload(audio: *Audio) void {
        stb.stb_vorbis_close(audio);
    }
};
```

## build.zig

去掉了之前的自定义方法。

```zig
const std = @import("std");

pub fn build(b: *std.Build) !void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});

    const exe = b.addExecutable(.{
        .name = "demo",
        .root_source_file = b.path("src/main.zig"),
        .target = target,
        .optimize = optimize,
    });

    if (optimize != .Debug) exe.subsystem = .Windows;

    b.installArtifact(exe);

    const sokol = b.dependency("sokol", .{ .target = target, .optimize = optimize });
    exe.root_module.addImport("sokol", sokol.module("sokol"));

    const writeFiles = b.addWriteFiles();
    exe.step.dependOn(&writeFiles.step);

    const stb = b.dependency("stb", .{ .target = target, .optimize = optimize });
    exe.root_module.addIncludePath(stb.path("."));
    const stbImagePath = writeFiles.add("stb_image.c", stbImageSource);
    exe.root_module.addCSourceFile(.{ .file = stbImagePath, .flags = &.{"-O2"} });

    const stbAudioPath = writeFiles.add("stb_audio.c", stbAudioSource);
    exe.root_module.addCSourceFile(.{ .file = stbAudioPath, .flags = &.{"-O2"} });

    const run_cmd = b.addRunArtifact(exe);
    run_cmd.step.dependOn(b.getInstallStep());
    if (b.args) |args| {
        run_cmd.addArgs(args);
    }
    const run_step = b.step("run", "Run the app");
    run_step.dependOn(&run_cmd.step);
}

const stbImageSource =
    \\
    \\#define STB_IMAGE_IMPLEMENTATION
    \\#define STBI_ONLY_PNG
    \\#include "stb_image.h"
    \\
;

const stbAudioSource =
    \\
    \\#define STB_VORBIS_NO_PUSHDATA_API
    \\#define STB_VORBIS_NO_INTEGER_CONVERSION
    \\
    \\#include "stb_vorbis.c"
    \\
;
```

## audio.zig

将声音分为了 music 和 sound，music 只能播放一个，而 sound 可以播放多个。

```zig
const std = @import("std");
const sk = @import("sokol");
const cache = @import("cache.zig");
const c = @import("c.zig");

pub fn init(soundBuffer: []Sound) void {
    sk.audio.setup(.{
        .num_channels = 2,
        .stream_cb = callback,
        .logger = .{ .func = sk.log.func },
    });
    sounds = .initBuffer(soundBuffer);
}

pub fn deinit() void {
    stopMusic();
    sk.audio.shutdown();
}

var musicMutex: std.Thread.Mutex = .{};
var soundMutex: std.Thread.Mutex = .{};

pub const Music = struct {
    source: *c.stbAudio.Audio,
    paused: bool = false,
    loop: bool = true,
};

var music: ?Music = null;

pub fn playMusic(path: [:0]const u8) void {
    doPlayMusic(path, true);
}

pub fn playMusicOnce(path: [:0]const u8) void {
    doPlayMusic(path, false);
}

fn doPlayMusic(path: [:0]const u8, loop: bool) void {
    stopMusic();

    const audio = c.stbAudio.load(path) catch unreachable;
    const info = c.stbAudio.getInfo(audio);
    const args = .{ info.sample_rate, info.channels, path };
    std.log.info("music sampleRate: {}, channels: {d}, path: {s}", args);

    musicMutex.lock();
    defer musicMutex.unlock();
    music = .{ .source = audio, .loop = loop };
}

pub fn pauseMusic() void {
    if (music) |*value| value.paused = true;
}

pub fn resumeMusic() void {
    if (music) |*value| value.paused = false;
}

pub fn stopMusic() void {
    musicMutex.lock();
    defer musicMutex.unlock();

    if (music) |*value| {
        c.stbAudio.unload(value.source);
        music = null;
    }
}

var sounds: std.ArrayListUnmanaged(Sound) = .empty;

pub const Sound = struct {
    source: []f32,
    valid: bool = true,
    loop: bool = true,
    index: usize = 0,
    sampleRate: u16 = 0,
    channels: u8 = 0,
};

pub fn playSound(path: [:0]const u8) void {
    _ = doPlaySound(path, false);
}

pub fn playSoundLoop(path: [:0]const u8) *Sound {
    return doPlaySound(path, true);
}

fn doPlaySound(path: [:0]const u8, loop: bool) *Sound {
    var sound = cache.Sound.load(path);
    sound.loop = loop;

    const args = .{ sound.sampleRate, sound.channels, path };
    std.log.info("audio sampleRate: {}, channels: {d}, path: {s}", args);

    soundMutex.lock();
    defer soundMutex.unlock();
    sounds.appendAssumeCapacity(sound);
    return &sounds.items[sounds.items.len - 1];
}

fn callback(b: [*c]f32, frames: i32, channels: i32) callconv(.C) void {
    const buffer = b[0..@as(usize, @intCast(frames * channels))];
    @memset(buffer, 0);
    {
        musicMutex.lock();
        defer musicMutex.unlock();
        if (music) |m| blk: {
            if (m.paused) break :blk;
            const count = c.stbAudio.fillSamples(m.source, buffer, channels);
            if (count == 0) {
                if (m.loop) c.stbAudio.reset(m.source) else music = null;
            }
        }
    }

    soundMutex.lock();
    defer soundMutex.unlock();

    for (sounds.items) |*sound| {
        var len = mixSamples(buffer, sound);
        while (len < buffer.len and sound.valid) {
            len += mixSamples(buffer[len..], sound);
        }
    }
    var i: usize = sounds.items.len;
    while (i > 0) : (i -= 1) {
        if (sounds.items[i - 1].valid) continue;
        _ = sounds.swapRemove(i - 1);
    }
}

fn mixSamples(buffer: []f32, sound: *Sound) usize {
    const len = if (sound.channels == 1)
        mixMonoSamples(buffer, sound)
    else if (sound.channels == 2)
        mixStereoSamples(buffer, sound)
    else
        @panic("unsupported channels");

    if (sound.index == sound.source.len) {
        if (sound.loop) sound.index = 0 else sound.valid = false;
    }

    return len;
}

fn mixStereoSamples(dstBuffer: []f32, sound: *Sound) usize {
    const srcBuffer = sound.source[sound.index..];
    const len = @min(dstBuffer.len, srcBuffer.len);

    for (0..len) |index| dstBuffer[index] += srcBuffer[index];
    sound.index += len;
    return len;
}

fn mixMonoSamples(dstBuffer: []f32, sound: *Sound) usize {
    const srcBuffer = sound.source[sound.index..];
    const len = @min(dstBuffer.len / 2, srcBuffer.len);

    for (0..len) |index| {
        dstBuffer[index * 2] += srcBuffer[index];
        dstBuffer[index * 2 + 1] += srcBuffer[index];
    }
    sound.index += len;
    return len * 2;
}
```

## cache.zig

```zig
...
const audio = @import("audio.zig");
pub const Sound = struct {
    var cache: std.StringHashMapUnmanaged(audio.Sound) = .empty;

    pub fn load(path: [:0]const u8) audio.Sound {
        const entry = cache.getOrPut(allocator, path) catch unreachable;
        if (entry.found_existing) return entry.value_ptr.*;

        const stbAudio = c.stbAudio.load(path) catch unreachable;
        defer c.stbAudio.unload(stbAudio);

        var sound: audio.Sound = .{ .source = undefined };
        const info = c.stbAudio.getInfo(stbAudio);
        sound.channels = @intCast(info.channels);
        sound.sampleRate = @intCast(info.sample_rate);

        const size = c.stbAudio.getSampleCount(stbAudio) * sound.channels;
        sound.source = allocator.alloc(f32, size) catch unreachable;

        _ = c.stbAudio.fillSamples(stbAudio, sound.source, sound.channels);

        entry.value_ptr.* = sound;
        return sound;
    }

    pub fn deinit() void {
        var iterator = cache.valueIterator();
        while (iterator.next()) |value| allocator.free(value.source);
        cache.deinit(allocator);
    }
};
...
```

## main.zig

同时播放两个音频。

```zig
const std = @import("std");

const cache = @import("cache.zig");
const window = @import("window.zig");
const gfx = @import("graphics.zig");
const audio = @import("audio.zig");
const scene = @import("scene.zig");

var soundBuffer: [10]audio.Sound = undefined;

pub fn init() void {
    cache.init(allocator);
    gfx.init(window.width, window.height);
    audio.init(&soundBuffer);

    scene.init();

    audio.playMusic("assets/audio/bgm.ogg");
    audio.playSound("assets/audio/barb_break.ogg");
}

pub fn event(ev: *const window.Event) void {
    scene.event(ev);
}

pub fn update() void {
    scene.update();
}

pub fn render() void {
    scene.render();
}

pub fn deinit() void {
    scene.deinit();
    audio.deinit();
    cache.deinit();
}

var allocator: std.mem.Allocator = undefined;

pub fn main() void {
    var debugAllocator = std.heap.DebugAllocator(.{}).init;
    defer _ = debugAllocator.deinit();

    allocator = debugAllocator.allocator();
    window.width = 1280;
    window.height = 720;

    var prng = std.Random.DefaultPrng.init(@intCast(std.time.timestamp()));
    window.rand = prng.random();

    window.run(.{
        .title = "空洞武士",
        .init = init,
        .event = event,
        .update = update,
        .render = render,
        .deinit = deinit,
    });
}
```

## 效果

由于是音频，所以就不上效果了。

## 附录
