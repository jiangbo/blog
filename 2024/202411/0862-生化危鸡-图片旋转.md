# 0862-生化危鸡-图片旋转

## 目标

将之前的 math 中的矩形修改了一下，保存的字段进行了修改，实现了图片旋转。

## 环境

- Time 2025-04-16
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1wmy6YuEzh>

## 想法

感觉涉及到的数学操作越来越多了，感觉是不是要引入一个数学库。

## math.zig

```zig
const std = @import("std");

pub const FourDirection = enum { up, down, left, right };
pub const EightDirection = enum { up, down, left, right, leftUp, leftDown, rightUp, rightDown };
pub const epsilon = 1e-4;

pub const Vector2 = struct {
    x: f32 = 0,
    y: f32 = 0,

    pub const zero = Vector2{ .x = 0, .y = 0 };

    pub fn init(x: f32, y: f32) Vector2 {
        return .{ .x = x, .y = y };
    }

    pub fn toVector3(self: Vector2, z: f32) Vector3 {
        return .{ .x = self.x, .y = self.y, .z = z };
    }

    pub fn add(self: Vector2, other: Vector2) Vector2 {
        return .{ .x = self.x + other.x, .y = self.y + other.y };
    }

    pub fn sub(self: Vector2, other: Vector2) Vector2 {
        return .{ .x = self.x - other.x, .y = self.y - other.y };
    }

    pub fn scale(self: Vector2, scalar: f32) Vector2 {
        return .{ .x = self.x * scalar, .y = self.y * scalar };
    }

    pub fn length(self: Vector2) f32 {
        return std.math.sqrt(self.x * self.x + self.y * self.y);
    }

    pub fn normalize(self: Vector2) Vector2 {
        return .{ .x = self.x / self.length(), .y = self.y / self.length() };
    }

    pub fn approx(self: Vector2, other: Vector2) bool {
        return std.math.approxEqAbs(f32, self.x, other.x, epsilon) and
            std.math.approxEqAbs(f32, self.y, other.y, epsilon);
    }
};

pub const Vector = Vector3;
pub const Vector3 = struct {
    x: f32 = 0,
    y: f32 = 0,
    z: f32 = 0,

    pub const zero = Vector3{ .x = 0, .y = 0, .z = 0 };

    pub fn init(x: f32, y: f32) Vector3 {
        return .{ .x = x, .y = y, .z = 0 };
    }

    pub fn toVector2(self: Vector3) Vector2 {
        return .{ .x = self.x, .y = self.y };
    }

    pub fn add(self: Vector3, other: Vector3) Vector3 {
        return .{ .x = self.x + other.x, .y = self.y + other.y, .z = self.z + other.z };
    }

    pub fn sub(self: Vector3, other: Vector3) Vector3 {
        return .{ .x = self.x - other.x, .y = self.y - other.y, .z = self.z - other.z };
    }

    pub fn scale(self: Vector3, scalar: f32) Vector3 {
        return .{ .x = self.x * scalar, .y = self.y * scalar, .z = self.z * scalar };
    }

    pub fn dot(self: Vector3, other: Vector3) f32 {
        return self.x * other.x + self.y * other.y + self.z * other.z;
    }

    pub fn mul(self: Vector3, other: Vector3) Vector3 {
        return .{ .x = self.x * other.x, .y = self.y * other.y, .z = self.z * other.z };
    }

    pub fn div(self: Vector3, other: Vector3) Vector3 {
        return .{ .x = self.x / other.x, .y = self.y / other.y, .z = self.z / other.z };
    }

    pub fn length(self: Vector3) f32 {
        return std.math.sqrt(self.x * self.x + self.y * self.y + self.z * self.z);
    }

    pub fn normalize(self: Vector3) Vector3 {
        const len = self.length();
        return .{ .x = self.x / len, .y = self.y / len, .z = self.z / len };
    }

    pub fn rotate(self: Vector3, radians: f32) Vector3 {
        const cos = @cos(radians);
        const sin = @sin(radians);
        return .{
            .x = self.x * cos - self.y * sin,
            .y = self.x * sin + self.y * cos,
            .z = self.z,
        };
    }

    pub fn angle(self: Vector3) f32 {
        return std.math.atan2(self.y, self.x);
    }

    pub fn approx(self: Vector3, other: Vector3) bool {
        return std.math.approxEqAbs(f32, self.x, other.x, epsilon) and
            std.math.approxEqAbs(f32, self.y, other.y, epsilon) and
            std.math.approxEqAbs(f32, self.z, other.z, epsilon);
    }
};

pub const Rectangle = struct {
    position: Vector = .zero,
    size: Vector = .zero,

    pub fn left(self: Rectangle) f32 {
        return self.position.x;
    }

    pub fn top(self: Rectangle) f32 {
        return self.position.y;
    }

    pub fn right(self: Rectangle) f32 {
        return self.position.x + self.size.x;
    }

    pub fn bottom(self: Rectangle) f32 {
        return self.position.y + self.size.y;
    }

    pub fn intersect(self: Rectangle, other: Rectangle) bool {
        return self.left() < other.right() and self.right() > other.left() and
            self.top() < other.bottom() and self.bottom() > other.top();
    }

    pub fn contains(self: Rectangle, point: Vector) bool {
        return point.x >= self.left() and point.x < self.right() and
            point.y >= self.top() and point.y < self.bottom();
    }
};

pub var rand: std.Random = undefined;

pub fn randomF32(min: f32, max: f32) f32 {
    return rand.float(f32) * (max - min) + min;
}

pub fn randomU8(min: u8, max: u8) u8 {
    return rand.intRangeLessThanBiased(u8, min, max);
}
```

## gpu.zig

新增了图片旋转功能。

```zig
const std = @import("std");
const sk = @import("sokol");

const render = @import("shader/single.glsl.zig");
const math = @import("math.zig");

pub const Color = sk.gfx.Color;
pub const Buffer = sk.gfx.Buffer;

pub const Texture = struct {
    value: sk.gfx.Image,

    pub fn init(w: u32, h: u32, data: []const u8) Texture {
        const image = sk.gfx.allocImage();

        sk.gfx.initImage(image, .{
            .width = @as(i32, @intCast(w)),
            .height = @as(i32, @intCast(h)),
            .pixel_format = .RGBA8,
            .data = init: {
                var imageData = sk.gfx.ImageData{};
                imageData.subimage[0][0] = sk.gfx.asRange(data);
                break :init imageData;
            },
        });

        return .{ .value = image };
    }

    pub fn width(self: Texture) f32 {
        return @floatFromInt(sk.gfx.queryImageWidth(self.value));
    }

    pub fn height(self: Texture) f32 {
        return @floatFromInt(sk.gfx.queryImageHeight(self.value));
    }

    pub fn size(self: Texture) math.Vector {
        return .{ .x = self.width(), .y = self.height(), .z = 1 };
    }

    pub fn deinit(self: *Texture) void {
        sk.gfx.destroyImage(self.value);
    }
};

pub const BindGroup = struct {
    value: sk.gfx.Bindings = .{},
    uniform: render.VsParams = undefined,

    pub fn bindIndexBuffer(self: *BindGroup, buffer: Buffer) void {
        self.value.index_buffer = buffer;
    }

    pub fn bindVertexBuffer(self: *BindGroup, index: u32, buffer: Buffer) void {
        self.value.vertex_buffers[index] = buffer;
    }

    pub fn bindTexture(self: *BindGroup, index: u32, texture: Texture) void {
        self.value.images[index] = texture.value;
    }

    pub fn bindSampler(self: *BindGroup, index: u32, sampler: Sampler) void {
        self.value.samplers[index] = sampler.value;
    }

    pub fn bindUniformBuffer(self: *BindGroup, uniform: UniformParams) void {
        self.uniform = uniform;
    }
};

pub const CommandEncoder = struct {
    pub fn beginRenderPass(color: Color, matrix: []const f32) RenderPassEncoder {
        sk.gl.defaults();
        sk.gl.matrixModeModelview();
        sk.gl.loadMatrix(@ptrCast(matrix));
        sk.gl.pushMatrix();
        return RenderPassEncoder.begin(color);
    }
};

pub const Rectangle = math.Rectangle;

pub const RenderPassEncoder = struct {
    pub fn begin(color: Color) RenderPassEncoder {
        var action = sk.gfx.PassAction{};
        action.colors[0] = .{ .load_action = .CLEAR, .clear_value = color };
        sk.gfx.beginPass(.{ .action = action, .swapchain = sk.glue.swapchain() });
        return RenderPassEncoder{};
    }

    pub fn setPipeline(self: *RenderPassEncoder, pipeline: RenderPipeline) void {
        _ = self;
        sk.gfx.applyPipeline(pipeline.value);
    }

    pub fn setBindGroup(self: *RenderPassEncoder, group: BindGroup) void {
        _ = self;
        sk.gfx.applyUniforms(render.UB_vs_params, sk.gfx.asRange(&group.uniform));
        sk.gfx.applyBindings(group.value);
    }

    pub fn draw(self: *RenderPassEncoder, number: u32) void {
        _ = self;
        sk.gfx.draw(0, number, 1);
    }

    pub fn submit(self: *RenderPassEncoder) void {
        _ = self;
        sk.gl.popMatrix();
        sk.gl.draw();
        sk.gfx.endPass();
        sk.gfx.commit();
    }
};

const UniformParams = render.VsParams;

pub const Renderer = struct {
    bind: BindGroup,
    renderPass: RenderPassEncoder,

    var indexBuffer: ?Buffer = null;
    var pipeline: ?RenderPipeline = null;
    var sampler: ?Sampler = null;

    pub fn init() Renderer {
        sk.gfx.setup(.{
            .environment = sk.glue.environment(),
            .logger = .{ .func = sk.log.func },
            .image_pool_size = 150,
        });

        sk.gl.setup(.{
            .logger = .{ .func = sk.log.func },
        });

        sk.debugtext.setup(.{
            .fonts = init: {
                var f: [8]sk.debugtext.FontDesc = @splat(.{});
                f[0] = sk.debugtext.fontKc854();
                break :init f;
            },
            .logger = .{ .func = sk.log.func },
        });

        var self = Renderer{ .bind = .{}, .renderPass = undefined };

        indexBuffer = indexBuffer orelse sk.gfx.makeBuffer(.{
            .type = .INDEXBUFFER,
            .data = sk.gfx.asRange(&[_]u16{ 0, 1, 2, 0, 2, 3 }),
        });
        self.bind.bindIndexBuffer(indexBuffer.?);

        sampler = sampler orelse Sampler.nearest();
        self.bind.bindSampler(render.SMP_smp, sampler.?);

        pipeline = pipeline orelse RenderPipeline{
            .value = sk.gfx.makePipeline(.{
                .shader = sk.gfx.makeShader(render.singleShaderDesc(sk.gfx.queryBackend())),
                .layout = init: {
                    var l = sk.gfx.VertexLayoutState{};
                    l.attrs[render.ATTR_single_position].format = .FLOAT3;
                    l.attrs[render.ATTR_single_color0].format = .FLOAT3;
                    l.attrs[render.ATTR_single_texcoord0].format = .FLOAT2;
                    break :init l;
                },
                .colors = init: {
                    var c: [4]sk.gfx.ColorTargetState = @splat(.{});
                    c[0] = .{
                        .blend = .{
                            .enabled = true,
                            .src_factor_rgb = .SRC_ALPHA,
                            .dst_factor_rgb = .ONE_MINUS_SRC_ALPHA,
                        },
                    };
                    break :init c;
                },
                .index_type = .UINT16,
                .depth = .{ .compare = .LESS_EQUAL, .write_enabled = true },
            }),
        };

        return self;
    }

    pub const DrawOptions = struct {
        uniform: UniformParams,
        texture: Texture,
        sourceRect: Rectangle,
        targetRect: Rectangle,
        radians: f32 = 0,
        pivot: math.Vector = .zero,
    };

    pub fn draw(self: *Renderer, options: DrawOptions) void {
        const dst = options.targetRect;

        const pos = options.sourceRect.position.div(options.texture.size());
        const size = options.sourceRect.size.div(options.texture.size());

        var vertex = [_]math.Vector3{
            .{ .x = dst.position.x, .y = dst.bottom() },
            .{ .x = dst.right(), .y = dst.bottom() },
            .{ .x = dst.right(), .y = dst.position.y },
            .{ .x = dst.position.x, .y = dst.position.y },
        };

        if (options.radians != 0) {
            const percent = options.pivot.div(options.texture.size());
            const pivot = dst.position.add(percent.mul(dst.size));

            for (&vertex) |*point| {
                point.* = pivot.add(point.sub(pivot).rotate(options.radians));
            }
        }

        const vertexBuffer = sk.gfx.makeBuffer(.{
            .data = sk.gfx.asRange(&[_]f32{
                // 顶点和颜色
                vertex[0].x, vertex[0].y, 0.5, 1.0, 1.0, 1.0, pos.x,  size.y,
                vertex[1].x, vertex[1].y, 0.5, 1.0, 1.0, 1.0, size.x, size.y,
                vertex[2].x, vertex[2].y, 0.5, 1.0, 1.0, 1.0, size.x, pos.y,
                vertex[3].x, vertex[3].y, 0.5, 1.0, 1.0, 1.0, pos.x,  pos.y,
            }),
        });

        self.bind.bindVertexBuffer(0, vertexBuffer);
        self.bind.bindUniformBuffer(options.uniform);

        self.renderPass.setPipeline(pipeline.?);
        self.bind.bindTexture(render.IMG_tex, options.texture);
        self.renderPass.setBindGroup(self.bind);
        sk.gfx.draw(0, 6, 1);
        sk.gfx.destroyBuffer(vertexBuffer);
    }
};

pub fn deinit() void {
    sk.gfx.shutdown();
}

pub const RenderPipeline = struct {
    value: sk.gfx.Pipeline,
};

pub const Sampler = struct {
    value: sk.gfx.Sampler,

    pub fn liner() Sampler {
        const sampler = sk.gfx.makeSampler(.{
            .min_filter = .LINEAR,
            .mag_filter = .LINEAR,
        });
        return .{ .value = sampler };
    }

    pub fn nearest() Sampler {
        const sampler = sk.gfx.makeSampler(.{
            .min_filter = .NEAREST,
            .mag_filter = .NEAREST,
        });
        return .{ .value = sampler };
    }
};

pub fn drawRectangleLine(rect: Rectangle) void {
    sk.gl.beginLineStrip();

    sk.gl.v2fC3b(rect.x, rect.y, 0, 255, 0);
    sk.gl.v2fC3b(rect.right(), rect.y, 0, 255, 0);
    sk.gl.v2fC3b(rect.right(), rect.bottom(), 0, 255, 0);
    sk.gl.v2fC3b(rect.x, rect.bottom(), 0, 255, 0);
    sk.gl.v2fC3b(rect.x, rect.y, 0, 255, 0);

    sk.gl.end();
}
```

## graphics.zig

将设置默认的合适值，从底层移动到了上层。

```zig
const std = @import("std");

const cache = @import("cache.zig");
const gpu = @import("gpu.zig");
const math = @import("math.zig");
const animation = @import("animation.zig");

pub const Texture = gpu.Texture;

pub const Camera = struct {
    rect: math.Rectangle = .{},

    pub fn lookAt(self: *Camera, pos: math.Vector) void {
        self.rect.x = pos.x - self.rect.w / 2;
        self.rect.y = pos.y - self.rect.h / 2;
    }
};

pub var renderer: gpu.Renderer = undefined;
var matrix: [16]f32 = undefined;
var passEncoder: gpu.RenderPassEncoder = undefined;
pub var camera: Camera = undefined;

pub fn init(size: math.Vector) void {
    matrix = .{
        2 / size.x, 0.0,         0.0, 0.0,
        0.0,        2 / -size.y, 0.0, 0.0,
        0.0,        0.0,         1,   0.0,
        -1,         1,           0,   1.0,
    };
    renderer = gpu.Renderer.init();
}

pub const deinit = gpu.deinit;

pub fn loadTexture(path: [:0]const u8) Texture {
    return cache.Texture.load(path);
}

pub fn beginDraw() void {
    passEncoder = gpu.CommandEncoder.beginRenderPass(
        .{ .r = 1, .b = 1, .a = 1.0 },
        &matrix,
    );

    renderer.renderPass = passEncoder;
}

pub fn drawRectangle(rect: math.Rectangle) void {
    gpu.drawRectangleLine(rect);
}

pub fn draw(tex: Texture, position: math.Vector) void {
    drawFlipX(tex, position, false);
}

pub fn drawFlipX(tex: Texture, pos: math.Vector, flipX: bool) void {
    const target: math.Rectangle = .{ .position = pos };
    const src = math.Rectangle{ .size = .{
        .x = if (flipX) -tex.width() else tex.width(),
    } };

    drawOptions(tex, .{ .sourceRect = src, .targetRect = target });
}

pub const DrawOptions = struct {
    sourceRect: math.Rectangle = .{},
    targetRect: math.Rectangle = .{},
    angle: f32 = 0,
    pivot: math.Vector = .zero,
};

pub fn drawOptions(texture: Texture, options: DrawOptions) void {
    matrix[12] = -1 - camera.rect.left() * matrix[0];
    matrix[13] = 1 - camera.rect.top() * matrix[5];

    var src, var dst = .{ options.sourceRect, options.targetRect };
    if (src.size.x == 0) src.size.x = texture.width();
    if (src.size.y == 0) src.size.y = texture.height();
    if (dst.size.x == 0) dst.size.x = texture.width();
    if (dst.size.y == 0) dst.size.y = texture.height();

    renderer.draw(.{
        .uniform = .{ .vp = matrix },
        .texture = texture,
        .sourceRect = src,
        .targetRect = dst,
        .radians = std.math.degreesToRadians(options.angle),
        .pivot = options.pivot,
    });
}

pub fn endDraw() void {
    passEncoder.submit();
}

pub const FrameAnimation = animation.FrameAnimation;
pub const SliceFrameAnimation = animation.SliceFrameAnimation;
pub const AtlasFrameAnimation = animation.AtlasFrameAnimation;

pub fn playSlice(frameAnimation: *const FrameAnimation, pos: math.Vector) void {
    playSliceFlipX(frameAnimation, pos, false);
}

pub fn playSliceFlipX(frame: *const FrameAnimation, pos: math.Vector, flipX: bool) void {
    const offset = pos.add(frame.offset);
    drawFlipX(frame.textures[frame.index], offset, flipX);
}

pub fn playAtlas(frameAnimation: *const AtlasFrameAnimation, pos: math.Vector) void {
    playAtlasFlipX(frameAnimation, pos, false);
}

pub fn playAtlasFlipX(frame: *const AtlasFrameAnimation, pos: math.Vector, flipX: bool) void {
    var src = frame.frames[frame.index];
    const offset = pos.add(frame.offset);

    const dst: gpu.Rectangle = .{ .x = offset.x, .y = offset.y, .w = src.w };
    if (flipX) src.w = -src.w;
    drawOptions(frame.texture, .{ .sourceRect = src, .targetRect = dst });
}
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");

pub fn init() void {}

pub fn event(ev: *const window.Event) void {
    _ = ev;
}
pub fn update(delta: f32) void {
    _ = delta;
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    const background = gfx.loadTexture("assets/background.png");
    gfx.draw(background, window.size.sub(background.size()).scale(0.5));

    const fire = gfx.loadTexture("assets/barrel_fire_1.png");
    gfx.drawOptions(fire, .{ .angle = 45, .pivot = .{ .x = 30, .y = 30 } });
}

pub fn deinit() void {}
```

## 效果

![图片旋转][1]

[1]: images/图片旋转02.png

## 附录
