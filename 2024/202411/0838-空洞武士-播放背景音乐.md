# 0838-空洞武士-播放背景音乐

## 目标

使用 sokol 自带的音频模块来播放音频。

## 环境

- Time 2025-04-09
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

之前使用 MiniAudio 来播放的音频，这里去掉了这个依赖，使用 sokol audio 来播放。
音频文件的解析使用 stb_vorbis 来解析，这个只可以解析 ogg 文件，所以需要将教程的 mp3 转换一下。

## cache.zig

缓存模块增加音频支持。如果播放的时候同时播放同一个音频，应该会错乱，不过遇到问题再解决，先就这样。

```zig
...
pub const Audio = struct {
    var cache: std.StringHashMapUnmanaged(*c.stbAudio.Audio) = .empty;

    pub fn load(path: [:0]const u8) *c.stbAudio.Audio {
        const entry = cache.getOrPut(allocator, path) catch unreachable;
        if (entry.found_existing) return entry.value_ptr.*;

        const audio = c.stbAudio.load(path) catch unreachable;

        entry.value_ptr.* = audio;
        return audio;
    }

    pub fn deinit() void {
        var iterator = cache.valueIterator();
        while (iterator.next()) |value| c.stbAudio.unload(value.*);
        cache.deinit(allocator);
    }
};
...
```

## audio.zig

增加一个音频模块。

```zig
const std = @import("std");
const sk = @import("sokol");
const cache = @import("cache.zig");
const c = @import("c.zig");

pub fn init() void {
    sk.audio.setup(.{
        .num_channels = 2,
        .stream_cb = callback,
        .logger = .{ .func = sk.log.func },
    });

    playList = std.BoundedArray(AudioNode, 10).init(0) catch unreachable;
}

pub fn deinit() void {
    sk.audio.shutdown();
}

pub var playList: std.BoundedArray(AudioNode, 10) = undefined;

pub fn play(source: AudioNode) void {
    playList.appendAssumeCapacity(source);
}

pub const AudioNode = struct {
    source: *c.stbAudio.Audio,
    valid: bool = true,
    loop: bool = false,

    pub fn load(path: [:0]const u8) AudioNode {
        const audio = cache.Audio.load(path);
        const info = c.stbAudio.getInfo(audio);

        std.log.info("audio path: {s}, info: {any}", .{ path, info });
        return .{ .source = audio };
    }

    pub fn loadLoop(path: [:0]const u8) AudioNode {
        var audio = load(path);
        audio.loop = true;
        return audio;
    }

    pub fn play(self: AudioNode) void {
        playList.appendAssumeCapacity(self);
    }
};

fn callback(b: [*c]f32, frames: i32, channels: i32) callconv(.C) void {
    const buffer = b[0..@as(usize, @intCast(frames * channels))];
    @memset(buffer, 0);

    for (playList.slice()) |*value| {
        const sampleCount = c.stbAudio.addSamples(value.source, buffer, channels);
        if (sampleCount != 0) continue;

        c.stbAudio.reset(value.source);
        if (!value.loop) value.valid = false;
    }
    {
        var i: usize = 0;
        while (i < playList.len) : (i += 1) {
            if (playList.slice()[i].valid) continue;
            _ = playList.swapRemove(i);
        }
    }
}
```

## c.zig

使用 stb_vorbis 来解析 ogg 文件。

```zig
const std = @import("std");

pub const stbImage = struct {
    const stb = @cImport(@cInclude("stb_image.h"));

    const Image = struct { data: []u8 = &[_]u8{}, width: u32, height: u32 };

    pub fn load(path: [:0]const u8) !Image {
        var width: c_int, var height: c_int = .{ 0, 0 };
        const result = stb.stbi_load(path, &width, &height, 0, 4);
        if (result == null) return error.LoadImageFailed;

        var image: Image = .{ .width = @intCast(width), .height = @intCast(height) };
        image.data = @as([*]u8, @ptrCast(result))[0 .. image.width * image.height * 4];
        return image;
    }

    pub fn unload(self: Image) void {
        stb.stbi_image_free(self.data.ptr);
    }
};

pub const stbAudio = stbVorbis;
pub const stbVorbis = struct {
    const stb = @cImport({
        @cDefine("STB_VORBIS_NO_PUSHDATA_API", {});
        @cDefine("STB_VORBIS_HEADER_ONLY", {});
        @cDefine("STB_VORBIS_NO_INTEGER_CONVERSION", {});
        @cInclude("stb_Vorbis.c");
    });

    pub const Audio = stb.stb_vorbis;
    pub const AudioInfo = stb.stb_vorbis_info;

    pub fn load(path: [:0]const u8) !*Audio {
        var errorCode: c_int = 0;

        const vorbis = stb.stb_vorbis_open_filename(path, &errorCode, null);
        if (errorCode != 0 or vorbis == null) return error.loadAudioFailed;
        return vorbis.?;
    }

    pub fn getInfo(audio: *Audio) AudioInfo {
        return stb.stb_vorbis_get_info(audio);
    }

    pub fn getSampleCount(audio: *Audio) usize {
        return stb.stb_vorbis_stream_length_in_samples(audio);
    }

    pub fn addSamples(audio: *Audio, buffer: []f32, channels: i32) c_int {
        return stb.stb_vorbis_get_samples_float_interleaved(
            audio,
            channels,
            @ptrCast(buffer),
            @intCast(buffer.len),
        );
    }

    pub fn reset(audio: *Audio) void {
        _ = stb.stb_vorbis_seek_start(audio);
    }

    pub fn unload(audio: *Audio) void {
        stb.stb_vorbis_close(audio);
    }
};
```

## build.zig

增加音频部分的编译。

```zig
const std = @import("std");

pub fn build(b: *std.Build) !void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});

    const exe = b.addExecutable(.{
        .name = "demo",
        .root_source_file = b.path("src/main.zig"),
        .target = target,
        .optimize = optimize,
    });

    if (optimize != .Debug) exe.subsystem = .Windows;

    b.installArtifact(exe);

    const sokol = b.dependency("sokol", .{ .target = target, .optimize = optimize });
    exe.root_module.addImport("sokol", sokol.module("sokol"));

    const writeFiles = b.addWriteFiles();
    exe.step.dependOn(&writeFiles.step);

    const stb = b.dependency("stb", .{ .target = target, .optimize = optimize });
    exe.root_module.addIncludePath(stb.path("."));
    const stbImagePath = writeFiles.add("stb_image.c", stbImageSource);
    exe.root_module.addCSourceFile(.{ .file = stbImagePath, .flags = &.{"-O2"} });

    const stbAudioPath = writeFiles.add("stb_audio.c", stbAudioSource);
    exe.root_module.addCSourceFile(.{ .file = stbAudioPath, .flags = &.{"-O2"} });

    const run_cmd = b.addRunArtifact(exe);
    run_cmd.step.dependOn(b.getInstallStep());
    if (b.args) |args| {
        run_cmd.addArgs(args);
    }
    const run_step = b.step("run", "Run the app");
    run_step.dependOn(&run_cmd.step);
}

const stbImageSource =
    \\
    \\#define STB_IMAGE_IMPLEMENTATION
    \\#define STBI_ONLY_PNG
    \\#include "stb_image.h"
    \\
;

const stbAudioSource =
    \\
    \\#define STB_VORBIS_NO_PUSHDATA_API
    \\#define STB_VORBIS_NO_INTEGER_CONVERSION
    \\
    \\#include "stb_vorbis.c"
    \\
;
```

## main.zig

播放一个背景音乐。

```zig
const std = @import("std");

const cache = @import("cache.zig");
const window = @import("window.zig");
const gfx = @import("graphics.zig");
const audio = @import("audio.zig");
const scene = @import("scene.zig");

pub fn init() void {
    cache.init(allocator);
    gfx.init(window.width, window.height);
    audio.init();

    scene.init();
    const bgm = audio.AudioNode.loadLoop("assets/audio/bgm.ogg");
    bgm.play();
}

pub fn event(ev: *const window.Event) void {
    scene.event(ev);
}

pub fn update() void {
    scene.update();
}

pub fn render() void {
    scene.render();
}

pub fn deinit() void {
    scene.deinit();
    audio.deinit();
    cache.deinit();
}

var allocator: std.mem.Allocator = undefined;

pub fn main() void {
    var debugAllocator = std.heap.DebugAllocator(.{}).init;
    defer _ = debugAllocator.deinit();

    allocator = debugAllocator.allocator();
    window.width = 1280;
    window.height = 720;

    var prng = std.Random.DefaultPrng.init(@intCast(std.time.timestamp()));
    window.rand = prng.random();

    window.run(.{
        .title = "空洞武士",
        .init = init,
        .event = event,
        .update = update,
        .render = render,
        .deinit = deinit,
    });
}
```

## 效果

由于是音频，所以就不上效果了。

## 附录
