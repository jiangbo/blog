# 0858-哈基米大冒险-角色进度同步

## 目标

通过网络对两个角色的进度进行同步。

## 环境

- Time 2025-04-15
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1wmy6YuEzh>

## 想法

两个客户端的角色可以相互看见对方的位置了，感觉很神奇。

## http.zig

进度使用的浮点，所以将其修改为泛型方法了。

```zig
...
pub fn sendValue(T: type, url: []const u8, value: ?T) T {
    var buffer: [16]u8 = undefined;

    var response: std.ArrayListUnmanaged(u8) = .initBuffer(&buffer);

    const status = client.fetch(.{
        .method = .POST,
        .payload = if (value == null) null else &std.mem.toBytes(value),
        .location = .{ .url = url },
        .response_storage = .{ .static = &response },
    }) catch unreachable;

    if (status.status != .ok)
        std.debug.panic("request error: {}", .{status.status});

    return std.mem.bytesToValue(T, response.items);
}
...
```

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const http = @import("http.zig");

const Player = @import("Player.zig");
const BASE_URL = "http://127.0.0.1:4444/api";
const SPEED = 100;

var cameraScene: gfx.Camera = .{};
var cameraUI: gfx.Camera = .{};

var text: std.ArrayList(u8) = undefined;
var lines: std.BoundedArray([]const u8, 100) = undefined;
const paths = blk: {
    var temp = [_]math.Vector{
        .{ .x = 842, .y = 842 },
        .{ .x = 1322, .y = 842 },
        .{ .x = 1322, .y = 442 },
        .{ .x = 2762, .y = 442 },
        .{ .x = 2762, .y = 842 },
        .{ .x = 3162, .y = 842 },
        .{ .x = 3162, .y = 1722 },
        .{ .x = 2122, .y = 1722 },
        .{ .x = 2122, .y = 1562 },
        .{ .x = 842, .y = 1562 },
    };
    for (0..temp.len - 1) |index| {
        const len = temp[index + 1].sub(temp[index]).length();
        temp[index + 1].z = len;
    }
    break :blk temp;
};
var totalLength: f32 = 0;
var totalChar: f32 = 0;
var currentLine: u8 = 0;
var currentChar: u8 = 0;
var finishedChar: f32 = 0;

var player1: Player = undefined;
var player2: Player = undefined;

var textbox: gfx.Texture = undefined;

pub fn init(allocator: std.mem.Allocator) void {
    for (paths) |path| totalLength += path.z;

    cameraScene.setSize(window.size);
    cameraUI.setSize(window.size);

    player1 = Player.init(1);
    player2 = Player.init(2);
    player1.anchorCenter();
    player2.anchorCenter();

    text = http.sendAlloc(allocator, BASE_URL ++ "/text");
    lines = std.BoundedArray([]const u8, 100).init(0) catch unreachable;

    var iter = std.mem.tokenizeScalar(u8, text.items, '\n');
    while (iter.next()) |line| {
        lines.appendAssumeCapacity(line);
        totalChar += @as(f32, @floatFromInt(line.len));
    }

    playerIndex = http.sendValue(i32, BASE_URL ++ "/login", null);
    player1.position = paths[0];
    player2.position = paths[0];

    textbox = gfx.loadTexture("assets/ui_textbox.png");

    audio.playMusic("assets/bgm.ogg");

    const thread = std.Thread.spawn(.{}, syncProgress, .{}) catch unreachable;
    thread.detach();
}

pub fn deinit() void {
    _ = http.sendValue(i32, BASE_URL ++ "/logout", playerIndex);
    text.deinit();
    audio.stopMusic();
}

pub fn event(ev: *const window.Event) void {
    if (ev.type == .CHAR and ev.char_code > 0 and ev.char_code < 127) {
        const line = lines.get(currentLine);
        if (@as(u8, @intCast(ev.char_code)) == line[currentChar]) {
            currentChar += 1;
            finishedChar += 1;
            if (currentChar == line.len) {
                currentLine += 1;
                currentChar = 0;
            }

            if (currentLine == lines.len) {
                if (playerIndex == 1) {
                    player1Progress.store(1, .release);
                } else {
                    player2Progress.store(1, .release);
                }
            }
        }
    }
}

var playerIndex: i32 = 0;
var player1Progress: std.atomic.Value(f32) = .init(0);
var player2Progress: std.atomic.Value(f32) = .init(0);

pub fn update(delta: f32) void {
    const self = if (playerIndex == 1) &player1 else &player2;
    if (self.keydown) |key| {
        const position: math.Vector = switch (key) {
            .up => .{ .y = -SPEED * delta },
            .down => .{ .y = SPEED * delta },
            .left => .{ .x = -SPEED * delta },
            .right => .{ .x = SPEED * delta },
        };
        self.current = key;
        self.position = self.position.add(position);
    }

    cameraScene.lookAt(self.position);

    if (playerIndex == 1) {
        player1Progress.store(finishedChar / totalChar, .release);
    } else {
        player2Progress.store(finishedChar / totalChar, .release);
    }

    updatePlayer(&player1, player1Progress.load(.acquire), delta);
    updatePlayer(&player2, player2Progress.load(.acquire), delta);

    player1.currentAnimation().update(delta);
    player2.currentAnimation().update(delta);
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    gfx.camera = cameraScene;
    const background = gfx.loadTexture("assets/background.png");
    gfx.draw(background, 0, 0);

    gfx.playSlice(player1.currentAnimation(), player1.position);
    gfx.playSlice(player2.currentAnimation(), player2.position);

    gfx.camera = cameraUI;
    gfx.draw(textbox, 0, 720 - textbox.height());

    var buffer: [100]u8 = undefined;
    const line = lines.get(currentLine);

    @memcpy(buffer[0..currentChar], line[0..currentChar]);
    buffer[currentChar] = 0;

    moveTo(11.5, 39.5);
    displayText(buffer[0..currentChar :0], 0, 149, 125);

    @memcpy(buffer[currentChar..line.len], line[currentChar..]);
    buffer[line.len] = 0;
    displayText(buffer[currentChar..line.len :0], 0, 0, 0);

    endDisplayText();
}

fn updatePlayer(player: *Player, progress: f32, delta: f32) void {
    const target = getProgressPosition(progress);
    if (player.position.approx(target)) {
        player.velocity = .zero;
    } else {
        const direction = target.sub(player.position).normalize();
        player.velocity = direction.scale(SPEED);

        player.current = if (direction.x > math.epsilon) .right //
            else if (direction.x < -math.epsilon) .left //
            else if (direction.y > math.epsilon) .down //
            else if (direction.y < -math.epsilon) .up //
            else unreachable;
    }

    const distance = player.velocity.scale(delta);
    if (target.sub(player.position).length() < distance.length()) {
        player.position = target;
    } else {
        player.position = player.position.add(distance);
    }
}

fn getProgressPosition(progress: f32) math.Vector {
    if (progress == 0) return paths[0];
    if (progress >= 1) return paths[paths.len - 1];

    var remaining = totalLength * progress;

    for (paths[1..], 1..) |path, index| {
        if (remaining < path.z) {
            const delta = path.sub(paths[index - 1]).scale(remaining / path.z);
            return paths[index - 1].add(delta);
        }
        remaining -= path.z;
    }
    unreachable;
}

fn syncProgress() void {
    while (true) {
        if (playerIndex == 1) {
            var progress = player1Progress.load(.acquire);
            progress = http.sendValue(f32, BASE_URL ++ "/update1", progress);
            player2Progress.store(progress, .release);
        } else {
            var progress = player2Progress.load(.acquire);
            progress = http.sendValue(f32, BASE_URL ++ "/update2", progress);
            player1Progress.store(progress, .release);
        }
        std.time.sleep(100 * std.time.ns_per_ms);
    }
}

const sk = @import("sokol");
fn moveTo(x: f32, y: f32) void {
    sk.debugtext.canvas(sk.app.widthf() * 0.5, sk.app.heightf() * 0.5);
    sk.debugtext.origin(x, y);
    sk.debugtext.home();
}

fn displayText(str: [:0]const u8, r: u8, g: u8, b: u8) void {
    sk.debugtext.font(0);
    sk.debugtext.color3b(r, g, b);
    sk.debugtext.puts(str);
}

fn endDisplayText() void {
    sk.debugtext.draw();
}
```

## 效果

![角色进度同步][1]

[1]: images/哈基米大冒险08.webp

## 附录
