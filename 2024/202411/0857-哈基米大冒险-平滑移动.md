# 0857-哈基米大冒险-平滑移动

## 目标

角色移动时，不是直接将其放置到目标点，根据速度来算距离，进行平滑移动。

## 环境

- Time 2025-04-15
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1wmy6YuEzh>

## 想法

之前在游戏中见过这种，不是很清楚怎么实现的，应该就是平滑和插值。

## scene.zig

```zig
const std = @import("std");

const window = @import("window.zig");
const gfx = @import("graphics.zig");
const math = @import("math.zig");
const audio = @import("audio.zig");
const http = @import("http.zig");

const Player = @import("Player.zig");
const BASE_URL = "http://127.0.0.1:4444/api";
const SPEED = 100;

var cameraScene: gfx.Camera = .{};
var cameraUI: gfx.Camera = .{};

var text: std.ArrayList(u8) = undefined;
var lines: std.BoundedArray([]const u8, 100) = undefined;
const paths = blk: {
    var temp = [_]math.Vector{
        .{ .x = 842, .y = 842 },
        .{ .x = 1322, .y = 842 },
        .{ .x = 1322, .y = 442 },
        .{ .x = 2762, .y = 442 },
        .{ .x = 2762, .y = 842 },
        .{ .x = 3162, .y = 842 },
        .{ .x = 3162, .y = 1722 },
        .{ .x = 2122, .y = 1722 },
        .{ .x = 2122, .y = 1562 },
        .{ .x = 842, .y = 1562 },
    };
    for (0..temp.len - 1) |index| {
        const len = temp[index + 1].sub(temp[index]).length();
        temp[index + 1].z = len;
    }
    break :blk temp;
};
var totalLength: f32 = 0;
var totalChar: f32 = 0;
var currentLine: u8 = 0;
var currentChar: u8 = 0;
var finishedChar: f32 = 0;

var player1: Player = undefined;
var player2: Player = undefined;

var self: *Player = undefined;
var other: *Player = undefined;

var textbox: gfx.Texture = undefined;

pub fn init(allocator: std.mem.Allocator) void {
    for (paths) |path| totalLength += path.z;

    cameraScene.setSize(window.size);
    cameraUI.setSize(window.size);

    player1 = Player.init(1);
    player2 = Player.init(2);
    player1.anchorCenter();
    player2.anchorCenter();

    text = http.sendAlloc(allocator, BASE_URL ++ "/text");
    lines = std.BoundedArray([]const u8, 100).init(0) catch unreachable;

    var iter = std.mem.tokenizeScalar(u8, text.items, '\n');
    while (iter.next()) |line| {
        lines.appendAssumeCapacity(line);
        totalChar += @as(f32, @floatFromInt(line.len));
    }

    const playerIndex = http.sendValue(BASE_URL ++ "/login", null);
    self = if (playerIndex == 1) &player1 else &player2;
    other = if (playerIndex == 1) &player2 else &player1;
    self.position = paths[0];
    other.position = paths[0];

    textbox = gfx.loadTexture("assets/ui_textbox.png");

    audio.playMusic("assets/bgm.ogg");
}

pub fn deinit() void {
    _ = http.sendValue(BASE_URL ++ "/logout", self.index);
    text.deinit();
    audio.stopMusic();
}

pub fn event(ev: *const window.Event) void {
    if (ev.type == .CHAR and ev.char_code > 0 and ev.char_code < 127) {
        const line = lines.get(currentLine);
        if (@as(u8, @intCast(ev.char_code)) == line[currentChar]) {
            currentChar += 1;
            finishedChar += 1;
            if (currentChar == line.len) {
                currentLine += 1;
                currentChar = 0;
            }

            if (currentLine == lines.len) player1Progress = 1;
        }
    }
}

var player1Progress: f32 = 0;
pub fn update(delta: f32) void {
    if (self.keydown) |key| {
        const position: math.Vector = switch (key) {
            .up => .{ .y = -SPEED * delta },
            .down => .{ .y = SPEED * delta },
            .left => .{ .x = -SPEED * delta },
            .right => .{ .x = SPEED * delta },
        };
        self.current = key;
        self.position = self.position.add(position);
    }

    cameraScene.lookAt(self.position);

    const target = getProgressPosition(finishedChar / totalChar);
    if (self.position.approx(target)) {
        self.velocity = .zero;
    } else {
        const direction = target.sub(self.position).normalize();
        self.velocity = direction.scale(SPEED);

        if (direction.x > math.epsilon) {
            self.current = .right;
        } else if (direction.x < -math.epsilon) {
            self.current = .left;
        } else if (direction.y > math.epsilon) {
            self.current = .down;
        } else if (direction.y < -math.epsilon) {
            self.current = .up;
        }
    }

    const distance = self.velocity.scale(delta);
    if (target.sub(self.position).length() < distance.length()) {
        self.position = target;
    } else {
        self.position = self.position.add(distance);
    }

    self.currentAnimation().update(delta);
    other.currentAnimation().update(delta);
}

pub fn render() void {
    gfx.beginDraw();
    defer gfx.endDraw();

    gfx.camera = cameraScene;
    const background = gfx.loadTexture("assets/background.png");
    gfx.draw(background, 0, 0);

    gfx.playSlice(other.currentAnimation(), other.position);
    gfx.playSlice(self.currentAnimation(), self.position);

    gfx.camera = cameraUI;
    gfx.draw(textbox, 0, 720 - textbox.height());

    var buffer: [100]u8 = undefined;
    const line = lines.get(currentLine);

    @memcpy(buffer[0..currentChar], line[0..currentChar]);
    buffer[currentChar] = 0;

    moveTo(11.5, 39.5);
    displayText(buffer[0..currentChar :0], 0, 149, 125);

    @memcpy(buffer[currentChar..line.len], line[currentChar..]);
    buffer[line.len] = 0;
    displayText(buffer[currentChar..line.len :0], 0, 0, 0);

    endDisplayText();
}

fn getProgressPosition(progress: f32) math.Vector {
    if (progress == 0) return paths[0];
    if (progress >= 1) return paths[paths.len - 1];

    var remaining = totalLength * progress;

    for (paths[1..], 1..) |path, index| {
        if (remaining < path.z) {
            const delta = path.sub(paths[index - 1]).scale(remaining / path.z);
            return paths[index - 1].add(delta);
        }
        remaining -= path.z;
    }
    unreachable;
}

const sk = @import("sokol");
fn moveTo(x: f32, y: f32) void {
    sk.debugtext.canvas(sk.app.widthf() * 0.5, sk.app.heightf() * 0.5);
    sk.debugtext.origin(x, y);
    sk.debugtext.home();
}

fn displayText(str: [:0]const u8, r: u8, g: u8, b: u8) void {
    sk.debugtext.font(0);
    sk.debugtext.color3b(r, g, b);
    sk.debugtext.puts(str);
}

fn endDisplayText() void {
    sk.debugtext.draw();
}
```

## 效果

![平滑移动][1]

[1]: images/哈基米大冒险07.webp

## 附录
