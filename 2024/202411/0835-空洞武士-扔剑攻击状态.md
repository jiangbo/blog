# 0835-空洞武士-扔剑攻击状态

## 目标

扔剑攻击状态，除了自身的攻击动作，还需要添加剑的动画的运动。

## 环境

- Time 2025-04-07
- Zig 0.14.0

## 参考

1. <https://www.bilibili.com/video/BV1sW42197dD>

## 想法

用来记录剑出现的时间的定时器，不能直接判断结束，否则会生成很多剑。

## Enemy.zig

```zig
const std = @import("std");

const gfx = @import("../graphics.zig");
const math = @import("../math.zig");
const window = @import("../window.zig");
const scene = @import("../scene.zig");
const SharedActor = @import("actor.zig").SharedActor;
const item = @import("item.zig");

const Sword = item.Sword;
const Enemy = @This();

shared: SharedActor,
state: State = .idle,

idleTimer: window.Timer = .init(0.5),
idleAnimation: gfx.SliceFrameAnimation,

jumpAnimation: gfx.SliceFrameAnimation,

fallAnimation: gfx.SliceFrameAnimation,

aimTimer: window.Timer = .init(0.5),
aimAnimation: gfx.SliceFrameAnimation,

dashInAirAnimation: gfx.SliceFrameAnimation,
dashInAirVfx: gfx.SliceFrameAnimation,

runAnimation: gfx.SliceFrameAnimation,

squatTimer: window.Timer = .init(0.5),
squatAnimation: gfx.SliceFrameAnimation,

dashTimer: window.Timer = .init(0.5),
dashOnFloorAnimation: gfx.SliceFrameAnimation,
dashOnFloorVfx: gfx.SliceFrameAnimation,

throwSilkTimer: window.Timer = .init(0.9),
throwSilkAnimation: gfx.SliceFrameAnimation,
silkAnimation: gfx.SliceFrameAnimation,

swords: std.BoundedArray(Sword, 4),
throwSwordTimer: window.Timer = .init(1),
appearSwordTimer: ?window.Timer = null,
throwSwordAnimation: gfx.SliceFrameAnimation,

pub fn init() Enemy {
    timer = std.time.Timer.start() catch unreachable;
    var enemy: Enemy = .{
        .shared = .{
            .position = .{ .x = 1050, .y = 200 },
            .faceLeft = true,
            .health = 4,
        },
        .swords = std.BoundedArray(Sword, 4).init(0) catch unreachable,
        .idleAnimation = .load("assets/enemy/idle/{}.png", 5),
        .jumpAnimation = .load("assets/enemy/jump/{}.png", 8),
        .fallAnimation = .load("assets/enemy/fall/{}.png", 4),
        .aimAnimation = .load("assets/enemy/aim/{}.png", 9),
        .dashInAirAnimation = .load("assets/enemy/dash_in_air/{}.png", 2),
        .dashInAirVfx = .load("assets/enemy/vfx_dash_in_air/{}.png", 5),
        .runAnimation = .load("assets/enemy/run/{}.png", 8),
        .squatAnimation = .load("assets/enemy/squat/{}.png", 10),
        .dashOnFloorAnimation = .load("assets/enemy/dash_on_floor/{}.png", 2),
        .dashOnFloorVfx = .load("assets/enemy/vfx_dash_on_floor/{}.png", 5),
        .throwSilkAnimation = .load("assets/enemy/throw_silk/{}.png", 17),
        .silkAnimation = .load("assets/enemy/silk/{}.png", 9),
        .throwSwordAnimation = .load("assets/enemy/throw_sword/{}.png", 16),
    };

    enemy.state.enter(&enemy);
    enemy.jumpAnimation.loop = false;
    enemy.silkAnimation.anchor = .centerCenter;
    enemy.dashInAirVfx.anchor = .centerCenter;
    enemy.dashOnFloorVfx.anchor = .centerCenter;

    enemy.throwSwordAnimation.loop = false;
    return enemy;
}

var timer: std.time.Timer = undefined;

pub fn update(self: *Enemy, delta: f32) void {
    self.shared.update(delta);
    self.state.update(self, delta);

    var i = self.swords.len;
    while (i > 0) : (i -= 1) {
        var sword = &self.swords.slice()[i - 1];
        if (!sword.valid) {
            _ = self.swords.swapRemove(i - 1);
            continue;
        }
        sword.update(delta);
    }
}

pub fn render(self: *const Enemy) void {
    self.shared.render();
    self.state.render(self);
    for (self.swords.slice()) |sword| {
        sword.render();
    }
}

fn changeState(self: *Enemy, new: State) void {
    self.state.exit(self);
    new.enter(self);
}

fn play(self: *const Enemy, animation: *const gfx.SliceFrameAnimation) void {
    gfx.playSliceFlipX(animation, self.shared.position, !self.shared.faceLeft);
}

fn isEnraged(self: *const Enemy) bool {
    return self.shared.health <= 5;
}

const State = union(enum) {
    idle: IdleState,
    jump: JumpState,
    fall: FallState,
    aim: AimState,
    dashInAir: DashInAirState,
    run: RunState,
    squat: SquatState,
    dashOnFloor: DashOnFloorState,
    throwSilk: ThrowSilkState,
    throwSword: ThrowSwordState,

    fn enter(self: State, enemy: *Enemy) void {
        switch (self) {
            inline else => |case| @TypeOf(case).enter(enemy),
        }
    }

    fn update(self: State, enemy: *Enemy, delta: f32) void {
        switch (self) {
            inline else => |case| @TypeOf(case).update(enemy, delta),
        }
    }

    fn render(self: State, enemy: *const Enemy) void {
        switch (self) {
            inline else => |case| @TypeOf(case).render(enemy),
        }
    }

    fn exit(self: State, enemy: *Enemy) void {
        switch (self) {
            inline else => |case| @TypeOf(case).exit(enemy),
        }
    }
};

const IdleState = struct {
    fn enter(enemy: *Enemy) void {
        enemy.state = .idle;
        enemy.shared.velocity.x = 0;

        const max: f32 = if (enemy.isEnraged()) 0.5 else 0.25;
        enemy.idleTimer.duration = window.randomFloat(0, max);
        enemy.idleTimer.reset();
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.idleAnimation.update(delta);
        if (enemy.idleTimer.isRunningAfterUpdate(delta)) return;

        if (!enemy.shared.isOnFloor()) return enemy.changeState(.fall);

        if (enemy.isEnraged()) return updateEnraged(enemy);

        const rand = window.rand.intRangeLessThanBiased(u8, 0, 100);
        const state: State = switch (rand) {
            0...24 => .jump,
            25...49 => .run,
            50...79 => .squat,
            80...89 => .throwSilk,
            else => .throwSword,
        };
        enemy.changeState(state);
    }

    fn updateEnraged(enemy: *Enemy) void {
        const rand = window.rand.intRangeLessThanBiased(u8, 0, 100);
        const state: State = switch (rand) {
            0...24 => .jump,
            25...59 => .throwSword,
            60...69 => .throwSilk,
            // throw barb
            70...89 => .idle,
            else => .squat,
        };
        enemy.changeState(state);
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.idleAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.idleAnimation.reset();
        const playerPosition = scene.player.shared.position;
        enemy.shared.faceLeft = playerPosition.x < enemy.shared.position.x;
    }
};

const JumpState = struct {
    const SPEED_JUMP = 1000;

    fn enter(enemy: *Enemy) void {
        enemy.state = .jump;
        enemy.shared.velocity.y = -SPEED_JUMP;
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.jumpAnimation.update(delta);
        if (enemy.shared.velocity.y < 0) return;

        const rand = window.rand.intRangeLessThanBiased(u8, 0, 100);
        if (enemy.isEnraged()) {
            switch (rand) {
                0...49 => enemy.changeState(.aim),
                50...79 => enemy.changeState(.fall),
                else => enemy.changeState(.throwSilk),
            }
        } else {
            switch (rand) {
                0...49 => enemy.changeState(.throwSilk),
                50...79 => enemy.changeState(.fall),
                else => enemy.changeState(.aim),
            }
        }
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.jumpAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.jumpAnimation.reset();
    }
};

const FallState = struct {
    fn enter(enemy: *Enemy) void {
        enemy.state = .fall;
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.fallAnimation.update(delta);
        if (enemy.shared.isOnFloor()) enemy.changeState(.idle);
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.fallAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.fallAnimation.reset();
    }
};

const AimState = struct {
    fn enter(enemy: *Enemy) void {
        enemy.state = .aim;
        enemy.shared.velocity = .{};
        enemy.shared.enableGravity = false;
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.aimAnimation.update(delta);
        if (enemy.aimTimer.isRunningAfterUpdate(delta)) return;

        enemy.changeState(.dashInAir);
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.aimAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.aimTimer.reset();
        enemy.shared.enableGravity = true;
        enemy.aimAnimation.reset();
    }
};

const DashInAirState = struct {
    const SPEED_DASH = 1500;

    fn enter(enemy: *Enemy) void {
        enemy.state = .dashInAir;
        enemy.shared.enableGravity = false;

        const playerPosition = scene.player.shared.position;
        const target: math.Vector = .{ .x = playerPosition.x, .y = SharedActor.FLOOR_Y };
        const direction = target.sub(enemy.shared.position).normalize();
        enemy.shared.faceLeft = direction.x < 0;
        enemy.shared.velocity = direction.scale(SPEED_DASH);
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.dashInAirAnimation.update(delta);
        enemy.dashInAirVfx.update(delta);
        if (enemy.shared.isOnFloor()) enemy.changeState(.idle);
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.dashInAirAnimation);
        const pos = enemy.shared.logicCenter();
        gfx.playSliceFlipX(&enemy.dashInAirVfx, pos, !enemy.shared.faceLeft);
    }

    fn exit(enemy: *Enemy) void {
        enemy.dashInAirAnimation.reset();
        enemy.dashInAirVfx.reset();
        enemy.shared.enableGravity = true;
    }
};

const RunState = struct {
    const SPEED_RUN = 500;
    const MIN_DISTANCE = 350;

    fn enter(enemy: *Enemy) void {
        enemy.state = .run;
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.runAnimation.update(delta);

        const playerX = scene.player.shared.position.x;
        const enemyX = enemy.shared.position.x;
        const direction: f32 = if (playerX > enemyX) 1 else -1;
        enemy.shared.velocity.x = direction * SPEED_RUN;

        if (@abs(playerX - enemyX) > MIN_DISTANCE) return;

        const rand = window.rand.intRangeLessThanBiased(u8, 0, 100);
        if (enemy.isEnraged()) {
            switch (rand) {
                0...74 => enemy.changeState(.throwSilk),
                else => enemy.changeState(.squat),
            }
        } else {
            switch (rand) {
                0...74 => enemy.changeState(.squat),
                else => enemy.changeState(.throwSilk),
            }
        }
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.runAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.runAnimation.reset();
        enemy.shared.velocity = .{};
    }
};

const SquatState = struct {
    fn enter(enemy: *Enemy) void {
        enemy.state = .squat;
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.squatAnimation.update(delta);
        if (enemy.squatTimer.isRunningAfterUpdate(delta)) return;

        enemy.changeState(.dashOnFloor);
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.squatAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.squatAnimation.reset();
        enemy.squatTimer.reset();
    }
};

const DashOnFloorState = struct {
    const SPEED_DASH = 1000;

    fn enter(enemy: *Enemy) void {
        enemy.state = .dashOnFloor;
        const direction: f32 = if (enemy.shared.faceLeft) -1 else 1;
        enemy.shared.velocity = .{ .x = direction * SPEED_DASH };
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.dashOnFloorAnimation.update(delta);
        enemy.dashOnFloorVfx.update(delta);
        if (enemy.dashTimer.isRunningAfterUpdate(delta)) return;

        enemy.changeState(.idle);
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.dashOnFloorAnimation);
        const pos = enemy.shared.logicCenter();
        gfx.playSliceFlipX(&enemy.dashOnFloorVfx, pos, !enemy.shared.faceLeft);
    }

    fn exit(enemy: *Enemy) void {
        enemy.dashOnFloorAnimation.reset();
        enemy.dashOnFloorVfx.reset();
        enemy.dashTimer.reset();
    }
};

const ThrowSilkState = struct {
    fn enter(enemy: *Enemy) void {
        enemy.state = .throwSilk;
        enemy.shared.enableGravity = false;
        enemy.shared.velocity = .zero;
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.throwSilkAnimation.update(delta);
        enemy.silkAnimation.update(delta);
        if (enemy.throwSilkTimer.isRunningAfterUpdate(delta)) return;

        if (enemy.shared.isOnFloor()) return enemy.changeState(.idle);

        const rand = window.rand.intRangeLessThanBiased(u8, 0, 100);
        if (!enemy.isEnraged() and rand < 25) {
            enemy.changeState(.aim);
        } else {
            enemy.changeState(.fall);
        }
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.throwSilkAnimation);
        const pos = enemy.shared.logicCenter();
        gfx.playSliceFlipX(&enemy.silkAnimation, pos, !enemy.shared.faceLeft);
    }

    fn exit(enemy: *Enemy) void {
        enemy.throwSilkAnimation.reset();
        enemy.throwSilkTimer.reset();
        enemy.shared.enableGravity = true;
        enemy.silkAnimation.reset();
    }
};

const ThrowSwordState = struct {
    fn enter(enemy: *Enemy) void {
        enemy.state = .throwSword;
        enemy.appearSwordTimer = .init(0.65);
    }

    fn update(enemy: *Enemy, delta: f32) void {
        enemy.throwSwordAnimation.update(delta);

        const shared = &enemy.shared;
        if (enemy.appearSwordTimer) |*appearTimer| {
            if (appearTimer.isFinishedAfterUpdate(delta)) {
                const sword = Sword.init(shared.logicCenter(), shared.faceLeft);
                enemy.swords.appendAssumeCapacity(sword);
                enemy.appearSwordTimer = null;
            }
        }

        if (enemy.throwSwordTimer.isRunningAfterUpdate(delta)) return;

        const rand = window.rand.intRangeLessThanBiased(u8, 0, 100);
        if (enemy.isEnraged()) {
            switch (rand) {
                0...49 => enemy.changeState(.jump),
                50...79 => enemy.changeState(.idle),
                else => enemy.changeState(.idle),
            }
        } else {
            switch (rand) {
                0...49 => enemy.changeState(.squat),
                50...79 => enemy.changeState(.jump),
                else => enemy.changeState(.idle),
            }
        }
    }

    fn render(enemy: *const Enemy) void {
        enemy.play(&enemy.throwSwordAnimation);
    }

    fn exit(enemy: *Enemy) void {
        enemy.throwSwordAnimation.reset();
        enemy.throwSwordTimer.reset();
    }
};
```

## item.zig

新增了一个物品的模块。

```zig
const std = @import("std");

const gfx = @import("../graphics.zig");
const math = @import("../math.zig");
const window = @import("../window.zig");
const scene = @import("../scene.zig");

pub const Sword = struct {
    const SPEED_MOVE = 1250;

    position: math.Vector,
    moveLeft: bool,
    valid: bool = true,
    animation: gfx.SliceFrameAnimation,

    pub fn init(pos: math.Vector, moveLeft: bool) Sword {
        var self: Sword = .{
            .position = pos,
            .moveLeft = moveLeft,
            .animation = .load("assets/enemy/sword/{}.png", 3),
        };

        self.animation.anchor = .centerCenter;
        return self;
    }

    pub fn update(self: *Sword, delta: f32) void {
        self.animation.update(delta);

        const direction: f32 = if (self.moveLeft) -1 else 1;
        self.position.x += direction * SPEED_MOVE * delta;

        if (self.position.x < -200 or self.position.x > window.width + 200) {
            self.valid = false;
        }
    }

    pub fn render(self: *const Sword) void {
        gfx.playSliceFlipX(&self.animation, self.position, self.moveLeft);
    }
};
```

## 效果

![扔剑攻击状态][1]

[1]: images/空洞武士17.webp

## 附录
